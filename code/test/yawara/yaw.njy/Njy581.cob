000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             NJY581.
000060 AUTHOR.                 池田  幸子
000070*
000080*----------------------------------------------------------------*
000090*        【療養費請求総括票 データ作成】
000110*         MED = NJY580 NJY582P 
      *
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2019-10-24
000140 DATE-COMPILED.          2019-10-24
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000270     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000280                             ORGANIZATION             IS  INDEXED
000290                             ACCESS MODE              IS  DYNAMIC
000300                             RECORD KEY               IS 受−施術和暦年月
000310                                                         受−患者コード
000320                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000330                                                         受−患者カナ
000340                                                         受−患者コード
000350                             ALTERNATE RECORD KEY     IS 受−患者コード
000360                                                         受−施術和暦年月
000370                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000380                                                         受−保険種別
000390                                                         受−保険者番号
000400                                                         受−患者コード
000410                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000420                                                         受−公費種別
000430                                                         受−費用負担者番号
000440                                                         受−患者コード
000450                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000460                                                         受−助成種別
000470                                                         受−費用負担者番号助成
000480                                                         受−患者コード
000490                             ALTERNATE RECORD KEY     IS 受−請求和暦年月
000500                                                         受−施術和暦年月
000510                                                         受−患者コード
000520                             FILE STATUS              IS 状態キー
000530                             LOCK        MODE         IS AUTOMATIC.
000130     SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
000140                             ORGANIZATION             IS  INDEXED
000150                             ACCESS MODE              IS  DYNAMIC
000160                             RECORD KEY               IS  レセ−施術和暦年月
000170                                                          レセ−患者コード
000180                                                          レセ−レセ種別
000190                             ALTERNATE RECORD KEY     IS  レセ−患者コード
000200                                                          レセ−施術和暦年月
000210                                                          レセ−レセ種別
000220                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000230                                                          レセ−施術和暦年月
000240                                                          レセ−患者コード
000250                                                          レセ−レセ種別
000260                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000270                                                          レセ−レセ種別
000280                                                          レセ−請求保険者番号
000290                                                          レセ−患者コード
000300                                                          レセ−施術和暦年月
000310                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000320                                                          レセ−請求保険者番号
000330                                                          レセ−患者コード
000340                                                          レセ−レセ種別
000350                                                          レセ−施術和暦年月
000360                             FILE STATUS              IS  状態キー
000370                             LOCK        MODE         IS  AUTOMATIC.
001020     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W58021L.DAT"
001030                             ORGANIZATION             IS  INDEXED
001040                             ACCESS                   IS  DYNAMIC
001050                             RECORD      KEY          IS  作１−保険区分
001060                             FILE        STATUS       IS  状態キー
001070                             LOCK        MODE         IS  AUTOMATIC.
001080******************************************************************
001090*                      DATA DIVISION                             *
001100******************************************************************
001110 DATA                    DIVISION.
001120 FILE                    SECTION.
001130*                           ［ＲＬ＝  ７６８］
001140 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001150     COPY JUSINJ         OF  XFDLIB  JOINING    受   AS  PREFIX.
      *                          ［ＲＬ＝  １５３６］
       FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS.
           COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
001310*
001320 FD  作業ファイル１ RECORD  CONTAINS 32 CHARACTERS.
001330 01  作１−レコード.
001340   03  作１−レコードキー.
001350     05  作１−保険区分                PIC 9(2).
001360   03  作１−レコードデータ.
001370     05  作１−件数                    PIC 9(4).
001380     05  作１−費用額                  PIC 9(8).
001390     05  作１−請求額                  PIC 9(8).
001400     05  FILLER                        PIC X(10).
001410*
001420******************************************************************
001430*                WORKING-STORAGE SECTION                         *
001440******************************************************************
001450 WORKING-STORAGE         SECTION.
001460 01 キー入力                           PIC X(1) VALUE SPACE.
001470 01 状態キー                           PIC X(2) VALUE SPACE.
001480 01 終了フラグ１                       PIC X(3) VALUE SPACE.
001490 01 終了フラグ３                       PIC X(3) VALUE SPACE.
001500 01 確認入力Ｗ                         PIC X(1) VALUE SPACE.
001510 01 ファイル名Ｗ                       PIC N(2) VALUE SPACE.
001520 01 ファイル名                         PIC N(2) VALUE SPACE.
001530 01 カレント元号Ｗ                     PIC 9(1) VALUE ZERO.
001540 01 実行キーＷ                         PIC X(4) VALUE SPACE.
001550 01 前和暦Ｗ                           PIC 9(1) VALUE ZERO.
001560 01 行桁フラグ                         PIC X(3) VALUE SPACE.
001570 01 処理移動キー                       PIC X(4) VALUE SPACE.
001580*
001590 01 施術記録有Ｗ                       PIC X(3) VALUE SPACE.
001600 01 請求和暦年月Ｗ.
001610     03 請求和暦Ｗ                     PIC 9.
001620     03 請求年月Ｗ.
001630        05 請求年Ｗ                    PIC 9(2).
001640        05 請求月Ｗ                    PIC 9(2).
001650 01 請求日Ｗ                           PIC 9(2).
001660 01 印刷形式Ｗ                         PIC 9(1)  VALUE ZERO.
001670 01 元号名称Ｗ                         PIC N(2)  VALUE SPACE.
001680 01 例カウンタ                         PIC 9(2)  VALUE ZERO.
001690*
001700**************
001710* 保険者情報 *
001720**************
001730*
001940 01 印刷制御.
001950     03 定義体名Ｐ                     PIC X(8) VALUE SPACE.
001960     03 項目群名Ｐ                     PIC X(8) VALUE SPACE.
001970     03 処理種別Ｐ                     PIC X(2) VALUE SPACE.
001980     03 拡張制御Ｐ.
001990         05 端末制御Ｐ.
002000             07 移動方向Ｐ             PIC X(1).
002010             07 移動行数Ｐ             PIC 9(3).
002020         05 詳細制御Ｐ                 PIC X(2).
002030     03 通知情報Ｐ                     PIC X(2) VALUE SPACE.
002040     03 ユニット名Ｐ                   PIC X(8) VALUE SPACE.
002050 01 計算機西暦年Ｗ                     PIC 9(2).
002060* 日付ＷＯＲＫ
002070 01 和暦終了年Ｗ                       PIC 9(4).
002080 01 計算機和暦年Ｗ                     PIC 9(2).
002090 01 計算機西暦.
002100    03 計算機西暦年                    PIC 9(4).
002110    03 計算機西暦月日                  PIC 9(4).
002120 01 計算機西暦Ｒ REDEFINES 計算機西暦.
002130    03 計算機世紀                      PIC 9(2).
002140    03 計算機日付                      PIC 9(6).
002150    03 計算機日付Ｒ REDEFINES 計算機日付.
002160       05 計算機年月                   PIC 9(4).
002170       05 計算機年月Ｒ REDEFINES 計算機年月.
002180         07 計算機年                   PIC 9(2).
002190         07 計算機月                   PIC 9(2).
002200       05 計算機日                     PIC 9(2).
002210*
002260 01 請求和暦年月ＷＲ.
002270    03 請求和暦ＷＲ                  PIC 9     VALUE ZERO.
002280    03 請求年ＷＲ                    PIC 9(2)  VALUE ZERO.
002290    03 請求月ＷＲ                    PIC 9(2)  VALUE ZERO.
002300*
002310 01 保険種別Ｗ                       PIC 9(2) VALUE ZERO.
002320 01 公費種別Ｗ                       PIC 9(2) VALUE ZERO.
002330 01 助成種別Ｗ                       PIC 9(2) VALUE ZERO.
002340 01 本人家族区分Ｗ                   PIC 9(1) VALUE ZERO.
002350 01 老人負担金免除Ｗ                 PIC 9(1) VALUE ZERO.
002360*
002370 01 対象件数Ｗ                       PIC 9(4) VALUE ZERO.
002450*
002460 01 終了フラグ２                     PIC X(3) VALUE SPACE.
002470*
002480 01 状態フラグ                       PIC 9(1) VALUE ZERO.
002570*
002580 01 総括表.
002590   03 合計枚数Ｗ                     PIC 9(4)  VALUE ZERO.
002590   03 合計件数Ｗ                     PIC 9(5)  VALUE ZERO.
002600   03 合計費用額Ｗ                   PIC 9(9)  VALUE ZERO.
002610   03 合計請求額Ｗ                   PIC 9(9)  VALUE ZERO.
002590   03 他合計件数Ｗ                   PIC 9(5)  VALUE ZERO.
002600   03 他合計費用額Ｗ                 PIC 9(9)  VALUE ZERO.
002610   03 他合計請求額Ｗ                 PIC 9(9)  VALUE ZERO.
002620*
002630******************************************************************
002640*                          連結項目                              *
002650******************************************************************
002660*
002670****************
002680* 画面入力情報 *
002690****************
       01 連印−画面情報ＮＪＹ５８０ IS EXTERNAL GLOBAL.
          03 連印−請求和暦年月.
             05 連印−請求和暦               PIC 9.
             05 連印−請求年月.
                07 連印−請求年              PIC 9(2).
                07 連印−請求月              PIC 9(2).
          03 連印−プレビュー区分            PIC 9.
003080*
003090 01 連印−印刷データＮＪＹ５８０ IS EXTERNAL.
003100   03 連印−合計.
003110     05 連印−合計枚数                 PIC 9(4).
003110     05 連印−合計件数                 PIC 9(5).
003120     05 連印−合計費用額               PIC 9(9).
003130     05 連印−合計請求額               PIC 9(9).
003110     05 連印−他合計件数               PIC 9(5).
003130     05 連印−他合計費用額             PIC 9(9).
003130     05 連印−他合計請求額             PIC 9(9).
003110     05 連印−総合計件数               PIC 9(5).
003120     05 連印−総合計費用額             PIC 9(9).
003130     05 連印−総合計請求額             PIC 9(9).
003140*
003150******************************************************************
003160*                      PROCEDURE  DIVISION                       *
003170******************************************************************
003180 PROCEDURE               DIVISION.
003190************
003200*           *
003210* 初期処理   *
003220*           *
003230************
003240     PERFORM 初期化.
003250************
003260*           *
003270* 主処理     *
003280*           *
003290************
003300     PERFORM 作業ファイル作成.
003310************
003320*           *
003330* 終了処理   *
003340*           *
003350************
003360     PERFORM 終了処理.
003370     MOVE ZERO TO PROGRAM-STATUS.
003380     EXIT PROGRAM.
003390*
003400*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003410*================================================================*
003420 初期化 SECTION.
003430*
003440     OPEN INPUT 受診者情報Ｆ.
003450             MOVE NC"受診" TO ファイル名Ｗ.
003460             PERFORM オープンチェック.
006630     OPEN INPUT レセプトＦ.
006640             MOVE NC"レセ" TO ファイル名.
006650             PERFORM オープンチェック.
003750*
003760* 連結項目の待避
003770     MOVE 連印−請求和暦 TO 請求和暦ＷＲ.
003780     MOVE 連印−請求年   TO 請求年ＷＲ.
003790     MOVE 連印−請求月   TO 請求月ＷＲ.
003800*================================================================*
003810 オープンチェック SECTION.
003820*
003830     IF 状態キー  NOT =  "00"
003840         DISPLAY ファイル名Ｗ NC"Ｆオープンエラー" UPON CONS
003850         DISPLAY NC"状態キー：" 状態キー           UPON CONS
003860         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003870                                                   UPON CONS
004380*-----------------------------------------*
004390         CALL "actcshm"  WITH C LINKAGE
004400*-----------------------------------------*
003880         ACCEPT  キー入力 FROM CONS
003890         PERFORM ファイル閉鎖
003900         EXIT PROGRAM.
004270*
004280*================================================================*
004290 ファイル閉鎖 SECTION.
004300*
004310     CLOSE  受診者情報Ｆ レセプトＦ.
004330*================================================================*
004340 終了処理 SECTION.
004350*
004360     PERFORM ファイル閉鎖.
004370*================================================================*
004380* データ作成 SECTION.
004390 作業ファイル作成 SECTION.
004400*
004430     OPEN OUTPUT 作業ファイル１.
004440         MOVE NC"作１" TO ファイル名.
004450         PERFORM オープンチェック.
004460     CLOSE 作業ファイル１.
004470     OPEN I-O 作業ファイル１.
004480         MOVE NC"作１" TO ファイル名.
004490         PERFORM オープンチェック.
004530*
004760     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
004770     MOVE 請求年ＷＲ    TO レセ−請求年.
004780     MOVE 請求月ＷＲ    TO レセ−請求月.
004790     MOVE ZERO          TO レセ−施術和暦.
004800     MOVE ZERO          TO レセ−施術年.
004810     MOVE ZERO          TO レセ−施術月.
004820     MOVE ZERO          TO レセ−患者番号.
004830     MOVE LOW-VALUE     TO レセ−枝番.
013730     MOVE ZERO          TO レセ−レセ種別.
004840     START レセプトＦ   KEY IS >= レセ−請求和暦年月
004850                                  レセ−施術和暦年月
004860                                  レセ−患者コード
000250                                  レセ−レセ種別
004690     END-START.
004700     IF 状態キー = "00"
004710         MOVE SPACE  TO 終了フラグ３
004720         PERFORM レセプトＦ読込
004760         PERFORM UNTIL ( 終了フラグ３ = "YES" ) OR
004920                       ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
004930                       ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
004940                       ( レセ−請求月   NOT = 請求月ＷＲ   )
                  IF レセ−レセ種別 = 1 OR 2 OR 3
005430                PERFORM データチェック
012328                IF  実行キーＷ = "YES"
004780*
                          IF レセ−レセ種別 = 3
                              PERFORM 作業ファイル作成２
                              PERFORM 金額計算２
                          ELSE
004850                        PERFORM 作業ファイル作成１
004860                        PERFORM 金額計算
                          END-IF
004881                END-IF
004900            END-IF
004882            PERFORM レセプトＦ読込
004890         END-PERFORM
004910     END-IF.
004920     CLOSE 作業ファイル１.
004930     PERFORM 印刷セット.
005570*================================================================*
005580 レセプトＦ読込 SECTION.
005590*
005600     READ レセプトＦ NEXT
005610     AT END
005620         MOVE "YES" TO 終了フラグ３
005630     END-READ.
005880*================================================================*
005890 エラー表示 SECTION.
005900*
005910     DISPLAY NC"ファイル書込エラー：" ファイル名Ｗ UPON CONS.
005920     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
005930     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
004380*-----------------------------------------*
004390     CALL "actcshm"  WITH C LINKAGE.
004400*-----------------------------------------*
005940     ACCEPT  キー入力 FROM CONS.
005950*================================================================*
005960 エラー表示Ｒ SECTION.
005970*
005980     DISPLAY NC"ファイル読込エラー" ファイル名Ｗ UPON CONS.
005990     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
004380*-----------------------------------------*
004390     CALL "actcshm"  WITH C LINKAGE.
004400*-----------------------------------------*
006000     ACCEPT  キー入力 FROM CONS.
006010     PERFORM ファイル閉鎖.
006020     EXIT PROGRAM.
006030*================================================================*
006040 エラー表示その他 SECTION.
006050*
006060     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
006070     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
004380*-----------------------------------------*
004390     CALL "actcshm"  WITH C LINKAGE.
004400*-----------------------------------------*
006080     ACCEPT  キー入力 FROM CONS.
006090     PERFORM ファイル閉鎖.
006100     EXIT PROGRAM.
006110*================================================================*
006120 データチェック SECTION.
006130*
006140     MOVE SPACE          TO 実行キーＷ.
019520* *****************************************************************
019530* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
019540* *****************************************************************
019640     IF ( レセ−請求対象区分 NOT = ZERO ) AND
005778        ( レセ−償還払い区分 NOT = 1 )
              IF (レセ−レセ種別 = 3) AND ( レセ−会総括表印刷対象区分 = 1 )
                  CONTINUE
              ELSE
004090           MOVE レセ−施術和暦  TO 受−施術和暦
004100           MOVE レセ−施術年    TO 受−施術年
004110           MOVE レセ−施術月    TO 受−施術月
004120           MOVE レセ−患者番号  TO 受−患者番号
004130           MOVE レセ−枝番      TO 受−枝番
                 READ 受診者情報Ｆ
                 NOT INVALID KEY
019880              MOVE "YES"  TO 実行キーＷ
019950           END-READ
              END-IF
019960     END-IF.
006600*
006680*================================================================*
006690 作業ファイル作成１ SECTION.
006700*
006710     MOVE SPACE TO 作１−レコード.
006720     INITIALIZE    作１−レコード.
           PERFORM 保険区分取得.
006970     READ 作業ファイル１
006980     INVALID KEY
006990         MOVE 1              TO 作１−件数 
007040         MOVE レセ−合計     TO 作１−費用額
007050         MOVE レセ−請求金額 TO 作１−請求額
007070         WRITE 作１−レコード
007080         INVALID KEY
007090             DISPLAY NC"状態キー" 状態キー
007100             MOVE NC"作１"  TO ファイル名Ｗ
007110             PERFORM エラー表示
007120         END-WRITE
007130     NOT INVALID KEY
007140         COMPUTE 作１−件数   = 作１−件数   + 1
007190         COMPUTE 作１−費用額 = 作１−費用額 + レセ−合計
007200         COMPUTE 作１−請求額 = 作１−請求額 + レセ−請求金額
007220         REWRITE 作１−レコード
007230         INVALID KEY
007240             DISPLAY NC"状態キー" 状態キー
007250             MOVE NC"作１"  TO ファイル名Ｗ
007260             PERFORM エラー表示
007270         END-REWRITE
007280     END-READ.
006680*================================================================*
006690 作業ファイル作成２ SECTION.
006700*
006710     MOVE SPACE TO 作１−レコード.
006720     INITIALIZE    作１−レコード.
           PERFORM 保険区分取得.
006970     READ 作業ファイル１
006980     INVALID KEY
006990         MOVE 1                  TO 作１−件数 
007040         MOVE レセ−合計         TO 作１−費用額
007050         MOVE レセ−助成請求金額 TO 作１−請求額
007070         WRITE 作１−レコード
007080         INVALID KEY
007090             DISPLAY NC"状態キー" 状態キー
007100             MOVE NC"作１"  TO ファイル名Ｗ
007110             PERFORM エラー表示
007120         END-WRITE
007130     NOT INVALID KEY
007140         COMPUTE 作１−件数   = 作１−件数   + 1
007190         COMPUTE 作１−費用額 = 作１−費用額 + レセ−合計
007200         COMPUTE 作１−請求額 = 作１−請求額 + レセ−助成請求金額
007220         REWRITE 作１−レコード
007230         INVALID KEY
007240             DISPLAY NC"状態キー" 状態キー
007250             MOVE NC"作１"  TO ファイル名Ｗ
007260             PERFORM エラー表示
007270         END-REWRITE
007280     END-READ.
007290*================================================================*
007300 金額計算 SECTION.
007310*
           IF 受−助成種別 = 50 OR 受−保険種別 > 9
007320         COMPUTE  他合計件数Ｗ   = 他合計件数Ｗ   + 1
007370         COMPUTE  他合計費用額Ｗ = 他合計費用額Ｗ + レセ−合計
007380         COMPUTE  他合計請求額Ｗ = 他合計請求額Ｗ + レセ−請求金額
           ELSE
007320         COMPUTE  合計件数Ｗ   = 合計件数Ｗ   + 1
007370         COMPUTE  合計費用額Ｗ = 合計費用額Ｗ + レセ−合計
007380         COMPUTE  合計請求額Ｗ = 合計請求額Ｗ + レセ−請求金額
007320         COMPUTE  合計枚数Ｗ   = 合計枚数Ｗ   + 1
007390     END-IF.
007290*================================================================*
007300 金額計算２ SECTION.
007310*
007320     COMPUTE  合計件数Ｗ   = 合計件数Ｗ   + 1.
007350     COMPUTE  合計請求額Ｗ = 合計請求額Ｗ + レセ−助成請求金額.
           IF (受−助成種別 = 60) AND (受−費用負担者番号助成(1:2) = 51)
007320         COMPUTE  合計枚数Ｗ   = 合計枚数Ｗ   + 1
           END-IF.
007400*================================================================*
007410 印刷セット SECTION.
007420*
007430     MOVE SPACE TO 連印−印刷データＮＪＹ５８０.
007440     INITIALIZE    連印−印刷データＮＪＹ５８０.
007450*
007460     MOVE  合計枚数Ｗ         TO 連印−合計枚数.
007460     MOVE  合計件数Ｗ         TO 連印−合計件数.
007470     MOVE  合計費用額Ｗ       TO 連印−合計費用額.
007480     MOVE  合計請求額Ｗ       TO 連印−合計請求額.
007460     MOVE  他合計件数Ｗ       TO 連印−他合計件数.
007470     MOVE  他合計費用額Ｗ     TO 連印−他合計費用額.
007480     MOVE  他合計請求額Ｗ     TO 連印−他合計請求額.
           COMPUTE 連印−総合計件数   = 合計件数Ｗ   + 他合計件数Ｗ.
           COMPUTE 連印−総合計費用額 = 合計費用額Ｗ + 他合計費用額Ｗ.
           COMPUTE 連印−総合計請求額 = 合計請求額Ｗ + 他合計請求額Ｗ.
007400*================================================================*
007410 保険区分取得 SECTION.
007420*
      *  1:社保　　　　　　6:国保
      *  2:船員            7:退職
      *  3:日雇            8:後期高齢
      *  4:組合            
      *  5:共済
      *  9:被爆           12:母子
      * 10:障害           13:福祉医療 
      * 11:乳幼児         14:その他
      * 21:労災保険       
      *
           IF レセ−レセ種別 = 3
               EVALUATE 受−助成種別
               WHEN 52
006900             MOVE 12    TO 作１−保険区分
               WHEN 53
006900             MOVE 10    TO 作１−保険区分
               WHEN 54
006900             MOVE  9    TO 作１−保険区分
               WHEN 55
006900             MOVE 11    TO 作１−保険区分
               WHEN 60
                   IF 受−費用負担者番号助成(1:2) = 51
006900                 MOVE 13    TO 作１−保険区分
                   ELSE
006900                 MOVE 11    TO 作１−保険区分
                   END-IF
               WHEN OTHER
006900             MOVE 14    TO 作１−保険区分
               END-EVALUATE
           ELSE
006730         IF レセ−レセ種別 = 2
006750             MOVE 8     TO 作１−保険区分
006790         ELSE
006800             EVALUATE 受−保険種別
006810             WHEN 01
006830                 MOVE 6 TO 作１−保険区分
006840             WHEN 02
006830                 MOVE 1 TO 作１−保険区分
006850             WHEN 06
006830                 MOVE 3 TO 作１−保険区分
006860             WHEN 07
006870                 MOVE 2     TO 作１−保険区分
006910             WHEN 03
006920                 MOVE 4     TO 作１−保険区分
006880             WHEN 04
006890             WHEN 09
006900                 MOVE 5     TO 作１−保険区分
006820             WHEN 08
006830                 MOVE 7     TO 作１−保険区分
006950             END-EVALUATE
006960         END-IF
006960     END-IF.
007290*================================================================*
007500******************************************************************
007510 END PROGRAM NJY581.
007520******************************************************************
