000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             NJY6061.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*      レセプト印刷表示【ﾃﾞｰﾀ作成】柔+ｳｨﾝﾄﾞｳｽﾞ版
000100* 
000110*      MED = YAW610
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2019-08-06
000140 DATE-COMPILED.          2019-08-06
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000270     SELECT  市町村マスタ    ASSIGN      TO        SITYOSNL
000280                             ORGANIZATION             IS  INDEXED
000290                             ACCESS MODE              IS  DYNAMIC
000300                             RECORD KEY               IS  市−公費種別
000310                                                          市−市町村番号
000320                             ALTERNATE RECORD KEY     IS  市−公費種別
000330                                                          市−市町村名称
000340                                                          市−市町村番号
000350                             FILE STATUS              IS  状態キー
000360                             LOCK        MODE         IS  AUTOMATIC.
000370     SELECT  生保情報Ｆ      ASSIGN      TO        SEIHOJL
000380                             ORGANIZATION             IS INDEXED
000390                             ACCESS MODE              IS DYNAMIC
000400                             RECORD KEY               IS 生保−施術和暦年月
000410                                                         生保−患者コード
000420                             ALTERNATE RECORD KEY     IS 生保−患者コード
000430                                                         生保−施術和暦年月
000440                             FILE STATUS              IS 状態キー
000450                             LOCK        MODE         IS AUTOMATIC.
000460     SELECT  施術記録Ｆ      ASSIGN      TO        SEKIROKL
000470                             ORGANIZATION             IS  INDEXED
000480                             ACCESS MODE              IS  DYNAMIC
000490                             RECORD KEY           IS 施記−施術和暦年月日
000500                                                     施記−患者コード
000510                             ALTERNATE RECORD KEY IS 施記−患者コード
000520                                                     施記−施術和暦年月日
000530                             FILE STATUS              IS  状態キー
000540                             LOCK        MODE         IS  AUTOMATIC.
000550     SELECT  負傷データＦ    ASSIGN      TO        HUSYOUL
000560                             ORGANIZATION             IS  INDEXED
000570                             ACCESS MODE              IS  DYNAMIC
000580                             RECORD KEY               IS 負−施術和暦年月
000590                                                         負−患者コード
000600                             ALTERNATE RECORD KEY     IS 負−患者コード
000610                                                         負−施術和暦年月
000620                             FILE STATUS              IS  状態キー
000630                             LOCK        MODE         IS  AUTOMATIC.
000640     SELECT  制御情報マスタ  ASSIGN      TO        SEIGYOL
000650                             ORGANIZATION             IS  INDEXED
000660                             ACCESS MODE              IS  DYNAMIC
000670                             RECORD KEY               IS  制−制御区分
000680                             FILE STATUS              IS  状態キー
000690                             LOCK        MODE         IS  AUTOMATIC.
000700     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000710                             ORGANIZATION             IS  INDEXED
000720                             ACCESS MODE              IS  DYNAMIC
000730                             RECORD KEY               IS  受−施術和暦年月
000740                                                          受−患者コード
000750                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000760                                                          受−患者カナ
000770                                                          受−患者コード
000780                             ALTERNATE RECORD KEY     IS  受−患者コード
000790                                                          受−施術和暦年月
000800                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000810                                                          受−保険種別
000820                                                          受−保険者番号
000830                                                          受−患者コード
000840                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000850                                                          受−公費種別
000860                                                          受−費用負担者番号
000870                                                          受−患者コード
000880                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000890                                                          受−助成種別
000900                                                          受−費用負担者番号助成
000910                                                          受−患者コード
000920                             ALTERNATE RECORD KEY     IS  受−請求和暦年月
000930                                                          受−施術和暦年月
000940                                                          受−患者コード
000950                             FILE STATUS              IS  状態キー
000960                             LOCK        MODE         IS  AUTOMATIC.
000970     SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
000980                             ORGANIZATION             IS  INDEXED
000990                             ACCESS MODE              IS  DYNAMIC
001000                             RECORD KEY               IS  レセ−施術和暦年月
001010                                                          レセ−患者コード
001020                                                          レセ−レセ種別
001030                             ALTERNATE RECORD KEY     IS  レセ−患者コード
001040                                                          レセ−施術和暦年月
001050                                                          レセ−レセ種別
001060                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
001070                                                          レセ−施術和暦年月
001080                                                          レセ−患者コード
001090                                                          レセ−レセ種別
001100                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
001110                                                          レセ−レセ種別
001120                                                          レセ−請求保険者番号
001130                                                          レセ−患者コード
001140                                                          レセ−施術和暦年月
001150                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
001160                                                          レセ−請求保険者番号
001170                                                          レセ−患者コード
001180                                                          レセ−レセ種別
001190                                                          レセ−施術和暦年月
001200                             FILE STATUS              IS  状態キー
001210                             LOCK        MODE         IS  AUTOMATIC.
001220     SELECT  作業ファイル１  ASSIGN      TO    "C:\MAKISHISYS\YAWOBJ\TEMP\W60610L.DAT"
001230                             ORGANIZATION             IS  INDEXED
001240                             ACCESS                   IS  DYNAMIC
001250                             RECORD      KEY          IS  作１−医コード
000908                                                          作１−印刷区分１
000908                                                          作１−保険者番号順
000908                                                          作１−印刷区分２
001280                                                          作１−患者カナ
001290                                                          作１−患者コード
001300                                                          作１−施術和暦年月
001310                             FILE        STATUS       IS  状態キー
001320                             LOCK        MODE         IS  AUTOMATIC.
001330* レセ画面並び順用
001340     SELECT  作業ファイル２  ASSIGN      TO   "C:\MAKISHISYS\YAWOBJ\TEMP\W6102L.DAT"
001350                             ORGANIZATION             IS  INDEXED
001360                             ACCESS                   IS  DYNAMIC
001370                             RECORD      KEY          IS  作２−保険区分
001380                                                          作２−順番
001390                             ALTERNATE RECORD KEY     IS  作２−施術和暦年月
001400                                                          作２−患者コード
001410                                                          作２−保険区分
001420                                                          作２−順番
001430                             FILE        STATUS       IS  状態キー
001440                             LOCK        MODE         IS  AUTOMATIC.
001450* レセ並び順用
001460     SELECT  作業ファイル３  ASSIGN      TO  "C:\MAKISHISYS\YAWOBJ\TEMP\W5912L.DAT"
001470                             ORGANIZATION             IS  INDEXED
001480                             ACCESS                   IS  DYNAMIC
001490                             RECORD      KEY          IS  作３−施術和暦年月
001500                                                          作３−患者コード
001510                                                          作３−保険種別
001520                             FILE        STATUS       IS  状態キー
001530                             LOCK        MODE         IS  AUTOMATIC.
001540******************************************************************
001550*                      DATA DIVISION                             *
001560******************************************************************
001570 DATA                    DIVISION.
001580 FILE                    SECTION.
001590*                           ［ＲＬ＝  ２５６］
001600 FD  市町村マスタ          BLOCK   CONTAINS   1   RECORDS.
001610     COPY SITYOSN        OF  XFDLIB  JOINING   市   AS  PREFIX.
001620* 
001630 FD  生保情報Ｆ          BLOCK   CONTAINS   1   RECORDS.
001640     COPY SEIHOJ          OF  XFDLIB  JOINING   生保   AS  PREFIX.
001650*                           ［ＲＬ＝  ２５６］
001660 FD  施術記録Ｆ          BLOCK   CONTAINS   1   RECORDS.
001670     COPY SEKIROK         OF  XFDLIB  JOINING   施記 AS  PREFIX.
001680*                           ［ＲＬ＝  １２８］
001690 FD  負傷データＦ        BLOCK   CONTAINS   1   RECORDS.
001700     COPY HUSYOU          OF  XFDLIB  JOINING   負   AS  PREFIX.
001710*                           ［ＲＬ＝  ２５６］
001720 FD  制御情報マスタ          BLOCK   CONTAINS   1   RECORDS.
001730     COPY SEIGYO          OF  XFDLIB  JOINING   制   AS  PREFIX.
001740*
001750 FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS GLOBAL.
001760     COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
001770*                           ［ＲＬ＝  ３２０］
001780 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001790     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
001800* 保険者番号順ファイル
001810 FD  作業ファイル１ RECORD  CONTAINS 256 CHARACTERS.
001820 01  作１−レコード.
001830     03  作１−レコードキー.
               05  作１−医コード                PIC 9(2).
               05  作１−印刷区分１.
                   07  作１−印刷区分１１        PIC 9(1).
                   07  作１−印刷区分１２        PIC 9(2).
001840         05  作１−保険者番号順            PIC 9(8).
               05  作１−印刷区分２.
                   07  作１−印刷区分２１        PIC 9(1).
                   07  作１−印刷区分２２        PIC 9(1).
                   07  作１−印刷区分２３        PIC 9(1).
001870         05  作１−患者カナ                PIC X(50).
001880         05  作１−患者コード.
001890             07 作１−患者番号             PIC 9(6).
001900             07 作１−枝番                 PIC X(1).
001910         05  作１−施術和暦年月.
001920             07  作１−施術和暦            PIC 9.
001930             07  作１−施術年              PIC 9(2).
001940             07  作１−施術月              PIC 9(2).
001950     03  作１−レコードデータ.
001960         05  作１−レセ種別                PIC 9(2).
001840         05  作１−保険者番号              PIC 9(8).
001970         05  作１−保険者番号親            PIC 9(8).
002190         05  作１−保険種別                PIC 9(2).
001860         05  作１−本人家族区分            PIC 9.
001980         05  作１−患者氏名                PIC X(50).
001990         05  作１−被保険者氏名            PIC X(50).
002000         05  作１−費用額                  PIC 9(6).
002010         05  作１−負担額                  PIC 9(6).
002020         05  作１−請求額                  PIC 9(6).
002030         05  作１−レセ印刷区分            PIC 9.
002040         05  作１−印刷順番                PIC 9(2).
002050         05  FILLER                        PIC X(36).
002060*
002070 FD  作業ファイル２ RECORD  CONTAINS 155 CHARACTERS.
002080 01  作２−レコード.
002090     03  作２−レコードキー.
002100         05  作２−保険区分                PIC 9(1).
002110         05  作２−順番                    PIC 9(4).
002120     03  作２−レコードデータ.
002130         05  作２−レセ種別                PIC 9(2).
002140         05  作２−患者コード.
002150             07 作２−患者番号             PIC 9(6).
002160             07 作２−枝番                 PIC X(1).
002170         05  作２−患者氏名                PIC X(50).
002180         05  作２−被保険者氏名            PIC X(50).
002190         05  作２−保険種別                PIC 9(2).
002200         05  作２−請求保険者番号          PIC X(10).
002210         05  作２−本人家族                PIC X(4).
002220         05  作２−施術和暦年月.
002230             07  作２−施術和暦            PIC 9.
002240             07  作２−施術年              PIC 9(2).
002250             07  作２−施術月              PIC 9(2).
002260         05  作２−印刷指定                PIC 9(1).
002270         05  作２−費用額                  PIC 9(6).
002280         05  作２−負担額                  PIC 9(6).
002290         05  作２−請求額                  PIC 9(6).
002300         05  作２−レセ印刷区分            PIC 9.
002310*
002320 FD  作業ファイル３ RECORD  CONTAINS 32 CHARACTERS.
002330 01  作３−レコード.
002340     03  作３−レコードキー.
002350         05  作３−施術和暦年月.
002360             07  作３−施術和暦            PIC 9.
002370             07  作３−施術年              PIC 9(2).
002380             07  作３−施術月              PIC 9(2).
002390         05  作３−患者コード.
002400             07 作３−患者番号             PIC 9(6).
002410             07 作３−枝番                 PIC X(1).
002420         05  作３−保険種別                PIC 9(2).
002430     03  作３−レコードデータ.
002440         05  作３−順番                    PIC 9(4).
002450         05  FILLER                        PIC X(14).
002460*
002470*----------------------------------------------------------------*
002480******************************************************************
002490*                WORKING-STORAGE SECTION                         *
002500******************************************************************
002510 WORKING-STORAGE         SECTION.
002520 01 キー入力                           PIC X    VALUE SPACE.
002530 01 状態キー                           PIC X(2) VALUE SPACE.
002540 01 終了フラグ                         PIC X(3) VALUE SPACE.
002550 01 終了フラグ２                       PIC X(3) VALUE SPACE.
002560 01 実行キーＷ                         PIC X(3)  VALUE SPACE.
002570 01 施術記録有Ｗ                       PIC X(3) VALUE SPACE.
002580 01 カウンタ                           PIC 9(2)  VALUE ZERO.
002590**
       01 並び順Ｗ.
002600    03 印刷順序Ｗ                      PIC 9(2) VALUE ZERO.
002610    03 印刷区分１１Ｗ                  PIC 9    VALUE ZERO.
002610    03 印刷区分１２Ｗ                  PIC 9(2) VALUE ZERO.
002610    03 印刷区分２１Ｗ                  PIC 9    VALUE ZERO.
002610    03 印刷区分２２Ｗ                  PIC 9    VALUE ZERO.
002610    03 印刷区分２３Ｗ                  PIC 9    VALUE ZERO.
002620 01 保険種別ＷＲ                       PIC 9(2) VALUE ZERO.
002630 01 印刷形式ＷＲ                       PIC 9    VALUE ZERO.
002640 01 保険者番号ＷＲ                     PIC X(10) VALUE SPACE.
002650 01 レセプト種類ＷＲ                   PIC X(4) VALUE SPACE.
002660 01 本人家族区分ＷＲ                   PIC 9    VALUE ZERO.
002670 01 施術和暦年月ＷＲ.
002680    03 施術和暦ＷＲ                    PIC 9    VALUE ZERO.
002690    03 施術年ＷＲ                      PIC 9(2) VALUE ZERO.
002700    03 施術月ＷＲ                      PIC 9(2) VALUE ZERO.
002710 01 請求和暦年月ＷＲ.
002720    03 請求和暦ＷＲ                    PIC 9    VALUE ZERO.
002730    03 請求年ＷＲ                      PIC 9(2) VALUE ZERO.
002740    03 請求月ＷＲ                      PIC 9(2) VALUE ZERO.
002750**
002760 01 ファイル名                         PIC N(8) VALUE SPACE.
002770 01 保険種別Ｗ                         PIC 9(2) VALUE ZERO.
002780 01 順番Ｗ                             PIC 9(4) VALUE ZERO.
002790 01 生保順番Ｗ                         PIC 9(4)  VALUE ZERO.
002800 01 レセ並び順昇降Ｗ                   PIC 9(1) VALUE ZERO.
002810*
002820*
002830** 助成レセまとめ用
002840 01 助成レセまとめフラグ               PIC X(3)  VALUE SPACE.
002850*
002860** 合計集計用
002870 01 合計Ｗ.
002880    03 件数合計Ｗ                      PIC 9(4) VALUE ZERO.
002890    03 費用額合計Ｗ                    PIC 9(8) VALUE ZERO.
002900    03 請求額合計Ｗ                    PIC 9(8) VALUE ZERO.
002910** エラーメッセージ用
002920 01 エラーメッセージＷ.
002930    03 エラー患者コードＷ              PIC X(7) VALUE SPACE.
002940    03 エラー区切りＷ                  PIC X(1) VALUE SPACE.
002950    03 エラー保険種別Ｗ                PIC X(2) VALUE SPACE.
002960    03 FILLER                          PIC X(10) VALUE SPACE.
002970*
002980** 保険者番号右詰め用
002990 01 保険者番号ＷＴ.
003000    03 保険者番号左詰めＷ.
003010      05 保険者番号左詰めＷ１          PIC X OCCURS 8 VALUE SPACE.
003020    03 保険者番号右詰めＷ.
003030      05 保険者番号右詰めＷ１          PIC X OCCURS 8 VALUE ZERO.
003040    03 保険者番号数字Ｗ                PIC 9(8)  VALUE ZERO.
003050    03 保険者番号Ｗ                    PIC X(8)  VALUE SPACE.
003060 01 金額Ｗ.
003070    03 費用額Ｗ                        PIC 9(6)  VALUE ZERO.
003080    03 負担額Ｗ                        PIC 9(6)  VALUE ZERO.
003090    03 請求額Ｗ                        PIC 9(6)  VALUE ZERO.
003250*
003260******************************************************************
003270*                          連結項目                              *
003280******************************************************************
003290*
003300********************
003310* メッセージ表示キー *
003320********************
003330 01 連メ−キー IS EXTERNAL.
003340    03  連メ−メッセージ               PIC N(20).
003350*
003360 01 連メ３−キー IS EXTERNAL.
003370    03  連メ３−メッセージ             PIC N(20).
003380    03  連メ３−メッセージ１           PIC X(20).
003390***
003400 01 連入−入力データ６１０ IS EXTERNAL.
003410    03 連入−請求年月.
003420       05 連入−請求和暦                  PIC 9.
003430       05 連入−請求年                    PIC 9(2).
003440       05 連入−請求月                    PIC 9(2).
003450    03 連入−保険種別                     PIC 9(2).
003460*
003470************************
003480* 助成レセまとめ
003490************************
003500 01 連レセまとめ−キー IS EXTERNAL.
003510    03 連レセまとめ−施術和暦年月.
003520       05 連レセまとめ−施術和暦               PIC 9.
003530       05 連レセまとめ−施術年月.
003540          07 連レセまとめ−施術年              PIC 9(2).
003550          07 連レセまとめ−施術月              PIC 9(2).
003560    03 連レセまとめ−患者コード.
003570       05 連レセまとめ−患者番号               PIC 9(6).
003580       05 連レセまとめ−枝番                   PIC X(1).
003590**-------------------------------------------------------**
003600*   1:助成レセプトなしの本体まとめの判定
003610*   2:横浜・川崎用の社保助成レセかの判定
003620    03 連レセまとめ−判定区分                  PIC 9.
003630**-------------------------------------------------------**
003640*  / OUT /　 0:対象外、1:対象
003650    03 連レセまとめ−判定結果                  PIC 9.
003660**
003670******************************************************************
003680*                      PROCEDURE  DIVISION                       *
003690******************************************************************
003700 PROCEDURE               DIVISION.
003710************
003720*           *
003730* 初期処理   *
003740*           *
003750************
003760     PERFORM 初期化.
003770************
003780*           *
003790* 主処理     *
003800*           *
003810************
003820     PERFORM 作業ファイル作成.
003830     PERFORM 並び順ファイル作成.
003840************
003850*           *
003860* 終了処理   *
003870*           *
003880************
003890     PERFORM 終了処理.
003900     MOVE ZERO TO PROGRAM-STATUS.
003910     EXIT PROGRAM.
003920*
003930*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003940*================================================================*
003950 初期化 SECTION.
003960*
003970     PERFORM ファイルオープン.
003980* 連結項目の待避
003990     MOVE 連入−請求和暦      TO 請求和暦ＷＲ.
004000     MOVE 連入−請求年        TO 請求年ＷＲ.
004010     MOVE 連入−請求月        TO 請求月ＷＲ.
004020     MOVE 連入−保険種別      TO 保険種別ＷＲ.
004030* レセ並び順昇順・降順セット
004040     MOVE ZEROS TO 制−制御区分.
004050     READ 制御情報マスタ
004060     NOT INVALID KEY
004070         MOVE 制−レセ並び順昇降 TO レセ並び順昇降Ｗ
004080     END-READ.
004090*
004100*================================================================*
004110 ファイルオープン SECTION.
004120*
004130     OPEN INPUT 施術記録Ｆ.
004140         MOVE NC"施術記録Ｆ" TO ファイル名.
004150         PERFORM オープンチェック.
004160     OPEN INPUT 負傷データＦ.
004170         MOVE NC"負傷データＦ" TO ファイル名.
004180         PERFORM オープンチェック.
004190     OPEN INPUT 制御情報マスタ
004200         MOVE NC"制御情報" TO ファイル名.
004210         PERFORM オープンチェック.
004220     OPEN INPUT 受診者情報Ｆ.
004230         MOVE NC"受診者情報Ｆ" TO ファイル名.
004240         PERFORM オープンチェック.
004250     OPEN INPUT 市町村マスタ.
004260         MOVE NC"市町村" TO ファイル名.
004270         PERFORM オープンチェック.
004280     OPEN INPUT 生保情報Ｆ.
004290         MOVE NC"生保" TO ファイル名.
004300         PERFORM オープンチェック.
004310     OPEN INPUT レセプトＦ.
004320         MOVE NC"レセプトＦ" TO ファイル名.
004330         PERFORM オープンチェック.
004340*================================================================*
004350 オープンチェック SECTION.
004360*
004370     IF 状態キー  NOT =  "00"
004380         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
004390         DISPLAY NC"状態キー：" 状態キー         UPON CONS
004400         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004410                                                 UPON CONS
004420*-----------------------------------------*
004430         CALL "actcshm"  WITH C LINKAGE
004440*-----------------------------------------*
004450         ACCEPT  キー入力 FROM CONS
004460         PERFORM ファイル閉鎖
004470         MOVE 99 TO PROGRAM-STATUS
004480         EXIT PROGRAM.
004490*================================================================*
004500 ファイル閉鎖 SECTION.
004510*
004520     CLOSE 施術記録Ｆ 負傷データＦ 制御情報マスタ
004530           生保情報Ｆ 受診者情報Ｆ 市町村マスタ   レセプトＦ.
004540*================================================================*
004550 終了処理 SECTION.
004560*
004570     PERFORM ファイル閉鎖.
004580*================================================================*
004590 エラー表示 SECTION.
004600*
004610     DISPLAY NC"状態キー" 状態キー  UPON CONS.
004620     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
004630     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
004640     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
004650*-----------------------------------------*
004660     CALL "actcshm"  WITH C LINKAGE.
004670*-----------------------------------------*
004680     ACCEPT  キー入力 FROM CONS.
004690     PERFORM ファイル閉鎖.
004700     MOVE 99 TO PROGRAM-STATUS.
004710     EXIT PROGRAM.
004720*================================================================*
004730 エラー表示Ｒ SECTION.
004740*
004750     DISPLAY NC"ファイル読込エラー" ファイル名     UPON CONS.
004760     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
004770     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
004780*-----------------------------------------*
004790     CALL "actcshm"  WITH C LINKAGE.
004800*-----------------------------------------*
004810     ACCEPT  キー入力 FROM CONS.
004820     PERFORM ファイル閉鎖.
004830     MOVE 99 TO PROGRAM-STATUS.
004840     EXIT PROGRAM.
004850*================================================================*
004860 作業ファイル作成 SECTION.
004870*
004880     PERFORM 保険者番号順ファイル作成.
004890*
004900* レセ並び順ファイル作成
004910     OPEN OUTPUT 作業ファイル２.
004920         MOVE NC"作２" TO ファイル名.
004930         PERFORM オープンチェック.
004940*
004950     IF レセ並び順昇降Ｗ = 1
004960         PERFORM レセ並び順降順ファイル作成
004970     ELSE
004980         PERFORM レセ並び順ファイル作成
004990     END-IF.
005000*
005010     CLOSE 作業ファイル２.
005020*
005030*================================================================*
005040 保険者番号順ファイル作成 SECTION.
005050**********************************************************************
005060**   受診者情報Ｆから、該当請求年月のデータを抽出し、
005070**   作業ファイル１(保険者番号順)に書き出す.
005080**********************************************************************
005090*
005100     OPEN OUTPUT 作業ファイル１.
005110         MOVE NC"作１" TO ファイル名.
005120         PERFORM オープンチェック.
005130*
005140     MOVE "YES"         TO 実行キーＷ.
005150     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
005160     MOVE 請求年ＷＲ    TO レセ−請求年.  
005170     MOVE 請求月ＷＲ    TO レセ−請求月.  
005180     MOVE ZERO          TO レセ−レセ種別.
005190     MOVE ZERO          TO レセ−施術和暦.
005200     MOVE ZERO          TO レセ−施術年.  
005210     MOVE ZERO          TO レセ−施術月.  
005220     MOVE ZERO          TO レセ−患者番号.
005230     MOVE SPACE         TO レセ−枝番.    
005240     START レセプトＦ   KEY IS >= レセ−請求和暦年月
005250                                  レセ−施術和暦年月
005260                                  レセ−患者コード
005270                                  レセ−レセ種別
005280     END-START.
005290*     PERFORM データ確認.
005300     IF 状態キー = "00"
005310         MOVE SPACE  TO 終了フラグ
005320         PERFORM レセプトＦ読込
005330         PERFORM UNTIL ( 終了フラグ = "YES" ) OR
005340                       ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
005350                       ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
005360                       ( レセ−請求月   NOT = 請求月ＷＲ   )
005370*
005380            PERFORM データチェック
005390** 労災・自賠責・自由は対象外
005400            IF  レセ−レセ種別 = 4 OR 5 OR 6 OR 8
005410               MOVE SPACE  TO 実行キーＷ
005420            END-IF
005430            IF 実行キーＷ = "YES"
005440*
005450               EVALUATE TRUE
005460*            ********
005470*            * 健保 *
005480*            ********
005490                WHEN レセ−レセ種別   = 1
005500                    IF (保険種別ＷＲ = ZERO) OR
005510                       ((受−保険種別 = 保険種別ＷＲ) AND ( 受−公費種別 = ZERO ))
005520*                **********************
005530*                * 作業ファイル作成 *
005540*                **********************
005550                       MOVE SPACE TO 作１−レコード
005560                       INITIALIZE 作１−レコード
005570                       MOVE 受−保険種別       TO 作１−保険種別
005580                       MOVE 受−保険者番号     TO 保険者番号Ｗ
005596                       PERFORM 保険者番号右詰め
005600                       MOVE 保険者番号数字Ｗ   TO 作１−保険者番号 作１−保険者番号順
005610                       PERFORM 料金セット
005620                       PERFORM 作１レコードセット
005630                       PERFORM 作１ファイル書込
005640*
005650                    END-IF
005660*            ********
005670*            * 老人 *
005680*            ********
005690                WHEN レセ−レセ種別   = 2 
005700                    IF (保険種別ＷＲ = ZERO) OR
005710                       (受−公費種別 = 保険種別ＷＲ)
005720*                    **********************
005730*                    * 作業ファイル作成 *
005740*                    **********************
005750                       MOVE SPACE TO 作１−レコード
005760                       INITIALIZE 作１−レコード
005770                       MOVE 受−公費種別       TO 作１−保険種別
005780                       MOVE 受−費用負担者番号  TO 保険者番号Ｗ
005790                       PERFORM 保険者番号右詰め
005800                       MOVE 保険者番号数字Ｗ   TO 作１−保険者番号 作１−保険者番号順
005810                       PERFORM 料金セット
005820                       PERFORM 作１レコードセット
005830                       PERFORM 作１ファイル書込
005840                    END-IF
005850*            ********
005860*            * 助成 *
005870*            ********
005880*↓* 特別処理（助成レセまとめ）**
005890                WHEN レセ−レセ種別   = 3
005900                    IF (保険種別ＷＲ = ZERO) OR
005910                       (受−助成種別 = 保険種別ＷＲ)
005920                       IF レセ−本体まとめ区分 NOT = 1
005930                          MOVE SPACE TO 作１−レコード
005940                          INITIALIZE 作１−レコード
005950                          MOVE 受−助成種別       TO 作１−保険種別
005960                          MOVE 受−保険者番号     TO 保険者番号Ｗ
005970                          PERFORM 保険者番号右詰め
005980                          MOVE 保険者番号数字Ｗ   TO 作１−保険者番号親
023280                          MOVE 受−助成種別       TO 市−公費種別
023290                          MOVE 受−費用負担者番号助成 TO 市−市町村番号  作１−保険者番号
009792                          IF (受−助成種別 = 54) OR
                                   ((受−助成種別 = 60) AND (受−費用負担者番号助成(1:2) = 51))
023290                             MOVE 受−費用負担者番号助成   TO 作１−保険者番号順
                                ELSE
023300                             READ 市町村マスタ
023310                             INVALID KEY
023320                                 MOVE ZERO                 TO  作１−保険者番号順
023330                             NOT INVALID KEY
005990                                 MOVE 市−保険者番号(1:8)  TO  作１−保険者番号順
023380                             END-READ
                                END-IF
006000                          PERFORM 助成料金セット
006010                          PERFORM 作１レコードセット
006020                          PERFORM 作１ファイル書込
006030                       END-IF
006040                    END-IF
006050*                ********
006060*                * 生保 *
006070*                ********
006080                WHEN レセ−レセ種別   = 7
006090                    IF (保険種別ＷＲ = ZERO) OR
006100                       (受−保険種別 = 保険種別ＷＲ)
006110                        MOVE SPACE TO 作１−レコード
006120                        INITIALIZE 作１−レコード
006130                        MOVE 受−保険種別     TO 作１−保険種別
006140                        MOVE 受−施術和暦年月 TO 生保−施術和暦年月
006150                        MOVE 受−患者コード   TO 生保−患者コード
006160                        READ 生保情報Ｆ
006170                        NOT INVALID KEY
006180                           MOVE 生保−負担者番号   TO 保険者番号Ｗ
006190                        END-READ
006200                        PERFORM 保険者番号右詰め
006210                        MOVE 保険者番号数字Ｗ   TO 作１−保険者番号  作１−保険者番号順
006220                        PERFORM 料金セット
006230                        PERFORM 作１レコードセット
006240                        PERFORM 作１ファイル書込
006250                    END-IF
006260                END-EVALUATE
006270             END-IF
006280*
006290             PERFORM レセプトＦ読込
006300         END-PERFORM
006310     END-IF.
006320*
006330     CLOSE 作業ファイル１.
006340*
006350*================================================================*
006360*================================================================*
006370 データ確認 SECTION.
006380*
006390     IF 状態キー  NOT =  "00"
006400         MOVE  NC"　　　該当年月のデータがありません。" TO 連メ−メッセージ
006410         CALL   "MSG001"
006420         CANCEL "MSG001"
006430         PERFORM ファイル閉鎖
006440         MOVE 99 TO PROGRAM-STATUS
006450         EXIT PROGRAM
006460     END-IF.
006470*
006480*================================================================*
006490 データチェック SECTION.
006500*
006510     MOVE SPACE  TO 実行キーＷ.
006520     IF ( レセ−レセ印刷対象区分 NOT = 1 )
006530        MOVE "YES"  TO 実行キーＷ
006540     END-IF.
006550* *****************************************************************
006560* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
006570* *****************************************************************
006580     IF 実行キーＷ  = "YES"
006590*      (再度、実行キーＷ SPACE)
006600        MOVE SPACE  TO 実行キーＷ
006610        IF レセ−請求対象区分 NOT = ZERO
006620           MOVE レセ−施術和暦  TO 受−施術和暦
006630           MOVE レセ−施術年    TO 受−施術年
006640           MOVE レセ−施術月    TO 受−施術月
006650           MOVE レセ−患者番号  TO 受−患者番号
006660           MOVE レセ−枝番      TO 受−枝番
006670           READ 受診者情報Ｆ
006680           NOT INVALID KEY
006690              MOVE "YES"  TO 実行キーＷ
006700           END-READ
006710        ELSE
006720           MOVE SPACE  TO 実行キーＷ
006730        END-IF
006740     END-IF.
006750*
006760*================================================================*
006770 作１レコードセット SECTION.
006780*
006790     MOVE レセ−施術和暦    TO 作１−施術和暦.
006800     MOVE レセ−施術年      TO 作１−施術年.
006810     MOVE レセ−施術月      TO 作１−施術月.
006820     PERFORM 印刷順序セット.
006830     MOVE 印刷順序Ｗ        TO 作１−医コード.
006830     MOVE 印刷区分１１Ｗ    TO 作１−印刷区分１１.
006830     MOVE 印刷区分１２Ｗ    TO 作１−印刷区分１２.
006830     MOVE 印刷区分２１Ｗ    TO 作１−印刷区分２１.
006830     MOVE 印刷区分２２Ｗ    TO 作１−印刷区分２２.
006830     MOVE 印刷区分２３Ｗ    TO 作１−印刷区分２３.
006840     MOVE 受−本人家族区分  TO 作１−本人家族区分.
006850     MOVE 受−患者カナ      TO 作１−患者カナ.
006860     MOVE 受−患者コード    TO 作１−患者コード.
006870     MOVE 受−患者氏名      TO 作１−患者氏名.
006880     MOVE 受−被保険者氏名  TO 作１−被保険者氏名.
006890     MOVE レセ−レセ種別    TO 作１−レセ種別.
006900     MOVE 費用額Ｗ          TO 作１−費用額.
006910     MOVE 請求額Ｗ          TO 作１−請求額.
006920     MOVE 負担額Ｗ          TO 作１−負担額.
006930*
006940*================================================================*
006950 料金セット SECTION.
006960     MOVE レセ−合計        TO 費用額Ｗ.
006970     MOVE レセ−請求金額    TO 請求額Ｗ.
006980     MOVE レセ−一部負担金  TO 負担額Ｗ.
006990     MOVE 受−レセ印刷区分  TO 作１−レセ印刷区分.
007000*
007010*================================================================*
007020 助成料金セット SECTION.
007030     MOVE レセ−合計           TO 費用額Ｗ.
007040     MOVE レセ−助成請求金額   TO 請求額Ｗ.
007050     MOVE レセ−受給者負担額   TO 負担額Ｗ.
007060     MOVE 受−レセ印刷区分助成 TO 作１−レセ印刷区分.
007070*
007080*================================================================*
007090 作１ファイル書込 SECTION.
007100*
007110     WRITE 作１−レコード
007120     INVALID KEY
007130         MOVE NC"作１"  TO ファイル名
007140         PERFORM エラー表示
007150     END-WRITE.
007160*
007170*================================================================*
007180 保険者番号右詰め SECTION.
007190*
007200     MOVE 保険者番号Ｗ    TO  保険者番号左詰めＷ.
007210     MOVE ZERO            TO  保険者番号右詰めＷ.
007220     MOVE ZERO            TO  保険者番号数字Ｗ.
007230*
007240     MOVE  9  TO  カウンタ.
007250*
007260     IF  保険者番号左詰めＷ１(8) NOT = SPACE
007270         COMPUTE カウンタ = カウンタ  -  1
007280         MOVE 保険者番号左詰めＷ１(8)  TO  保険者番号右詰めＷ１(カウンタ)
007290     END-IF.
007300     IF  保険者番号左詰めＷ１(7) NOT = SPACE
007310         COMPUTE カウンタ = カウンタ  -  1
007320         MOVE 保険者番号左詰めＷ１(7)  TO  保険者番号右詰めＷ１(カウンタ)
007330     END-IF.
007340     IF  保険者番号左詰めＷ１(6) NOT = SPACE
007350         COMPUTE カウンタ = カウンタ  -  1
007360         MOVE 保険者番号左詰めＷ１(6)  TO  保険者番号右詰めＷ１(カウンタ)
007370     END-IF.
007380     IF  保険者番号左詰めＷ１(5) NOT = SPACE
007390         COMPUTE カウンタ = カウンタ  -  1
007400         MOVE 保険者番号左詰めＷ１(5)  TO  保険者番号右詰めＷ１(カウンタ)
007410     END-IF.
007420     IF  保険者番号左詰めＷ１(4) NOT = SPACE
007430         COMPUTE カウンタ = カウンタ  -  1
007440         MOVE 保険者番号左詰めＷ１(4)  TO  保険者番号右詰めＷ１(カウンタ)
007450     END-IF.
007460     IF  保険者番号左詰めＷ１(3) NOT = SPACE
007470         COMPUTE カウンタ = カウンタ  -  1
007480         MOVE 保険者番号左詰めＷ１(3)  TO  保険者番号右詰めＷ１(カウンタ)
007490     END-IF.
007500     IF  保険者番号左詰めＷ１(2) NOT = SPACE
007510         COMPUTE カウンタ = カウンタ  -  1
007520         MOVE 保険者番号左詰めＷ１(2)  TO  保険者番号右詰めＷ１(カウンタ)
007530     END-IF.
007540     IF  保険者番号左詰めＷ１(1) NOT = SPACE
007550         COMPUTE カウンタ = カウンタ  -  1
007560         MOVE 保険者番号左詰めＷ１(1)  TO  保険者番号右詰めＷ１(カウンタ)
007570     END-IF.
007580*
007590     MOVE 保険者番号右詰めＷ TO 保険者番号数字Ｗ.
007600*
007610*================================================================*
007620 作業ファイル１読込 SECTION.
007630*
007640     READ 作業ファイル１ NEXT
007650     AT END
007660         MOVE "YES" TO 終了フラグ
007670     NOT AT END
007680         MOVE 作１−施術和暦    TO 受−施術和暦
007690         MOVE 作１−施術年      TO 受−施術年
007700         MOVE 作１−施術月      TO 受−施術月
007710         MOVE 作１−患者番号    TO 受−患者番号
007720         MOVE 作１−枝番        TO 受−枝番
007730         READ 受診者情報Ｆ
007740         INVALID KEY
007750              MOVE NC"受診者"   TO ファイル名
007760              PERFORM エラー表示Ｒ
007770         END-READ
007780     END-READ.
007790*
007800*================================================================*
007810 レセプトＦ読込 SECTION.
007820*
007830     READ レセプトＦ NEXT
007840     AT END
007850         MOVE "YES" TO 終了フラグ
007860     END-READ.
007870*================================================================*
007880 印刷順序セット SECTION.
007890*
007900**///  印刷順序の変更はこのSECTIONで   ////**
007920*
007930***************************************************
007940* 保険種別順序
007990*     1:社保     本家区分 特別区分                      *
007960*     2:日雇     本家区分 特別区分                      *
008040*     3:船員     本家区分 特別区分                      *
008010*     4:組合     通常・退職 県 / 本家区分 特別区分      *
008030*     5:自衛官   本家区分 特別区分                      *
008030*     6:共済     本家区分 特別区分                      *
007950*     7:国保     国・退 特別区分 本家区分               *
007960*     8:後期高齢 本家区分 割合                          *
008060*                                                       *
008090*     9:原爆・水俣 本家区分                             *
008070*    10:助成       障害・乳幼児・母子・その他 本家区分  *
008100*
008110***************************************************
008120*
            MOVE ZERO                    TO 並び順Ｗ.
008130      EVALUATE レセ−レセ種別
008140      WHEN 1
008150         EVALUATE  受−保険種別
008160* 国保
008170         WHEN  01
009793            MOVE 7                 TO 印刷順序Ｗ
009793            MOVE 1                 TO 印刷区分２１Ｗ
                  MOVE 受−本人家族区分  TO 印刷区分２３Ｗ
008230* 社保
008260         WHEN  02
009793            MOVE 1                 TO 印刷順序Ｗ
                  MOVE 受−本人家族区分  TO 印刷区分２１Ｗ
008340* 組合
008350         WHEN  03
009793            MOVE 4                 TO 印刷順序Ｗ
                  MOVE 受−本人家族区分  TO 印刷区分２１Ｗ
                  IF 受−保険者番号(1:2) = "06"
009793                MOVE 1             TO 印刷区分１１Ｗ
                  ELSE
009793                MOVE 2             TO 印刷区分１１Ｗ
                  END-IF
                  IF 受−保険者番号(1:4) = "0663"
                      MOVE "13"                TO 印刷区分１２Ｗ
                  ELSE
                      MOVE 受−保険者番号(3:2) TO 印刷区分１２Ｗ
                  END-IF
008410* 共済・自衛官
008420         WHEN  04
009793            MOVE 6                 TO 印刷順序Ｗ
                  MOVE 受−本人家族区分  TO 印刷区分２１Ｗ
008430         WHEN  09
009793            MOVE 5                 TO 印刷順序Ｗ
                  MOVE 受−本人家族区分  TO 印刷区分２１Ｗ
008240* 日雇
008270         WHEN  06
009793            MOVE 2                 TO 印刷順序Ｗ
                  MOVE 受−本人家族区分  TO 印刷区分２１Ｗ
008250* 船員
008280         WHEN  07
009793            MOVE 3                 TO 印刷順序Ｗ
                  MOVE 受−本人家族区分  TO 印刷区分２１Ｗ
008490* 退職国保
008500         WHEN  08
009793            MOVE 7                 TO 印刷順序Ｗ
009793            MOVE 2                 TO 印刷区分２１Ｗ
                  MOVE 受−本人家族区分  TO 印刷区分２３Ｗ
008560         END-EVALUATE
008570*
008580      WHEN 2
009793         MOVE 8                    TO 印刷順序Ｗ
               MOVE 受−本人家族区分     TO 印刷区分２１Ｗ
008600      WHEN 3
009792         IF (受−助成種別 = 54) OR
                  ((受−助成種別 = 60) AND (受−費用負担者番号助成(1:2) = 51))
009793            MOVE 9                 TO 印刷順序Ｗ
                  MOVE 受−本人家族区分  TO 印刷区分２１Ｗ
               ELSE
009793            MOVE 10                TO 印刷順序Ｗ
                  MOVE 受−本人家族区分  TO 印刷区分２３Ｗ
009791            EVALUATE 受−助成種別
009792            WHEN 52
008610               MOVE 3              TO  印刷区分２１Ｗ
009792            WHEN 53
008610               MOVE 1              TO  印刷区分２１Ｗ
009792            WHEN 55
009792            WHEN 60
008610               MOVE 2              TO  印刷区分２１Ｗ
009792            WHEN OTHER
008610               MOVE 4              TO  印刷区分２１Ｗ
                  END-EVALUATE
               END-IF
008620      WHEN 7
008630         MOVE  99  TO  印刷順序Ｗ
008640     END-EVALUATE.
      *
009791     EVALUATE 受−特別区分
009792     WHEN 1
009793         MOVE 2    TO 印刷区分２２Ｗ
009794     WHEN 2
009793         MOVE 3    TO 印刷区分２２Ｗ
009795     WHEN 3
009796         MOVE 4    TO 印刷区分２２Ｗ
009797     WHEN 6
009798         MOVE 5    TO 印刷区分２２Ｗ
009799     WHEN OTHER
009801         MOVE 1    TO 印刷区分２２Ｗ
009803     END-EVALUATE.
008650*
008660*================================================================*
008670 レセ並び順降順ファイル作成 SECTION.
008680******************************************************************************
008690*  全印刷の場合、
008700*  レセプトを総括表の印字順番と逆に並べるため、順番を出力する。
008710******************************************************************************
008720*
008730     OPEN INPUT 作業ファイル１.
008740         MOVE NC"作１" TO ファイル名.
008750         PERFORM オープンチェック.
008760*
008770     MOVE SPACE TO 終了フラグ.
008780     MOVE ZERO  TO 順番Ｗ.      
008790     MOVE 99         TO 作１−医コード.
008790     MOVE 999        TO 作１−印刷区分１.
008800     MOVE 99999999   TO  作１−保険者番号順.
008790     MOVE 999        TO 作１−印刷区分２
008810     MOVE HIGH-VALUE TO 作１−患者カナ.
008820     MOVE 999999     TO 作１−患者番号.
008830     MOVE HIGH-VALUE TO 作１−枝番.
008850     MOVE 9          TO 作１−施術和暦.
008860     MOVE 99         TO 作１−施術年.
008870     MOVE 99         TO 作１−施術月.
008880     START 作業ファイル１ KEY IS <=  作１−医コード    
                                           作１−印刷区分１  
                                            作１−保険者番号順
008890                                     作１−印刷区分２  
008930                                     作１−患者カナ    
008910                                     作１−患者コード  
008920                                     作１−施術和暦年月
008940                                     REVERSED
008950     END-START.
008960     IF 状態キー = "00"
008970*
008980         MOVE SPACE TO 終了フラグ
008990         PERFORM 作業ファイル１読込
009000         PERFORM UNTIL 終了フラグ = "YES"
009010             PERFORM 作２レコードセット
009020             PERFORM 作２ファイル書込
009030             PERFORM 作業ファイル１読込
009040         END-PERFORM
009050     END-IF.
009060*
009070     CLOSE 作業ファイル１.
009080*
009090*================================================================*
009100 レセ並び順ファイル作成 SECTION.
009110******************************************************************************
009120*  全印刷の場合、
009130*  レセプトを総括表の印字順番と同じ様に並べるため、順番を出力する。
009140******************************************************************************
009150*
009160     OPEN INPUT 作業ファイル１.
009170         MOVE NC"作１" TO ファイル名.
009180         PERFORM オープンチェック.
009190*
009200     MOVE SPACE TO 終了フラグ.
009210     MOVE ZERO  TO 順番Ｗ.      
009220     PERFORM 作業ファイル１読込.
009230     PERFORM UNTIL 終了フラグ = "YES"
009240             PERFORM 作２レコードセット
009250             PERFORM 作２ファイル書込
009260             PERFORM 作業ファイル１読込
009270     END-PERFORM.
009280*
009290     CLOSE 作業ファイル１.
009300*
009310*================================================================*
009320 作２レコードセット SECTION.
009330*
009340     MOVE 作１−レセ種別       TO 作２−レセ種別.
009350     MOVE 作１−施術和暦       TO 作２−施術和暦.
009360     MOVE 作１−施術年         TO 作２−施術年.
009370     MOVE 作１−施術月         TO 作２−施術月.
009380     MOVE 作１−患者コード     TO 作２−患者コード.
009390     MOVE 作１−保険種別       TO 作２−保険種別
009400     MOVE 作１−患者氏名       TO 作２−患者氏名.
009410     MOVE 作１−被保険者氏名   TO 作２−被保険者氏名.
009420     IF 作１−本人家族区分 = 1
009430         MOVE "本人"           TO 作２−本人家族
009440     ELSE
009450         MOVE "家族"           TO 作２−本人家族
009460     END-IF.
009480     MOVE 作１−保険者番号     TO 保険者番号ＷＲ.
009520     IF 保険者番号ＷＲ(1:4) = "0000"
009530        MOVE 保険者番号ＷＲ(5:6)  TO 作２−請求保険者番号
009540     ELSE
009550        IF 保険者番号ＷＲ(1:2) = "00"
009560           MOVE 保険者番号ＷＲ(3:8)  TO 作２−請求保険者番号
009570        ELSE
009580           MOVE 保険者番号ＷＲ    TO 作２−請求保険者番号
009590        END-IF
009600     END-IF.
009610     MOVE 作１−費用額         TO 作２−費用額.
009620     MOVE 作１−請求額         TO 作２−請求額.
009630     MOVE 作１−負担額         TO 作２−負担額.
009640     MOVE 作１−レセ印刷区分   TO 作２−レセ印刷区分.
009650     IF 作１−レセ種別 = 7
009660        MOVE 1                 TO 作２−保険区分
009670        COMPUTE 生保順番Ｗ  = 生保順番Ｗ + 1
009680        MOVE 生保順番Ｗ            TO 作２−順番
009690     ELSE
009700        MOVE ZERO              TO 作２−保険区分
009710        COMPUTE 順番Ｗ  = 順番Ｗ + 1
009720        MOVE 順番Ｗ            TO 作２−順番
009730     END-IF.
009740*
009750*================================================================*
009760 作２ファイル書込 SECTION.
009770*
009780     WRITE 作２−レコード
009790     INVALID KEY
009800         MOVE NC"作２"  TO ファイル名
009810         PERFORM エラー表示
009820     END-WRITE.
009830*================================================================*
009840 並び順ファイル作成 SECTION.
009850*
009860     CLOSE 作業ファイル１.
009870     OPEN INPUT 作業ファイル１.
009880         MOVE NC"作１" TO ファイル名.
009890         PERFORM オープンチェック.
009900     OPEN OUTPUT 作業ファイル３.
009910     CLOSE 作業ファイル３.
009920     OPEN I-O 作業ファイル３.
009930         MOVE NC"作３" TO ファイル名.
009940         PERFORM オープンチェック.
009950*
009960     MOVE SPACE TO 終了フラグ.
009970     MOVE ZERO  TO 順番Ｗ.      
009980     MOVE ZERO  TO 生保順番Ｗ.      
009990     PERFORM 作業ファイル１読込.
010010     PERFORM UNTIL 終了フラグ = "YES"
010020         PERFORM 作３レコードセット
010030         PERFORM 作業ファイル１読込
010040     END-PERFORM.
010050
010060*================================================================*
010070 作３レコードセット SECTION.
010080*
010100     MOVE 作１−施術和暦       TO 作３−施術和暦.
010110     MOVE 作１−施術年         TO 作３−施術年.
010120     MOVE 作１−施術月         TO 作３−施術月.
010130     MOVE 作１−患者コード     TO 作３−患者コード.
010140     MOVE 作１−保険種別       TO 作３−保険種別.
010150     IF 作１−レセ種別 = 7
010160        COMPUTE 生保順番Ｗ  = 生保順番Ｗ + 1
010170        MOVE 生保順番Ｗ        TO 作３−順番
010180     ELSE
010190        COMPUTE 順番Ｗ  = 順番Ｗ + 1
010200        MOVE 順番Ｗ            TO 作３−順番
010210     END-IF.
010220*
010230     WRITE 作３−レコード
010240     INVALID KEY
010250         MOVE NC"作３"  TO ファイル名
010260         PERFORM エラー表示
010270     END-WRITE.
010280*
010290*================================================================*
010300*================================================================*
010310******************************************************************
010320 END PROGRAM NJY6061.
010330******************************************************************
