000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YAI585.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*      ７号用紙   請求書【印刷】柔+ｳｨﾝﾄﾞｳｽﾞ版
000100*  請求年月バージョン
000101*         MED = YAI585P
000110*----------------------------------------------------------------*
000120 DATE-WRITTEN.           2013-06-27
000130 DATE-COMPILED.          2013-06-27
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000260     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000270                             ORGANIZATION             IS  INDEXED
000280                             ACCESS MODE              IS  DYNAMIC
000290                             RECORD KEY               IS  元−元号区分
000300                             FILE STATUS              IS  状態キー
000310                             LOCK        MODE         IS  AUTOMATIC.
000380     SELECT  保険者マスタ    ASSIGN      TO        HOKENSL
000390                             ORGANIZATION             IS  INDEXED
000400                             ACCESS MODE              IS  DYNAMIC
000410                             RECORD KEY               IS  保−保険種別
000420                                                          保−保険者番号
000430                             ALTERNATE RECORD KEY     IS  保−保険種別
000440                                                          保−保険者名称
000450                                                          保−保険者番号
000460                             FILE STATUS              IS  状態キー
000470                             LOCK        MODE         IS  AUTOMATIC.
000480     SELECT  市町村マスタ    ASSIGN      TO        SITYOSNL
000490                             ORGANIZATION             IS  INDEXED
000500                             ACCESS MODE              IS  DYNAMIC
000510                             RECORD KEY               IS  市−公費種別
000520                                                          市−市町村番号
000530                             ALTERNATE RECORD KEY     IS  市−公費種別
000540                                                          市−市町村名称
000550                                                          市−市町村番号
000560                             FILE STATUS              IS  状態キー
000570                             LOCK        MODE         IS  AUTOMATIC.
000650     SELECT  施術所情報マスタ ASSIGN      TO        SEJOHOL
000660                             ORGANIZATION             IS  INDEXED
000670                             ACCESS MODE              IS  DYNAMIC
000680                             RECORD KEY               IS 施情−施術所番号
000690                             FILE STATUS              IS  状態キー
000700                             LOCK        MODE         IS  AUTOMATIC.
000710     SELECT  請求先マスタ    ASSIGN      TO        SEIKYUSL
000720                             ORGANIZATION             IS  INDEXED
000730                             ACCESS MODE              IS  DYNAMIC
000740                             RECORD KEY               IS  請先−保険種別
000750                                                          請先−保険者番号
000760                             FILE STATUS              IS  状態キー
000770                             LOCK    MODE             IS  AUTOMATIC.
000783*
000108     SELECT  作業ファイル２  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W5831L.DAT"
000109                             ORGANIZATION             IS  INDEXED
000110                             ACCESS                   IS  DYNAMIC
000111                             RECORD      KEY          IS  作２−保険種別
000113                                                          作２−保険者番号
000114                             ALTERNATE RECORD KEY     IS  作２−印刷順序１
000115                                                          作２−県
000116                                                          作２−印刷順序
000117                                                          作２−保険種別
000118                                                          作２−保険者番号
000119                             FILE        STATUS       IS  状態キー
000120                             LOCK        MODE         IS  AUTOMATIC.
000806*
000980     SELECT  印刷ファイル    ASSIGN      TO     GS-PRTF001
000990                             SYMBOLIC    DESTINATION  IS "PRT"
001000                             FORMAT                   IS  定義体名Ｐ
001010                             GROUP                    IS  項目群名Ｐ
001020                             PROCESSING  MODE         IS  処理種別Ｐ
001030                             UNIT        CONTROL      IS  拡張制御Ｐ
001040                             FILE        STATUS       IS  通知情報Ｐ.
001050******************************************************************
001060*                      DATA DIVISION                             *
001070******************************************************************
001080 DATA                    DIVISION.
001090 FILE                    SECTION.
001100*                           ［ＲＬ＝  １２８］
001110 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
001120     COPY GENGOU          OF  XFDLIB  JOINING   元   AS  PREFIX.
001160*                           ［ＲＬ＝  ３２０］
001170 FD  保険者マスタ        BLOCK   CONTAINS   1   RECORDS.
001180     COPY HOKENS          OF  XFDLIB  JOINING   保   AS  PREFIX.
001190*                           ［ＲＬ＝  ２５６］
001200 FD  市町村マスタ        BLOCK   CONTAINS   1   RECORDS.
001210     COPY SITYOSN         OF  XFDLIB  JOINING   市   AS  PREFIX.
001250*                           ［ＲＬ＝  １２８］
001260 FD  施術所情報マスタ          BLOCK   CONTAINS   1   RECORDS.
001270     COPY SEJOHO         OF  XFDLIB  JOINING   施情   AS  PREFIX.
001280*                           ［ＲＬ＝  １２８］
001290 FD  請求先マスタ          BLOCK   CONTAINS   1   RECORDS.
001300     COPY SEIKYUS         OF  XFDLIB  JOINING   請先   AS  PREFIX.
001320*                           ［ＲＬ＝  １２８］
001330 FD  作業ファイル２ RECORD  CONTAINS 128 CHARACTERS.
001340 01  作２−レコード.
001350     03  作２−レコードキー.
001400         05  作２−保険種別                  PIC 9(2).
001410         05  作２−保険者番号                PIC X(10).
001440     03  作２−レコードデータ.
001420         05  作２−県                        PIC X(2).
001431         05  作２−印刷順序.
001432             07  作２−印刷順序１            PIC 9(2).
001433             07  作２−印刷順序２            PIC 9.
001450         05  作２−件数                      PIC 9(4).
001460         05  作２−費用額                    PIC 9(9).
001470         05  作２−負担額                    PIC 9(9).
001480         05  作２−請求額                    PIC 9(9).
001490         05  作２−本人件数                  PIC 9(3).
001500         05  作２−本人費用額                PIC 9(7).
001510         05  作２−本人負担額                PIC 9(7).
001520         05  作２−本人請求額                PIC 9(7).
001530         05  作２−家族件数                  PIC 9(3).
001540         05  作２−家族費用額                PIC 9(7).
001550         05  作２−家族負担額                PIC 9(7).
001560         05  作２−家族請求額                PIC 9(7).
001570         05  FILLER                          PIC X(32).
001700*
001710 FD  印刷ファイル.
001720     COPY YAI585P        OF  XMDLIB.
001721*
001730*----------------------------------------------------------------*
001740******************************************************************
001750*                WORKING-STORAGE SECTION                         *
001760******************************************************************
001770 WORKING-STORAGE         SECTION.
001780 01 キー入力                           PIC X     VALUE SPACE.
001790 01 状態キー                           PIC X(2)  VALUE SPACE.
001800 01 終了フラグ                         PIC X(3)  VALUE SPACE.
001810 01 終了フラグ２                       PIC X(3)  VALUE SPACE.
001860 01 ファイル名                         PIC N(2)  VALUE SPACE.
001890 01 保険種別Ｗ                         PIC 9(2)  VALUE ZERO.
001900 01 保険者番号Ｗ                       PIC X(10) VALUE SPACE.
001910 01 保険者番号１Ｗ                     PIC X(10) VALUE SPACE.
001920 01 保険者番号２Ｗ                     PIC X(10) VALUE SPACE.
001921 01 印刷フラグ                         PIC X(3)  VALUE SPACE.
001210 01 オープンフラグ                     PIC X(3)   VALUE SPACE.
001930*
001940 01 行カウンタ                         PIC 9(2)  VALUE ZERO.
001950 01 頁カウンタ                         PIC 9(4)  VALUE ZERO.
001960 01 最大行数                           PIC 9(2)  VALUE ZERO.
001970 01 ヘッダ行数                         PIC 9(2)  VALUE ZERO.
001980 01 移動行数Ｗ                         PIC 9(2)  VALUE ZERO.
002000 01 保険名称Ｗ                         PIC N(2) VALUE SPACE.
002010*
002030 01 施術和暦年月Ｗ.
002040     03 施術和暦Ｗ                     PIC 9(1)  VALUE ZERO.
002050     03 施術年月Ｗ.
002060        05 施術年Ｗ                    PIC 9(2)  VALUE ZERO.
002070        05 施術月Ｗ                    PIC 9(2)  VALUE ZERO.
002220**
002290**************
002300* 施術所情報 *
002310**************
002320 01 施術所情報Ｗ.
002330    03 代表者名Ｗ                      PIC X(50)  VALUE SPACE.
002340    03 接骨院名Ｗ                      PIC X(50)  VALUE SPACE.
001940    03 柔整師番号Ｗ.
             05 柔整師記号Ｗ                 PIC X(2)   VALUE SPACE.
             05 柔整師番号１Ｗ               PIC X(7)   VALUE SPACE.
             05 柔整師区切１Ｗ               PIC X(1)   VALUE SPACE.
             05 柔整師番号２Ｗ               PIC X(1)   VALUE SPACE.
             05 柔整師区切２Ｗ               PIC X(1)   VALUE SPACE.
             05 柔整師番号３Ｗ               PIC X(1)   VALUE SPACE.
002350    03 施術所住所Ｗ.
002360       05 施術所住所１Ｗ               PIC X(50)  VALUE SPACE.
002370       05 施術所住所２Ｗ               PIC X(50)  VALUE SPACE.
002380    03 施術所郵便番号Ｗ.
002390       05 施術所郵便番号１Ｗ           PIC X(3)   VALUE SPACE.
002400       05 施術所郵便番号区切Ｗ         PIC X(1)   VALUE SPACE.
002410       05 施術所郵便番号２Ｗ           PIC X(4)   VALUE SPACE.
002420    03 施術所電話番号Ｗ                PIC X(15)  VALUE SPACE.
002430    03 取引先情報Ｗ.
002440        05 取引先銀行名Ｗ              PIC X(40)  VALUE SPACE.
002450        05 取引先銀行支店名Ｗ          PIC X(40)  VALUE SPACE.
002460        05 預金種別Ｗ                  PIC 9(1)   VALUE ZERO.
002470        05 銀行番号Ｗ                  PIC X(4)   VALUE SPACE.
002480        05 店番号Ｗ                    PIC X(3)   VALUE SPACE.
002490        05 口座番号Ｗ                  PIC X(10)  VALUE SPACE.
002500        05 口座名義人カナＷ.
002500           07 口座名義人カナ１Ｗ       PIC X(60)  VALUE SPACE.
002500           07 口座名義人カナ２Ｗ       PIC X(60)  VALUE SPACE.
002501        05 口座名義人Ｗ.
002501           07 口座名義人１Ｗ           PIC X(60)  VALUE SPACE.
002501           07 口座名義人２Ｗ           PIC X(60)  VALUE SPACE.
002510*
002520 01 連番Ｗ                             PIC 9(3)   VALUE ZERO.
002530 01 銀行名支店名Ｗ                     PIC X(82)  VALUE SPACE.
002540 01 預金種別コメントＷ                 PIC N(2)   VALUE SPACE.
002550 01 控えＷ                             PIC N(4)   VALUE SPACE.
002551**
002552* 社保用
002553 01 接尾語区分Ｗ                       PIC 9     VALUE ZERO.
002554*
002560********************
002570* 保険者別合計金額 *
002580********************
002590 01 保険者別合計金額.
002600    03 金額Ｗ                          PIC N(2)  VALUE SPACE.
002610    03 請求メッセージＷ                PIC N(15) VALUE SPACE.
002620    03 円Ｗ                            PIC N(1)  VALUE SPACE.
002630    03 件数Ｗ                          PIC 9(3)  VALUE ZERO.
002640    03 費用額Ｗ                        PIC 9(8)  VALUE ZERO.
002650    03 請求額Ｗ                        PIC 9(7)  VALUE ZERO.
002660    03 請求先名称Ｗ                    PIC X(40) VALUE SPACE.
002670    03 支部部署名Ｗ                    PIC X(40) VALUE SPACE.
002680    03 宛名Ｗ                          PIC X(24) VALUE SPACE.
002690    03 保険者宛名Ｗ.
002700       05 保険者宛名１Ｗ               PIC X(40) VALUE SPACE.
002710       05 保険者宛名２Ｗ               PIC X(40) VALUE SPACE.
002711**
002720 01 画面情報Ｗ.
002730    03 請求年月Ｗ.
002740       05 請求和暦Ｗ                   PIC 9     VALUE ZERO.
002750       05 請求年Ｗ                     PIC 9(2)  VALUE ZERO.
002760       05 請求月Ｗ                     PIC 9(2)  VALUE ZERO.
002770    03 提出年月日Ｗ.
002780       05 提出和暦Ｗ                   PIC 9     VALUE ZERO.
002790       05 提出年Ｗ                     PIC 9(2)  VALUE ZERO.
002800       05 提出月Ｗ                     PIC 9(2)  VALUE ZERO.
002810       05 提出日Ｗ                     PIC 9(2)  VALUE ZERO.
002821***
002822* エラーメッセージ用
002823 01 エラーメッセージＷ.
002824    03 エラー保険種別Ｗ                PIC X(2) VALUE SPACE.
002825    03 エラー区切りＷ                  PIC X(1) VALUE SPACE.
002826    03 エラー保険者番号Ｗ              PIC X(10) VALUE SPACE.
002827    03 FILLER                          PIC X(7) VALUE SPACE.
002828*
002829***
002842* その他会用
002844 01 その他編集Ｗ.
002845    03 その他編集内容Ｗ                PIC X(42) VALUE SPACE.
002846*
003280 01 カウンタ                           PIC 9(3)  VALUE ZERO.
003290 01 カウンタ２                         PIC 9(3)  VALUE ZERO.
004200 01 商Ｗ                               PIC 9(3)  VALUE ZERO.
004210 01 余Ｗ                               PIC 9(3)  VALUE ZERO.
002878***********************************************************************
002879 01 印刷制御.
002880     03 定義体名Ｐ                     PIC X(8) VALUE SPACE.
002881     03 項目群名Ｐ                     PIC X(8) VALUE SPACE.
002882     03 処理種別Ｐ                     PIC X(2) VALUE SPACE.
002883     03 拡張制御Ｐ.
002890         05 端末制御Ｐ.
002900             07 移動方向Ｐ             PIC X(1).
002910             07 移動行数Ｐ             PIC 9(3).
002920         05 詳細制御Ｐ                 PIC X(2).
002930     03 通知情報Ｐ                     PIC X(2) VALUE SPACE.
002940     03 ユニット名Ｐ                   PIC X(8) VALUE SPACE.
002950*
002960 01 計算機西暦年Ｗ                     PIC 9(2).
002970* 日付ＷＯＲＫ
002980 01 和暦終了年Ｗ                       PIC 9(4).
002990 01 計算機西暦.
003000    03 計算機西暦年                    PIC 9(4).
003010    03 計算機西暦月日                  PIC 9(4).
003020 01 計算機西暦Ｒ REDEFINES 計算機西暦.
003030    03 計算機世紀                      PIC 9(2).
003040    03 計算機日付                      PIC 9(6).
003050    03 計算機日付Ｒ REDEFINES 計算機日付.
003060       05 計算機年月                   PIC 9(4).
003070       05 計算機年月Ｒ REDEFINES 計算機年月.
003080         07 計算機年                   PIC 9(2).
003090         07 計算機月                   PIC 9(2).
003100       05 計算機日                     PIC 9(2).
003110*
003120******************************************************************
003130*                          連結項目                              *
003140******************************************************************
003150*
003160********************
003170* メッセージ表示キー *
003180********************
003184 01 連メ−キー IS EXTERNAL.
003185    03  連メ−メッセージ               PIC N(20).
003186*
003190 01 連メ３−キー IS EXTERNAL.
003200    03  連メ３−メッセージ             PIC N(20).
003210    03  連メ３−メッセージ１           PIC X(20).
003220*
       01 連入−画面情報ＹＡＩ５８０ IS EXTERNAL.
          03 連入−請求和暦年月.
             05 連入−請求和暦               PIC 9(1).
             05 連入−請求年月.
                07 連入−請求年              PIC 9(2).
                07 連入−請求月              PIC 9(2).
          03 連入−作成和暦年月日.
             05 連入−作成和暦年月.
                07 連入−作成和暦            PIC 9(1).
                07 連入−作成年              PIC 9(2).
                07 連入−作成月              PIC 9(2).
             05 連入−作成日                 PIC 9(2).
       01 連入−入力データＹＡＩ５８０   IS EXTERNAL.
          03 連入−保険種別                  PIC 9(2).
          03 連入−保険者番号                PIC X(10).
002351*
       01 連印−プレビュー IS EXTERNAL.
          03 連印−プレビュー区分            PIC 9.
003020*
000540************************************
000550* プリンタファイル作成用           *
000560************************************
000570 01 Ｈ連ＰＲＴＦ−作成データ IS EXTERNAL.
000580     03 Ｈ連ＰＲＴＦ−ファイル名           PIC X(8).
000590     03 Ｈ連ＰＲＴＦ−プレビュー区分       PIC 9.
000600     03 Ｈ連ＰＲＴＦ−帳票プログラム名     PIC X(8).
000610     03 Ｈ連ＰＲＴＦ−オーバレイ名         PIC X(8).
003470*
003480******************************************************************
003490*                      PROCEDURE  DIVISION                       *
003500******************************************************************
003510 PROCEDURE               DIVISION.
003520************
003530*           *
003540* 初期処理   *
003550*           *
003560************
002570     PERFORM プリンタファイル作成.
003581     PERFORM 初期化.
003980     PERFORM 施術所情報取得.
003583************
003590*           *
003600* 主処理     *
003610*           *
003620************
003660     PERFORM 印刷処理.
003760************
003770*           *
003780* 終了処理   *
003790*           *
003800************
003810     PERFORM 終了処理.
003820     EXIT PROGRAM.
003830*
003840*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002860*================================================================*
002870 プリンタファイル作成 SECTION.
002880*================================================================*
002890*   / 初期化 /
002900     MOVE SPACE TO Ｈ連ＰＲＴＦ−作成データ.
002910     INITIALIZE Ｈ連ＰＲＴＦ−作成データ.
002920*
002930*
002940*--↓↓ 変更箇所 ↓↓--------------------------------------*
002970*   使用するプリンタファイル名セット
002231     MOVE "PRTF001"             TO Ｈ連ＰＲＴＦ−ファイル名.
002972*
002973*   使用する帳票プログラム名セット
002974     MOVE "YAI585"              TO Ｈ連ＰＲＴＦ−帳票プログラム名.
002975*
002976*--↑↑-----------------------------------------------------*
002980*
002990*   / プレビュー区分セット /
003000     MOVE 連印−プレビュー区分 TO Ｈ連ＰＲＴＦ−プレビュー区分.
003010*
003020     CALL   "CRTPRTF".
003030     CANCEL "CRTPRTF".
003040*
003850*================================================================*
003860 初期化 SECTION.
003870*
003880     PERFORM ファイルオープン.
003961*
003970     PERFORM 連結項目退避.
004400*================================================================*
004410 連結項目退避 SECTION.
004420*
004430     MOVE 連入−請求和暦  TO 請求和暦Ｗ.
004440     MOVE 連入−請求年    TO 請求年Ｗ.
004450     MOVE 連入−請求月    TO 請求月Ｗ.
004460     MOVE 連入−作成和暦  TO 提出和暦Ｗ.
004470     MOVE 連入−作成年    TO 提出年Ｗ.
004480     MOVE 連入−作成月    TO 提出月Ｗ.
004490     MOVE 連入−作成日    TO 提出日Ｗ.
004510*
004520*================================================================*
004530 施術所情報取得 SECTION.
004540*
004550     MOVE ZERO  TO 施情−施術所番号.
004560     READ 施術所情報マスタ
004570     INVALID KEY
004580         CONTINUE
004590     NOT INVALID KEY
004600*
004610         MOVE 施情−郵便番号１       TO 施術所郵便番号１Ｗ
004620         MOVE "-"                    TO 施術所郵便番号区切Ｗ
004630         MOVE 施情−郵便番号２       TO 施術所郵便番号２Ｗ
004640         MOVE 施情−代表者名         TO 代表者名Ｗ
004650         MOVE 施情−接骨院名         TO 接骨院名Ｗ
004660         STRING 施情−住所１  DELIMITED BY SPACE
004670                施情−住所２  DELIMITED BY SPACE
004680           INTO 施術所住所Ｗ
004690         END-STRING
004700         MOVE 施情−電話番号         TO 施術所電話番号Ｗ
004701         MOVE 施情−新柔整師番号     TO 柔整師番号Ｗ
004710*
004720         MOVE 施情−取引先銀行名     TO 取引先銀行名Ｗ
004730         MOVE 施情−取引先銀行支店名 TO 取引先銀行支店名Ｗ
004740         MOVE 施情−預金種別         TO 預金種別Ｗ
004750         MOVE 施情−銀行番号         TO 銀行番号Ｗ
004760         MOVE 施情−店番号           TO 店番号Ｗ
004770         MOVE 施情−口座番号         TO 口座番号Ｗ
004780         MOVE 施情−口座名義人カナ   TO 口座名義人カナＷ
004781         MOVE 施情−口座名義人       TO 口座名義人Ｗ
004790         STRING 取引先銀行名Ｗ     DELIMITED BY SPACE
004800                " "                DELIMITED BY SIZE
004810                取引先銀行支店名Ｗ DELIMITED BY SPACE
004820                INTO 銀行名支店名Ｗ
004830         END-STRING
004840         EVALUATE 預金種別Ｗ
004850         WHEN 1
004860             MOVE NC"普通" TO 預金種別コメントＷ
004870         WHEN 2
004880             MOVE NC"当座" TO 預金種別コメントＷ
004890         WHEN OTHER
004900             MOVE SPACE    TO 預金種別コメントＷ
004910         END-EVALUATE
004920*
004930     END-READ.
004940*================================================================*
004950 ファイルオープン SECTION.
004960*
004970     OPEN INPUT   元号マスタ
004980         MOVE NC"元号" TO ファイル名.
004990         PERFORM オープンチェック.
005030     OPEN INPUT   保険者マスタ
005040         MOVE NC"保険" TO ファイル名.
005050         PERFORM オープンチェック.
005060     OPEN INPUT   市町村マスタ
005070         MOVE NC"市町" TO ファイル名.
005080         PERFORM オープンチェック.
005120     OPEN INPUT   施術所情報マスタ
005130         MOVE NC"施情" TO ファイル名.
005140         PERFORM オープンチェック.
005150     OPEN INPUT   請求先マスタ
005160         MOVE NC"請先" TO ファイル名.
005170         PERFORM オープンチェック.
005200*================================================================*
005210 オープンチェック SECTION.
005220*
005230     IF 状態キー  NOT =  "00"
005240         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
005250         DISPLAY NC"状態キー：" 状態キー         UPON CONS
005260         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
005270                                                 UPON CONS
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
005280         ACCEPT  キー入力 FROM CONS
005290         PERFORM ファイル閉鎖
005300         EXIT PROGRAM.
005310*================================================================*
005320 ファイル閉鎖 SECTION.
005330*
002990     IF ( オープンフラグ = "YES" )
002991         CLOSE 印刷ファイル
003041     END-IF.
005340     CLOSE 元号マスタ     保険者マスタ
005350           市町村マスタ   施術所情報マスタ 請求先マスタ.
005370*================================================================*
005380 終了処理 SECTION.
005390*
005400     PERFORM ファイル閉鎖.
005410*================================================================*
005420 エラー表示 SECTION.
005430*
005440     DISPLAY NC"状態キー" 状態キー  UPON CONS.
005450     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
005460     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
005470     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
000080*-----------------------------------------*
000090     CALL "actcshm"  WITH C LINKAGE.
000100*-----------------------------------------*
005480     ACCEPT  キー入力 FROM CONS.
005490     PERFORM ファイル閉鎖.
005500     EXIT PROGRAM.
005510*================================================================*
005520 印刷処理 SECTION.
005530*
005570     OPEN INPUT  作業ファイル２.
005580         MOVE NC"作２" TO ファイル名.
005590         PERFORM オープンチェック.
005611     MOVE 連入−保険種別    TO  作２−保険種別.
005612     MOVE 連入−保険者番号  TO  作２−保険者番号.
005617     READ 作業ファイル２
           NOT INVALID KEY
005703         MOVE SPACE TO YAI585P
005704****                INITIALIZE YAI585P
005705         PERFORM ヘッダセット
005706         PERFORM 明細セット
005707         PERFORM 印字処理
005720     END-READ.
006570*
006580     CLOSE 作業ファイル２.
006581*
006660*================================================================*
006670 作業ファイル２読込 SECTION.
006680*
006690     READ 作業ファイル２ NEXT
006700     AT END
006710         MOVE "YES" TO 終了フラグ
006720     END-READ.
006721*================================================================*
006722 印字処理  SECTION.
006723*
004310     IF ( オープンフラグ NOT = "YES" )
004320        MOVE "YES" TO オープンフラグ
004330        OPEN I-O  印刷ファイル
004340        PERFORM エラー処理Ｐ
004350     END-IF.
013440*
006724     MOVE "YAI585P" TO  定義体名Ｐ.
006725     MOVE SPACE     TO  処理種別Ｐ.
006726     MOVE "SCREEN"  TO  項目群名Ｐ.
006727     WRITE YAI585P.
006728     PERFORM エラー処理Ｐ.
006930*================================================================*
006940 ヘッダセット SECTION.
006950*
006982* 当月の和暦を取得
006983*     MOVE 請求和暦Ｗ         TO 元−元号区分.
006984*     READ 元号マスタ
006985*     INVALID KEY
006986*         MOVE SPACE          TO 請求和暦名称
006987*     NOT INVALID KEY
006988*         MOVE 元−元号名称   TO 請求和暦名称
006989*     END-READ.
006990*
      */元号修正/↓↓↓20190514
037370     IF 請求和暦Ｗ > 4
              MOVE 請求和暦Ｗ         TO 元−元号区分
037380        READ 元号マスタ
037390        NOT INVALID KEY
037400            MOVE 元−元号名称   TO 施術和暦
037410        END-READ
              MOVE "===="             TO 施術和暦訂正
           END-IF.
      */元号修正/↑↑↑20190514
006991     MOVE 請求年Ｗ           TO 請求年.
006992     MOVE 請求月Ｗ           TO 請求月.
007001*
007211*     MOVE 施術所住所Ｗ       TO 住所.
007220*     MOVE 施術所郵便番号Ｗ   TO 郵便番号.
007230     MOVE 代表者名Ｗ         TO 代表者名.
007240     MOVE 接骨院名Ｗ         TO 接骨院名.
004972     MOVE 柔整師番号１Ｗ     TO 柔整師番号１.
004972     MOVE 柔整師番号２Ｗ     TO 柔整師番号２.
004972     MOVE 柔整師番号３Ｗ     TO 柔整師番号３.
007250*     MOVE 施術所電話番号Ｗ   TO 電話番号.
007254*
007260*     MOVE 銀行名支店名Ｗ     TO 銀行名支店名.
007270*     MOVE 預金種別コメントＷ TO 預金種別.
007280*     MOVE 口座番号Ｗ         TO 口座番号.
      */委任者情報対応
007281*     MOVE 口座名義人カナＷ   TO 口座名義人カナ.
007282*     MOVE 口座名義人Ｗ       TO 口座名義人.
007281*     MOVE 口座名義人カナ１Ｗ   TO 口座名義人カナ.
007281*     MOVE 口座名義人カナ２Ｗ   TO 口座名義人カナ２.
007282*     MOVE 口座名義人１Ｗ       TO 口座名義人.
007282*     MOVE 口座名義人２Ｗ       TO 口座名義人２.
007283*
007284*     MOVE NC"振込先："       TO 振込先表題.
007285*
007292* / 保険者/
007300     MOVE 作２−保険種別     TO 保険種別Ｗ.
007310     MOVE 作２−保険者番号   TO 保険者番号Ｗ.
007320     EVALUATE 保険種別Ｗ
007330     WHEN 1 THRU 4
007340     WHEN 6 THRU 9
007350*     WHEN 70 
007360*     WHEN 80
007370         PERFORM 保険者情報取得
007380     WHEN 5
007390     WHEN 50 THRU 60
007400         PERFORM 市町村情報取得
007410     END-EVALUATE.
007420     PERFORM 結合宛名取得.
007421*
007430     MOVE 保険者宛名Ｗ  TO 保険者名称.
007490*
      */保険者番号を印字する/081016
      *     IF 作２−保険種別 <= 09
      *         MOVE NC"保険者番号"   TO 保険者番号表題
      *         MOVE 作２−保険者番号 TO 保険者番号
      *     END-IF.
007741*================================================================*
007742 明細セット SECTION.
007743*
007760     IF 作２−本人件数 NOT = ZERO
007761        MOVE 作２−本人件数     TO 本人件数
007762        MOVE 作２−本人費用額   TO 本人費用額
007763        MOVE 作２−本人負担額   TO 本人負担額
007770        MOVE 作２−本人請求額   TO 本人請求額
007771     END-IF.
007772     IF 作２−家族件数 NOT = ZERO
007774        MOVE 作２−家族件数     TO 家族件数
007775        MOVE 作２−家族費用額   TO 家族費用額
007776        MOVE 作２−家族負担額   TO 家族負担額
007777        MOVE 作２−家族請求額   TO 家族請求額
007778     END-IF.
007780*
008290*================================================================*
008300 エラー処理Ｐ SECTION.
008310*
008320     IF 通知情報Ｐ NOT = "00"
008330         DISPLAY NC"帳票エラー"              UPON CONS
008340         DISPLAY NC"項目群名Ｐ：" 項目群名Ｐ UPON CONS
008350         DISPLAY NC"通知情報Ｐ：" 通知情報Ｐ UPON CONS
008360         DISPLAY NC"拡張制御Ｐ：" 拡張制御Ｐ UPON CONS
008370         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
008380                                             UPON CONS
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
008390         ACCEPT  キー入力 FROM CONS
008400         PERFORM ファイル閉鎖
008410         EXIT PROGRAM
008420     END-IF.
008430*================================================================*
008440 保険者情報取得 SECTION.
008450*
008451     MOVE  SPACE         TO 請求先名称Ｗ.
008452     MOVE  SPACE         TO 支部部署名Ｗ.
008453     MOVE  ZERO          TO 接尾語区分Ｗ.
008454*
008460     MOVE 保険種別Ｗ     TO 保−保険種別.
008470     MOVE 保険者番号Ｗ   TO 保−保険者番号.
008480     READ 保険者マスタ
008490     INVALID KEY
008500         MOVE SPACE      TO 請求先名称Ｗ
008510         MOVE SPACE      TO 支部部署名Ｗ
008520     NOT INVALID KEY
008530         IF 保−請求先情報区分 = 1
008540             MOVE 保−保険種別   TO 請先−保険種別
008550             MOVE 保−保険者番号 TO 請先−保険者番号
008560             READ 請求先マスタ
008570             INVALID KEY
008580                 MOVE SPACE             TO 請求先名称Ｗ
008590                 MOVE SPACE             TO 支部部署名Ｗ
008600             NOT INVALID KEY
008610                 MOVE 請先−保険者名称  TO 請求先名称Ｗ
008620                 MOVE 請先−支部部署名  TO 支部部署名Ｗ
008630             END-READ
008640         ELSE
008650             MOVE 保−保険者名称        TO 請求先名称Ｗ
008660             MOVE 保−支部部署名        TO 支部部署名Ｗ
008661             MOVE 保−接尾語区分        TO 接尾語区分Ｗ
008670         END-IF
008680     END-READ.
008690*================================================================*
008700 市町村情報取得 SECTION.
008710*
008711     MOVE  SPACE         TO 請求先名称Ｗ.
008712     MOVE  SPACE         TO 支部部署名Ｗ.
008713*
008720     MOVE 保険種別Ｗ               TO 市−公費種別.
008730     MOVE 保険者番号Ｗ             TO 市−市町村番号.
008740     READ 市町村マスタ
008750     INVALID KEY
008760         MOVE SPACE                TO 請求先名称Ｗ
008770         MOVE SPACE                TO 支部部署名Ｗ
008780     NOT INVALID KEY
008790         IF 市−請求先区分 = 1
008800             MOVE 保険種別Ｗ       TO 請先−保険種別
008810             MOVE 保険者番号Ｗ     TO 請先−保険者番号
008820             READ 請求先マスタ
008830             INVALID KEY
008840                 MOVE SPACE        TO 請求先名称Ｗ
008850                 MOVE SPACE        TO 支部部署名Ｗ
008860             NOT INVALID KEY
008868                 MOVE 請先−保険者名称   TO 請求先名称Ｗ
008869                 MOVE 請先−支部部署名   TO 支部部署名Ｗ
008890             END-READ
008900          ELSE
008908             MOVE 市−市町村名称   TO 請求先名称Ｗ
008909             MOVE 市−支部部署名   TO 支部部署名Ｗ
008930          END-IF
008940      END-READ.
008950*================================================================*
008960 結合宛名取得 SECTION.
008970*
008971     MOVE SPACE TO 保険者宛名Ｗ.
008972     IF 請求先名称Ｗ NOT = SPACE
008980         EVALUATE 保険種別Ｗ
008981         WHEN 2
008982             IF 接尾語区分Ｗ = 1
008983                MOVE SPACE            TO 宛名Ｗ
008984             ELSE
008985                MOVE "社会保険事務所" TO 宛名Ｗ
008986             END-IF
008987         WHEN 6
008988             IF 接尾語区分Ｗ = 1
008989*                MOVE "（日雇）"               TO 宛名Ｗ
                      CONTINUE
008991             ELSE
008992*                MOVE "社会保険事務所（日雇）" TO 宛名Ｗ
008992                MOVE "社会保険事務所" TO 宛名Ｗ
008993             END-IF
008994         WHEN 7
008995*             MOVE "（船員）"       TO 宛名Ｗ
                   CONTINUE
009020         WHEN 3
009030             MOVE "健康保険組合"   TO 宛名Ｗ
009031         WHEN 4
009032             MOVE "共済組合"       TO 宛名Ｗ
009033         WHEN 8
009034             MOVE "（退職）"       TO 宛名Ｗ
009040         WHEN OTHER
009050             MOVE SPACE            TO 宛名Ｗ
009060         END-EVALUATE
009070*
009080         IF 支部部署名Ｗ = SPACE
009090             STRING  請求先名称Ｗ  DELIMITED BY SPACE
009100                     宛名Ｗ        DELIMITED BY SPACE
009120                    INTO 保険者宛名Ｗ
009130             END-STRING
009140         ELSE
009150             STRING  請求先名称Ｗ  DELIMITED BY SPACE
009160                     宛名Ｗ        DELIMITED BY SPACE
009170                     " "           DELIMITED BY SIZE
009180                     支部部署名Ｗ  DELIMITED BY SPACE
009200                    INTO 保険者宛名Ｗ
009210             END-STRING
009220         END-IF
009221     END-IF.
009222*
009605*================================================================*
009606******************************************************************
009607 END PROGRAM YAI585.
009608******************************************************************
