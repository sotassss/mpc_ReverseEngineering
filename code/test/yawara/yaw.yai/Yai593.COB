000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YAI593.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*   【柔+ｳｨﾝﾄﾞｳｽﾞ版　愛知用 後高保健療養費総括表】
000100*   【データ作成】
000110*   後高１割３割の対応
000120*         MED = YAI580 YAI594P 
000130*----------------------------------------------------------------*
000140 DATE-WRITTEN.           2013-07-01
000150 DATE-COMPILED.          2013-07-01
000160*----------------------------------------------------------------*
000170******************************************************************
000180*            ENVIRONMENT         DIVISION                        *
000190******************************************************************
000200 ENVIRONMENT             DIVISION.
000210 CONFIGURATION           SECTION.
000220 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000230 OBJECT-COMPUTER.        FMV-DESKPOWER.
000240 SPECIAL-NAMES.          CONSOLE  IS  CONS
000250                         SYSERR   IS  MSGBOX.
000260 INPUT-OUTPUT            SECTION.
000270 FILE-CONTROL.
000280     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000290                             ORGANIZATION             IS  INDEXED
000300                             ACCESS MODE              IS  DYNAMIC
000310                             RECORD KEY               IS  受−施術和暦年月
000320                                                          受−患者コード
000330                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000340                                                          受−患者カナ
000350                                                          受−患者コード
000360                             ALTERNATE RECORD KEY     IS  受−患者コード
000370                                                          受−施術和暦年月
000380                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000390                                                          受−保険種別
000400                                                          受−保険者番号
000410                                                          受−患者コード
000420                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000430                                                          受−公費種別
000440                                                          受−費用負担者番号
000450                                                          受−患者コード
000460                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000470                                                          受−助成種別
000480                                                          受−費用負担者番号助成
000490                                                          受−患者コード
000500                             ALTERNATE RECORD KEY     IS  受−請求和暦年月
000510                                                          受−施術和暦年月
000520                                                          受−患者コード
000530                             FILE STATUS              IS  状態キー
000540                             LOCK        MODE         IS  AUTOMATIC.
           SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
                                   ORGANIZATION             IS  INDEXED
                                   ACCESS MODE              IS  DYNAMIC
                                   RECORD KEY               IS  レセ−施術和暦年月
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−患者コード
                                                                レセ−施術和暦年月
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−施術和暦年月
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−レセ種別
                                                                レセ−請求保険者番号
                                                                レセ−患者コード
                                                                レセ−施術和暦年月
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−請求保険者番号
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                                                レセ−施術和暦年月
                                   FILE STATUS              IS  状態キー
                                   LOCK        MODE         IS  AUTOMATIC.
001090     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W5931L.DAT"
001100                             ORGANIZATION             IS  INDEXED
001110                             ACCESS                   IS  DYNAMIC
001120                             RECORD      KEY          IS  作１−保険者番号
001160                                                          作１−負担割合
001180                             FILE        STATUS       IS  状態キー
001190                             LOCK        MODE         IS  AUTOMATIC.
001200******************************************************************
001210*                      DATA DIVISION                             *
001220******************************************************************
001230 DATA                    DIVISION.
001240 FILE                    SECTION.
001250*                           ［ＲＬ＝  ７６８］
001260 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001270     COPY JUSINJ         OF  XFDLIB  JOINING    受   AS  PREFIX.
      *                          ［ＲＬ＝  １５３６］
       FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS.
           COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
001460*
001470 FD  作業ファイル１ RECORD  CONTAINS 64 CHARACTERS.
001480 01  作１−レコード.
001490   03  作１−レコードキー.
001520     05  作１−保険者番号.
001530       07 作１−法別番号               PIC X(2).
001540       07 作１−保番.
001550         09 作１−都道府県番号         PIC X(2).
001560         09 作１−保番号               PIC X(3).
001570         09 作１−検証番号             PIC X.
001580         09 FILLER                     PIC X(2).
001600     05  作１−負担割合                PIC 9(1).
001610   03  作１−レコードデータ.
001620     05  作１−件数                    PIC 9(3).
001640     05  作１−費用額                  PIC 9(7).
001650     05  作１−請求額                  PIC 9(7).
001660     05  作１−負担額                  PIC 9(7).
001670     05  FILLER                        PIC X(29).
001680*
001690******************************************************************
001700*                WORKING-STORAGE SECTION                         *
001710******************************************************************
001720 WORKING-STORAGE         SECTION.
001730 01 キー入力                           PIC X(1) VALUE SPACE.
001740 01 状態キー                           PIC X(2) VALUE SPACE.
001750 01 終了フラグ１                       PIC X(3) VALUE SPACE.
001760 01 終了フラグ３                       PIC X(3) VALUE SPACE.
001780 01 ファイル名Ｗ                       PIC N(2) VALUE SPACE.
001790 01 ファイル名                         PIC N(2) VALUE SPACE.
001810 01 実行キーＷ                         PIC X(4) VALUE SPACE.
001850*
001950 01 印刷形式Ｗ                         PIC 9(1)  VALUE ZERO.
001960 01 元号名称Ｗ                         PIC N(2)  VALUE SPACE.
001970*
001980**************
001990* 保険者情報 *
002000**************
002010*
002560 01 請求和暦年月ＷＲ.
002570    03 請求和暦ＷＲ                  PIC 9     VALUE ZERO.
002580    03 請求年ＷＲ                    PIC 9(2)  VALUE ZERO.
002590    03 請求月ＷＲ                    PIC 9(2)  VALUE ZERO.
002600*
002610 01 保険者番号Ｗ.
002620    03 法別番号Ｗ                    PIC X(2) VALUE SPACE.
002630    03 保番Ｗ.
002640       05 都道府県番号Ｗ             PIC X(2) VALUE SPACE.
002650       05 保険番号Ｗ                 PIC X(3) VALUE SPACE.
002660       05 検証番号Ｗ                 PIC X    VALUE SPACE.
002670       05 FILLER                     PIC X(2).
002680 01 保険種別Ｗ                       PIC 9(2) VALUE ZERO.
002690 01 公費種別Ｗ                       PIC 9(2) VALUE ZERO.
002700 01 助成種別Ｗ                       PIC 9(2) VALUE ZERO.
002710 01 本人家族区分Ｗ                   PIC 9(1) VALUE ZERO.
002720 01 法別番号Ｗ                       PIC X(2) VALUE SPACE.
002730 01 老人負担金免除Ｗ                 PIC 9(1) VALUE ZERO.
002740*
002750 01 対象件数Ｗ                       PIC 9(4) VALUE ZERO.
002760*
002840 01 終了フラグ２                     PIC X(3) VALUE SPACE.
002850*
002860 01 状態フラグ                       PIC 9(1) VALUE ZERO.
003000*
003010******************************************************************
003020*                          連結項目                              *
003030******************************************************************
003040*
003050****************
003060* 画面入力情報 *
003070****************
       01 連入−画面情報ＹＡＩ５８０ IS EXTERNAL.
          03 連入−請求和暦年月.
             05 連入−請求和暦               PIC 9(1).
             05 連入−請求年月.
                07 連入−請求年              PIC 9(2).
                07 連入−請求月              PIC 9(2).
          03 連入−作成和暦年月日.
             05 連入−作成和暦年月.
                07 連入−作成和暦            PIC 9(1).
                07 連入−作成年              PIC 9(2).
                07 連入−作成月              PIC 9(2).
             05 連入−作成日                 PIC 9(2).
       01 連入−入力データＹＡＩ５８０   IS EXTERNAL.
          03 連入−保険種別                  PIC 9(2).
          03 連入−保険者番号                PIC X(10).
003590*
003600******************************************************************
003610*                      PROCEDURE  DIVISION                       *
003620******************************************************************
003630 PROCEDURE               DIVISION.
003640************
003650*           *
003660* 初期処理   *
003670*           *
003680************
003690     PERFORM 初期化.
003700************
003710*           *
003720* 主処理     *
003730*           *
003740************
003750     PERFORM 作業ファイル作成.
003760************
003770*           *
003780* 終了処理   *
003790*           *
003800************
003810     PERFORM 終了処理.
003820     MOVE ZERO TO PROGRAM-STATUS.
003830     EXIT PROGRAM.
003840*
003850*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003860*================================================================*
003870 初期化 SECTION.
003880*
003890     OPEN INPUT 受診者情報Ｆ.
003900             MOVE NC"受診" TO ファイル名Ｗ.
003910             PERFORM オープンチェック.
003250     OPEN INPUT レセプトＦ.
003260         MOVE NC"レセプトＦ" TO ファイル名Ｗ.
003270         PERFORM オープンチェック.
004070*
004250* 連結項目の待避
004260     MOVE 連入−請求和暦 TO 請求和暦ＷＲ.
004270     MOVE 連入−請求年   TO 請求年ＷＲ.
004280     MOVE 連入−請求月   TO 請求月ＷＲ.
004290*================================================================*
004300 オープンチェック SECTION.
004310*
004320     IF 状態キー  NOT =  "00"
004330         DISPLAY ファイル名Ｗ NC"Ｆオープンエラー" UPON CONS
004340         DISPLAY NC"状態キー：" 状態キー           UPON CONS
004350         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004360                                                   UPON CONS
003131*-----------------------------------------*
003132         CALL "actcshm"  WITH C LINKAGE
003133*-----------------------------------------*
004370         ACCEPT  キー入力 FROM CONS
004380         PERFORM ファイル閉鎖
004390         EXIT PROGRAM.
004920*================================================================*
004930 ファイル閉鎖 SECTION.
004940*
004950     CLOSE  受診者情報Ｆ レセプトＦ.
004970*================================================================*
004980 終了処理 SECTION.
004990*
005000     PERFORM ファイル閉鎖.
005010*================================================================*
005020* データ作成 SECTION.
005030 作業ファイル作成 SECTION.
005040*
005070     OPEN OUTPUT 作業ファイル１.
005080         MOVE NC"作１" TO ファイル名.
005090         PERFORM オープンチェック.
005100     CLOSE 作業ファイル１.
005110     OPEN I-O 作業ファイル１.
005120         MOVE NC"作１" TO ファイル名.
005130         PERFORM オープンチェック.
005170* */老人
007470     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
007480     MOVE 請求年ＷＲ    TO レセ−請求年.
007490     MOVE 請求月ＷＲ    TO レセ−請求月.
013730     MOVE 2             TO レセ−レセ種別.
007500     MOVE ZERO          TO レセ−請求保険者番号.
007500     MOVE ZERO          TO レセ−施術和暦.
007510     MOVE ZERO          TO レセ−施術年.
007520     MOVE ZERO          TO レセ−施術月.
007530     MOVE ZERO          TO レセ−患者番号.
007540     MOVE SPACE         TO レセ−枝番.
007550     START レセプトＦ   KEY IS >= レセ−請求和暦年月
000250                                  レセ−レセ種別
007560                                  レセ−請求保険者番号
007570                                  レセ−患者コード
                                        レセ−施術和暦年月
005340     END-START.
005350     IF 状態キー = "00"
005360         MOVE SPACE  TO 終了フラグ３
005370         PERFORM レセプトＦ読込
005410         PERFORM UNTIL (終了フラグ３         = "YES" ) OR
007630                       ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
007640                       ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
007650                       ( レセ−請求月   NOT = 請求月ＷＲ   ) OR
                             ( レセ−レセ種別 NOT = 2 )
005700             PERFORM データチェック
005710             IF 実行キーＷ = "YES"
006130                IF ( 受−府県老人 = "23" )
005530*/          */請求額がゼロの場合は対象外に
005540                   IF レセ−請求金額 NOT = ZERO
005550                      PERFORM 作業ファイル作成１
005560                   END-IF
                      END-IF
005580             END-IF
005590             PERFORM レセプトＦ読込
005600         END-PERFORM
005620     END-IF.
005630     CLOSE 作業ファイル１.
006270*================================================================*
006280 レセプトＦ読込 SECTION.
006290*
006300     READ レセプトＦ NEXT
006310     AT END
006320         MOVE "YES" TO 終了フラグ３
006330     END-READ.
006580*================================================================*
006590 エラー表示 SECTION.
006600*
006610     DISPLAY NC"ファイル書込エラー：" ファイル名Ｗ UPON CONS.
006620     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
006630     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
003331*-----------------------------------------*
003332     CALL "actcshm"  WITH C LINKAGE.
003333*-----------------------------------------*
006640     ACCEPT  キー入力 FROM CONS.
006650*================================================================*
006660 エラー表示Ｒ SECTION.
006670*
006680     DISPLAY NC"ファイル読込エラー" ファイル名Ｗ UPON CONS.
006690     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
003331*-----------------------------------------*
003332     CALL "actcshm"  WITH C LINKAGE.
003333*-----------------------------------------*
006700     ACCEPT  キー入力 FROM CONS.
006710     PERFORM ファイル閉鎖.
006720     EXIT PROGRAM.
006730*================================================================*
006740 エラー表示その他 SECTION.
006750*
006760     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
006770     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
003331*-----------------------------------------*
003332     CALL "actcshm"  WITH C LINKAGE.
003333*-----------------------------------------*
006780     ACCEPT  キー入力 FROM CONS.
006790     PERFORM ファイル閉鎖.
006800     EXIT PROGRAM.
006810*================================================================*
006820 データチェック SECTION.
006830*
006840     MOVE SPACE          TO 実行キーＷ.
007260* *****************************************************************
007270* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
007280* *****************************************************************
007290     IF ( レセ−請求対象区分 NOT = ZERO ) AND
007300        ( レセ−償還払い区分 NOT = 1 )
007310        IF (レセ−レセ種別 = 3) AND ( レセ−会総括表印刷対象区分 = 1 )
007320            CONTINUE
007330        ELSE
007340           MOVE レセ−施術和暦  TO 受−施術和暦
007350           MOVE レセ−施術年    TO 受−施術年
007360           MOVE レセ−施術月    TO 受−施術月
007370           MOVE レセ−患者番号  TO 受−患者番号
007380           MOVE レセ−枝番      TO 受−枝番
007390           READ 受診者情報Ｆ
007400           NOT INVALID KEY
007430              MOVE "YES"  TO 実行キーＷ
007450           END-READ
007460        END-IF
007470     END-IF.
007300*
007380*================================================================*
007390 作業ファイル作成１ SECTION.
007400*
007410     MOVE SPACE TO 作１−レコード.
007420     INITIALIZE    作１−レコード.
007490*
007500*     MOVE 受−費用負担者番号   TO 作１−保険者番号.
007500     MOVE "39230008"           TO 作１−保険者番号.
      */後期高齢１割負担と２割負担(９割給付と８割給付)を合算して集計する↓↓↓/202209207
007540*     EVALUATE 受−特別区分
007560*     WHEN 1
007570*         MOVE 1                TO 作１−負担割合
      *     WHEN 2
      *     WHEN 3
007550*         MOVE 3                TO 作１−負担割合
007580*     END-EVALUATE.
007540     EVALUATE 受−特別区分
007560     WHEN 1
           WHEN 2
007570         MOVE 1                TO 作１−負担割合
           WHEN 3
007550         MOVE 3                TO 作１−負担割合
007580     END-EVALUATE.
      */後期高齢１割負担と２割負担(９割給付と８割給付)を合算して集計する↑↑↑/20220927
007590*
007600     READ 作業ファイル１
007610     INVALID KEY
007620         MOVE 1                TO 作１−件数 
007640         MOVE レセ−合計       TO 作１−費用額
007650         MOVE レセ−一部負担金 TO 作１−負担額
007660         MOVE レセ−請求金額   TO 作１−請求額
007670         WRITE 作１−レコード
007680         INVALID KEY
007690             DISPLAY NC"状態キー" 状態キー
007700             MOVE NC"作１"  TO ファイル名Ｗ
007710             PERFORM エラー表示
007720         END-WRITE
007730     NOT INVALID KEY
007740         COMPUTE 作１−件数   = 作１−件数   + 1
007760         COMPUTE 作１−費用額 = 作１−費用額 + レセ−合計
007770         COMPUTE 作１−負担額 = 作１−負担額 + レセ−一部負担金
007780         COMPUTE 作１−請求額 = 作１−請求額 + レセ−請求金額
007790         REWRITE 作１−レコード
007800         INVALID KEY
007810             DISPLAY NC"状態キー" 状態キー
007820             MOVE NC"作１"  TO ファイル名Ｗ
007830             PERFORM エラー表示
007840         END-REWRITE
007850     END-READ.
007860*================================================================*
007870******************************************************************
007880 END PROGRAM YAI593.
007890******************************************************************
