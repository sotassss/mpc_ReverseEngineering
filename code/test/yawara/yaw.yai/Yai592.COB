000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YAI592.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*  【柔+ｳｨﾝﾄﾞｳｽﾞ版　愛知用 国民健康保険療養費申請集計表】
000100*  【印刷】
000110*   高齢者１割２割、３歳未満、の対応
000120*       MED = YAI580 YAI592P 
000130*----------------------------------------------------------------*
000140 DATE-WRITTEN.           2013-07-01
000150 DATE-COMPILED.          2013-07-01
000160*----------------------------------------------------------------*
000170******************************************************************
000180*            ENVIRONMENT         DIVISION                        *
000190******************************************************************
000200 ENVIRONMENT             DIVISION.
000210 CONFIGURATION           SECTION.
000220 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000230 OBJECT-COMPUTER.        FMV-DESKPOWER.
000240 SPECIAL-NAMES.          CONSOLE  IS  CONS
000250                         SYSERR   IS  MSGBOX.
000260 INPUT-OUTPUT            SECTION.
000270 FILE-CONTROL.
000280     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000290                             ORGANIZATION             IS  INDEXED
000300                             ACCESS MODE              IS  DYNAMIC
000310                             RECORD KEY               IS  元−元号区分
000320                             FILE STATUS              IS  状態キー
000330                             LOCK        MODE         IS  AUTOMATIC.
000400     SELECT  施術所情報マスタ ASSIGN     TO        SEJOHOL
000410                             ORGANIZATION             IS  INDEXED
000420                             ACCESS MODE              IS  DYNAMIC
000430                             RECORD KEY               IS  施情−施術所番号
000440                             FILE STATUS              IS  状態キー
000450                             LOCK        MODE         IS  AUTOMATIC.
000460     SELECT  保険者マスタ    ASSIGN      TO        HOKENSL
000470                             ORGANIZATION             IS  INDEXED
000480                             ACCESS MODE              IS  DYNAMIC
000490                             RECORD KEY               IS  保−保険種別
000500                                                          保−保険者番号
000510                             ALTERNATE RECORD KEY     IS  保−保険種別
000520                                                          保−保険者名称
000530                                                          保−保険者番号
000540                             FILE STATUS              IS  状態キー
000550                             LOCK        MODE         IS  AUTOMATIC.
000730     SELECT  ＩＤ管理マスタ    ASSIGN      TO        IDKANRL
000740                             ORGANIZATION             IS  INDEXED
000750                             ACCESS MODE              IS  DYNAMIC
000760                             RECORD KEY               IS  ＩＤ管−ＩＤ区分
000770                                                          ＩＤ管−施術所番号
000780                                                          ＩＤ管−保険種別
000790                                                          ＩＤ管−保険者番号
000800                             ALTERNATE RECORD KEY     IS  ＩＤ管−施術ＩＤ番号
000810                                                          ＩＤ管−ＩＤ区分
000820                                                          ＩＤ管−施術所番号
000830                                                          ＩＤ管−保険種別
000840                                                          ＩＤ管−保険者番号
000850                             FILE STATUS              IS  状態キー
000860                             LOCK        MODE         IS  AUTOMATIC.
001360     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W5911L.DAT"
001370                             ORGANIZATION             IS  INDEXED
001380                             ACCESS                   IS  DYNAMIC
001390                             RECORD      KEY          IS  作１−保険者番号
001450                                                          作１−印刷区分
001490                             FILE        STATUS       IS  状態キー
001500                             LOCK        MODE         IS  AUTOMATIC.
001020     SELECT  印刷ファイル    ASSIGN      TO     GS-PRTF001
001030                             SYMBOLIC    DESTINATION  IS "PRT"
001040                             FORMAT                   IS  定義体名Ｐ
001050                             GROUP                    IS  項目群名Ｐ
001060                             PROCESSING  MODE         IS  処理種別Ｐ
001070                             UNIT        CONTROL      IS  拡張制御Ｐ
001080                             FILE        STATUS       IS  通知情報Ｐ.
001090******************************************************************
001100*                      DATA DIVISION                             *
001110******************************************************************
001120 DATA                    DIVISION.
001130 FILE                    SECTION.
001140*                           ［ＲＬ＝  １２８］
001150 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
001160     COPY GENGOU         OF  XFDLIB  JOINING    元   AS  PREFIX.
001200*                           ［ＲＬ＝  ６４０］
001210 FD  施術所情報マスタ    BLOCK   CONTAINS   1   RECORDS.
001220     COPY SEJOHO         OF  XFDLIB  JOINING    施情 AS  PREFIX.
001230*                           ［ＲＬ＝  ３２０］
001240 FD  保険者マスタ        BLOCK   CONTAINS   1   RECORDS.
001250     COPY HOKENS          OF  XFDLIB  JOINING   保   AS  PREFIX.
001320*                           ［ＲＬ＝  １２８］
001330 FD  ＩＤ管理マスタ          BLOCK   CONTAINS   1   RECORDS.
001340     COPY IDKANR    OF  XFDLIB  JOINING   ＩＤ管   AS  PREFIX.
001350*
001870 FD  作業ファイル１ RECORD  CONTAINS 64 CHARACTERS.
001880 01  作１−レコード.
001890   03  作１−レコードキー.
001920     05  作１−保険者番号.
001930       07 作１−法別番号               PIC X(2).
001940       07 作１−保番.
001950         09 作１−都道府県番号         PIC X(2).
001960         09 作１−保番号               PIC X(3).
001970         09 作１−検証番号             PIC X.
001980         09 FILLER                     PIC X(2).
001910     05  作１−印刷区分                PIC 9(1).
002050   03  作１−レコードデータ.
002060     05  作１−件数                    PIC 9(3).
002080     05  作１−費用額                  PIC 9(7).
002090     05  作１−請求額                  PIC 9(7).
002100     05  作１−負担額                  PIC 9(7).
002110     05  FILLER                        PIC X(29).
001610*
001620 FD  印刷ファイル.
001630     COPY YAI592P        OF  XMDLIB.
001640******************************************************************
001650*                WORKING-STORAGE SECTION                         *
001660******************************************************************
001670 WORKING-STORAGE         SECTION.
001680 01 キー入力                           PIC X    VALUE SPACE.
001690 01 状態キー                           PIC X(2) VALUE SPACE.
001700 01 終了フラグ                         PIC X(3) VALUE SPACE.
001710 01 終了フラグ２                       PIC X(3) VALUE SPACE.
001720 01 終了フラグ３                       PIC X(3) VALUE SPACE.
001730 01 確認入力Ｗ                         PIC X(1) VALUE SPACE.
001740 01 ファイル名Ｗ                       PIC N(2) VALUE SPACE.
001750 01 カレント元号Ｗ                     PIC 9(1) VALUE ZERO.
001350 01 オープンフラグ                     PIC X(3)   VALUE SPACE.
001760 01 実行キーＷ                         PIC X(4) VALUE SPACE.
001770 01 前和暦Ｗ                           PIC 9 VALUE ZERO.
001780 01 行桁フラグ                         PIC X(3) VALUE SPACE.
001790 01 処理移動キー                       PIC X(4) VALUE SPACE.
001800 01 施術和暦年月Ｗ.
001810    03 施術和暦Ｗ                      PIC 9.
001820    03 施術年月Ｗ.
001830       05 施術年Ｗ                     PIC 9(2).
001840       05 施術月Ｗ                     PIC 9(2).
001850 01 施術日Ｗ                           PIC 9(2).
001860 01 請求和暦年月日Ｗ.
001870    03 請求年Ｗ                        PIC 9(2).
001880    03 請求月Ｗ                        PIC 9(2).
001890    03 請求日Ｗ                        PIC 9(2).
001900 01 元号名称Ｗ                         PIC N(2) VALUE SPACE.
001910 01 外字文字Ｗ.
001920    03 外字文字１Ｗ                    PIC X(1) OCCURS 16.
001930 01 外字コードＷ.
001940    03 外字コード１Ｗ                  PIC N(1) OCCURS 16.
001950 01 カウンタ                           PIC 9(2) VALUE ZERO.
001960 01 印刷枚数ＣＮＴ                     PIC 9(2) VALUE ZERO.
001970 01 請求書Ｗ.
001980    03 登録番号コードＷ.
001990       05 登録番号ＷＰ.
002000          07 登録番号Ｗ                PIC X(16) VALUE SPACE.
002010    03 会員番号Ｗ.
002020       05 会員番号頭Ｗ                 PIC X(2)  VALUE SPACE.
002030       05 会員番号ＷＰ                 PIC X(10) VALUE SPACE.
002040    03 施術所コードＷ                  PIC X(7)  VALUE SPACE.
002050    03 施術所住所Ｗ.
002060        05 施術所住所１Ｗ              PIC X(50) VALUE SPACE.
002070        05 施術所住所２Ｗ              PIC X(50) VALUE SPACE.
002080    03 接骨院名Ｗ                      PIC X(50) VALUE SPACE.
002090    03 代表者名Ｗ                      PIC X(50) VALUE SPACE.
002100    03 施術所電話番号Ｗ                PIC X(15) VALUE SPACE.
002110    03 ＯＣＲ頁数Ｗ                    PIC N(1)  VALUE SPACE.
002120    03 ＯＣＲ全頁Ｗ                    PIC N(1)  VALUE SPACE.
002130    03 合計金額Ｗ.
002140        05 保険者名称Ｗ.
002150           07 印刷保険者名称Ｗ         PIC X(32) VALUE SPACE.
002400*     */国保高齢者通常所得１割用
002410       05 国保高齢１件数Ｗ             PIC 9(3)  VALUE ZERO.
002430       05 国保高齢１負担額Ｗ           PIC 9(7)  VALUE ZERO.
002430       05 国保高齢１費用額Ｗ           PIC 9(7)  VALUE ZERO.
002440       05 国保高齢１請求額Ｗ           PIC 9(7)  VALUE ZERO.
002450*     */国保高齢者高額所得２割用
002460       05 国保高齢３件数Ｗ             PIC 9(3)  VALUE ZERO.
002480       05 国保高齢３負担額Ｗ           PIC 9(7)  VALUE ZERO.
002480       05 国保高齢３費用額Ｗ           PIC 9(7)  VALUE ZERO.
002490       05 国保高齢３請求額Ｗ           PIC 9(7)  VALUE ZERO.
002450*     */国保１０割・９割・８割用
002160       05 国保１０件数Ｗ               PIC 9(3)  VALUE ZERO.
002180       05 国保１０負担額Ｗ             PIC 9(7)  VALUE ZERO.
002180       05 国保１０費用額Ｗ             PIC 9(7)  VALUE ZERO.
002190       05 国保１０請求額Ｗ             PIC 9(7)  VALUE ZERO.
002450*     */国保７割用
002220       05 国保７件数Ｗ                 PIC 9(3)  VALUE ZERO.
002240       05 国保７負担額Ｗ               PIC 9(7)  VALUE ZERO.
002240       05 国保７費用額Ｗ               PIC 9(7)  VALUE ZERO.
002250       05 国保７請求額Ｗ               PIC 9(7)  VALUE ZERO.
002500*     */国保６歳未満用
002510       05 国保６歳未件数Ｗ             PIC 9(3)  VALUE ZERO.
002530       05 国保６歳未負担額Ｗ           PIC 9(7)  VALUE ZERO.
002530       05 国保６歳未費用額Ｗ           PIC 9(7)  VALUE ZERO.
002540       05 国保６歳未請求額Ｗ           PIC 9(7)  VALUE ZERO.
002650*     */退職本人用
002260       05 退本件数Ｗ                   PIC 9(3)  VALUE ZERO.
002280       05 退本負担額Ｗ                 PIC 9(7)  VALUE ZERO.
002280       05 退本費用額Ｗ                 PIC 9(7)  VALUE ZERO.
002290       05 退本請求額Ｗ                 PIC 9(7)  VALUE ZERO.
002650*     */退職家族用
002300       05 退家件数Ｗ                   PIC 9(3)  VALUE ZERO.
002320       05 退家負担額Ｗ                 PIC 9(7)  VALUE ZERO.
002320       05 退家費用額Ｗ                 PIC 9(7)  VALUE ZERO.
002330       05 退家請求額Ｗ                 PIC 9(7)  VALUE ZERO.
002650*     */退職６歳未満用
002660       05 退職６歳未件数Ｗ             PIC 9(3)  VALUE ZERO.
002680       05 退職６歳未負担額Ｗ           PIC 9(7)  VALUE ZERO.
002680       05 退職６歳未費用額Ｗ           PIC 9(7)  VALUE ZERO.
002690       05 退職６歳未請求額Ｗ           PIC 9(7)  VALUE ZERO.
002760*
002770    03 取引先情報Ｗ.
002780       05 取引先銀行名Ｗ.
002790          07 取引先銀行名Ｗ１          PIC X(14) VALUE SPACE.
002800          07 取引先銀行名Ｗ２          PIC X(14) VALUE SPACE.
002810          07 取引先銀行名Ｗ３          PIC X(14) VALUE SPACE.
002820       05 取引先銀行支店名Ｗ.
002830          07 取引先銀行支店名Ｗ１      PIC X(10) VALUE SPACE.
002840          07 取引先銀行支店名Ｗ２      PIC X(10) VALUE SPACE.
002850          07 取引先銀行支店名Ｗ３      PIC X(10) VALUE SPACE.
002860       05 預金種別Ｗ                   PIC 9(1)  VALUE ZERO.
002870       05 口座番号Ｗ                   PIC X(10) VALUE SPACE.
002880       05 口座名義人Ｗ                 PIC X(40) VALUE SPACE.
002890       05 預金種別コメントＷ           PIC N(3)  VALUE SPACE.
002900 01 頁数Ｗ                             PIC 9(1)  VALUE ZERO.
002910 01 全頁Ｗ                             PIC 9(1)  VALUE ZERO.
002920 01 請求先名称Ｗ.
002930    03 印刷請求先名称Ｗ                PIC X(30) VALUE SPACE.
002940    03 FILLER                          PIC X(2)  VALUE SPACE.
002950 01 請求先名称ＷＰ                     PIC X(32) VALUE SPACE.
002960 01 ワーク                             PIC X(2)  VALUE SPACE.
002970 01 保険者番号Ｗ.
002980    03 保険者法別番号Ｗ                PIC X(2)  VALUE SPACE.
002990    03 保険者番号１Ｗ.
003000       05 保険者県コードＷ             PIC X(2)  VALUE SPACE.
003010       05 保険者番号２Ｗ               PIC X(6)  VALUE SPACE.
003020 01 保険番号Ｗ                         PIC X(3)  VALUE SPACE.
003030 01 保険種別Ｗ                         PIC 9(2)  VALUE ZERO.
003040 01 公費種別Ｗ                         PIC 9(2)  VALUE ZERO.
003050 01 本人家族区分Ｗ                     PIC 9(1)  VALUE ZERO.
003060 01 費用負担者番号Ｗ                   PIC X(10) VALUE SPACE.
003070 01 給付割合Ｗ                         PIC 9(2)  VALUE ZERO.
003080 01 県コードＪＩＳＷ                   PIC X(2)  VALUE SPACE.
003090 01 県コード政管Ｗ                     PIC X(2)  VALUE SPACE.
003100 01 県名Ｗ                             PIC N(5)  VALUE SPACE.
003110 01 保険者宛先Ｗ                       PIC N(3)  VALUE SPACE.
003120 01 結合保険者名称Ｗ.
003130    03 結合印刷保険者名称Ｗ            PIC N(20) VALUE SPACE.
003140 01 分割保険者名称Ｗ.
003150    03 印刷保険者名称１Ｗ              PIC X(16) VALUE SPACE.
003160    03 印刷保険者名称２Ｗ              PIC X(16) VALUE SPACE.
003170 01 施術月区分Ｗ                       PIC 9     VALUE ZERO.
003380*
003390 01 印刷制御.
003400     03 定義体名Ｐ                     PIC X(8) VALUE SPACE.
003410     03 項目群名Ｐ                     PIC X(8) VALUE SPACE.
003420     03 処理種別Ｐ                     PIC X(2) VALUE SPACE.
003430     03 拡張制御Ｐ.
003440         05 端末制御Ｐ.
003450             07 移動方向Ｐ             PIC X(1).
003460             07 移動行数Ｐ             PIC 9(3).
003470         05 詳細制御Ｐ                 PIC X(2).
003480     03 通知情報Ｐ                     PIC X(2) VALUE SPACE.
003490     03 ユニット名Ｐ                   PIC X(8) VALUE SPACE.
003500 01 計算機西暦年Ｗ                     PIC 9(2).
003510* 日付ＷＯＲＫ
003520 01 和暦終了年Ｗ                       PIC 9(4).
003530 01 計算機和暦年Ｗ                     PIC 9(2).
003540 01 計算機西暦.
003550    03 計算機西暦年                    PIC 9(4).
003560    03 計算機西暦月日                  PIC 9(4).
003570 01 計算機西暦Ｒ REDEFINES 計算機西暦.
003580    03 計算機世紀                      PIC 9(2).
003590    03 計算機日付                      PIC 9(6).
003600    03 計算機日付Ｒ REDEFINES 計算機日付.
003610       05 計算機年月                   PIC 9(4).
003620       05 計算機年月Ｒ REDEFINES 計算機年月.
003630         07 計算機年                   PIC 9(2).
003640         07 計算機月                   PIC 9(2).
003650       05 計算機日                     PIC 9(2).
003660*
003670******************************************************************
003680*                          連結項目                              *
003690******************************************************************
002360*
002370 01 連メ−キー IS EXTERNAL.
002380    03  連メ−メッセージ                 PIC N(20).
003700*
003710****************
003720* 画面入力情報 *
003730****************
       01 連入−画面情報ＹＡＩ５８０ IS EXTERNAL.
          03 連入−請求和暦年月.
             05 連入−請求和暦               PIC 9(1).
             05 連入−請求年月.
                07 連入−請求年              PIC 9(2).
                07 連入−請求月              PIC 9(2).
          03 連入−作成和暦年月日.
             05 連入−作成和暦年月.
                07 連入−作成和暦            PIC 9(1).
                07 連入−作成年              PIC 9(2).
                07 連入−作成月              PIC 9(2).
             05 連入−作成日                 PIC 9(2).
       01 連入−入力データＹＡＩ５８０   IS EXTERNAL.
          03 連入−保険種別                  PIC 9(2).
          03 連入−保険者番号                PIC X(10).
004250*
       01 連印−プレビュー IS EXTERNAL.
          03 連印−プレビュー区分          PIC 9.
010440*
000540************************************
000550* プリンタファイル作成用           *
000560************************************
000570 01 Ｈ連ＰＲＴＦ−作成データ IS EXTERNAL.
000580     03 Ｈ連ＰＲＴＦ−ファイル名           PIC X(8).
000590     03 Ｈ連ＰＲＴＦ−プレビュー区分       PIC 9.
000600     03 Ｈ連ＰＲＴＦ−帳票プログラム名     PIC X(8).
000610     03 Ｈ連ＰＲＴＦ−オーバレイ名         PIC X(8).
003190*
004260******************************************************************
004270*                      PROCEDURE  DIVISION                       *
004280******************************************************************
004290 PROCEDURE               DIVISION.
004300************
004310*           *
004320* 初期処理   *
004330*           *
004340************
002570     PERFORM プリンタファイル作成.
004350     PERFORM 初期化.
004360************
004370*           *
004380* 主処理     *
004390*           *
004400************
004410     PERFORM 印刷処理.
004420************
004430*           *
004440* 終了処理   *
004450*           *
004460************
004470     PERFORM 終了処理.
004480     EXIT PROGRAM.
004490*
004500*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002860*================================================================*
002870 プリンタファイル作成 SECTION.
002880*================================================================*
002890*   / 初期化 /
002900     MOVE SPACE TO Ｈ連ＰＲＴＦ−作成データ.
002910     INITIALIZE Ｈ連ＰＲＴＦ−作成データ.
002920*
002930*
002940*--↓↓ 変更箇所 ↓↓--------------------------------------*
002970*   使用するプリンタファイル名セット
002971     MOVE "PRTF001"             TO Ｈ連ＰＲＴＦ−ファイル名.
002972*
002973*   使用する帳票プログラム名セット
002974     MOVE "YAI592"             TO Ｈ連ＰＲＴＦ−帳票プログラム名.
002975*
002976*--↑↑-----------------------------------------------------*
002980*
002990*   / プレビュー区分セット /
003000     MOVE 連印−プレビュー区分  TO Ｈ連ＰＲＴＦ−プレビュー区分.
003010*
003020     CALL   "CRTPRTF".
003030     CANCEL "CRTPRTF".
003040*
004510*================================================================*
004520 初期化 SECTION.
004530*
004540     OPEN INPUT 元号マスタ.
004550             MOVE NC"元号" TO ファイル名Ｗ.
004560             PERFORM オープンチェック.
004600     OPEN INPUT 施術所情報マスタ.
004610             MOVE NC"施情" TO ファイル名Ｗ.
004620             PERFORM オープンチェック.
004630     OPEN INPUT 保険者マスタ.
004640         MOVE NC"保険者マスタ" TO ファイル名Ｗ.
004650         PERFORM オープンチェック.
004720     OPEN INPUT ＩＤ管理マスタ
004730         MOVE NC"ＩＤ" TO ファイル名Ｗ.
004740         PERFORM オープンチェック.
004780*
004940     PERFORM 連結項目待避.
004950*
004960*================================================================*
004970 オープンチェック SECTION.
004980*
004990     IF 状態キー  NOT =  "00"
005000         DISPLAY ファイル名Ｗ NC"Ｆオープンエラー" UPON CONS
005010         DISPLAY NC"状態キー：" 状態キー           UPON CONS
005020         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
005030                                                   UPON CONS
003630*-----------------------------------------*
003640         CALL "actcshm"  WITH C LINKAGE
003650*-----------------------------------------*
005040         ACCEPT  キー入力 FROM CONS
005050         PERFORM ファイル閉鎖
005060         EXIT PROGRAM.
005480*================================================================*
005490 施術所情報マスタ読込 SECTION.
005500*
005510     MOVE ZERO TO 施情−施術所番号.
005520     READ 施術所情報マスタ
005530     INVALID KEY
005540         MOVE NC"施情" TO ファイル名Ｗ
005550         PERFORM エラー表示Ｒ
005560         PERFORM ファイル閉鎖
005570         MOVE 99 TO PROGRAM-STATUS
005580         EXIT PROGRAM
005590     NOT INVALID KEY
005600         MOVE 施情−新柔整師番号     TO 登録番号コードＷ
005610*         STRING 施情−住所１  DELIMITED BY SPACE
005620*                施情−住所２  DELIMITED BY SPACE
005630*           INTO 施術所住所Ｗ
005640*         END-STRING
005650         MOVE 施情−住所１           TO 施術所住所１Ｗ
005660         MOVE 施情−住所２           TO 施術所住所２Ｗ
005670         MOVE 施情−接骨院名         TO 接骨院名Ｗ
005680         MOVE 施情−代表者名         TO 代表者名Ｗ
005690         MOVE 施情−電話番号         TO 施術所電話番号Ｗ
005700         MOVE 施情−接骨師会会員番号 TO 会員番号ＷＰ
005720     END-READ.
005730*
005740*********************************************
005750** ＩＤ管理マスタより　施術ＩＤを取得する。
005760*********************************************
005770     MOVE 01                   TO ＩＤ管−ＩＤ区分.
005780     MOVE ZERO                 TO ＩＤ管−施術所番号.
005790     MOVE 23                   TO ＩＤ管−保険種別.
005800     MOVE SPACE                TO ＩＤ管−保険者番号.
005810     READ ＩＤ管理マスタ
005820     INVALID KEY
005830         MOVE SPACE                  TO 施術所コードＷ
005840     NOT INVALID KEY
005850         MOVE ＩＤ管−施術ＩＤ番号   TO 施術所コードＷ
005860     END-READ.
005870*
005920*================================================================*
005930 ファイル閉鎖 SECTION.
005940*
003720     IF ( オープンフラグ = "YES" )
003730         CLOSE 印刷ファイル
003740     ELSE
003750         MOVE  NC"　　データが０件です。確認して下さい。" TO 連メ−メッセージ
003760         CALL   "MSG001"
003770         CANCEL "MSG001"
003780     END-IF.
003790*
005950     CLOSE  元号マスタ 施術所情報マスタ ＩＤ管理マスタ 保険者マスタ.
005980*================================================================*
005990 終了処理 SECTION.
006000*
006010     PERFORM ファイル閉鎖.
006020*================================================================*
006030 連結項目待避 SECTION.
006040*
006050     MOVE 連入−請求年     TO 施術年Ｗ.
006060     MOVE 連入−請求月     TO 施術月Ｗ.
006070     MOVE 連入−作成年     TO 請求年Ｗ.
006080     MOVE 連入−作成月     TO 請求月Ｗ.
006090     MOVE 連入−作成日     TO 請求日Ｗ.
006100     MOVE 連入−保険者番号 TO 保険者番号Ｗ.
006130*
006140*================================================================*
006150 印刷処理 SECTION.
006160*
006170     PERFORM 施術所情報マスタ読込.
006180     OPEN INPUT 作業ファイル１.
006190         MOVE NC"作１" TO ファイル名Ｗ.
006200         PERFORM オープンチェック.
006210     MOVE 1     TO 保険種別Ｗ.
006220     MOVE SPACE TO 終了フラグ２.
006230     MOVE 保険者番号Ｗ   TO 作１−保険者番号.
006300     MOVE ZERO           TO 作１−印刷区分
006310     START 作業ファイル１ KEY IS >= 作１−保険者番号
006390                                    作１−印刷区分
006400     END-START.
006410     IF 状態キー = "00"
006420         PERFORM 作業ファイル１読込
006430         MOVE SPACE TO 合計金額Ｗ
006440         INITIALIZE    合計金額Ｗ
006450         MOVE 作１−保険者番号     TO 保険者番号Ｗ
006480         PERFORM UNTIL (作１−保険者番号 NOT = 保険者番号Ｗ) OR
006490                       (終了フラグ２     NOT = SPACE)
006520             PERFORM 集計処理１
006530             PERFORM 作業ファイル１読込
006540         END-PERFORM
006550         PERFORM 印刷レコードセット
006560         PERFORM 明細印刷処理
006570     END-IF
006580     CLOSE 作業ファイル１.
006590*================================================================*
006600 作業ファイル１読込 SECTION.
006610*
006620     READ 作業ファイル１ NEXT
006630     AT END
006640         MOVE "YES" TO 終了フラグ２
006650         INITIALIZE 作１−レコード
006660     END-READ.
006670*================================================================*
006680 集計処理１ SECTION.
006690*
006900     EVALUATE 作１−印刷区分
006910     WHEN 1
006930         MOVE 作１−件数   TO 国保高齢１件数Ｗ
006940         MOVE 作１−負担額 TO 国保高齢１負担額Ｗ
006950         MOVE 作１−費用額 TO 国保高齢１費用額Ｗ
006960         MOVE 作１−請求額 TO 国保高齢１請求額Ｗ
006970     WHEN 2
006990         MOVE 作１−件数   TO 国保高齢３件数Ｗ
007000         MOVE 作１−負担額 TO 国保高齢３負担額Ｗ
007010         MOVE 作１−費用額 TO 国保高齢３費用額Ｗ
007020         MOVE 作１−請求額 TO 国保高齢３請求額Ｗ
007030     WHEN 3
007120         MOVE 作１−件数   TO 国保１０件数Ｗ
007130         MOVE 作１−負担額 TO 国保１０負担額Ｗ
007140         MOVE 作１−費用額 TO 国保１０費用額Ｗ
007150         MOVE 作１−請求額 TO 国保１０請求額Ｗ
007030     WHEN 4
007170         MOVE 作１−件数   TO 国保７件数Ｗ
007180         MOVE 作１−負担額 TO 国保７負担額Ｗ
007190         MOVE 作１−費用額 TO 国保７費用額Ｗ
007200         MOVE 作１−請求額 TO 国保７請求額Ｗ
007030     WHEN 5
007050         MOVE 作１−件数   TO 国保６歳未件数Ｗ
007060         MOVE 作１−負担額 TO 国保６歳未負担額Ｗ
007070         MOVE 作１−費用額 TO 国保６歳未費用額Ｗ
007080         MOVE 作１−請求額 TO 国保６歳未請求額Ｗ
007090     WHEN 6
007620         MOVE 作１−件数   TO 退本件数Ｗ
007630         MOVE 作１−負担額 TO 退本負担額Ｗ
007640         MOVE 作１−費用額 TO 退本費用額Ｗ
007650         MOVE 作１−請求額 TO 退本請求額Ｗ
007090     WHEN 7
007670         MOVE 作１−件数   TO 退家件数Ｗ
007680         MOVE 作１−負担額 TO 退家負担額Ｗ
007690         MOVE 作１−費用額 TO 退家費用額Ｗ
007700         MOVE 作１−請求額 TO 退家請求額Ｗ
007530     WHEN 8
007550         MOVE 作１−件数   TO 退職６歳未件数Ｗ
007560         MOVE 作１−負担額 TO 退職６歳未負担額Ｗ
007570         MOVE 作１−費用額 TO 退職６歳未費用額Ｗ
007580         MOVE 作１−請求額 TO 退職６歳未請求額Ｗ
007740     END-EVALUATE.
007750*================================================================*
007760 印刷レコードセット SECTION.
007770*
007780     MOVE SPACE TO YAI592P.
007790     INITIALIZE YAI592P.
007800     PERFORM 請求先情報取得.
008010*
008020     MOVE 請求先名称Ｗ     TO 保険者名.
008030*     MOVE 請求先名称ＷＰ   TO 保険者名.
      */元号修正/↓↓↓20190514
037370     IF 連入−請求和暦 > 4
              MOVE 連入−請求和暦     TO 元−元号区分
037380        READ 元号マスタ
037390        NOT INVALID KEY
037400            MOVE 元−元号名称   TO 施術和暦
037410        END-READ
              MOVE "===="             TO 施術和暦訂正
           END-IF.
      */元号修正/↑↑↑20190514
008040     MOVE 施術年Ｗ         TO 施術年.
008050     MOVE 施術月Ｗ         TO 施術月.
      */元号修正/↓↓↓20190514
037370     IF 連入−作成和暦 > 4
              MOVE 連入−作成和暦     TO 元−元号区分
037380        READ 元号マスタ
037390        NOT INVALID KEY
037400            MOVE 元−元号名称   TO 請求和暦
037410        END-READ
              MOVE "===="             TO 請求和暦訂正
           END-IF.
      */元号修正/↑↑↑20190514
008060     MOVE 請求年Ｗ         TO 請求年.
008070     MOVE 請求月Ｗ         TO 請求月.
008080     MOVE 請求日Ｗ         TO 請求日.
008090     MOVE 保険者番号Ｗ     TO 保険者番号.
008100*     MOVE 登録番号コードＷ TO 契約番号.
008110*     MOVE 会員番号Ｗ       TO 会員番号.
008120     MOVE 施術所コードＷ   TO 機関コード.
008130     MOVE 施術所住所１Ｗ   TO 住所１.
008140     MOVE 施術所住所２Ｗ   TO 住所２.
008150     MOVE 接骨院名Ｗ       TO 接骨院名.
008160     MOVE 代表者名Ｗ       TO 代表者名.
008170     MOVE 施術所電話番号Ｗ TO 電話番号.
008180*     MOVE 印刷枚数ＣＮＴ   TO 頁数Ｗ.
008430     MOVE 国保高齢１件数Ｗ   TO 国保高齢１件数  .
008440     MOVE 国保高齢１負担額Ｗ TO 国保高齢１負担額  .
008450     MOVE 国保高齢１費用額Ｗ TO 国保高齢１費用額.
008460     MOVE 国保高齢１請求額Ｗ TO 国保高齢１請求額.
008470     MOVE 国保高齢３件数Ｗ   TO 国保高齢３件数  .
008480     MOVE 国保高齢３負担額Ｗ TO 国保高齢３負担額.
008490     MOVE 国保高齢３費用額Ｗ TO 国保高齢３費用額.
008500     MOVE 国保高齢３請求額Ｗ TO 国保高齢３請求額.
008190     MOVE 国保１０件数Ｗ     TO 国保１０件数  .
008200     MOVE 国保１０負担額Ｗ   TO 国保１０負担額  .
008210     MOVE 国保１０費用額Ｗ   TO 国保１０費用額.
008220     MOVE 国保１０請求額Ｗ   TO 国保１０請求額.
008250     MOVE 国保７件数Ｗ       TO 国保７件数  .
008260     MOVE 国保７負担額Ｗ     TO 国保７負担額.
008270     MOVE 国保７費用額Ｗ     TO 国保７費用額.
008280     MOVE 国保７請求額Ｗ     TO 国保７請求額.
008510     MOVE 国保６歳未件数Ｗ   TO 国保６歳未件数  .
008520     MOVE 国保６歳未負担額Ｗ TO 国保６歳未負担額.
008530     MOVE 国保６歳未費用額Ｗ TO 国保６歳未費用額.
008540     MOVE 国保６歳未請求額Ｗ TO 国保６歳未請求額.
008290     MOVE 退本件数Ｗ         TO 退職本人件数  .
008300     MOVE 退本負担額Ｗ       TO 退職本人負担額.
008310     MOVE 退本費用額Ｗ       TO 退職本人費用額.
008320     MOVE 退本請求額Ｗ       TO 退職本人請求額.
008330     MOVE 退家件数Ｗ         TO 退職家族件数  .
008340     MOVE 退家負担額Ｗ       TO 退職家族負担額.
008350     MOVE 退家費用額Ｗ       TO 退職家族費用額.
008360     MOVE 退家請求額Ｗ       TO 退職家族請求額.
008640     MOVE 退職６歳未件数Ｗ   TO 退職６歳未件数  .
008650     MOVE 退職６歳未負担額Ｗ TO 退職６歳未負担額.
008660     MOVE 退職６歳未費用額Ｗ TO 退職６歳未費用額.
008670     MOVE 退職６歳未請求額Ｗ TO 退職６歳未請求額.
008680*
008740*================================================================*
008750 請求先情報取得 SECTION.
008760*
008770*********************************************************
008780* 連結データから保険者マスタより請求先を取得する。      *
008790* ● 請求先...... 請求先名称Ｗに格納                    *
008800*********************************************************
008810     MOVE 01               TO 保−保険種別.
008820     MOVE 保険者番号Ｗ     TO 保−保険者番号.
008830     READ 保険者マスタ
008840     INVALID KEY
008850         MOVE SPACE           TO 請求先名称Ｗ
008860     NOT INVALID KEY
008870*         MOVE 保−保険者名称  TO 請求先名称Ｗ
008880         IF 保険者番号Ｗ(3:1) NOT = "3"
008890            STRING 保−保険者名称     DELIMITED BY SPACE
008900                   "長"               DELIMITED BY SIZE
008910                   INTO 請求先名称Ｗ
008920            END-STRING
008930         ELSE
008940*/ (国保組合)
008950             IF 保険者番号Ｗ(1:3)  = "063"
008960                STRING 保−保険者名称     DELIMITED BY SPACE
008970                       "理事長"           DELIMITED BY SIZE
008980                       INTO 請求先名称Ｗ
008990                END-STRING
009000             ELSE
009010                MOVE 保−保険者名称    TO 請求先名称Ｗ
009020             END-IF
009030         END-IF
009040     END-READ.
009200*================================================================*
009210 明細印刷処理 SECTION.
009220*
005140     IF ( オープンフラグ NOT = "YES" )
005150        MOVE "YES" TO オープンフラグ
005160        OPEN I-O  印刷ファイル
005170        PERFORM エラー処理Ｐ
005180     END-IF.
006020*
009230     MOVE "YAI592P"  TO  定義体名Ｐ.
009240     MOVE "SCREEN"   TO  項目群名Ｐ.
009250     WRITE YAI592P.
009260     PERFORM エラー処理Ｐ.
009270*================================================================*
009280 エラー表示 SECTION.
009290*
009300     DISPLAY NC"ファイル書込エラー：" ファイル名Ｗ UPON CONS.
009310     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
009320     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
003930*-----------------------------------------*
003940     CALL "actcshm"  WITH C LINKAGE.
003950*-----------------------------------------*
009330     ACCEPT  キー入力 FROM CONS.
009340*================================================================*
009350 エラー処理Ｐ SECTION.
009360*
009370     IF 通知情報Ｐ NOT = "00"
009380         DISPLAY NC"帳票エラー"              UPON CONS
009390         DISPLAY NC"項目群名Ｐ：" 項目群名Ｐ UPON CONS
009400         DISPLAY NC"通知情報Ｐ：" 通知情報Ｐ UPON CONS
009410         DISPLAY NC"拡張制御Ｐ：" 拡張制御Ｐ UPON CONS
009420         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
009430                                             UPON CONS
007140*-----------------------------------------*
007150         CALL "actcshm"  WITH C LINKAGE
007160*-----------------------------------------*
009440         ACCEPT  キー入力 FROM CONS
009450         PERFORM ファイル閉鎖
009460         MOVE 99 TO PROGRAM-STATUS
009470         EXIT PROGRAM
009480     END-IF.
009490*================================================================*
009500 エラー表示Ｒ SECTION.
009510*
009520     DISPLAY NC"ファイル読込エラー" ファイル名Ｗ UPON CONS.
009530     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
003930*-----------------------------------------*
003940     CALL "actcshm"  WITH C LINKAGE.
003950*-----------------------------------------*
009540     ACCEPT  キー入力 FROM CONS.
009550     PERFORM ファイル閉鎖.
009560     EXIT PROGRAM.
009570*================================================================*
009580 エラー表示その他 SECTION.
009590*
009600     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
009610     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
009620                                                   UPON CONS.
003930*-----------------------------------------*
003940     CALL "actcshm"  WITH C LINKAGE.
003950*-----------------------------------------*
009630     ACCEPT  キー入力 FROM CONS.
009640     PERFORM ファイル閉鎖.
009650     EXIT PROGRAM.
009660*================================================================*
009670******************************************************************
009680 END PROGRAM YAI592.
009690******************************************************************
