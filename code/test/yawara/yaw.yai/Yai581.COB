000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YAI581.
000060 AUTHOR.                 池田  幸子
000070*
000080*----------------------------------------------------------------*
000090*   【柔+ｳｨﾝﾄﾞｳｽﾞ版　各種保険取扱表】
000100*   【データ作成】
000110*         MED = YAI580 YAI582P 
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2013-06-26
000140 DATE-COMPILED.          2013-06-26
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000270     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000280                             ORGANIZATION             IS  INDEXED
000290                             ACCESS MODE              IS  DYNAMIC
000300                             RECORD KEY               IS 受−施術和暦年月
000310                                                         受−患者コード
000320                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000330                                                         受−患者カナ
000340                                                         受−患者コード
000350                             ALTERNATE RECORD KEY     IS 受−患者コード
000360                                                         受−施術和暦年月
000370                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000380                                                         受−保険種別
000390                                                         受−保険者番号
000400                                                         受−患者コード
000410                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000420                                                         受−公費種別
000430                                                         受−費用負担者番号
000440                                                         受−患者コード
000450                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000460                                                         受−助成種別
000470                                                         受−費用負担者番号助成
000480                                                         受−患者コード
000490                             ALTERNATE RECORD KEY     IS 受−請求和暦年月
000500                                                         受−施術和暦年月
000510                                                         受−患者コード
000520                             FILE STATUS              IS 状態キー
000530                             LOCK        MODE         IS AUTOMATIC.
000130     SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
000140                             ORGANIZATION             IS  INDEXED
000150                             ACCESS MODE              IS  DYNAMIC
000160                             RECORD KEY               IS  レセ−施術和暦年月
000170                                                          レセ−患者コード
000180                                                          レセ−レセ種別
000190                             ALTERNATE RECORD KEY     IS  レセ−患者コード
000200                                                          レセ−施術和暦年月
000210                                                          レセ−レセ種別
000220                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000230                                                          レセ−施術和暦年月
000240                                                          レセ−患者コード
000250                                                          レセ−レセ種別
000260                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000270                                                          レセ−レセ種別
000280                                                          レセ−請求保険者番号
000290                                                          レセ−患者コード
000300                                                          レセ−施術和暦年月
000310                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000320                                                          レセ−請求保険者番号
000330                                                          レセ−患者コード
000340                                                          レセ−レセ種別
000350                                                          レセ−施術和暦年月
000360                             FILE STATUS              IS  状態キー
000370                             LOCK        MODE         IS  AUTOMATIC.
000720     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W5811L.DAT"
000730                             ORGANIZATION             IS  INDEXED
000740                             ACCESS                   IS  DYNAMIC
000750                             RECORD      KEY          IS  作１−保険種別
000780                                                          作１−県内外区分
000790                                                          作１−返戻区分
000800                             FILE        STATUS       IS  状態キー
000810                             LOCK        MODE         IS  AUTOMATIC.
000820******************************************************************
000830*                      DATA DIVISION                             *
000840******************************************************************
000850 DATA                    DIVISION.
000860 FILE                    SECTION.
000870*                           ［ＲＬ＝  ７６８］
000880 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
000890     COPY JUSINJ         OF  XFDLIB  JOINING    受   AS  PREFIX.
      *                          ［ＲＬ＝  １５３６］
       FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS.
           COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
000960*
000970 FD  作業ファイル１ RECORD  CONTAINS 16 CHARACTERS.
000980 01  作１−レコード.
000990   03  作１−レコードキー.
001020     05  作１−保険種別                PIC 9(2).
001030     05  作１−県内外区分              PIC 9(1).
001040     05  作１−返戻区分                PIC 9(1).
001050   03  作１−レコードデータ.
001060     05  作１−件数                    PIC 9(3).
001070     05  作１−費用額                  PIC 9(7).
001080     05  FILLER                        PIC X(2).
001090*
001100******************************************************************
001110*                WORKING-STORAGE SECTION                         *
001120******************************************************************
001130 WORKING-STORAGE         SECTION.
001140 01 キー入力                           PIC X(1) VALUE SPACE.
001150 01 状態キー                           PIC X(2) VALUE SPACE.
001170 01 終了フラグ                         PIC X(3) VALUE SPACE.
001190 01 ファイル名Ｗ                       PIC N(2) VALUE SPACE.
001220 01 実行キーＷ                         PIC X(4) VALUE SPACE.
001260*
001280 01 請求和暦年月Ｗ.
001290     03 請求和暦Ｗ                     PIC 9.
001300     03 請求年月Ｗ.
001310        05 請求年Ｗ                    PIC 9(2).
001320        05 請求月Ｗ                    PIC 9(2).
001350 01 元号名称Ｗ                         PIC N(2)  VALUE SPACE.
001370*
001380**************
001390* 保険者情報 *
001400**************
001410*
001660 01 請求和暦年月ＷＲ.
001670    03 請求和暦ＷＲ                  PIC 9     VALUE ZERO.
001680    03 請求年ＷＲ                    PIC 9(2)  VALUE ZERO.
001690    03 請求月ＷＲ                    PIC 9(2)  VALUE ZERO.
001700*
001710 01 保険種別Ｗ                       PIC 9(2) VALUE ZERO.
001740 01 県内外Ｗ                         PIC 9(1) VALUE ZERO.
001760*
001980******************************************************************
001990*                          連結項目                              *
002000******************************************************************
002010*
002020****************
002030* 画面入力情報 *
002040****************
002050 01 連入−画面情報ＹＡＩ５８０ IS EXTERNAL.
002060    03 連入−請求和暦年月.
002070       05 連入−請求和暦                PIC 9.
002080       05 連入−請求年月.
002090         07 連入−請求年                PIC 9(2).
002100         07 連入−請求月                PIC 9(2).
          03 連入−作成和暦年月日.
             05 連入−作成和暦年月.
                07 連入−作成和暦             PIC 9(1).
                07 連入−作成年               PIC 9(2).
                07 連入−作成月               PIC 9(2).
             05 連入−作成日                  PIC 9(2).
002110*
002440******************************************************************
002450*                      PROCEDURE  DIVISION                       *
002460******************************************************************
002470 PROCEDURE               DIVISION.
002480************
002490*           *
002500* 初期処理   *
002510*           *
002520************
002530     PERFORM 初期化.
002540************
002550*           *
002560* 主処理     *
002570*           *
002580************
002590     PERFORM 作業ファイル作成.
002600************
002610*           *
002620* 終了処理   *
002630*           *
002640************
002650     PERFORM 終了処理.
002660     MOVE ZERO TO PROGRAM-STATUS.
002670     EXIT PROGRAM.
002680*
002690*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002700*================================================================*
002710 初期化 SECTION.
002720*
002730     OPEN INPUT 受診者情報Ｆ.
002740             MOVE NC"受診" TO ファイル名Ｗ.
002750             PERFORM オープンチェック.
006630     OPEN INPUT レセプトＦ.
006640             MOVE NC"レセ" TO ファイル名Ｗ.
006650             PERFORM オープンチェック.
002820*
002830* 連結項目の待避
002840     MOVE 連入−請求和暦 TO 請求和暦ＷＲ.
002850     MOVE 連入−請求年   TO 請求年ＷＲ.
002860     MOVE 連入−請求月   TO 請求月ＷＲ.
002870*================================================================*
002880 オープンチェック SECTION.
002890*
002900     IF 状態キー  NOT =  "00"
002910         DISPLAY ファイル名Ｗ NC"Ｆオープンエラー" UPON CONS
002920         DISPLAY NC"状態キー：" 状態キー           UPON CONS
002930         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
002940                                                   UPON CONS
004380*-----------------------------------------*
004390         CALL "actcshm"  WITH C LINKAGE
004400*-----------------------------------------*
002950         ACCEPT  キー入力 FROM CONS
002960         PERFORM ファイル閉鎖
002970         EXIT PROGRAM.
002980*================================================================*
002990 ファイル閉鎖 SECTION.
003000*
003010     CLOSE  受診者情報Ｆ レセプトＦ.
003020*================================================================*
003030 終了処理 SECTION.
003040*
003050     PERFORM ファイル閉鎖.
003060*================================================================*
003070* データ作成 SECTION.
003080 作業ファイル作成 SECTION.
003090*
003100     OPEN OUTPUT 作業ファイル１.
003110         MOVE NC"作１" TO ファイル名Ｗ.
003120         PERFORM オープンチェック.
003130     CLOSE 作業ファイル１.
003140     OPEN I-O 作業ファイル１.
003150         MOVE NC"作１" TO ファイル名Ｗ.
003160         PERFORM オープンチェック.
003170*
004760     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
004770     MOVE 請求年ＷＲ    TO レセ−請求年.
004780     MOVE 請求月ＷＲ    TO レセ−請求月.
004790     MOVE ZERO          TO レセ−施術和暦.
004800     MOVE ZERO          TO レセ−施術年.
004810     MOVE ZERO          TO レセ−施術月.
004820     MOVE ZERO          TO レセ−患者番号.
004830     MOVE LOW-VALUE     TO レセ−枝番.
013730     MOVE ZERO          TO レセ−レセ種別.
004840     START レセプトＦ   KEY IS >= レセ−請求和暦年月
004850                                  レセ−施術和暦年月
004860                                  レセ−患者コード
000250                                  レセ−レセ種別
003290     END-START.
003300     IF 状態キー = "00"
003310         MOVE SPACE  TO 終了フラグ
003320         PERFORM レセプトＦ読込
003330         PERFORM UNTIL ( 終了フラグ = "YES" ) OR
004920                       ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
004930                       ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
004940                       ( レセ−請求月   NOT = 請求月ＷＲ   )
003370             PERFORM データチェック
003380***
003390             IF 実行キーＷ = "YES"
003400*
000487                 IF ( レセ−レセ種別   = 1 OR 2 OR 4 OR 7 )
003470                     MOVE SPACE TO 作１−レコード
003480                     INITIALIZE 作１−レコード
003490                     PERFORM 料金計算
003510                 END-IF
003710             END-IF
003720             PERFORM レセプトＦ読込
003730         END-PERFORM
003740     END-IF.
003750     CLOSE 作業ファイル１.
003760*================================================================*
003770 レセプトＦ読込 SECTION.
003780*
003790     READ レセプトＦ NEXT
003800     AT END
003810         MOVE "YES" TO 終了フラグ
003820     END-READ.
004070*================================================================*
004080 エラー表示 SECTION.
004090*
004100     DISPLAY NC"ファイル書込エラー：" ファイル名Ｗ UPON CONS.
004110     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
004120     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
004380*-----------------------------------------*
004390     CALL "actcshm"  WITH C LINKAGE.
004400*-----------------------------------------*
004130     ACCEPT  キー入力 FROM CONS.
004300*================================================================*
004310 データチェック SECTION.
004320*
004330     MOVE SPACE          TO 実行キーＷ.
019520* *****************************************************************
019530* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
019540* *****************************************************************
019640     IF レセ−請求対象区分 NOT = ZERO
004090         MOVE レセ−施術和暦  TO 受−施術和暦
004100         MOVE レセ−施術年    TO 受−施術年
004110         MOVE レセ−施術月    TO 受−施術月
004120         MOVE レセ−患者番号  TO 受−患者番号
004130         MOVE レセ−枝番      TO 受−枝番
               READ 受診者情報Ｆ
               NOT INVALID KEY
019880            MOVE "YES"  TO 実行キーＷ
               END-READ
019900     ELSE
019910         MOVE SPACE  TO 実行キーＷ
019950     END-IF.
004790*
004870*================================================================*
004880 料金計算 SECTION.
004890*
005400     IF レセ−請求区分 = 2
005410         MOVE  2           TO   作１−返戻区分
           ELSE
004900         MOVE  1           TO 作１−返戻区分
005430     END-IF.
004930     MOVE 受−保険種別     TO 作１−保険種別.
           IF 受−保険種別 = 6 OR 7
               MOVE 2            TO 作１−保険種別
           END-IF.
           IF 受−保険種別 = 9
               MOVE 4            TO 作１−保険種別
           END-IF.
004940     EVALUATE 受−保険種別
004950     WHEN 1
               IF 受−保険者番号(1:2) NOT = "23"
004960            MOVE ZERO      TO 作１−県内外区分
               ELSE
004960            MOVE 1         TO 作１−県内外区分
               END-IF
      */全国土木建築・全国左官タイル塗装業・全国板金業は組合に分類
               IF 受−保険者番号(1:6) = "133033" OR "133231" OR "133280"
                  MOVE 03        TO 作１−保険種別
004960            MOVE ZERO      TO 作１−県内外区分
               END-IF
      */中央建設・全国建設工事業は県内
               IF 受−保険者番号 = "133264" OR "133298"
004960            MOVE 1         TO 作１−県内外区分
               END-IF
005080     WHEN 8
               IF 受−保険者番号(3:2) NOT = "23"
004960            MOVE ZERO      TO 作１−県内外区分
               ELSE
004960            MOVE 1         TO 作１−県内外区分
               END-IF
005100     WHEN 5
               IF 受−費用負担者番号(3:2) NOT = "23"
004960            MOVE ZERO      TO 作１−県内外区分
               ELSE
004960            MOVE 1         TO 作１−県内外区分
               END-IF
005120     END-EVALUATE.
005200     READ 作業ファイル１
005210     INVALID KEY
005220         MOVE 1              TO 作１−件数 
005230         MOVE レセ−合計     TO 作１−費用額
005240         WRITE 作１−レコード
005250         INVALID KEY
005260             DISPLAY NC"状態キー" 状態キー
005270             MOVE NC"作１"  TO ファイル名Ｗ
005280             PERFORM エラー表示
005290         END-WRITE
005300     NOT INVALID KEY
005310         COMPUTE 作１−件数   = 作１−件数   + 1
005320         COMPUTE 作１−費用額 = 作１−費用額 + レセ−合計
005330         REWRITE 作１−レコード
005340         INVALID KEY
005350             DISPLAY NC"状態キー" 状態キー
005360             MOVE NC"作１"  TO ファイル名Ｗ
005370             PERFORM エラー表示
005380         END-REWRITE
005390     END-READ.
005400*     IF レセ−請求区分 = 2
005410*         MOVE  2               TO   作１−返戻区分
005420*         PERFORM 返戻料金計算
005430*     END-IF.
006290*================================================================*
006300 返戻料金計算 SECTION.
006310*
006320     READ 作業ファイル１
006330     INVALID KEY
006340         MOVE 1              TO 作１−件数 
006350         MOVE レセ−合計     TO 作１−費用額
006360         WRITE 作１−レコード
006370         INVALID KEY
006380             DISPLAY NC"状態キー" 状態キー
006390             MOVE NC"作１"  TO ファイル名Ｗ
006400             PERFORM エラー表示
006410         END-WRITE
006420     NOT INVALID KEY
006430         COMPUTE 作１−件数   = 作１−件数   + 1
006440         COMPUTE 作１−費用額 = 作１−費用額 + レセ−合計
006450         REWRITE 作１−レコード
006460         INVALID KEY
006470             DISPLAY NC"状態キー" 状態キー
006480             MOVE NC"作１"  TO ファイル名Ｗ
006490             PERFORM エラー表示
006500         END-REWRITE
006510     END-READ.
006750*================================================================*
006760******************************************************************
006770 END PROGRAM YAI581.
006780******************************************************************
