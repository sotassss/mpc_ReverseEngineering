000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YAI102.
000060 AUTHOR.                 山田 浩之
000070*
000080*----------------------------------------------------------------*
000090*      提出FPD作成【FPD書込】柔ｳｨﾝﾄﾞｳｽﾞ95版
000120*----------------------------------------------------------------*
000120 DATE-WRITTEN.           2010-04-08
000130 DATE-COMPILED.          2010-04-08
000150*----------------------------------------------------------------*
      */金属副子運動後療追加/20180612
      */明細書発行以降追加/20221117/池田
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000330     SELECT  制御情報マスタ  ASSIGN      TO        SEIGYOL
000340                             ORGANIZATION             IS  INDEXED
000350                             ACCESS MODE              IS  DYNAMIC
000360                             RECORD KEY               IS  制−制御区分
000370                             FILE STATUS              IS  状態キー
000380                             LOCK        MODE         IS  AUTOMATIC.
001090     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W1011L.DAT"
001100                             ORGANIZATION             IS  SEQUENTIAL
001110                             ACCESS                   IS  SEQUENTIAL
001120                             FILE        STATUS       IS  状態キー
001130                             LOCK        MODE         IS  AUTOMATIC.
001520     SELECT 作業ファイル２   ASSIGN      TO "C:\MAKISHISYS\YAWOBJ\TEMP\W1012L.DAT"
001530                             ORGANIZATION             IS  INDEXED
001540                             ACCESS MODE              IS  DYNAMIC
000861                             RECORD      KEY          IS  作２−保険者区分
000952                              FILE STATUS             IS  状態キー
001600                             LOCK        MODE         IS  AUTOMATIC.
001520     SELECT 作業ファイル３   ASSIGN      TO "C:\MAKISHISYS\YAWOBJ\TEMP\W1013L.DAT"
001530                             ORGANIZATION             IS  INDEXED
001540                             ACCESS MODE              IS  DYNAMIC
000861                             RECORD      KEY          IS  作３−番号
000952                              FILE STATUS             IS  状態キー
001600                             LOCK        MODE         IS  AUTOMATIC.
001520     SELECT 作業ファイル４   ASSIGN      TO "C:\MAKISHISYS\YAWOBJ\TEMP\W1014L.DAT"
001530                             ORGANIZATION             IS  INDEXED
001540                             ACCESS MODE              IS  DYNAMIC
000861                             RECORD      KEY          IS  作４−施術和暦年月
                                                                作４−患者コード
                                                                作４−レセ種別
000310                             ALTERNATE RECORD KEY     IS  作４−レコード区分
                                                                作４−提出区分
                                                                作４−保険者番号
                                                                作４−施術和暦年月
                                                                作４−患者コード
                                                                作４−レセ種別
000952                             FILE STATUS              IS  状態キー
001600                             LOCK        MODE         IS  AUTOMATIC.
000390*
000400*     SELECT 会提出ファイル   ASSIGN      TO       "A:\JBFDV2.dat"
001270     SELECT 会提出ファイル ASSIGN    TO    FD-NAME
000410                             ORGANIZATION             IS  LINE SEQUENTIAL
000420                             ACCESS MODE              IS  SEQUENTIAL
000430                             FILE STATUS              IS  状態キー
000440                             LOCK      MODE           IS  AUTOMATIC.
000450******************************************************************
000460*                      DATA DIVISION                             *
000470******************************************************************
000480 DATA                    DIVISION.
000490 FILE                    SECTION.
000500*
001400*                           ［ＲＬ＝  ２５６］
001410 FD  制御情報マスタ          BLOCK   CONTAINS   1   RECORDS.
001420     COPY SEIGYO          OF  XFDLIB  JOINING   制   AS  PREFIX.
000511*
001520 FD  作業ファイル１ RECORD  CONTAINS 20 CHARACTERS.
001530 01  作１−レコード.
           03 作１−レコードデータ.
               05 作１−レコード区分             PIC 9(2).
               05 作１−会員番号                 PIC 9(5).
               05 作１−請求年月                 PIC 9(6).
               05 作１−診療年月                 PIC 9(6).
               05 作１−レコードＩＤ             PIC X(1).
000583*
001520 FD 作業ファイル２ RECORD  CONTAINS 24 CHARACTERS.
001530 01 作２−レコード.
           03 作２−レコードデータ.
               05 作２−レコード区分             PIC 9(2).
               05 作２−保険者区分               PIC 9(2).
               05 作２−施術件数                 PIC 9(3).
               05 作２−施術料金                 PIC 9(7).
               05 作２−返戻件数                 PIC 9(3).
               05 作２−返戻金額                 PIC 9(7).
002352*
001520 FD 作業ファイル３ RECORD  CONTAINS 24 CHARACTERS.
001530 01 作３−レコード.
           03 作３−レコードデータ.
               05 作３−レコード区分             PIC 9(2).
               05 作３−番号                     PIC 9(2).
               05 作３−本人請求件数             PIC 9(3).
               05 作３−本人請求金額             PIC 9(7).
               05 作３−家族請求件数             PIC 9(3).
               05 作３−家族請求金額             PIC 9(7).
002352*
001520* FD 作業ファイル４ RECORD  CONTAINS 1777 CHARACTERS.
001520* FD 作業ファイル４ RECORD  CONTAINS 1784 CHARACTERS.
001520 FD 作業ファイル４ RECORD  CONTAINS 2023 CHARACTERS.
001530 01 作４−レコード.
000510     03 作４−レコードキー.
000520         05 作４−施術和暦年月.
000530             07 作４−施術和暦             PIC 9.
000540             07 作４−施術年月.
000550                 09 作４−施術年           PIC 9(2).
000560                 09 作４−施術月           PIC 9(2).
000570         05 作４−患者コード.
000580             07 作４−患者番号             PIC 9(6).
000590             07 作４−枝番                 PIC X.
               05 作４−レセ種別                 PIC 9(2).
           03 作４−レコードデータ.
               05 作４−レコード区分             PIC 9(2).
               05 作４−提出区分                 PIC 9(1).
               05 作４−保険者番号               PIC X(8).
               05 作４−保険証記号.
                   07 作４−保険証記号Ｎ         PIC N(10).
               05 作４−保険証番号               PIC X(16).
               05 作４−老人医療助成区分         PIC 9(1).
               05 作４−市町村番号               PIC X(8).
               05 作４−受給者番号               PIC X(16).
               05 作４−本人家族区分             PIC 9(1).
               05 作４−医療助成区分             PIC 9(1).
               05 作４−被保険者カナ             PIC X(20).
               05 作４−被保険者氏名.
                   07 作４−被保険者氏名Ｎ       PIC N(16).
               05 作４−患者カナ                 PIC X(20).
               05 作４−患者氏名.
                   07 作４−患者氏名Ｎ           PIC N(16).
               05 作４−患者性別                 PIC 9(1).
               05 作４−患者生年月日             PIC 9(8).
               05 作４−合計金額                 PIC 9(7).
               05 作４−給付割合                 PIC 9(2).
               05 作４−一部負担金               PIC 9(6).
               05 作４−請求金額                 PIC 9(6).
               05 作４−診療年月                 PIC 9(6).
               05 作４−部位数                   PIC 9(1).
               05 作４−実日数                   PIC 9(3).
               05 作４−端末区分                 PIC 9(2).
               05 作４−給付分類                 PIC X(1).
               05 作４−新規区分                 PIC 9(1).
               05 作４−継続区分                 PIC 9(1).
               05 作４−初検回数                 PIC 9(1).
               05 作４−初検時間外加算回数       PIC 9(1).
               05 作４−初検休日加算回数         PIC 9(1).
               05 作４−初検深夜加算回数         PIC 9(1).
               05 作４−再検回数                 PIC 9(1).
               05 作４−往療距離                 PIC 9(3).
               05 作４−往療回数                 PIC 9(2).
               05 作４−夜間加算往療回数         PIC 9(1).
               05 作４−難路加算往療回数         PIC 9(2).
               05 作４−暴風雨雪加算往療回数     PIC 9(2).
000561         05 作４−金属副子大回数           PIC 9.
000562         05 作４−金属副子中回数           PIC 9.
000563         05 作４−金属副子小回数           PIC 9.
000564         05 作４−情報提供料回数           PIC 9.
000566         05 作４−負傷部位データ  OCCURS 8.
000567             07 作４−負傷区分             PIC 9.
                   07 作４−会部位コード.
                       09 作４−負傷名コード     PIC 9(3).
                       09 作４−細部位コード     PIC 9(2).
                       09 作４−左右区分         PIC 9(1).
000568             07 作４−負傷名               PIC N(16).
000569             07 作４−初回処置回数         PIC 9.
                   07 作４−負傷原因.
                       09 作４−負傷原因Ｎ       PIC N(20).
                   07 作４−長期理由.
                       09 作４−長期理由Ｎ       PIC N(30).
000570             07 作４−負傷年月日           PIC 9(8).
000571             07 作４−初検年月日           PIC 9(8).
000572             07 作４−施術開始年月日       PIC 9(8).
000573             07 作４−施術終了年月日       PIC 9(8).
000574             07 作４−部位実日数           PIC 9(2).
000575             07 作４−転帰区分             PIC 9.
000576             07 作４−後療回数             PIC 9(2).
000577             07 作４−冷罨法回数           PIC 9.
000578             07 作４−温罨法回数           PIC 9(2).
000579             07 作４−電療回数             PIC 9(2).
000580             07 作４−多部位逓減区分       PIC 9.
000581             07 作４−長期逓減区分         PIC 9.
                   07 作４−当部位費用額         PIC 9(6).
               05 作４−相談支援回数             PIC 9.
      */金属副子運動後療追加/20180612
               05 作４−金属副子回数             PIC 9.
               05 作４−運動後療料回数           PIC 9.
               05 作４−運動後療料               PIC 9(5).
      */明細書発行以降追加/20221117
               05 作４−明細書発行回数           PIC 9.
               05 作４−明細書発行料             PIC 9(3).
               05 作４−明細書発行月日           PIC 9(4).
002210         05 作４−日.
002210             07 作４−施術日               PIC X(1) OCCURS 31.
002210         05 作４−初検料                   PIC 9(5).
002210         05 作４−初検加算                 PIC 9(5).
002210         05 作４−相談支援料               PIC 9(5).
002210         05 作４−再検料                   PIC 9(5).
002210         05 作４−往療料                   PIC 9(5).
002210         05 作４−往療加算                 PIC 9(5).
000561         05 作４−金属副子                 PIC 9(5).
002210         05 作４−情報提供料               PIC 9(5).
002760         05 作４−部位データ  OCCURS 8.
                   07 作４−後療料               PIC 9(5).
                   07 作４−冷罨法料             PIC 9(5).
                   07 作４−温罨法料             PIC 9(5).
                   07 作４−電療料               PIC 9(5).
001320*
001330* FD  会提出ファイル RECORD IS VARYING IN SIZE FROM 1 TO 1763 DEPENDING ON 文字ＣＮＴ.
001330* FD  会提出ファイル RECORD IS VARYING IN SIZE FROM 1 TO 1770 DEPENDING ON 文字ＣＮＴ.
001330 FD  会提出ファイル RECORD IS VARYING IN SIZE FROM 1 TO 2009 DEPENDING ON 文字ＣＮＴ.
001340 01  会提−レコード.
001350*     03  会提−レコードデータ PIC X(1763).
      */仕様変更/20180612
001350*     03  会提−レコードデータ PIC X(1770).
      */仕様変更/20221117
001350     03  会提−レコードデータ PIC X(2009).
001980*
001990*----------------------------------------------------------------*
002000******************************************************************
002010*                WORKING-STORAGE SECTION                         *
002020******************************************************************
002030 WORKING-STORAGE         SECTION.
002040 01 キー入力                           PIC X    VALUE SPACE.
002050 01 状態キー                           PIC X(2) VALUE SPACE.
002060 01 終了フラグ                         PIC X(3) VALUE SPACE.
002070 01 終了フラグ２                       PIC X(3) VALUE SPACE.
002080 01 実行キーＷ                         PIC X(3)  VALUE SPACE.
002090 01 施術記録有Ｗ                       PIC X(3) VALUE SPACE.
002100 01 ファイル名                         PIC N(8) VALUE SPACE.
002110 01 カウンタ                           PIC 9(1) VALUE ZERO.
002111 01 連番Ｗ                             PIC 9(4) VALUE ZERO.
       01 文字ＣＮＴ                         PIC 9(4)  VALUE ZERO.
002120**
002130** エラーメッセージ用
002140 01 エラーメッセージＷ.
002150    03 エラー患者コードＷ              PIC X(7) VALUE SPACE.
002160    03 エラー区切りＷ                  PIC X(1) VALUE SPACE.
002170    03 エラー保険種別Ｗ                PIC X(2) VALUE SPACE.
002180    03 FILLER                          PIC X(10) VALUE SPACE.
002181*
002250* フロッピーパス用
002251 01 定期修正ＦＤＷ                     PIC X VALUE SPACE.
002252 01 FD-NAME                            PIC X(30) VALUE SPACE.
002190******************************************************************
002200*                          連結項目                              *
002210******************************************************************
      *
       01 連入−画面情報ＹＡＩ１００ IS EXTERNAL.
          03 連入−定期修正ＦＤ              PIC X.
002220*
002230********************
002240* メッセージ表示キー *
002250********************
002260 01 連メ−キー IS EXTERNAL.
002270    03  連メ−メッセージ               PIC N(20).
004090*
004091 01 連メ７−キー IS EXTERNAL.
004092    03  連メ７−メッセージ１               PIC X(40).
004102    03  連メ７−メッセージ２               PIC X(40).
      *
      *0:通常 1:固定パス
       01 連入−固定フラグ３２１１ IS EXTERNAL.
          03 連入−固定フラグ                PIC 9(1).
002320*
002330******************************************************************
002340*                      PROCEDURE  DIVISION                       *
002350******************************************************************
002360 PROCEDURE               DIVISION.
002370************
002380*           *
002390* 初期処理   *
002400*           *
002410************
002420     PERFORM 初期化.
002430************
002440*           *
002450* 主処理     *
002460*           *
002470************
002480     PERFORM ヘッダレコード作成.
002480     PERFORM 保険レコード作成.
002480     PERFORM 社保レコード作成.
002480     PERFORM 申請書レコード作成.
002480     PERFORM エンドレコード作成.
002490************
002500*           *
002510* 終了処理   *
002520*           *
002530************
002540     PERFORM 終了処理.
002550     MOVE ZERO TO PROGRAM-STATUS.
002560     EXIT PROGRAM.
002570*
002580*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002590*================================================================*
002600 初期化 SECTION.
002610*
002620     PERFORM ファイルオープン.
002630*
002640*================================================================*
002650 ファイルオープン SECTION.
002660*
011000*     OPEN INPUT 制御情報マスタ.
011010*         MOVE NC"制御情報マスタ" TO ファイル名.
011020*         PERFORM オープンチェック.
003321*     MOVE ZERO TO 制−制御区分.
003322*     READ 制御情報マスタ
003323*     NOT INVALID KEY
003324*         MOVE 制−定期修正ＦＤ TO 定期修正ＦＤＷ
003325*     END-READ.
003324*     MOVE 連入−ドライブ TO 定期修正ＦＤＷ
           IF 連入−固定フラグ = 1
002890        MOVE "C:\makishisys\AICHIFD.DAT" TO FD-NAME
           ELSE
003324        MOVE 連入−定期修正ＦＤ TO 定期修正ＦＤＷ
003326        IF 定期修正ＦＤＷ = SPACE
003327           MOVE "A" TO 定期修正ＦＤＷ
003328        END-IF
003330        STRING 定期修正ＦＤＷ DELIMITED BY SIZE
                    ":\AICHIFD.DAT"  DELIMITED BY SIZE
003334              INTO FD-NAME
003335        END-STRING
           END-IF.
      *
002670     OPEN OUTPUT 会提出ファイル.
002680     IF 状態キー  NOT =  "00"
002690         MOVE  "指定されたドライブがないか、" TO 連メ７−メッセージ１
               MOVE  "書き込むことが出来ません。" TO 連メ７−メッセージ２
002700         CALL   "MSG007"
002710         CANCEL "MSG007"
002720         MOVE 99 TO PROGRAM-STATUS
002730         EXIT PROGRAM
002770     END-IF.
002780*
002790     OPEN INPUT 作業ファイル１.
002800         MOVE NC"作１" TO ファイル名.
002810         PERFORM オープンチェック.
002790     OPEN INPUT 作業ファイル２.
002800         MOVE NC"作２" TO ファイル名.
002810         PERFORM オープンチェック.
002790     OPEN INPUT 作業ファイル３.
002800         MOVE NC"作３" TO ファイル名.
002810         PERFORM オープンチェック.
002790     OPEN INPUT 作業ファイル４.
002800         MOVE NC"作３" TO ファイル名.
002810         PERFORM オープンチェック.
002820*================================================================*
002830 オープンチェック SECTION.
002840*
002850     IF 状態キー  NOT =  "00"
002860         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
002870         DISPLAY NC"状態キー：" 状態キー         UPON CONS
002880         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
002890                                                 UPON CONS
004380*-----------------------------------------*
004390         CALL "actcshm"  WITH C LINKAGE
004400*-----------------------------------------*
002900         ACCEPT  キー入力 FROM CONS
002910         PERFORM ファイル閉鎖
002920         MOVE 99 TO PROGRAM-STATUS
002930         EXIT PROGRAM.
002940*================================================================*
002950 ファイル閉鎖 SECTION.
002960*
002970     CLOSE  会提出ファイル  作業ファイル１  作業ファイル２ 
                  作業ファイル３  作業ファイル４.
      *            制御情報マスタ.
002980*================================================================*
002990 終了処理 SECTION.
003000*
003010     PERFORM ファイル閉鎖.
003020*================================================================*
003030 エラー表示 SECTION.
003040*
003050     DISPLAY NC"状態キー" 状態キー  UPON CONS.
003060     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
003070     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
003080     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
004380*-----------------------------------------*
004390     CALL "actcshm"  WITH C LINKAGE.
004400*-----------------------------------------*
003090     ACCEPT  キー入力 FROM CONS.
003100     PERFORM ファイル閉鎖.
003110     MOVE 99 TO PROGRAM-STATUS.
003120     EXIT PROGRAM.
003130*================================================================*
002480 ヘッダレコード作成 SECTION.
004020*
           PERFORM 作業ファイル１読込.
           MOVE 作１−レコードデータ TO 会提−レコードデータ.
           MOVE 20 TO 文字ＣＮＴ.
           PERFORM ファイル書込.
004030*================================================================*
004040 作業ファイル１読込 SECTION.
004050*
004060     READ 作業ファイル１ NEXT
004070     AT END
004080         MOVE "YES" TO 終了フラグ
004090     END-READ.
004100*
004110*================================================================*
004120 ファイル書込 SECTION.
004130*
004140     WRITE 会提−レコード.
004150     IF 状態キー  NOT =  "00"
004160         MOVE NC"会提出ファイル" TO ファイル名
004170         PERFORM エラー表示
004180     END-IF.
004190*================================================================*
002480 保険レコード作成 SECTION.
004020*
           MOVE 24 TO 文字ＣＮＴ.
           MOVE SPACE TO 終了フラグ.
           PERFORM 作業ファイル２読込.
           PERFORM UNTIL 終了フラグ = "YES"
               MOVE 作２−レコードデータ TO 会提−レコードデータ
               PERFORM ファイル書込
               PERFORM 作業ファイル２読込
           END-PERFORM.
004030*================================================================*
004040 作業ファイル２読込 SECTION.
004050*
004060     READ 作業ファイル２ NEXT
004070     AT END
004080         MOVE "YES" TO 終了フラグ
004090     END-READ.
004100*
004110*================================================================*
002480 社保レコード作成 SECTION.
004020*
           MOVE 24 TO 文字ＣＮＴ.
           MOVE SPACE TO 終了フラグ.
           PERFORM 作業ファイル３読込.
           PERFORM UNTIL 終了フラグ = "YES"
               MOVE 作３−レコードデータ TO 会提−レコードデータ
               PERFORM ファイル書込
               PERFORM 作業ファイル３読込
           END-PERFORM.
004030*================================================================*
004040 作業ファイル３読込 SECTION.
004050*
004060     READ 作業ファイル３ NEXT
004070     AT END
004080         MOVE "YES" TO 終了フラグ
004090     END-READ.
004100*
004110*================================================================*
002480 申請書レコード作成 SECTION.
004020*
      */仕様変更/20180612
      *     MOVE 1763  TO 文字ＣＮＴ.
      */仕様変更/20221117
      *     MOVE 1770  TO 文字ＣＮＴ.
           MOVE 2009  TO 文字ＣＮＴ.
           MOVE SPACE TO 終了フラグ.
           MOVE ZERO  TO 作４−レコード区分.
           MOVE ZERO  TO 作４−提出区分.
           MOVE SPACE TO 作４−保険者番号.
           MOVE ZERO  TO 作４−施術和暦年月.
000580     MOVE ZERO  TO 作４−患者番号.
000590     MOVE SPACE TO 作４−枝番.
           MOVE ZERO  TO 作４−レセ種別.
013770     START 作業ファイル４ KEY IS >= 作４−レコード区分
                                          作４−提出区分
                                          作４−保険者番号
                                          作４−施術和暦年月
                                          作４−患者コード
                                          作４−レセ種別
           END-START.
011444     IF 状態キー = "00"
               PERFORM 作業ファイル４読込
               PERFORM UNTIL 終了フラグ = "YES"
                   MOVE 作４−レコードデータ TO 会提−レコードデータ
                   PERFORM ファイル書込
                   PERFORM 作業ファイル４読込
               END-PERFORM
           END-IF.
004030*================================================================*
004040 作業ファイル４読込 SECTION.
004050*
004060     READ 作業ファイル４ NEXT
004070     AT END
004080         MOVE "YES" TO 終了フラグ
004090     END-READ.
004100*
004110*================================================================*
002480 エンドレコード作成 SECTION.
      *
           MOVE 2  TO 文字ＣＮＴ.
           MOVE 90 TO 会提−レコードデータ(1:2).
           PERFORM ファイル書込.
004110*================================================================*
004200******************************************************************
004210 END PROGRAM YAI102.
004220******************************************************************
