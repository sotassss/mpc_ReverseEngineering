000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YAI597.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*   【柔+ｳｨﾝﾄﾞｳｽﾞ版　愛知用 総集計表】
000100*   【データ作成】
000110*
000120*         MED = YAI580 YAI598P 
000130*----------------------------------------------------------------*
000140 DATE-WRITTEN.           2013-07-29
000150 DATE-COMPILED.          2013-07-29
000160*----------------------------------------------------------------*
000170******************************************************************
000180*            ENVIRONMENT         DIVISION                        *
000190******************************************************************
000200 ENVIRONMENT             DIVISION.
000210 CONFIGURATION           SECTION.
000220 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000230 OBJECT-COMPUTER.        FMV-DESKPOWER.
000240 SPECIAL-NAMES.          CONSOLE  IS  CONS
000250                         SYSERR   IS  MSGBOX.
000260 INPUT-OUTPUT            SECTION.
000270 FILE-CONTROL.
000280     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000290                             ORGANIZATION             IS  INDEXED
000300                             ACCESS MODE              IS  DYNAMIC
000310                             RECORD KEY               IS  受−施術和暦年月
000320                                                          受−患者コード
000330                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000340                                                          受−患者カナ
000350                                                          受−患者コード
000360                             ALTERNATE RECORD KEY     IS  受−患者コード
000370                                                          受−施術和暦年月
000380                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000390                                                          受−保険種別
000400                                                          受−保険者番号
000410                                                          受−患者コード
000420                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000430                                                          受−公費種別
000440                                                          受−費用負担者番号
000450                                                          受−患者コード
000460                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000470                                                          受−助成種別
000480                                                          受−費用負担者番号助成
000490                                                          受−患者コード
000500                             ALTERNATE RECORD KEY     IS  受−請求和暦年月
000510                                                          受−施術和暦年月
000520                                                          受−患者コード
000530                             FILE STATUS              IS  状態キー
000540                             LOCK        MODE         IS  AUTOMATIC.
           SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
                                   ORGANIZATION             IS  INDEXED
                                   ACCESS MODE              IS  DYNAMIC
                                   RECORD KEY               IS  レセ−施術和暦年月
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−患者コード
                                                                レセ−施術和暦年月
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−施術和暦年月
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−レセ種別
                                                                レセ−請求保険者番号
                                                                レセ−患者コード
                                                                レセ−施術和暦年月
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−請求保険者番号
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                                                レセ−施術和暦年月
                                   FILE STATUS              IS  状態キー
                                   LOCK        MODE         IS  AUTOMATIC.
001510******************************************************************
001520*                      DATA DIVISION                             *
001530******************************************************************
001540 DATA                    DIVISION.
001550 FILE                    SECTION.
001560*                           ［ＲＬ＝  ７６８］
001570 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001580     COPY JUSINJ         OF  XFDLIB  JOINING    受   AS  PREFIX.
      *                          ［ＲＬ＝  １５３６］
       FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS.
           COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
002120*
002130******************************************************************
002140*                WORKING-STORAGE SECTION                         *
002150******************************************************************
002160 WORKING-STORAGE         SECTION.
002170 01 キー入力                           PIC X(1) VALUE SPACE.
002180 01 状態キー                           PIC X(2) VALUE SPACE.
002190 01 終了フラグ１                       PIC X(3) VALUE SPACE.
002200 01 終了フラグ３                       PIC X(3) VALUE SPACE.
002090 01 エラーフラグ                       PIC X(3) VALUE SPACE.
002230 01 ファイル名Ｗ                       PIC N(2) VALUE SPACE.
002240 01 ファイル名                         PIC N(2) VALUE SPACE.
002260 01 実行キーＷ                         PIC X(4) VALUE SPACE.
002300*
002310 01 県コードＪＩＳＷ                   PIC X(2) VALUE SPACE.
002320 01 県コード政管Ｗ                     PIC X(2) VALUE SPACE.
002420*
002430**************
002440* 保険者情報 *
002450**************
002460*
003010 01 請求和暦年月ＷＲ.
003020    03 請求和暦ＷＲ                  PIC 9     VALUE ZERO.
003030    03 請求年ＷＲ                    PIC 9(2)  VALUE ZERO.
003040    03 請求月ＷＲ                    PIC 9(2)  VALUE ZERO.
003050*
003060 01 保険者番号Ｗ.
003070    03 法別番号Ｗ                    PIC X(2) VALUE SPACE.
003080    03 保番Ｗ.
003090       05 都道府県番号Ｗ             PIC X(2) VALUE SPACE.
003100       05 保険番号Ｗ                 PIC X(3) VALUE SPACE.
003110       05 検証番号Ｗ                 PIC X    VALUE SPACE.
003120       05 FILLER                     PIC X(2).
003130 01 保険種別Ｗ                       PIC 9(2) VALUE ZERO.
003140 01 公費種別Ｗ                       PIC 9(2) VALUE ZERO.
003150 01 助成種別Ｗ                       PIC 9(2) VALUE ZERO.
003160 01 本人家族区分Ｗ                   PIC 9(1) VALUE ZERO.
003170 01 法別番号Ｗ                       PIC X(2) VALUE SPACE.
003240*
       01 データＷ.
          03 国保件数Ｗ                    PIC 9(4) VALUE ZERO.
          03 国保費用額Ｗ                  PIC 9(7) VALUE ZERO.
          03 退職件数Ｗ                    PIC 9(4) VALUE ZERO.
          03 退職費用額Ｗ                  PIC 9(7) VALUE ZERO.
          03 後高件数Ｗ                    PIC 9(4) VALUE ZERO.
          03 後高費用額Ｗ                  PIC 9(7) VALUE ZERO.
          03 合計件数Ｗ                    PIC 9(5) VALUE ZERO.
          03 合計費用額Ｗ                  PIC 9(8) VALUE ZERO.
003330*
003340 01 終了フラグ２                     PIC X(3) VALUE SPACE.
003350*
003360 01 状態フラグ                       PIC 9(1) VALUE ZERO.
003570*
003580******************************************************************
003590*                          連結項目                              *
003600******************************************************************
003610*
003620****************
003630* 画面入力情報 *
003640****************
       01 連入−画面情報ＹＡＩ５８０ IS EXTERNAL.
          03 連入−請求和暦年月.
             05 連入−請求和暦               PIC 9(1).
             05 連入−請求年月.
                07 連入−請求年              PIC 9(2).
                07 連入−請求月              PIC 9(2).
          03 連入−作成和暦年月日.
             05 連入−作成和暦年月.
                07 連入−作成和暦            PIC 9(1).
                07 連入−作成年              PIC 9(2).
                07 連入−作成月              PIC 9(2).
             05 連入−作成日                 PIC 9(2).
       01 連印−印刷データＹＡＩ５９７   IS EXTERNAL.
          03 連印−国保件数                  PIC 9(4).
          03 連印−国保費用額                PIC X(7).
          03 連印−退職件数                  PIC 9(4).
          03 連印−退職費用額                PIC X(7).
          03 連印−後高件数                  PIC 9(4).
          03 連印−後高費用額                PIC X(7).
          03 連印−合計件数                  PIC 9(5).
          03 連印−合計費用額                PIC X(8).
004160*
004170******************************************************************
004180*                      PROCEDURE  DIVISION                       *
004190******************************************************************
004200 PROCEDURE               DIVISION.
004220************
004230*           *
004240* 初期処理   *
004250*           *
004260************
004270     PERFORM 初期化.
004280************
004290*           *
004300* 主処理     *
004310*           *
004320************
004330     PERFORM データ作成.
004330     PERFORM 連結項目セット.
004340************
004350*           *
004360* 終了処理   *
004370*           *
004380************
004390     PERFORM 終了処理.
004400     MOVE ZERO TO PROGRAM-STATUS.
004410     EXIT PROGRAM.
004420*
004430*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
004440*================================================================*
004450 初期化 SECTION.
004460*
004470     OPEN INPUT 受診者情報Ｆ.
004480             MOVE NC"受診" TO ファイル名Ｗ.
004490             PERFORM オープンチェック.
003250     OPEN INPUT レセプトＦ.
003260         MOVE NC"レセプトＦ" TO ファイル名Ｗ.
003270         PERFORM オープンチェック.
004740*
004920* 連結項目の待避
004930     MOVE 連入−請求和暦 TO 請求和暦ＷＲ.
004940     MOVE 連入−請求年   TO 請求年ＷＲ.
004950     MOVE 連入−請求月   TO 請求月ＷＲ.
004960*================================================================*
004970 オープンチェック SECTION.
004980*
004990     IF 状態キー  NOT =  "00"
005000         DISPLAY ファイル名Ｗ NC"Ｆオープンエラー" UPON CONS
005010         DISPLAY NC"状態キー：" 状態キー           UPON CONS
005020         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
005030                                                   UPON CONS
003131*-----------------------------------------*
003132         CALL "actcshm"  WITH C LINKAGE
003133*-----------------------------------------*
005040         ACCEPT  キー入力 FROM CONS
005050         PERFORM ファイル閉鎖
005060         EXIT PROGRAM.
005590*================================================================*
005600 ファイル閉鎖 SECTION.
005610*
005620     CLOSE  受診者情報Ｆ レセプトＦ.
005650*================================================================*
005660 終了処理 SECTION.
005670*
005680     PERFORM ファイル閉鎖.
005690*================================================================*
005700 データ作成 SECTION.
005720*
005850*  /*国保
007470     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
007480     MOVE 請求年ＷＲ    TO レセ−請求年.
007490     MOVE 請求月ＷＲ    TO レセ−請求月.
013730     MOVE 1             TO レセ−レセ種別.
007500     MOVE ZERO          TO レセ−請求保険者番号.
007500     MOVE ZERO          TO レセ−施術和暦.
007510     MOVE ZERO          TO レセ−施術年.
007520     MOVE ZERO          TO レセ−施術月.
007530     MOVE ZERO          TO レセ−患者番号.
007540     MOVE SPACE         TO レセ−枝番.
007550     START レセプトＦ   KEY IS >= レセ−請求和暦年月
000250                                  レセ−レセ種別
007560                                  レセ−請求保険者番号
007570                                  レセ−患者コード
                                        レセ−施術和暦年月
006010     END-START.
006020     IF 状態キー = "00"
006030         MOVE SPACE  TO 終了フラグ３
006040         PERFORM レセプトＦ読込
006080         PERFORM UNTIL ( 終了フラグ３ = "YES" ) OR
007630                       ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
007640                       ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
007650                       ( レセ−請求月   NOT = 請求月ＷＲ   )
                   IF ( レセ−レセ種別 = 1 OR 2 )
005700                PERFORM データチェック
005710                IF 実行キーＷ = "YES"
006110*
006130                   IF (( 受−保険種別 = 01 ) AND ( 受−法別番号 = "23" )) OR
                            (( 受−保険種別 = 08 ) AND ( 受−都道府県番号 = "23" )) OR
                            (( 受−公費種別 = 05 ) AND ( 受−府県老人 = "23" )) 
006240                      PERFORM 集計処理
006260                   END-IF
                      END-IF
006270             END-IF
006280             PERFORM レセプトＦ読込
006290         END-PERFORM
006310     END-IF.
007910*================================================================*
007920 レセプトＦ読込 SECTION.
007930*
007940     READ レセプトＦ NEXT
007950     AT END
007960         MOVE "YES" TO 終了フラグ３
007970     END-READ.
008220*================================================================*
008230 エラー表示 SECTION.
008240*
008250     DISPLAY NC"ファイル書込エラー：" ファイル名Ｗ UPON CONS.
008260     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
008270     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
003331*-----------------------------------------*
003332     CALL "actcshm"  WITH C LINKAGE.
003333*-----------------------------------------*
008280     ACCEPT  キー入力 FROM CONS.
008290*================================================================*
008300 エラー表示Ｒ SECTION.
008310*
008320     DISPLAY NC"ファイル読込エラー" ファイル名Ｗ UPON CONS.
008330     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
003331*-----------------------------------------*
003332     CALL "actcshm"  WITH C LINKAGE.
003333*-----------------------------------------*
008340     ACCEPT  キー入力 FROM CONS.
008350     PERFORM ファイル閉鎖.
008360     EXIT PROGRAM.
008370*================================================================*
008380 エラー表示その他 SECTION.
008390*
008400     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
008410     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
003331*-----------------------------------------*
003332     CALL "actcshm"  WITH C LINKAGE.
003333*-----------------------------------------*
008420     ACCEPT  キー入力 FROM CONS.
008430     PERFORM ファイル閉鎖.
008440     EXIT PROGRAM.
008450*================================================================*
008460 データチェック SECTION.
008470*
008480     MOVE SPACE          TO 実行キーＷ.
007260* *****************************************************************
007270* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
007280* *****************************************************************
007290     IF ( レセ−請求対象区分 NOT = ZERO ) AND
007300        ( レセ−償還払い区分 NOT = 1 )
007310        IF (レセ−レセ種別 = 3) AND ( レセ−会総括表印刷対象区分 = 1 )
007320            CONTINUE
007330        ELSE
007340           MOVE レセ−施術和暦  TO 受−施術和暦
007350           MOVE レセ−施術年    TO 受−施術年
007360           MOVE レセ−施術月    TO 受−施術月
007370           MOVE レセ−患者番号  TO 受−患者番号
007380           MOVE レセ−枝番      TO 受−枝番
007390           READ 受診者情報Ｆ
007400           NOT INVALID KEY
007430              MOVE "YES"  TO 実行キーＷ
007450           END-READ
007460        END-IF
007470     END-IF.
008940*
009020*================================================================*
009030 集計処理 SECTION.
009040*
009446     IF 受−公費種別 = 5
010010         COMPUTE 後高件数Ｗ   = 後高件数Ｗ   + 1
010080         COMPUTE 後高費用額Ｗ = 後高費用額Ｗ + レセ−合計
           ELSE
009400         EVALUATE 受−保険種別
009410         WHEN 1
010010             COMPUTE 国保件数Ｗ   = 国保件数Ｗ   + 1
010080             COMPUTE 国保費用額Ｗ = 国保費用額Ｗ + レセ−合計
009446         WHEN 8
010010             COMPUTE 退職件数Ｗ   = 退職件数Ｗ   + 1
010080             COMPUTE 退職費用額Ｗ = 退職費用額Ｗ + レセ−合計
009520         END-EVALUATE
           END-IF.
010010     COMPUTE 合計件数Ｗ   = 合計件数Ｗ   + 1.
010080     COMPUTE 合計費用額Ｗ = 合計費用額Ｗ + レセ−合計.
011230*
009020*================================================================*
009030 連結項目セット SECTION.
009040*
           MOVE 国保件数Ｗ      TO 連印−国保件数.
           MOVE 国保費用額Ｗ    TO 連印−国保費用額.
           MOVE 退職件数Ｗ      TO 連印−退職件数.
           MOVE 退職費用額Ｗ    TO 連印−退職費用額.
           MOVE 後高件数Ｗ      TO 連印−後高件数.
           MOVE 後高費用額Ｗ    TO 連印−後高費用額.
           MOVE 合計件数Ｗ      TO 連印−合計件数.
           MOVE 合計費用額Ｗ    TO 連印−合計費用額.
      *
011520*================================================================*
011530******************************************************************
011540 END PROGRAM YAI597.
011550******************************************************************
