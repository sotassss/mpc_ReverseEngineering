000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YAI594.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*  【柔+ｳｨﾝﾄﾞｳｽﾞ版　愛知用 後高保健療養費総括表】
000100*  【印刷】
      *   後高１割３割
000110*       MED = YAI580 YAI594P 
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2013-07-01
000140 DATE-COMPILED.          2013-07-01
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000270     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000280                             ORGANIZATION             IS  INDEXED
000290                             ACCESS MODE              IS  DYNAMIC
000300                             RECORD KEY               IS  元−元号区分
000310                             FILE STATUS              IS  状態キー
000320                             LOCK        MODE         IS  AUTOMATIC.
000390     SELECT  施術所情報マスタ ASSIGN     TO        SEJOHOL
000400                             ORGANIZATION             IS  INDEXED
000410                             ACCESS MODE              IS  DYNAMIC
000420                             RECORD KEY               IS  施情−施術所番号
000430                             FILE STATUS              IS  状態キー
000440                             LOCK        MODE         IS  AUTOMATIC.
000550     SELECT  市町村マスタ    ASSIGN      TO        SITYOSNL
000560                             ORGANIZATION             IS  INDEXED
000570                             ACCESS MODE              IS  DYNAMIC
000580                             RECORD KEY               IS  市−公費種別
000590                                                          市−市町村番号
000600                             ALTERNATE RECORD KEY     IS  市−公費種別
000610                                                          市−市町村名称
000620                                                          市−市町村番号
000630                             FILE STATUS              IS  状態キー
000640                             LOCK        MODE         IS  AUTOMATIC.
000720     SELECT  ＩＤ管理マスタ    ASSIGN      TO        IDKANRL
000730                             ORGANIZATION             IS  INDEXED
000740                             ACCESS MODE              IS  DYNAMIC
000750                             RECORD KEY               IS  ＩＤ管−ＩＤ区分
000760                                                          ＩＤ管−施術所番号
000770                                                          ＩＤ管−保険種別
000780                                                          ＩＤ管−保険者番号
000790                             ALTERNATE RECORD KEY     IS  ＩＤ管−施術ＩＤ番号
000800                                                          ＩＤ管−ＩＤ区分
000810                                                          ＩＤ管−施術所番号
000820                                                          ＩＤ管−保険種別
000830                                                          ＩＤ管−保険者番号
000840                             FILE STATUS              IS  状態キー
000850                             LOCK        MODE         IS  AUTOMATIC.
001090     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W5931L.DAT"
001100                             ORGANIZATION             IS  INDEXED
001110                             ACCESS                   IS  DYNAMIC
001120                             RECORD      KEY          IS  作１−保険者番号
001160                                                          作１−負担割合
001180                             FILE        STATUS       IS  状態キー
001190                             LOCK        MODE         IS  AUTOMATIC.
000920     SELECT  印刷ファイル    ASSIGN      TO     GS-PRTF001
000930                             SYMBOLIC    DESTINATION  IS "PRT"
000940                             FORMAT                   IS  定義体名Ｐ
000950                             GROUP                    IS  項目群名Ｐ
000960                             PROCESSING  MODE         IS  処理種別Ｐ
000970                             UNIT        CONTROL      IS  拡張制御Ｐ
000980                             FILE        STATUS       IS  通知情報Ｐ.
000990******************************************************************
001000*                      DATA DIVISION                             *
001010******************************************************************
001020 DATA                    DIVISION.
001030 FILE                    SECTION.
001040*                           ［ＲＬ＝  １２８］
001050 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
001060     COPY GENGOU         OF  XFDLIB  JOINING    元   AS  PREFIX.
001100*                           ［ＲＬ＝  ６４０］
001110 FD  施術所情報マスタ    BLOCK   CONTAINS   1   RECORDS.
001120     COPY SEJOHO         OF  XFDLIB  JOINING    施情 AS  PREFIX.
001160*                           ［ＲＬ＝  ２５６］
001170 FD  市町村マスタ          BLOCK   CONTAINS   1   RECORDS.
001180     COPY SITYOSN        OF  XFDLIB  JOINING   市   AS  PREFIX.
001220*                           ［ＲＬ＝  １２８］
001230 FD  ＩＤ管理マスタ          BLOCK   CONTAINS   1   RECORDS.
001240     COPY IDKANR    OF  XFDLIB  JOINING   ＩＤ管   AS  PREFIX.
001250*
001470 FD  作業ファイル１ RECORD  CONTAINS 64 CHARACTERS.
001480 01  作１−レコード.
001490   03  作１−レコードキー.
001520     05  作１−保険者番号.
001530       07 作１−法別番号               PIC X(2).
001540       07 作１−保番.
001550         09 作１−都道府県番号         PIC X(2).
001560         09 作１−保番号               PIC X(3).
001570         09 作１−検証番号             PIC X.
001580         09 FILLER                     PIC X(2).
001600     05  作１−負担割合                PIC 9(1).
001610   03  作１−レコードデータ.
001620     05  作１−件数                    PIC 9(3).
001640     05  作１−費用額                  PIC 9(7).
001650     05  作１−請求額                  PIC 9(7).
001660     05  作１−負担額                  PIC 9(7).
001670     05  FILLER                        PIC X(29).
001430*
001440 FD  印刷ファイル.
001450     COPY YAI594P        OF  XMDLIB.
001460******************************************************************
001470*                WORKING-STORAGE SECTION                         *
001480******************************************************************
001490 WORKING-STORAGE         SECTION.
001500 01 キー入力                           PIC X    VALUE SPACE.
001510 01 状態キー                           PIC X(2) VALUE SPACE.
001520 01 終了フラグ                         PIC X(3) VALUE SPACE.
001530 01 終了フラグ２                       PIC X(3) VALUE SPACE.
001540 01 終了フラグ３                       PIC X(3) VALUE SPACE.
001550 01 確認入力Ｗ                         PIC X(1) VALUE SPACE.
001560 01 ファイル名Ｗ                       PIC N(2) VALUE SPACE.
001570 01 カレント元号Ｗ                     PIC 9(1) VALUE ZERO.
001350 01 オープンフラグ                     PIC X(3) VALUE SPACE.
001580 01 実行キーＷ                         PIC X(4) VALUE SPACE.
001590 01 前和暦Ｗ                           PIC 9 VALUE ZERO.
001600 01 行桁フラグ                         PIC X(3) VALUE SPACE.
001610 01 処理移動キー                       PIC X(4) VALUE SPACE.
001620 01 施術和暦年月Ｗ.
001630    03 施術和暦Ｗ                      PIC 9.
001640    03 施術年月Ｗ.
001650       05 施術年Ｗ                     PIC 9(2).
001660       05 施術月Ｗ                     PIC 9(2).
001670 01 施術日Ｗ                           PIC 9(2).
001680 01 請求和暦年月日Ｗ.
001690    03 請求年Ｗ                        PIC 9(2).
001700    03 請求月Ｗ                        PIC 9(2).
001710    03 請求日Ｗ                        PIC 9(2).
001720 01 元号名称Ｗ                         PIC N(2) VALUE SPACE.
001730 01 外字文字Ｗ.
001740    03 外字文字１Ｗ                    PIC X(1) OCCURS 16.
001750 01 外字コードＷ.
001760    03 外字コード１Ｗ                  PIC N(1) OCCURS 16.
001770 01 カウンタ                           PIC 9(2) VALUE ZERO.
001780 01 印刷枚数ＣＮＴ                     PIC 9(2) VALUE ZERO.
001790 01 請求書Ｗ.
001800    03 登録番号コードＷ.
001810       05 登録番号ＷＰ.
001820          07 登録番号Ｗ                PIC X(16) VALUE SPACE.
001830    03 会員番号Ｗ.
001840       05 会員番号頭Ｗ                 PIC X(2)  VALUE SPACE.
001850       05 会員番号ＷＰ                 PIC X(10) VALUE SPACE.
001860    03 施術所コードＷ                  PIC X(7)  VALUE SPACE.
001870    03 施術所住所Ｗ.
001880        05 施術所住所１Ｗ              PIC X(50) VALUE SPACE.
001890        05 施術所住所２Ｗ              PIC X(50) VALUE SPACE.
001900    03 接骨院名Ｗ                      PIC X(50) VALUE SPACE.
001910    03 代表者名Ｗ                      PIC X(50) VALUE SPACE.
001920    03 施術所電話番号Ｗ                PIC X(15) VALUE SPACE.
001930    03 ＯＣＲ頁数Ｗ                    PIC N(1)  VALUE SPACE.
001940    03 ＯＣＲ全頁Ｗ                    PIC N(1)  VALUE SPACE.
001950    03 合計金額Ｗ.
001960        05 保険者名称Ｗ.
001970           07 印刷保険者名称Ｗ         PIC X(32) VALUE SPACE.
001980       05 ９割件数Ｗ                   PIC 9(3)  VALUE ZERO.
002000       05 ９割費用額Ｗ                 PIC 9(7)  VALUE ZERO.
002020       05 ９割負担額Ｗ                 PIC 9(6)  VALUE ZERO.
001980       05 ７割件数Ｗ                   PIC 9(3)  VALUE ZERO.
002000       05 ７割費用額Ｗ                 PIC 9(7)  VALUE ZERO.
002020       05 ７割負担額Ｗ                 PIC 9(6)  VALUE ZERO.
002030    03 取引先情報Ｗ.
002040       05 取引先銀行名Ｗ.
002050          07 取引先銀行名Ｗ１          PIC X(14) VALUE SPACE.
002060          07 取引先銀行名Ｗ２          PIC X(14) VALUE SPACE.
002070          07 取引先銀行名Ｗ３          PIC X(14) VALUE SPACE.
002080       05 取引先銀行支店名Ｗ.
002090          07 取引先銀行支店名Ｗ１      PIC X(10) VALUE SPACE.
002100          07 取引先銀行支店名Ｗ２      PIC X(10) VALUE SPACE.
002110          07 取引先銀行支店名Ｗ３      PIC X(10) VALUE SPACE.
002120       05 預金種別Ｗ                   PIC 9(1)  VALUE ZERO.
002130       05 口座番号Ｗ                   PIC X(10) VALUE SPACE.
002140       05 口座名義人Ｗ                 PIC X(40) VALUE SPACE.
002150       05 預金種別コメントＷ           PIC N(3)  VALUE SPACE.
002160 01 頁数Ｗ                             PIC 9(1)  VALUE ZERO.
002170 01 全頁Ｗ                             PIC 9(1)  VALUE ZERO.
002180 01 請求先名称Ｗ.
002190    03 印刷請求先名称Ｗ                PIC X(30) VALUE SPACE.
002200    03 FILLER                          PIC X(2)  VALUE SPACE.
002210 01 請求先名称ＷＰ                     PIC X(32) VALUE SPACE.
002220 01 ワーク                             PIC X(2)  VALUE SPACE.
002230 01 保険者番号Ｗ.
002240    03 保険者法別番号Ｗ                PIC X(2)  VALUE SPACE.
002250    03 保険者番号１Ｗ.
002260       05 保険者県コードＷ             PIC X(2)  VALUE SPACE.
002270       05 保険者番号２Ｗ               PIC X(6)  VALUE SPACE.
002280 01 保険番号Ｗ                         PIC X(3)  VALUE SPACE.
002290 01 保険種別Ｗ                         PIC 9(2)  VALUE ZERO.
002300 01 公費種別Ｗ                         PIC 9(2)  VALUE ZERO.
002310 01 本人家族区分Ｗ                     PIC 9(1)  VALUE ZERO.
002320 01 費用負担者番号Ｗ                   PIC X(10) VALUE SPACE.
002330 01 給付割合Ｗ                         PIC 9(2)  VALUE ZERO.
002360 01 県名Ｗ                             PIC N(5)  VALUE SPACE.
002370 01 保険者宛先Ｗ                       PIC N(3)  VALUE SPACE.
002380 01 結合保険者名称Ｗ.
002390    03 結合印刷保険者名称Ｗ            PIC N(20) VALUE SPACE.
002400 01 分割保険者名称Ｗ.
002410    03 印刷保険者名称１Ｗ              PIC X(16) VALUE SPACE.
002420    03 印刷保険者名称２Ｗ              PIC X(16) VALUE SPACE.
002630*
002640 01 印刷制御.
002650     03 定義体名Ｐ                     PIC X(8) VALUE SPACE.
002660     03 項目群名Ｐ                     PIC X(8) VALUE SPACE.
002670     03 処理種別Ｐ                     PIC X(2) VALUE SPACE.
002680     03 拡張制御Ｐ.
002690         05 端末制御Ｐ.
002700             07 移動方向Ｐ             PIC X(1).
002710             07 移動行数Ｐ             PIC 9(3).
002720         05 詳細制御Ｐ                 PIC X(2).
002730     03 通知情報Ｐ                     PIC X(2) VALUE SPACE.
002740     03 ユニット名Ｐ                   PIC X(8) VALUE SPACE.
002910*
002920******************************************************************
002930*                          連結項目                              *
002940******************************************************************
002360*
002370 01 連メ−キー IS EXTERNAL.
002380    03  連メ−メッセージ                 PIC N(20).
002950*
002960****************
002970* 画面入力情報 *
002980****************
       01 連入−画面情報ＹＡＩ５８０ IS EXTERNAL.
          03 連入−請求和暦年月.
             05 連入−請求和暦               PIC 9(1).
             05 連入−請求年月.
                07 連入−請求年              PIC 9(2).
                07 連入−請求月              PIC 9(2).
          03 連入−作成和暦年月日.
             05 連入−作成和暦年月.
                07 連入−作成和暦            PIC 9(1).
                07 連入−作成年              PIC 9(2).
                07 連入−作成月              PIC 9(2).
             05 連入−作成日                 PIC 9(2).
       01 連入−入力データＹＡＩ５８０   IS EXTERNAL.
          03 連入−保険種別                  PIC 9(2).
          03 連入−保険者番号                PIC X(10).
014774*
       01 連印−プレビュー IS EXTERNAL.
          03 連印−プレビュー区分          PIC 9.
010440*
000540************************************
000550* プリンタファイル作成用           *
000560************************************
000570 01 Ｈ連ＰＲＴＦ−作成データ IS EXTERNAL.
000580     03 Ｈ連ＰＲＴＦ−ファイル名           PIC X(8).
000590     03 Ｈ連ＰＲＴＦ−プレビュー区分       PIC 9.
000600     03 Ｈ連ＰＲＴＦ−帳票プログラム名     PIC X(8).
000610     03 Ｈ連ＰＲＴＦ−オーバレイ名         PIC X(8).
003480*
003490******************************************************************
003500*                      PROCEDURE  DIVISION                       *
003510******************************************************************
003520 PROCEDURE               DIVISION.
003530************
003540*           *
003550* 初期処理   *
003560*           *
003570************
002570     PERFORM プリンタファイル作成.
003580     PERFORM 初期化.
003590************
003600*           *
003610* 主処理     *
003620*           *
003630************
003640     PERFORM 印刷処理.
003650************
003660*           *
003670* 終了処理   *
003680*           *
003690************
003700     PERFORM 終了処理.
003710     EXIT PROGRAM.
003720*
003730*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002860*================================================================*
002870 プリンタファイル作成 SECTION.
002880*================================================================*
002890*   / 初期化 /
002900     MOVE SPACE TO Ｈ連ＰＲＴＦ−作成データ.
002910     INITIALIZE Ｈ連ＰＲＴＦ−作成データ.
002920*
002930*
002940*--↓↓ 変更箇所 ↓↓--------------------------------------*
002970*   使用するプリンタファイル名セット
002971     MOVE "PRTF001"             TO Ｈ連ＰＲＴＦ−ファイル名.
002972*
002973*   使用する帳票プログラム名セット
002974     MOVE "YAI594"             TO Ｈ連ＰＲＴＦ−帳票プログラム名.
002975*
002976*--↑↑-----------------------------------------------------*
002980*
002990*   / プレビュー区分セット /
003000     MOVE 連印−プレビュー区分  TO Ｈ連ＰＲＴＦ−プレビュー区分.
003010*
003020     CALL   "CRTPRTF".
003030     CANCEL "CRTPRTF".
003040*
003740*================================================================*
003750 初期化 SECTION.
003760*
003770     OPEN INPUT 元号マスタ.
003780             MOVE NC"元号" TO ファイル名Ｗ.
003790             PERFORM オープンチェック.
003830     OPEN INPUT 施術所情報マスタ.
003840             MOVE NC"施情" TO ファイル名Ｗ.
003850             PERFORM オープンチェック.
003890     OPEN INPUT 市町村マスタ.
003900         MOVE NC"市町村マスタ" TO ファイル名Ｗ.
003910         PERFORM オープンチェック.
003950     OPEN INPUT ＩＤ管理マスタ
003960         MOVE NC"ＩＤ" TO ファイル名Ｗ.
003970         PERFORM オープンチェック.
004010*
004170     PERFORM 連結項目待避.
004180*
004190*================================================================*
004200 オープンチェック SECTION.
004210*
004220     IF 状態キー  NOT =  "00"
004230         DISPLAY ファイル名Ｗ NC"Ｆオープンエラー" UPON CONS
004240         DISPLAY NC"状態キー：" 状態キー           UPON CONS
004250         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004260                                                   UPON CONS
003630*-----------------------------------------*
003640         CALL "actcshm"  WITH C LINKAGE
003650*-----------------------------------------*
004270         ACCEPT  キー入力 FROM CONS
004280         PERFORM ファイル閉鎖
004290         EXIT PROGRAM.
004710*================================================================*
004720 施術所情報マスタ読込 SECTION.
004730*
004740     MOVE ZERO TO 施情−施術所番号.
004750     READ 施術所情報マスタ
004760     INVALID KEY
004770         MOVE NC"施情" TO ファイル名Ｗ
004780         PERFORM エラー表示Ｒ
004790         PERFORM ファイル閉鎖
004800         MOVE 99 TO PROGRAM-STATUS
004810         EXIT PROGRAM
004820     NOT INVALID KEY
004830         MOVE 施情−新柔整師番号     TO 登録番号コードＷ
004840*         STRING 施情−住所１  DELIMITED BY SPACE
004850*                施情−住所２  DELIMITED BY SPACE
004860*           INTO 施術所住所Ｗ
004870*         END-STRING
006050         MOVE 施情−住所１ TO 施術所住所１Ｗ
006060         MOVE 施情−住所２ TO 施術所住所２Ｗ
004880         MOVE 施情−接骨院名         TO 接骨院名Ｗ
004890         MOVE 施情−代表者名         TO 代表者名Ｗ
004900         MOVE 施情−電話番号         TO 施術所電話番号Ｗ
004910         MOVE 施情−接骨師会会員番号 TO 会員番号ＷＰ
004930     END-READ.
004940*
004950*********************************************
004960** ＩＤ管理マスタより　施術ＩＤを取得する。
004970*********************************************
004980     MOVE 01                   TO ＩＤ管−ＩＤ区分.
004990     MOVE ZERO                 TO ＩＤ管−施術所番号.
005000     MOVE 23                   TO ＩＤ管−保険種別.
005010     MOVE SPACE                TO ＩＤ管−保険者番号.
005020     READ ＩＤ管理マスタ
005030     INVALID KEY
005040         MOVE SPACE                  TO 施術所コードＷ
005050     NOT INVALID KEY
005060         MOVE ＩＤ管−施術ＩＤ番号   TO 施術所コードＷ
005070     END-READ.
005080*
005130*================================================================*
005140 ファイル閉鎖 SECTION.
005150*
003720     IF ( オープンフラグ = "YES" )
003730         CLOSE 印刷ファイル
003740     ELSE
003750         MOVE  NC"　　データが０件です。確認して下さい。" TO 連メ−メッセージ
003760         CALL   "MSG001"
003770         CANCEL "MSG001"
003780     END-IF.
003790*
005160     CLOSE  元号マスタ   施術所情報マスタ ＩＤ管理マスタ
005170            市町村マスタ.
005190*================================================================*
005200 終了処理 SECTION.
005210*
005220     PERFORM ファイル閉鎖.
005230*================================================================*
005240 連結項目待避 SECTION.
005250*
005260     MOVE 連入−請求年     TO 施術年Ｗ.
005270     MOVE 連入−請求月     TO 施術月Ｗ.
005280     MOVE 連入−作成年     TO 請求年Ｗ.
005290     MOVE 連入−作成月     TO 請求月Ｗ.
005300     MOVE 連入−作成日     TO 請求日Ｗ.
005310     MOVE 連入−保険者番号 TO 保険者番号Ｗ.
005320*
005330*================================================================*
005340 印刷処理 SECTION.
005350*
005360     PERFORM 施術所情報マスタ読込.
005370     OPEN INPUT 作業ファイル１.
005380         MOVE NC"作１" TO ファイル名Ｗ.
005390         PERFORM オープンチェック.
005410     MOVE SPACE TO 終了フラグ２.
005420     MOVE 保険者番号Ｗ   TO 作１−保険者番号.
           MOVE ZERO           TO 作１−負担割合
005430     START 作業ファイル１ KEY IS >= 作１−保険者番号
                                          作１−負担割合
005440     END-START.
005450     IF 状態キー = "00"
005460         PERFORM 作業ファイル１読込
005470         MOVE SPACE TO 合計金額Ｗ
005480         INITIALIZE    合計金額Ｗ
005490         MOVE 作１−保険者番号     TO 保険者番号Ｗ
005500         PERFORM UNTIL (作１−保険者番号 NOT = 保険者番号Ｗ) OR
005510                       (終了フラグ２     NOT = SPACE)
005520             PERFORM 集計処理１
005530             PERFORM 作業ファイル１読込
005540         END-PERFORM
005550         PERFORM 印刷レコードセット
005560         PERFORM 明細印刷処理
005570     END-IF
005580     CLOSE 作業ファイル１.
005590*================================================================*
005600 作業ファイル１読込 SECTION.
005610*
005620     READ 作業ファイル１ NEXT
005630     AT END
005640         MOVE "YES" TO 終了フラグ２
005650         INITIALIZE 作１−レコード
005660     END-READ.
005670*================================================================*
005680 集計処理１ SECTION.
005690*
      */老人１割２割の対応0210
           IF 作１−負担割合 = 1
005700         MOVE 作１−件数   TO ９割件数Ｗ
005720         MOVE 作１−費用額 TO ９割費用額Ｗ
005740         MOVE 作１−負担額 TO ９割負担額Ｗ
           ELSE
005700         MOVE 作１−件数   TO ７割件数Ｗ
005720         MOVE 作１−費用額 TO ７割費用額Ｗ
005740         MOVE 作１−負担額 TO ７割負担額Ｗ
           END-IF.
005750*================================================================*
005760 印刷レコードセット SECTION.
005770*
005780     MOVE SPACE TO YAI594P.
005790     INITIALIZE YAI594P.
      *
005800*     PERFORM 市町村情報取得.
005810*     MOVE 請求先名称Ｗ(1:2) TO ワーク.
005820*     PERFORM VARYING カウンタ FROM 3 BY 2
005830*             UNTIL ( ワーク = SPACE )
005840*         MOVE 請求先名称Ｗ(カウンタ:2) TO ワーク
005850*     END-PERFORM.
005860*     COMPUTE カウンタ = カウンタ - 4.
005870*     IF 請求先名称Ｗ(カウンタ:2) = "市" OR "町" OR "村"
005880*         STRING 請求先名称Ｗ  DELIMITED BY SPACE
005890*                "長"          DELIMITED BY SIZE
005900*             INTO 請求先名称ＷＰ
005910*         END-STRING
005920*     ELSE
005930*         MOVE 請求先名称Ｗ TO 請求先名称ＷＰ
005940*     END-IF.
005950*     MOVE 請求先名称ＷＰ   TO 保険者名.
      */元号修正/↓↓↓20190514
037370     IF 連入−請求和暦 > 4
              MOVE 連入−請求和暦     TO 元−元号区分
037380        READ 元号マスタ
037390        NOT INVALID KEY
037400            MOVE 元−元号名称   TO 施術和暦
037410        END-READ
              MOVE "===="             TO 施術和暦訂正
           END-IF.
      */元号修正/↑↑↑20190514
005960     MOVE 施術年Ｗ         TO 施術年.
005970     MOVE 施術月Ｗ         TO 施術月.
      */元号修正/↓↓↓20190514
037370     IF 連入−作成和暦 > 4
              MOVE 連入−作成和暦     TO 元−元号区分
037380        READ 元号マスタ
037390        NOT INVALID KEY
037400            MOVE 元−元号名称   TO 請求和暦
037410        END-READ
              MOVE "===="             TO 請求和暦訂正
           END-IF.
      */元号修正/↑↑↑20190514
005980     MOVE 請求年Ｗ         TO 請求年.
005990     MOVE 請求月Ｗ         TO 請求月.
006000     MOVE 請求日Ｗ         TO 請求日.
006010     MOVE 保険者番号Ｗ(5:4) TO 保険者番号.
006020*     MOVE 登録番号コードＷ TO 契約番号.
006030*     MOVE 会員番号Ｗ       TO 会員番号.
006040     MOVE 施術所コードＷ   TO 機関コード.
006050     MOVE 施術所住所１Ｗ   TO 住所１.
006060     MOVE 施術所住所２Ｗ   TO 住所２.
006070     MOVE 接骨院名Ｗ       TO 接骨院名.
006080     MOVE 代表者名Ｗ       TO 代表者名.
006090     MOVE 施術所電話番号Ｗ TO 電話番号.
006100*     MOVE 印刷枚数ＣＮＴ   TO 頁数Ｗ.
006110     MOVE ９割件数Ｗ       TO ９割件数  .
006130     MOVE ９割費用額Ｗ     TO ９割費用額.
006150     MOVE ９割負担額Ｗ     TO ９割負担金.
006110     MOVE ７割件数Ｗ       TO ７割件数  .
006130     MOVE ７割費用額Ｗ     TO ７割費用額.
006150     MOVE ７割負担額Ｗ     TO ７割負担金.
006160*================================================================*
006170 市町村情報取得 SECTION.
006180*
006190****************************************************
006200* 連結データから市町村マスタより請求先を取得する。 *
006210* ● 請求先...... 請求先名称Ｗに格納               *
006220****************************************************
           IF 保険者番号Ｗ(1:2) NOT = "39"
006230         MOVE 05              TO 市−公費種別
006240         MOVE 保険者番号Ｗ    TO 市−市町村番号
006250         READ 市町村マスタ
006260         INVALID KEY
006270             MOVE SPACE           TO 請求先名称Ｗ
006280         NOT INVALID KEY
006290             MOVE 市−市町村名称  TO 請求先名称Ｗ
006300         END-READ
           ELSE
               MOVE 05            TO 市−公費種別
               MOVE 保険者番号Ｗ  TO 市−市町村番号
               START 市町村マスタ KEY IS >= 市−公費種別 市−市町村番号
               END-START
               IF 状態キー = "00"
                   READ 市町村マスタ NEXT
                   NOT AT END
                       IF 保険者番号Ｗ(1:4) = 市−市町村番号(1:4)
                          MOVE 市−市町村名称  TO 請求先名称Ｗ
                       END-IF
                   END-READ
               END-IF
           END-IF.
006310*================================================================*
006320 明細印刷処理 SECTION.
006330*
005140     IF ( オープンフラグ NOT = "YES" )
005150        MOVE "YES" TO オープンフラグ
005160        OPEN I-O  印刷ファイル
005170        PERFORM エラー処理Ｐ
005180     END-IF.
006020*
006340     MOVE "YAI594P"  TO  定義体名Ｐ.
006350     MOVE "SCREEN"   TO  項目群名Ｐ.
006360     WRITE YAI594P.
006370     PERFORM エラー処理Ｐ.
006380*================================================================*
006390 エラー表示 SECTION.
006400*
006410     DISPLAY NC"ファイル書込エラー：" ファイル名Ｗ UPON CONS.
006420     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
006430     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
003930*-----------------------------------------*
003940     CALL "actcshm"  WITH C LINKAGE.
003950*-----------------------------------------*
006440     ACCEPT  キー入力 FROM CONS.
006450*================================================================*
006460 エラー処理Ｐ SECTION.
006470*
006480     IF 通知情報Ｐ NOT = "00"
006490         DISPLAY NC"帳票エラー"              UPON CONS
006500         DISPLAY NC"項目群名Ｐ：" 項目群名Ｐ UPON CONS
006510         DISPLAY NC"通知情報Ｐ：" 通知情報Ｐ UPON CONS
006520         DISPLAY NC"拡張制御Ｐ：" 拡張制御Ｐ UPON CONS
006530         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
006540                                             UPON CONS
007140*-----------------------------------------*
007150         CALL "actcshm"  WITH C LINKAGE
007160*-----------------------------------------*
006550         ACCEPT  キー入力 FROM CONS
006560         PERFORM ファイル閉鎖
006570         MOVE 99 TO PROGRAM-STATUS
006580         EXIT PROGRAM
006590     END-IF.
006600*================================================================*
006610 エラー表示Ｒ SECTION.
006620*
006630     DISPLAY NC"ファイル読込エラー" ファイル名Ｗ UPON CONS.
006640     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
003930*-----------------------------------------*
003940     CALL "actcshm"  WITH C LINKAGE.
003950*-----------------------------------------*
006650     ACCEPT  キー入力 FROM CONS.
006660     PERFORM ファイル閉鎖.
006670     EXIT PROGRAM.
006680*================================================================*
006690 エラー表示その他 SECTION.
006700*
006710     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
006720     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
006730                                                   UPON CONS.
003930*-----------------------------------------*
003940     CALL "actcshm"  WITH C LINKAGE.
003950*-----------------------------------------*
006740     ACCEPT  キー入力 FROM CONS.
006750     PERFORM ファイル閉鎖.
006760     EXIT PROGRAM.
006770*================================================================*
006780******************************************************************
006790 END PROGRAM YAI594.
006800******************************************************************
