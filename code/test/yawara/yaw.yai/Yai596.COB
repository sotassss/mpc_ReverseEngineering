000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YAI596.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*  【柔+ｳｨﾝﾄﾞｳｽﾞ版　愛知用 国民健康保険療養費申請集計表】
000100*  【印刷】
000120*       MED = YAI580 YAI596P 
000130*----------------------------------------------------------------*
000140 DATE-WRITTEN.           2013-07-03
000150 DATE-COMPILED.          2013-07-03
000160*----------------------------------------------------------------*
000170******************************************************************
000180*            ENVIRONMENT         DIVISION                        *
000190******************************************************************
000200 ENVIRONMENT             DIVISION.
000210 CONFIGURATION           SECTION.
000220 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000230 OBJECT-COMPUTER.        FMV-DESKPOWER.
000240 SPECIAL-NAMES.          CONSOLE  IS  CONS
000250                         SYSERR   IS  MSGBOX.
000260 INPUT-OUTPUT            SECTION.
000270 FILE-CONTROL.
000400     SELECT  施術所情報マスタ ASSIGN     TO        SEJOHOL
000410                             ORGANIZATION             IS  INDEXED
000420                             ACCESS MODE              IS  DYNAMIC
000430                             RECORD KEY               IS  施情−施術所番号
000440                             FILE STATUS              IS  状態キー
000450                             LOCK        MODE         IS  AUTOMATIC.
001360     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W5951L.DAT"
001370                             ORGANIZATION             IS  INDEXED
001380                             ACCESS                   IS  DYNAMIC
001390                             RECORD      KEY          IS  作１−本人家族区分
001450                                                          作１−患者カナ
001450                                                          作１−患者コード
001450                                                          作１−施術和暦年月
001490                             FILE        STATUS       IS  状態キー
001500                             LOCK        MODE         IS  AUTOMATIC.
001020     SELECT  印刷ファイル    ASSIGN      TO     GS-PRTF001
001030                             SYMBOLIC    DESTINATION  IS "PRT"
001040                             FORMAT                   IS  定義体名Ｐ
001050                             GROUP                    IS  項目群名Ｐ
001060                             PROCESSING  MODE         IS  処理種別Ｐ
001070                             UNIT        CONTROL      IS  拡張制御Ｐ
001080                             FILE        STATUS       IS  通知情報Ｐ.
001090******************************************************************
001100*                      DATA DIVISION                             *
001110******************************************************************
001120 DATA                    DIVISION.
001130 FILE                    SECTION.
001200*                           ［ＲＬ＝  ６４０］
001210 FD  施術所情報マスタ    BLOCK   CONTAINS   1   RECORDS.
001220     COPY SEJOHO         OF  XFDLIB  JOINING    施情 AS  PREFIX.
001350*
001870 FD  作業ファイル１ RECORD  CONTAINS 128 CHARACTERS.
001880 01  作１−レコード.
001890   03  作１−レコードキー.
001910     05  作１−本人家族区分            PIC 9(1).
001910     05  作１−患者カナ                PIC X(50).
001910     05  作１−患者コード.
               07 作１−患者番号             PIC 9(6).
               07 作１−枝番                 PIC X(1).
001910     05  作１−施術和暦年月.
               07 作１−施術和暦             PIC 9(1).
               07 作１−施術年               PIC 9(2).
               07 作１−施術月               PIC 9(2).
002050   03  作１−レコードデータ.
002080     05  作１−費用額                  PIC 9(7).
002090     05  作１−請求額                  PIC 9(7).
002100     05  作１−負担割合                PIC 9(2).
002110     05  FILLER                        PIC X(49).
001610*
001620 FD  印刷ファイル.
001630     COPY YAI596P        OF  XMDLIB.
001640******************************************************************
001650*                WORKING-STORAGE SECTION                         *
001660******************************************************************
001670 WORKING-STORAGE         SECTION.
001680 01 キー入力                           PIC X    VALUE SPACE.
001690 01 状態キー                           PIC X(2) VALUE SPACE.
001700 01 終了フラグ                         PIC X(3) VALUE SPACE.
001710 01 終了フラグ２                       PIC X(3) VALUE SPACE.
001720 01 終了フラグ３                       PIC X(3) VALUE SPACE.
001740 01 ファイル名Ｗ                       PIC N(2) VALUE SPACE.
001350 01 オープンフラグ                     PIC X(3)   VALUE SPACE.
001760 01 実行キーＷ                         PIC X(4) VALUE SPACE.
001950 01 カウンタ                           PIC 9(2) VALUE ZERO.
001960 01 印刷枚数ＣＮＴ                     PIC 9(2) VALUE ZERO.
001910 01 施術和暦年月Ｗ.
          03 施術和暦Ｗ                      PIC 9(1) VALUE ZERO.
          03 施術年Ｗ                        PIC 9(2) VALUE ZERO.
          03 施術月Ｗ                        PIC 9(2) VALUE ZERO.
001970 01 請求書Ｗ.
002010    03 会員番号Ｗ.
002030       05 会員番号ＷＰ                 PIC X(5)  VALUE SPACE.
002090    03 代表者名Ｗ                      PIC X(50) VALUE SPACE.
002900    03 番号Ｗ                          PIC 9(3)  VALUE ZERO.
          03 明細Ｗ                          OCCURS 99.
             05 明細行Ｗ                     OCCURS 30.
                07 患者カナＷ                PIC X(50) VALUE SPACE.
                07 本人家族Ｗ                PIC N(1)  VALUE SPACE.
                07 合計金額Ｗ                PIC 9(7)  VALUE ZERO.
                07 負担割合Ｗ                PIC 9(2)  VALUE ZERO.
                07 請求金額Ｗ                PIC 9(7)  VALUE ZERO.
002760*
      */桁あふれが発生する為桁を増やす↓↓↓/20200904
002900 01 頁Ｗ                               PIC 9(2)  VALUE ZERO.
002910 01 全頁Ｗ                             PIC 9(2)  VALUE ZERO.
      */桁あふれが発生する為桁を増やす↑↑↑/20200904
003050 01 本人家族区分Ｗ                     PIC 9(1)  VALUE ZERO.
003380*
003390 01 印刷制御.
003400     03 定義体名Ｐ                     PIC X(8) VALUE SPACE.
003410     03 項目群名Ｐ                     PIC X(8) VALUE SPACE.
003420     03 処理種別Ｐ                     PIC X(2) VALUE SPACE.
003430     03 拡張制御Ｐ.
003440         05 端末制御Ｐ.
003450             07 移動方向Ｐ             PIC X(1).
003460             07 移動行数Ｐ             PIC 9(3).
003470         05 詳細制御Ｐ                 PIC X(2).
003480     03 通知情報Ｐ                     PIC X(2) VALUE SPACE.
003490     03 ユニット名Ｐ                   PIC X(8) VALUE SPACE.
003660*
003670******************************************************************
003680*                          連結項目                              *
003690******************************************************************
002360*
002370 01 連メ−キー IS EXTERNAL.
002380    03  連メ−メッセージ                 PIC N(20).
003700*
003710****************
003720* 画面入力情報 *
003730****************
       01 連入−画面情報ＹＡＩ５８０ IS EXTERNAL.
          03 連入−請求和暦年月.
             05 連入−請求和暦               PIC 9(1).
             05 連入−請求年月.
                07 連入−請求年              PIC 9(2).
                07 連入−請求月              PIC 9(2).
          03 連入−作成和暦年月日.
             05 連入−作成和暦年月.
                07 連入−作成和暦            PIC 9(1).
                07 連入−作成年              PIC 9(2).
                07 連入−作成月              PIC 9(2).
             05 連入−作成日                 PIC 9(2).
      *
       01 連印−印刷情報ＹＡＩ５９５ IS EXTERNAL.
          03 連印−県外件数                  PIC 9(4).
          03 連印−本人件数                  PIC 9(3).
          03 連印−本人費用額                PIC 9(7).
          03 連印−本人請求額                PIC 9(7).
          03 連印−家族件数                  PIC 9(3).
          03 連印−家族費用額                PIC 9(7).
          03 連印−家族請求額                PIC 9(7).
          03 連印−合計件数                  PIC 9(4).
          03 連印−合計費用額                PIC 9(8).
          03 連印−合計請求額                PIC 9(8).
004250*
       01 連印−プレビュー IS EXTERNAL.
          03 連印−プレビュー区分          PIC 9.
010440*
000540************************************
000550* プリンタファイル作成用           *
000560************************************
000570 01 Ｈ連ＰＲＴＦ−作成データ IS EXTERNAL.
000580     03 Ｈ連ＰＲＴＦ−ファイル名           PIC X(8).
000590     03 Ｈ連ＰＲＴＦ−プレビュー区分       PIC 9.
000600     03 Ｈ連ＰＲＴＦ−帳票プログラム名     PIC X(8).
000610     03 Ｈ連ＰＲＴＦ−オーバレイ名         PIC X(8).
003190*
004260******************************************************************
004270*                      PROCEDURE  DIVISION                       *
004280******************************************************************
004290 PROCEDURE               DIVISION.
004300************
004310*           *
004320* 初期処理   *
004330*           *
004340************
002570     PERFORM プリンタファイル作成.
004350     PERFORM 初期化.
004360************
004370*           *
004380* 主処理     *
004390*           *
004400************
004410     PERFORM 印刷処理.
004420************
004430*           *
004440* 終了処理   *
004450*           *
004460************
004470     PERFORM 終了処理.
004480     EXIT PROGRAM.
004490*
004500*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002860*================================================================*
002870 プリンタファイル作成 SECTION.
002880*================================================================*
002890*   / 初期化 /
002900     MOVE SPACE TO Ｈ連ＰＲＴＦ−作成データ.
002910     INITIALIZE Ｈ連ＰＲＴＦ−作成データ.
002920*
002930*
002940*--↓↓ 変更箇所 ↓↓--------------------------------------*
002970*   使用するプリンタファイル名セット
002971     MOVE "PRTF001"             TO Ｈ連ＰＲＴＦ−ファイル名.
002972*
002973*   使用する帳票プログラム名セット
002974     MOVE "YAI596"             TO Ｈ連ＰＲＴＦ−帳票プログラム名.
002975*
002976*--↑↑-----------------------------------------------------*
002980*
002990*   / プレビュー区分セット /
003000     MOVE 連印−プレビュー区分  TO Ｈ連ＰＲＴＦ−プレビュー区分.
003010*
003020     CALL   "CRTPRTF".
003030     CANCEL "CRTPRTF".
003040*
004510*================================================================*
004520 初期化 SECTION.
004530*
004600     OPEN INPUT 施術所情報マスタ.
004610             MOVE NC"施情" TO ファイル名Ｗ.
004620             PERFORM オープンチェック.
004780*
004940     PERFORM 連結項目待避.
004950*
004960*================================================================*
004970 オープンチェック SECTION.
004980*
004990     IF 状態キー  NOT =  "00"
005000         DISPLAY ファイル名Ｗ NC"Ｆオープンエラー" UPON CONS
005010         DISPLAY NC"状態キー：" 状態キー           UPON CONS
005020         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
005030                                                   UPON CONS
003630*-----------------------------------------*
003640         CALL "actcshm"  WITH C LINKAGE
003650*-----------------------------------------*
005040         ACCEPT  キー入力 FROM CONS
005050         PERFORM ファイル閉鎖
005060         EXIT PROGRAM.
005480*================================================================*
005490 施術所情報マスタ読込 SECTION.
005500*
005510     MOVE ZERO TO 施情−施術所番号.
005520     READ 施術所情報マスタ
005530     INVALID KEY
005540         MOVE NC"施情" TO ファイル名Ｗ
005550         PERFORM エラー表示Ｒ
005560         PERFORM ファイル閉鎖
005570         MOVE 99 TO PROGRAM-STATUS
005580         EXIT PROGRAM
005590     NOT INVALID KEY
005680         MOVE 施情−代表者名         TO 代表者名Ｗ
005700         MOVE 施情−接骨師会会員番号 TO 会員番号ＷＰ
005720     END-READ.
005730*
005920*================================================================*
005930 ファイル閉鎖 SECTION.
005940*
003720     IF ( オープンフラグ = "YES" )
003730         CLOSE 印刷ファイル
003740     ELSE
003750         MOVE  NC"　　データが０件です。確認して下さい。" TO 連メ−メッセージ
003760         CALL   "MSG001"
003770         CANCEL "MSG001"
003780     END-IF.
003790*
005950     CLOSE  施術所情報マスタ.
005980*================================================================*
005990 終了処理 SECTION.
006000*
006010     PERFORM ファイル閉鎖.
006020*================================================================*
006030 連結項目待避 SECTION.
006040*
006050     MOVE 連入−請求年     TO 施術年Ｗ.
006060     MOVE 連入−請求月     TO 施術月Ｗ.
006130*
006140*================================================================*
006150 印刷処理 SECTION.
006160*
006170     PERFORM 施術所情報マスタ読込.
006180     OPEN INPUT 作業ファイル１.
006190         MOVE NC"作１" TO ファイル名Ｗ.
006200         PERFORM オープンチェック.
           MOVE 1              TO 印刷枚数ＣＮＴ.
           MOVE 1              TO 番号Ｗ.
006210     MOVE ZERO           TO 作１−本人家族区分.
006230     MOVE SPACE          TO 作１−患者カナ.
006300     MOVE ZERO           TO 作１−患者番号
006230     MOVE SPACE          TO 作１−枝番.
006300     MOVE ZERO           TO 作１−施術和暦年月
006310     START 作業ファイル１ KEY IS >= 作１−本人家族区分
006390                                    作１−患者カナ
006390                                    作１−患者コード
006390                                    作１−施術和暦年月
006400     END-START.
006410     IF 状態キー = "00"
006220         MOVE SPACE TO 終了フラグ２
006420         PERFORM 作業ファイル１読込
006480         PERFORM UNTIL (終了フラグ２     NOT = SPACE)
                  PERFORM VARYING カウンタ FROM 1 BY 1
                            UNTIL (カウンタ > 30) OR
                                  (終了フラグ２ NOT = SPACE)
006520               PERFORM 集計処理１
006530               PERFORM 作業ファイル１読込
                  END-PERFORM
                  IF (カウンタ > 30) AND (終了フラグ２ = SPACE)
                     COMPUTE 印刷枚数ＣＮＴ = 印刷枚数ＣＮＴ + 1
                  END-IF
006540         END-PERFORM
               MOVE 印刷枚数ＣＮＴ  TO 全頁Ｗ
               PERFORM VARYING 頁Ｗ FROM 1 BY 1
                         UNTIL 頁Ｗ > 印刷枚数ＣＮＴ
                   MOVE NC"甲"          TO 甲乙
                   PERFORM ヘッダセット
                   PERFORM VARYING カウンタ FROM 1 BY 1
                             UNTIL (カウンタ > 30) OR (患者カナＷ(頁Ｗ カウンタ) = SPACE)
006550                PERFORM 印刷レコードセット
                   END-PERFORM
                   IF 頁Ｗ = 1
                      PERFORM 合計セット
                   END-IF
006560             PERFORM 明細印刷処理
006560             PERFORM 改頁処理
               END-PERFORM
               MOVE 1               TO 番号Ｗ
               PERFORM VARYING 頁Ｗ FROM 1 BY 1
                         UNTIL 頁Ｗ > 印刷枚数ＣＮＴ
                   MOVE NC"乙"          TO 甲乙
                   PERFORM ヘッダセット
                   PERFORM VARYING カウンタ FROM 1 BY 1
                             UNTIL (カウンタ > 30) OR (患者カナＷ(頁Ｗ カウンタ) = SPACE)
006550                PERFORM 印刷レコードセット
                   END-PERFORM
                   IF 頁Ｗ = 1
                      PERFORM 合計セット
                   END-IF
006560             PERFORM 明細印刷処理
006560             PERFORM 改頁処理
               END-PERFORM
           ELSE
               IF 連印−県外件数 NOT = ZERO
                   MOVE 1               TO 頁Ｗ 全頁Ｗ
                   MOVE NC"甲"          TO 甲乙
                   PERFORM ヘッダセット
                   MOVE 連印−県外件数 TO 県外件数
006560             PERFORM 明細印刷処理
006560             PERFORM 改頁処理
                   MOVE NC"乙"          TO 甲乙
                   PERFORM ヘッダセット
                   MOVE 連印−県外件数 TO 県外件数
006560             PERFORM 明細印刷処理
006560             PERFORM 改頁処理
               END-IF
006570     END-IF.
006580     CLOSE 作業ファイル１.
006590*================================================================*
006600 作業ファイル１読込 SECTION.
006610*
006620     READ 作業ファイル１ NEXT
006630     AT END
006640         MOVE "YES" TO 終了フラグ２
006650         INITIALIZE 作１−レコード
006660     END-READ.
007750*================================================================*
007760 集計処理１ SECTION.
007770*
006950     MOVE 作１−患者カナ   TO 患者カナＷ(印刷枚数ＣＮＴ カウンタ).
006950     IF 作１−本人家族区分 = 1
              MOVE NC"本"        TO 本人家族Ｗ(印刷枚数ＣＮＴ カウンタ)
           ELSE
              MOVE NC"家"        TO 本人家族Ｗ(印刷枚数ＣＮＴ カウンタ)
           END-IF.
006950     MOVE 作１−費用額     TO 合計金額Ｗ(印刷枚数ＣＮＴ カウンタ).
006940     MOVE 作１−負担割合   TO 負担割合Ｗ(印刷枚数ＣＮＴ カウンタ).
006960     MOVE 作１−請求額     TO 請求金額Ｗ(印刷枚数ＣＮＴ カウンタ).
008680*
006670*================================================================*
006680 ヘッダセット SECTION.
006690*
008050     MOVE 施術月Ｗ         TO 請求月.
008160     MOVE 代表者名Ｗ       TO 代表者名.
008110     MOVE 会員番号Ｗ       TO 会員番号.
008180     MOVE 頁Ｗ             TO 頁.
008180     MOVE 全頁Ｗ           TO 全頁.
007750*================================================================*
007760 印刷レコードセット SECTION.
007770*
           MOVE 番号Ｗ                      TO 番号(カウンタ).
           COMPUTE 番号Ｗ = 番号Ｗ + 1.
006950     MOVE 患者カナＷ(頁Ｗ カウンタ)   TO 患者カナ(カウンタ).
006950     MOVE 本人家族Ｗ(頁Ｗ カウンタ)   TO 本人家族(カウンタ).
006950     MOVE 合計金額Ｗ(頁Ｗ カウンタ)   TO 合計金額(カウンタ).
006940     MOVE 負担割合Ｗ(頁Ｗ カウンタ)   TO 負担割合(カウンタ).
006960     MOVE 請求金額Ｗ(頁Ｗ カウンタ)   TO 請求金額(カウンタ).
008680*
007750*================================================================*
007760 合計セット SECTION.
007770*
           MOVE 連印−県外件数    TO 県外件数.
           MOVE 連印−本人件数    TO 本人件数.
           MOVE 連印−本人費用額  TO 本人費用額.
           MOVE 連印−本人請求額  TO 本人請求額.
           MOVE 連印−家族件数    TO 家族件数.
           MOVE 連印−家族費用額  TO 家族費用額.
           MOVE 連印−家族請求額  TO 家族請求額.
           MOVE 連印−合計件数    TO 合計件数.
           MOVE 連印−合計費用額  TO 合計費用額.
           MOVE 連印−合計請求額  TO 合計請求額.
009200*================================================================*
009210 明細印刷処理 SECTION.
009220*
005140     IF ( オープンフラグ NOT = "YES" )
005150        MOVE "YES" TO オープンフラグ
005160        OPEN I-O  印刷ファイル
005170        PERFORM エラー処理Ｐ
005180     END-IF.
006020*
009230     MOVE "YAI596P"  TO  定義体名Ｐ.
006690     MOVE SPACE      TO  処理種別Ｐ.
009240     MOVE "SCREEN"   TO  項目群名Ｐ.
009250     WRITE YAI596P.
009260     PERFORM エラー処理Ｐ.
006650*================================================================*
006660 改頁処理  SECTION.
006670
006680     MOVE "YAI596P" TO  定義体名Ｐ.
006690     MOVE "CT"      TO  処理種別Ｐ.
006700     MOVE "PAGE"    TO  拡張制御Ｐ.
006710     MOVE SPACE     TO  項目群名Ｐ.
006730     WRITE YAI596P.
006740     PERFORM エラー処理Ｐ.
006750     MOVE SPACE     TO  拡張制御Ｐ.
           INITIALIZE YAI596P.
009270*================================================================*
009280 エラー表示 SECTION.
009290*
009300     DISPLAY NC"ファイル書込エラー：" ファイル名Ｗ UPON CONS.
009310     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
009320     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
003930*-----------------------------------------*
003940     CALL "actcshm"  WITH C LINKAGE.
003950*-----------------------------------------*
009330     ACCEPT  キー入力 FROM CONS.
009340*================================================================*
009350 エラー処理Ｐ SECTION.
009360*
009370     IF 通知情報Ｐ NOT = "00"
009380         DISPLAY NC"帳票エラー"              UPON CONS
009390         DISPLAY NC"項目群名Ｐ：" 項目群名Ｐ UPON CONS
009400         DISPLAY NC"通知情報Ｐ：" 通知情報Ｐ UPON CONS
009410         DISPLAY NC"拡張制御Ｐ：" 拡張制御Ｐ UPON CONS
009420         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
009430                                             UPON CONS
007140*-----------------------------------------*
007150         CALL "actcshm"  WITH C LINKAGE
007160*-----------------------------------------*
009440         ACCEPT  キー入力 FROM CONS
009450         PERFORM ファイル閉鎖
009460         MOVE 99 TO PROGRAM-STATUS
009470         EXIT PROGRAM
009480     END-IF.
009490*================================================================*
009500 エラー表示Ｒ SECTION.
009510*
009520     DISPLAY NC"ファイル読込エラー" ファイル名Ｗ UPON CONS.
009530     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
003930*-----------------------------------------*
003940     CALL "actcshm"  WITH C LINKAGE.
003950*-----------------------------------------*
009540     ACCEPT  キー入力 FROM CONS.
009550     PERFORM ファイル閉鎖.
009560     EXIT PROGRAM.
009570*================================================================*
009580 エラー表示その他 SECTION.
009590*
009600     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
009610     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
009620                                                   UPON CONS.
003930*-----------------------------------------*
003940     CALL "actcshm"  WITH C LINKAGE.
003950*-----------------------------------------*
009630     ACCEPT  キー入力 FROM CONS.
009640     PERFORM ファイル閉鎖.
009650     EXIT PROGRAM.
009660*================================================================*
009670******************************************************************
009680 END PROGRAM YAI596.
009690******************************************************************
