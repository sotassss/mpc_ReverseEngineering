000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             EDACHK.
000060 AUTHOR.                 佐野 誠
000070*
000080*------------------------------------------------------------------*
000100* 受診者情報Ｆに該当月＋患者の枝番があるかチェック
000110*------------------------------------------------------------------*
000120 DATE-WRITTEN.           2008-02-04
000130 DATE-COMPILED.          2008-02-04
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000260     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000270                             ORGANIZATION             IS  INDEXED
000280                             ACCESS MODE              IS  DYNAMIC
000290                             RECORD KEY               IS  受−施術和暦年月
000300                                                          受−患者コード
000310                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000320                                                          受−患者カナ
000330                                                          受−患者コード
000340                             ALTERNATE RECORD KEY     IS  受−患者コード
000350                                                          受−施術和暦年月
000360                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000370                                                          受−保険種別
000380                                                          受−保険者番号
000390                                                          受−患者コード
000400                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000410                                                          受−公費種別
000420                                                          受−費用負担者番号
000430                                                          受−患者コード
000440                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000450                                                          受−助成種別
000460                                                          受−費用負担者番号助成
000470                                                          受−患者コード
000480                             ALTERNATE RECORD KEY     IS  受−請求和暦年月
000490                                                          受−施術和暦年月
000500                                                          受−患者コード
000510                             FILE STATUS              IS  状態キー
000520                             LOCK        MODE         IS  AUTOMATIC.
001130*
001140******************************************************************
001150*                      DATA DIVISION                             *
001160******************************************************************
001170 DATA                    DIVISION.
001180 FILE                    SECTION.
001181*
001190*                           ［ＲＬ＝  ３２０］
001200 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001210     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
001220*
002080*----------------------------------------------------------------*
002090******************************************************************
002100*                WORKING-STORAGE SECTION                         *
002110******************************************************************
002120 WORKING-STORAGE         SECTION.
002130 01 キー入力                           PIC X    VALUE SPACE.
002140 01 状態キー                           PIC X(2) VALUE SPACE.
002150 01 終了フラグ                         PIC X(3) VALUE SPACE.
002170 01 ファイル名                         PIC N(6) VALUE SPACE.
002171 01 患者カウンタ                       PIC 9(2) VALUE ZERO.
002180 01 後期高齢カウンタ                   PIC 9(2) VALUE ZERO.
002271*
002292*
002590******************************************************************
002600*                          連結項目                              *
002610******************************************************************
002620*
002622* 受診者Ｆの枝番チェック
002623 01 連受診枝番ＣＨＫ−キー IS EXTERNAL.
002624    03 連受診枝番ＣＨＫ−施術和暦年月.
002625       05 連受診枝番ＣＨＫ−施術和暦          PIC 9.
002626       05 連受診枝番ＣＨＫ−施術年月.
002627          07 連受診枝番ＣＨＫ−施術年         PIC 9(2).
002628          07 連受診枝番ＣＨＫ−施術月         PIC 9(2).
002629    03 連受診枝番ＣＨＫ−患者コード.
002630       05 連受診枝番ＣＨＫ−患者番号          PIC 9(6).
002631       05 連受診枝番ＣＨＫ−枝番              PIC X.
002634*   OUT（0:なし、1:あり）
002635    03 連受診枝番ＣＨＫ−枝番あり             PIC 9.
002636    03 連受診枝番ＣＨＫ−後期高齢枝番あり     PIC 9.
002730*
003481******************************************************************
003482*                      PROCEDURE  DIVISION                       *
003483******************************************************************
003490 PROCEDURE               DIVISION.
003500************
003510*           *
003520* 初期処理   *
003530*           *
003540************
003550     PERFORM 初期化.
003560************
003570*           *
003580* 主処理     *
003590*           *
003600************
003601*
003627     PERFORM 枝番確認処理.
003629*
003630************
003631*           *
003640* 終了処理   *
003650*           *
003660************
003670     PERFORM 終了処理.
003690     EXIT PROGRAM.
003700*
003710*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003720*================================================================*
003730 初期化 SECTION.
003740*
003741     PERFORM ファイルオープン.
003862*
003874*================================================================*
003884 ファイルオープン SECTION.
003894*
003904     OPEN INPUT 受診者情報Ｆ.
003914         MOVE NC"受診者情報Ｆ" TO ファイル名.
003924         PERFORM オープンチェック.
003925*
003934*================================================================*
003944 オープンチェック SECTION.
003954*
003964     IF 状態キー  NOT =  "00"
003974         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
003984         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003994         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004004                                                 UPON CONS
004005*-----------------------------------------*
004006         CALL "actcshm"  WITH C LINKAGE
004007*-----------------------------------------*
004014         ACCEPT  キー入力 FROM CONS
004024         PERFORM ファイル閉鎖
004034         MOVE 99 TO PROGRAM-STATUS
004044         EXIT PROGRAM.
004111*================================================================*
004112*================================================================*
004113 ファイル閉鎖 SECTION.
004114*
004115     CLOSE 受診者情報Ｆ.
004116*
004117*================================================================*
004118 終了処理 SECTION.
004119*
004124     PERFORM ファイル閉鎖.
004125*
004134*================================================================*
004135*================================================================*
004136 枝番確認処理 SECTION.
004137*
004139*
004140     MOVE ZERO TO 連受診枝番ＣＨＫ−枝番あり.
004142     MOVE ZERO TO 連受診枝番ＣＨＫ−後期高齢枝番あり.
004144*
004145     MOVE 連受診枝番ＣＨＫ−施術和暦     TO 受−施術和暦.
004146     MOVE 連受診枝番ＣＨＫ−施術年       TO 受−施術年.
004147     MOVE 連受診枝番ＣＨＫ−施術月       TO 受−施術月.
004148     MOVE 連受診枝番ＣＨＫ−患者番号     TO 受−患者番号.
004149     MOVE SPACE                          TO 受−枝番.
004150*
004151     START 受診者情報Ｆ KEY IS >= 受−施術和暦年月 受−患者コード
004152     END-START.
004153     IF 状態キー = "00"
004154             MOVE ZERO  TO 患者カウンタ 後期高齢カウンタ
004155             MOVE SPACE TO 終了フラグ
004156             PERFORM 受診者情報Ｆ読込
004157             PERFORM UNTIL ( 終了フラグ   NOT = SPACE      ) OR
004158                           ( 受−施術和暦 NOT = 連受診枝番ＣＨＫ−施術和暦  ) OR
004159                           ( 受−施術年   NOT = 連受診枝番ＣＨＫ−施術年    ) OR
004160                           ( 受−施術月   NOT = 連受診枝番ＣＨＫ−施術月    ) OR
004161                           ( 受−患者番号 NOT = 連受診枝番ＣＨＫ−患者番号  ) 
004162*
004163                 COMPUTE 患者カウンタ = 患者カウンタ + 1
004164*
004165                 IF ( 受−施術和暦年月 >= 42004 ) AND ( 受−保険種別 = 05 )
004166                     COMPUTE 後期高齢カウンタ = 後期高齢カウンタ + 1
004167                 END-IF
004168*
004170                 PERFORM 受診者情報Ｆ読込
004171             END-PERFORM
004172*
004173*------------------------------------------------------------------*
004174             IF 患者カウンタ > 1
004185                MOVE 1  TO 連受診枝番ＣＨＫ−枝番あり
004187                IF 後期高齢カウンタ > ZERO
004188                   MOVE 1  TO 連受診枝番ＣＨＫ−後期高齢枝番あり
004189                END-IF
004192             END-IF
004193*------------------------------------------------------------------*
004194*
004197     END-IF.
004199*
004201*================================================================*
004202 受診者情報Ｆ読込 SECTION.
004203*
004204     READ 受診者情報Ｆ NEXT
004205     AT END
004206         MOVE "YES" TO 終了フラグ
004207     END-READ.
004208*
006560*================================================================*
010106*================================================================*
010200******************************************************************
010210 END PROGRAM EDACHK.
010220******************************************************************
