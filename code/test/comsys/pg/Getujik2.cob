000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             GETUJIK2.
000060 AUTHOR.                 佐野　誠
000070*
000080*----------------------------------------------------------------*
000090*  月次更新 共通
000100*  労災・自賠責・生保のファイルを月次更新
000110*----------------------------------------------------------------*
000120 DATE-WRITTEN.           2009-09-11
000130 DATE-COMPILED.          2009-09-11
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000260*
000270     SELECT  自賠責情報Ｆ    ASSIGN      TO        JIBAIJL
000280                             ORGANIZATION             IS INDEXED
000290                             ACCESS MODE              IS DYNAMIC
000300                             RECORD KEY               IS 自賠−施術和暦年月
000310                                                         自賠−患者コード
000320                             ALTERNATE RECORD KEY     IS 自賠−患者コード
000330                                                         自賠−施術和暦年月
000340                             FILE STATUS              IS 状態キー
000350                             LOCK        MODE         IS AUTOMATIC.
000360*
000370     SELECT  労災情報Ｆ      ASSIGN      TO        ROUSAIJL
000380                             ORGANIZATION             IS INDEXED
000390                             ACCESS MODE              IS DYNAMIC
000400                             RECORD KEY               IS 労災−施術和暦年月
000410                                                         労災−患者コード
000420                             ALTERNATE RECORD KEY     IS 労災−患者コード
000430                                                         労災−施術和暦年月
000440                             FILE STATUS              IS 状態キー
000450                             LOCK        MODE         IS AUTOMATIC.
000460*
000470     SELECT  生保情報Ｆ      ASSIGN      TO        SEIHOJL
000480                             ORGANIZATION             IS INDEXED
000490                             ACCESS MODE              IS DYNAMIC
000500                             RECORD KEY               IS 生保−施術和暦年月
000510                                                         生保−患者コード
000520                             ALTERNATE RECORD KEY     IS 生保−患者コード
000530                                                         生保−施術和暦年月
000540                             FILE STATUS              IS 状態キー
000550                             LOCK        MODE         IS AUTOMATIC.
000560*
000570     SELECT  負傷データＦ    ASSIGN      TO        HUSYOUL
000580                             ORGANIZATION             IS  INDEXED
000590                             ACCESS MODE              IS  DYNAMIC
000600                             RECORD KEY               IS  負−施術和暦年月
000610                                                          負−患者コード
000620                             ALTERNATE RECORD KEY     IS  負−患者コード
000630                                                          負−施術和暦年月
000640                             FILE STATUS              IS  状態キー
000650                             LOCK        MODE         IS  AUTOMATIC.
000660     SELECT  施術記録Ｆ      ASSIGN      TO        SEKIROKL
000670                             ORGANIZATION             IS  INDEXED
000680                             ACCESS MODE              IS  DYNAMIC
000690                             RECORD KEY           IS 施記−施術和暦年月日
000700                                                     施記−患者コード
000710                             ALTERNATE RECORD KEY IS 施記−患者コード
000720                                                     施記−施術和暦年月日
000730                             FILE STATUS              IS  状態キー
000740                             LOCK        MODE         IS  AUTOMATIC.
000750*
000760     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000770                             ORGANIZATION             IS  INDEXED
000780                             ACCESS MODE              IS  DYNAMIC
000790                             RECORD KEY               IS  元−元号区分
000800                             FILE STATUS              IS  状態キー
000810                             LOCK        MODE         IS  AUTOMATIC.
000820******************************************************************
000830*                      DATA DIVISION                             *
000840******************************************************************
000850 DATA                    DIVISION.
000860 FILE                    SECTION.
000870*
000880 FD  自賠責情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
000890     COPY JIBAIJ      OF  XFDLIB  JOINING   自賠   AS  PREFIX.
000900*
000910 FD  労災情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
000920     COPY ROUSAIJ      OF  XFDLIB  JOINING   労災   AS  PREFIX.
000930*
000940 FD  生保情報Ｆ       BLOCK   CONTAINS   1   RECORDS.
000950     COPY SEIHOJ      OF  XFDLIB  JOINING   生保   AS  PREFIX.
000960*                           ［ＲＬ＝  １２８］
000970 FD  負傷データＦ        BLOCK   CONTAINS   1   RECORDS.
000980     COPY HUSYOU          OF  XFDLIB  JOINING   負   AS  PREFIX.
000990*                           ［ＲＬ＝  ２５６］
001000 FD  施術記録Ｆ          BLOCK   CONTAINS   1   RECORDS.
001010     COPY SEKIROK         OF  XFDLIB  JOINING   施記 AS  PREFIX.
001020*                           ［ＲＬ＝  １２８］
001030 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
001040     COPY GENGOU          OF  XFDLIB  JOINING   元   AS  PREFIX.
001050*
001060*----------------------------------------------------------------*
001070******************************************************************
001080*                WORKING-STORAGE SECTION                         *
001090******************************************************************
001100 WORKING-STORAGE         SECTION.
001110 01 キー入力                           PIC X    VALUE SPACE.
001120 01 状態キー                           PIC X(2) VALUE SPACE.
001130 01 終了フラグ                         PIC X(3) VALUE SPACE.
001140 01 更新フラグ                         PIC X(3) VALUE SPACE.
001150 01 ファイル名                         PIC N(6) VALUE SPACE.
001160 01 施術和暦年月ＮＷ.
001170   03 施術和暦ＮＷ                     PIC 9.
001180   03 施術年ＮＷ                       PIC 9(2).
001190   03 施術月ＮＷ                       PIC 9(2).
001200*
001210 01 前月フラグ                         PIC X(3) VALUE SPACE.
001220 01 終了フラグ２                       PIC X(3) VALUE SPACE.
001230*/前回通院月判定
001240 01 計算年月日Ｗ.
001250    03 計算和暦Ｗ                      PIC 9(1)  VALUE ZERO.
001260    03 計算年Ｗ                        PIC S9(2)  VALUE ZERO.
001270    03 計算月Ｗ                        PIC S9(2)  VALUE ZERO.
001280    03 計算日Ｗ                        PIC S9(2)  VALUE ZERO.
001290 01 開始年月日２Ｗ.
001300    03 開始和暦２Ｗ                    PIC 9(1)  VALUE ZERO.
001310    03 開始年２Ｗ                      PIC 9(2)  VALUE ZERO.
001320    03 開始月２Ｗ                      PIC 9(2)  VALUE ZERO.
001330    03 開始日２Ｗ                      PIC 9(2)  VALUE ZERO.
001340    03 開始西暦年Ｗ                    PIC S9(4) VALUE ZERO.
001350 01 終了年月日２Ｗ.
001360    03 終了和暦２Ｗ                    PIC 9(1)  VALUE ZERO.
001370    03 終了年２Ｗ                      PIC 9(2)  VALUE ZERO.
001380    03 終了月２Ｗ                      PIC 9(2)  VALUE ZERO.
001390    03 終了日２Ｗ                      PIC 9(2)  VALUE ZERO.
001400    03 終了西暦年Ｗ                    PIC S9(4) VALUE ZERO.
001410**データチェック用
001420 01 実行キーＷ                         PIC X(3)  VALUE SPACE.
001430 01 施術記録有Ｗ                       PIC X(3) VALUE SPACE.
001440**
001450 01 前詰終了フラグ                     PIC X(3) VALUE SPACE.
001460 01 部位カウンタ                       PIC 9(2) VALUE ZERO.
001470 01 部位カウンタＷ                     PIC 9(2) VALUE ZERO.
001480 01 部位カウンタ２                     PIC 9(2) VALUE ZERO.
001490 01 レコードデータＴＷ.
001500    03 部位別料金ＴＷ１.
001510       05 部位別料金ＴＷ OCCURS 7.
001520          07 整復料ＴＷ                PIC 9(5) VALUE ZERO.
001530          07 後療料２０ＴＷ            PIC 9(4) VALUE ZERO.
001540          07 後療料２３ＴＷ            PIC 9(4) VALUE ZERO.
001550          07 後療料２４ＴＷ            PIC 9(4) VALUE ZERO.
001560          07 後療料３０ＴＷ            PIC 9(4) VALUE ZERO.
001570          07 特別材料費ＴＷ            PIC 9(4) VALUE ZERO.
001580          07 交換包帯料ＴＷ            PIC 9(4) VALUE ZERO.
001590          07 拘縮後療料２０ＴＷ        PIC 9(4) VALUE ZERO.
001600          07 拘縮後療料３０ＴＷ        PIC 9(4) VALUE ZERO.
001610*****************************************************************
001620*                          連結項目                             *
001630*****************************************************************
001640*
001650*
001660*-------------------------------------------------------------------*
001670* 労災・自賠責・生保ファイルの月次更新連携キー
001680 01 連月労災等−キー IS EXTERNAL.
001690    03  連月労災等−施術和暦年月.
001700      05  連月労災等−施術和暦               PIC 9.
001710      05  連月労災等−施術年月.
001720        07  連月労災等−施術年               PIC 9(2).
001730        07  連月労災等−施術月               PIC 9(2).
001740    03  連月労災等−患者コード.
001750      05  連月労災等−患者番号               PIC 9(6).
001760      05  連月労災等−枝番                   PIC X.
001770*   / 1:健康保険、2:労災、3:自賠責、4:生保、5:自費 /
001780    03  連月労災等−保険分類                 PIC 9(2).
001790    03  連月労災等−最新施術和暦年月.
001800      05  連月労災等−最新施術和暦           PIC 9.
001810      05  連月労災等−最新施術年月.
001820        07  連月労災等−最新施術年           PIC 9(2).
001830        07  連月労災等−最新施術月           PIC 9(2).
001840*
001850*
001860 01 連メ−キー IS EXTERNAL.
001870    03  連メ−メッセージ                 PIC N(20).
001880*
001890*
001900******************************************************************
001910*                      PROCEDURE  DIVISION                       *
001920******************************************************************
001930 PROCEDURE               DIVISION.
001940************
001950*           *
001960* 初期処理   *
001970*           *
001980************
001990     PERFORM 初期化.
002000************
002010*           *
002020* 主処理     *
002030*           *
002040************
002050*
002060     EVALUATE 連月労災等−保険分類
002070     WHEN 2
002080        PERFORM 労災月次更新
002090     WHEN 3
002100        PERFORM 自賠責月次更新
002110     WHEN 4
002120        PERFORM 生保月次更新
002130     END-EVALUATE.
002140*
002150************
002160*           *
002170* 終了処理   *
002180*           *
002190************
002200     PERFORM 終了処理.
002210     EXIT PROGRAM.
002220*
002230*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002240*================================================================*
002250 初期化 SECTION.
002260*
002270*
002280*================================================================*
002290 オープンチェック SECTION.
002300*
002310     IF 状態キー  NOT =  "00"
002320         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
002330         DISPLAY NC"状態キー：" 状態キー         UPON CONS
002340         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
002350                                                 UPON CONS
002360*-----------------------------------------*
002370         CALL "actcshm"  WITH C LINKAGE
002380*-----------------------------------------*
002390         ACCEPT  キー入力 FROM CONS
002400         PERFORM ファイル閉鎖
002410         EXIT PROGRAM.
002420*================================================================*
002430 ファイル閉鎖 SECTION.
002440*
002450*================================================================*
002460 終了処理 SECTION.
002470*
002480     PERFORM ファイル閉鎖.
002490     MOVE 1 TO PROGRAM-STATUS.
002500*================================================================*
002510 エラー表示 SECTION.
002520*
002530     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
002540     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
002550     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
002560     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
002570*-----------------------------------------*
002580     CALL "actcshm"  WITH C LINKAGE.
002590*-----------------------------------------*
002600     ACCEPT  キー入力 FROM CONS.
002610     PERFORM 終了処理.
002620     MOVE ZERO TO PROGRAM-STATUS.
002630     EXIT PROGRAM.
002640*
002650*================================================================*
002660 エラー表示Ｒ SECTION.
002670*
002680     DISPLAY NC"ファイル読込エラー" ファイル名     UPON CONS.
002690     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
002700     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
002710*-----------------------------------------*
002720     CALL "actcshm"  WITH C LINKAGE.
002730*-----------------------------------------*
002740     ACCEPT  キー入力 FROM CONS.
002750     PERFORM 終了処理.
002760     MOVE ZERO TO PROGRAM-STATUS.
002770     EXIT PROGRAM.
002780*
002790*================================================================*
002800*================================================================*
002810 労災月次更新 SECTION.
002820*
002830     OPEN I-O 労災情報Ｆ.
002840         MOVE NC"労災" TO ファイル名.
002850         PERFORM オープンチェック.
002860*
002870     MOVE 連月労災等−最新施術和暦 TO 労災−施術和暦.
002880     MOVE 連月労災等−最新施術年   TO 労災−施術年.
002890     MOVE 連月労災等−最新施術月   TO 労災−施術月.
002900     MOVE 連月労災等−患者番号     TO 労災−患者番号.
002910     MOVE 連月労災等−枝番         TO 労災−枝番.
002920     READ 労災情報Ｆ
002930     INVALID KEY
002940          CLOSE 労災情報Ｆ
002950          DISPLAY NC"労災情報Ｆの月次更新ができません。" UPON CONS
002960          DISPLAY NC"最新年月のデータがありません。" UPON CONS
002970          MOVE NC"労災" TO ファイル名
002980          PERFORM エラー表示Ｒ
002990     NOT INVALID KEY
003000          MOVE 連月労災等−施術和暦 TO 労災−施術和暦
003010          MOVE 連月労災等−施術年   TO 労災−施術年
003020          MOVE 連月労災等−施術月   TO 労災−施術月
003030*
003040          WRITE 労災−レコード
003050          IF 状態キー NOT = "00"
003060             IF 状態キー NOT = "22"
003070                MOVE NC"労災" TO ファイル名
003080                PERFORM エラー表示
003090             END-IF
003100          END-IF
003110     END-READ.
003120*
003130     CLOSE 労災情報Ｆ.
003140*
003150*================================================================*
003160*================================================================*
003170 自賠責月次更新 SECTION.
003180*
003190     OPEN I-O 自賠責情報Ｆ.
003200         MOVE NC"自賠責" TO ファイル名.
003210         PERFORM オープンチェック.
003220     OPEN INPUT 負傷データＦ.
003230         MOVE NC"負傷" TO ファイル名.
003240         PERFORM オープンチェック.
003250     OPEN INPUT 施術記録Ｆ.
003260         MOVE NC"施記Ｆ" TO ファイル名.
003270         PERFORM オープンチェック.
003280     OPEN INPUT   元号マスタ.
003290         MOVE NC"元号" TO ファイル名.
003300         PERFORM オープンチェック.
003310*
003320     MOVE 連月労災等−最新施術和暦 TO 自賠−施術和暦.
003330     MOVE 連月労災等−最新施術年   TO 自賠−施術年.
003340     MOVE 連月労災等−最新施術月   TO 自賠−施術月.
003350     MOVE 連月労災等−患者番号     TO 自賠−患者番号.
003360     MOVE 連月労災等−枝番         TO 自賠−枝番.
003370     READ 自賠責情報Ｆ
003380*/コンバート直後はレコードがない。なくても問題なし↓↓↓/130227
003390*     INVALID KEY
003400*          CLOSE 自賠責情報Ｆ
003410*          DISPLAY NC"自賠責情報Ｆの月次更新ができません。" UPON CONS
003420*          DISPLAY NC"最新年月のデータがありません。"       UPON CONS
003430*          MOVE NC"自賠責" TO ファイル名
003440*          PERFORM エラー表示Ｒ
003450*/コンバート直後はレコードがない。なくても問題なし↑↑↑/130227
003460     NOT INVALID KEY
003470          MOVE 連月労災等−施術和暦 TO 自賠−施術和暦
003480          MOVE 連月労災等−施術年   TO 自賠−施術年
003490          MOVE 連月労災等−施術月   TO 自賠−施術月
003500*/前月に負傷通院がある場合データを繰り越す
003510*/前月に負傷通院があっても固有単価使用区分がゼロなら固有単価をクリア
003520*/前月に負傷通院がない場合はキー情報のみ繰り越す
003530          PERFORM 前回通院月判定
003540          IF 実行キーＷ = "YES"
003550              IF 自賠−固有単価使用区分 = 1
003560                  PERFORM 固有単価前詰処理
003570              ELSE
003580                  PERFORM 固有単価料金クリア
003590              END-IF
003600          ELSE
003610              MOVE SPACE TO 自賠−レコードデータ
003620              INITIALIZE    自賠−レコードデータ
003630          END-IF
003640*
003650          WRITE 自賠−レコード
003660          IF 状態キー NOT = "00"
003670             IF 状態キー NOT = "22"
003680                MOVE NC"自賠責" TO ファイル名
003690                PERFORM エラー表示
003700             END-IF
003710          END-IF
003720     END-READ.
003730*
003740     CLOSE 自賠責情報Ｆ 負傷データＦ 施術記録Ｆ 元号マスタ.
003750*
003760*================================================================*
003770*================================================================*
003780 生保月次更新 SECTION.
003790*
003800     OPEN I-O 生保情報Ｆ.
003810         MOVE NC"生保" TO ファイル名.
003820         PERFORM オープンチェック.
003830*
003840     MOVE 連月労災等−最新施術和暦 TO 生保−施術和暦.
003850     MOVE 連月労災等−最新施術年   TO 生保−施術年.
003860     MOVE 連月労災等−最新施術月   TO 生保−施術月.
003870     MOVE 連月労災等−患者番号     TO 生保−患者番号.
003880     MOVE 連月労災等−枝番         TO 生保−枝番.
003890     READ 生保情報Ｆ
003900     INVALID KEY
003910          CLOSE 生保情報Ｆ
003920          DISPLAY NC"生保情報Ｆの月次更新ができません。" UPON CONS
003930          DISPLAY NC"最新年月のデータがありません。" UPON CONS
003940          MOVE NC"生保" TO ファイル名
003950          PERFORM エラー表示Ｒ
003960     NOT INVALID KEY
003970          MOVE 連月労災等−施術和暦 TO 生保−施術和暦
003980          MOVE 連月労災等−施術年   TO 生保−施術年
003990          MOVE 連月労災等−施術月   TO 生保−施術月
004000*
004010          WRITE 生保−レコード
004020          IF 状態キー NOT = "00"
004030             IF 状態キー NOT = "22"
004040                MOVE NC"生保" TO ファイル名
004050                PERFORM エラー表示
004060             END-IF
004070          END-IF
004080     END-READ.
004090*
004100     CLOSE 生保情報Ｆ.
004110*
004120*================================================================*
004130*================================================================*
004140*================================================================*
004150 前回通院月判定 SECTION.
004160*
004170*** 前回の負傷データが前月か判定 
004180     MOVE  SPACE                 TO 前月フラグ.
004190     MOVE 連月労災等−患者コード TO 負−患者コード.
004200     MOVE 連月労災等−施術和暦   TO 負−施術和暦.
004210     MOVE 連月労災等−施術年     TO 負−施術年.
004220     MOVE 連月労災等−施術月     TO 負−施術月.
004230     START 負傷データＦ KEY IS <  負−患者コード
004240                                  負−施術和暦年月
004250                                  REVERSED
004260     END-START.
004270     IF 状態キー = "00"
004280         MOVE SPACE  TO 終了フラグ２
004290         PERFORM 負傷データＦ読込
004300         IF ( 終了フラグ２    = SPACE                  ) AND
004310            ( 負−患者コード  = 連月労災等−患者コード )
004320            PERFORM 前月判定
004330         END-IF
004340     END-IF.
004350*
004360*================================================================*
004370 前月判定 SECTION.
004380* 
004390*** 読み込んだ負傷データの年月が、前月かどうか判定 (年月の差が 1 か?)
004400      MOVE  SPACE  TO  前月フラグ.
004410      INITIALIZE  計算年月日Ｗ 開始年月日２Ｗ 終了年月日２Ｗ.
004420**
004430      MOVE 連月労災等−施術和暦  TO 終了和暦２Ｗ.
004440      MOVE 連月労災等−施術年    TO 終了年２Ｗ.
004450      MOVE 連月労災等−施術月    TO 終了月２Ｗ.
004460      MOVE 負−施術和暦          TO 開始和暦２Ｗ.
004470      MOVE 負−施術年            TO 開始年２Ｗ.
004480      MOVE 負−施術月            TO 開始月２Ｗ.
004490*
004500      EVALUATE TRUE
004510       WHEN (開始和暦２Ｗ = 終了和暦２Ｗ) AND (開始年２Ｗ = 終了年２Ｗ)
004520            PERFORM  前月比較月
004530       WHEN (開始和暦２Ｗ = 終了和暦２Ｗ) AND (開始年２Ｗ NOT = 終了年２Ｗ)
004540            PERFORM  前月比較年
004550       WHEN  開始和暦２Ｗ NOT = 終了和暦２Ｗ 
004560            PERFORM  前月比較元号
004570      END-EVALUATE.
004580*
004590*--- 元号日付チェック ---*
004600*****      IF 計算月Ｗ = 1
004610*****         MOVE  "YES"  TO  前月フラグ
004620*****         PERFORM データチェック
004630******      END-IF.
004640      IF ( 計算月Ｗ = 1 )
004650         MOVE  "YES"  TO  前月フラグ
004660         IF ( 開始和暦２Ｗ NOT = 終了和暦２Ｗ )
004670            MOVE "YES"  TO  実行キーＷ
004680         ELSE
004690            PERFORM データチェック
004700         END-IF
004710      END-IF.
004720*
004730*================================================================*
004740 前月比較月  SECTION.
004750*
004760     IF  終了月２Ｗ >  開始月２Ｗ
004770         COMPUTE 計算月Ｗ = 終了月２Ｗ - 開始月２Ｗ
004780     ELSE
004790        MOVE ZERO TO 計算月Ｗ
004800     END-IF.
004810*
004820*================================================================*
004830 前月比較年  SECTION.
004840*
004850     IF  終了年２Ｗ >  開始年２Ｗ
004860         COMPUTE 計算年Ｗ = 終了年２Ｗ - 開始年２Ｗ
004870         COMPUTE 計算月Ｗ = (計算年Ｗ * 12 + 終了月２Ｗ) - 開始月２Ｗ
004880     ELSE
004890        MOVE ZERO TO 計算月Ｗ
004900     END-IF.
004910*
004920*================================================================*
004930 前月比較元号  SECTION.
004940*
004950     MOVE 開始和暦２Ｗ TO 元−元号区分.
004960     READ 元号マスタ
004970     NOT INVALID KEY
004980         MOVE 元−開始西暦年 TO 開始西暦年Ｗ
004990     END-READ.
005000     MOVE 終了和暦２Ｗ TO 元−元号区分.
005010     READ 元号マスタ
005020     NOT INVALID KEY
005030         MOVE 元−開始西暦年 TO 終了西暦年Ｗ
005040     END-READ.
005050**
005060     IF (開始西暦年Ｗ NOT = ZERO) AND (終了西暦年Ｗ NOT = ZERO)
005070        COMPUTE 開始西暦年Ｗ = 開始西暦年Ｗ + 開始年２Ｗ - 1
005080        COMPUTE 終了西暦年Ｗ = 終了西暦年Ｗ + 終了年２Ｗ - 1
005090*
005100        IF 終了西暦年Ｗ =  開始西暦年Ｗ
005110           PERFORM  前月比較月
005120*--- 元号日付チェック ---*
005130           IF ( 計算月Ｗ = ZERO )
005140              MOVE 1 TO 計算月Ｗ
005150           END-IF
005160*
005170        ELSE
005180           IF  終了西暦年Ｗ >  開始西暦年Ｗ
005190               COMPUTE 計算年Ｗ = 終了西暦年Ｗ - 開始西暦年Ｗ
005200               COMPUTE 計算月Ｗ = (計算年Ｗ * 12 + 終了月２Ｗ) - 開始月２Ｗ
005210           ELSE
005220               MOVE ZERO TO 計算月Ｗ
005230           END-IF
005240        END-IF
005250     ELSE
005260        MOVE ZERO TO 計算月Ｗ
005270     END-IF.
005280*
005290*================================================================*
005300 負傷データＦ読込 SECTION.
005310*
005320     READ 負傷データＦ NEXT
005330     AT END
005340         MOVE "YES" TO 終了フラグ２
005350     END-READ.
005360*================================================================*
005370 データチェック SECTION.
005380*
005390     MOVE SPACE  TO 実行キーＷ.
005400* *****************************************************************
005410* * 負傷部位有無チェック：部位数 = 0 の場合データ作成対象としない *
005420* *****************************************************************
005430     IF 負−部位数 NOT = ZERO
005440*    *************************************************************
005450*    * 施術記録チェック：通院数 = 0 の場合データ作成対象としない *
005460*    *************************************************************
005470         MOVE 負−患者番号  TO 施記−患者番号
005480         MOVE 負−枝番      TO 施記−枝番
005490         MOVE 負−施術和暦  TO 施記−施術和暦
005500         MOVE 負−施術年    TO 施記−施術年
005510         MOVE 負−施術月    TO 施記−施術月
005520         MOVE ZERO          TO 施記−施術日
005530         START 施術記録Ｆ   KEY IS >= 施記−患者コード
005540                                      施記−施術和暦年月日
005550         END-START
005560         IF 状態キー = "00"
005570             MOVE SPACE TO 終了フラグ２
005580             MOVE SPACE TO 施術記録有Ｗ
005590             PERFORM 施術記録Ｆ読込
005600             PERFORM UNTIL ( 終了フラグ２         = "YES"          ) OR
005610                           ( 施記−患者コード NOT = 負−患者コード ) OR
005620                           ( 施記−施術和暦   NOT = 負−施術和暦   ) OR
005630                           ( 施記−施術年     NOT = 負−施術年     ) OR
005640                           ( 施記−施術月     NOT = 負−施術月     ) OR
005650                           ( 施術記録有Ｗ         = "YES"          )
005660                 MOVE "YES"  TO 施術記録有Ｗ
005670                 MOVE "YES"  TO 実行キーＷ
005680             END-PERFORM
005690         ELSE
005700             MOVE SPACE  TO 実行キーＷ
005710         END-IF
005720     ELSE
005730         MOVE SPACE  TO 実行キーＷ
005740     END-IF.
005750*
005760*================================================================*
005770 施術記録Ｆ読込 SECTION.
005780*
005790     READ 施術記録Ｆ NEXT
005800     AT END
005810         MOVE "YES" TO 終了フラグ２
005820     END-READ.
005830*================================================================*
005840*================================================================*
005850 固有単価前詰処理 SECTION.
005860*
005870     MOVE 連月労災等−最新施術和暦 TO 負−施術和暦.
005880     MOVE 連月労災等−最新施術年   TO 負−施術年.
005890     MOVE 連月労災等−最新施術月   TO 負−施術月.
005900     MOVE 連月労災等−患者番号     TO 負−患者番号.
005910     MOVE 連月労災等−枝番         TO 負−枝番.
005920     READ 負傷データＦ
005930     INVALID KEY
005940         MOVE NC"負傷" TO ファイル名
005950         PERFORM エラー表示Ｒ
005960     NOT INVALID KEY
005970         MOVE ZERO  TO 部位カウンタ２
005980         MOVE SPACE TO 部位別料金ＴＷ１
005990         INITIALIZE    部位別料金ＴＷ１
006000         PERFORM VARYING 部位カウンタ FROM 1 BY 1 UNTIL 部位カウンタ > 負−部位数
006010             IF 負−転帰区分(部位カウンタ) = 9
006020                 COMPUTE 部位カウンタ２ = 部位カウンタ２ + 1
006030                 MOVE 自賠−部位別料金(部位カウンタ) TO 部位別料金ＴＷ(部位カウンタ２)
006040             END-IF
006050         END-PERFORM
006060         PERFORM VARYING 部位カウンタ FROM 1 BY 1 UNTIL 部位カウンタ > 負−部位数
006070             MOVE 部位別料金ＴＷ(部位カウンタ) TO 自賠−部位別料金(部位カウンタ)
006080         END-PERFORM
006090     END-READ.
006100*================================================================*
006110 固有単価料金クリア SECTION.
006120*
006130     MOVE ZERO TO 自賠−初検料.
006140     MOVE ZERO TO 自賠−初検加算料.
006150     MOVE ZERO TO 自賠−再検料.
006160     MOVE ZERO TO 自賠−指導管理料.
006170     MOVE ZERO TO 自賠−運動療法料.
006180     MOVE ZERO TO 自賠−施術情報提供料.
006190     MOVE ZERO TO 自賠−施術証明書料.
006200     MOVE ZERO TO 自賠−施術明細書料.
006210     MOVE ZERO TO 自賠−冷罨法料.
006220     MOVE ZERO TO 自賠−冷罨法料３０.
006230     MOVE ZERO TO 自賠−温罨法料２３.
006240     MOVE ZERO TO 自賠−温罨法料２４骨折.
006250     MOVE ZERO TO 自賠−温罨法料２４他.
006260     MOVE ZERO TO 自賠−温罨法料３０.
006270     MOVE ZERO TO 自賠−電療料２３.
006280     MOVE ZERO TO 自賠−電療料２４骨折.
006290     MOVE ZERO TO 自賠−電療料２４他.
006300     MOVE ZERO TO 自賠−電療料３０.
006310     MOVE ZERO TO 自賠−運動算定間隔.
006320     MOVE ZERO TO 自賠−運動上限回数.
006330     MOVE ZERO TO 自賠−指導算定間隔.
006340     MOVE ZERO TO 自賠−指導上限回数.
006350     MOVE ZERO TO 自賠−月基準区分.
006360     MOVE ZERO TO 自賠−初検時相談料.
006370     MOVE ZERO TO 自賠−長期逓減月数.
006380     MOVE ZERO TO 自賠−多部位逓減部位数.
006390     PERFORM VARYING 部位カウンタ FROM 1 BY 1 UNTIL 部位カウンタ > 7
006400         MOVE ZERO TO 自賠−部位別料金(部位カウンタ)
006410     END-PERFORM.
006420*================================================================*
006430******************************************************************
006440 END PROGRAM GETUJIK2.
006450******************************************************************
