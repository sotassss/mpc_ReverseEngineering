000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             TOPPATU.
000060 AUTHOR.                 山田 浩之
000070*
000080*------------------------------------------------------------------*
000090* 突発往療の制限
000100* 当日に突発往料が算定可能か調べる
000110* 
000170*------------------------------------------------------------------*
000180 DATE-WRITTEN.           2004-08-26
000190 DATE-COMPILED.          2004-08-26
000200*----------------------------------------------------------------*
000372******************************************************************
000380*            ENVIRONMENT         DIVISION                        *
000390******************************************************************
000400 ENVIRONMENT             DIVISION.
000410 CONFIGURATION           SECTION.
000420 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000430 OBJECT-COMPUTER.        FMV-DESKPOWER.
000440 SPECIAL-NAMES.          CONSOLE  IS  CONS
000450                         SYSERR   IS  MSGBOX.
000460 INPUT-OUTPUT            SECTION.
000470 FILE-CONTROL.
000480*
001710     SELECT  参照Ｈ日計データＦ  ASSIGN      TO        HNIKEIL
001720                             ORGANIZATION             IS  INDEXED
001730                             ACCESS MODE              IS  DYNAMIC
001740                             RECORD KEY               IS  参照Ｈ日−施術区分
001750                                                          参照Ｈ日−施術和暦年月日
001760                                                          参照Ｈ日−患者コード
001770                             ALTERNATE RECORD KEY     IS  参照Ｈ日−施術区分
001780                                                          参照Ｈ日−患者コード
001790                                                          参照Ｈ日−施術和暦年月日
001800                             ALTERNATE RECORD KEY     IS  参照Ｈ日−施術和暦年月日
001810                                                          参照Ｈ日−登録順
001820                             FILE STATUS              IS  状態キー
001830                             LOCK        MODE         IS  AUTOMATIC.
001840*
001160     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
001170                             ORGANIZATION             IS  INDEXED
001180                             ACCESS MODE              IS  DYNAMIC
001190                             RECORD KEY               IS  元−元号区分
001200                             FILE STATUS              IS  状態キー
001210                             LOCK        MODE         IS  AUTOMATIC.
002340     SELECT  Ｈ往療実績Ｆ    ASSIGN      TO        HNOURYOL
002350                             ORGANIZATION             IS  INDEXED
002360                             ACCESS MODE              IS  DYNAMIC
002370                             RECORD KEY               IS  Ｈ往実−施術区分
002380                                                          Ｈ往実−施術和暦年月日
002390                                                          Ｈ往実−患者コード
002400                             ALTERNATE RECORD KEY     IS  Ｈ往実−施術区分
002410                                                          Ｈ往実−患者コード
002420                                                          Ｈ往実−施術和暦年月日
002430                             ALTERNATE RECORD KEY     IS  Ｈ往実−施術和暦年月日
002440                                                          Ｈ往実−施術者番号
002450                                                          Ｈ往実−登録順
002460                             ALTERNATE RECORD KEY     IS  Ｈ往実−施術和暦年月日
002470                                                          Ｈ往実−施術開始時間
002480                                                          Ｈ往実−施術者番号
002490                                                          Ｈ往実−登録順
002500                             FILE STATUS              IS  状態キー
002510                             LOCK        MODE         IS  AUTOMATIC.
002770******************************************************************
002780*                      DATA DIVISION                             *
002790******************************************************************
002800 DATA                    DIVISION.
002810 FILE                    SECTION.
002820**
003090*
003100 FD  参照Ｈ日計データＦ  BLOCK   CONTAINS   1   RECORDS.
003110     COPY H_NIKEI    OF  XFDLIB  JOINING   参照Ｈ日   AS  PREFIX.
002200*                           ［ＲＬ＝  １２８］
002210 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
002220     COPY GENGOU     OF  XFDLIB  JOINING   元   AS  PREFIX.
003210*
003220 FD  Ｈ往療実績Ｆ        BLOCK   CONTAINS   1   RECORDS.
003230     COPY H_NOURYO   OF  XFDLIB  JOINING   Ｈ往実   AS  PREFIX.
003280******************************************************************
003290*                WORKING-STORAGE SECTION                         *
003300******************************************************************
003310 WORKING-STORAGE         SECTION.
003320 01 キー入力                           PIC X    VALUE SPACE.
003330 01 状態キー                           PIC X(2) VALUE SPACE.
003340 01 終了フラグ                         PIC X(3) VALUE SPACE.
003350 01 終了フラグ２                       PIC X(3) VALUE SPACE.
003360 01 終了フラグ３                       PIC X(3) VALUE SPACE.
003360 01 終了フラグ４                       PIC X(3) VALUE SPACE.
003370 01 ファイル名                         PIC N(10) VALUE SPACE.
002490 01 計算和暦年月日Ｗ.
002500     03 計算和暦Ｗ                     PIC 9    VALUE ZERO.
002510     03 計算年月Ｗ.
002520        05 計算年Ｗ                    PIC 9(2) VALUE ZERO.
002530        05 計算月Ｗ                    PIC 9(2) VALUE ZERO.
002540     03 計算日Ｗ                       PIC 9(2) VALUE ZERO.
002550*
002560 01 絶対値Ｗ                           PIC 9(6) VALUE ZERO.
002570 01 絶対値月Ｗ                         PIC 9(3) VALUE ZERO.
002580 01 絶対値月Ｗ２                       PIC 9(3) VALUE ZERO.
002590 01 負傷経過日Ｗ                       PIC 9(4) VALUE ZERO.
002600 01 西暦年Ｗ                           PIC 9(4) VALUE ZERO.
002610 01 西暦年差Ｗ                         PIC 9(2) VALUE ZERO.
002620 01 西暦年差２Ｗ                       PIC 9(2) VALUE ZERO.
002630 01 閏年回数Ｗ                         PIC 9(2) VALUE ZERO.
002640 01 通常年回数Ｗ                       PIC 9(2) VALUE ZERO.
002650 01 通常年絶対値Ｗ                     PIC 9(6) VALUE ZERO.
002660 01 閏年絶対値Ｗ                       PIC 9(6) VALUE ZERO.
002670 01 年絶対値Ｗ                         PIC 9(6) VALUE ZERO.
002680 01 負傷絶対値Ｗ                       PIC 9(6) VALUE ZERO.
002690 01 施術絶対値Ｗ                       PIC 9(6) VALUE ZERO.
002700 01 加算年Ｗ                           PIC 9(4) VALUE ZERO.
002560 01 当日絶対値Ｗ                       PIC 9(6) VALUE ZERO.
001540 01 剰余Ｗ                             PIC 9(4) VALUE ZERO.
006256******************************************************************
006257*                          連結項目                              *
006258******************************************************************
006259*
       01 連入２０１−画面情報 IS EXTERNAL GLOBAL.
          03 連入２０１−施術区分            PIC 9(1).
          03 連入２０１−施術和暦年月日.
             05 連入２０１−施術和暦年月.
                07 連入２０１−施術和暦         PIC 9(1).
                07 連入２０１−施術年           PIC 9(2).
                07 連入２０１−施術月           PIC 9(2).
             05 連入２０１−施術日              PIC 9(2).
          03 連入２０１−患者コード.
             05 連入２０１−患者番号         PIC 9(6).
             05 連入２０１−枝番             PIC X(1).
          03 連入２０１−エラーフラグ        PIC X(3).

007476******************************************************************
007477*                      PROCEDURE  DIVISION                       *
007478******************************************************************
007479 PROCEDURE               DIVISION.
HILO  *     DISPLAY "Top:St----" 連入２０１−施術和暦年月日 " " 連入２０１−患者コード
007482************
007483*           *
007484* 初期処理   *
007485*           *
007486************
007720     PERFORM ファイルオープン.
007492************
007493*           *
007494* 主処理     *
007495*           *
007496************
           MOVE SPACE TO 連入２０１−エラーフラグ.
           MOVE SPACE TO 終了フラグ.
027940     MOVE 連入２０１−施術和暦年月日 TO 計算和暦年月日Ｗ.
014470     PERFORM 日付絶対値計算.
014400     MOVE 絶対値Ｗ                   TO 当日絶対値Ｗ.
HILO***       DISPLAY "TOP-1 " 連入２０１−施術和暦年月日 " 当日絶対値Ｗ=" 当日絶対値Ｗ.
027910     MOVE 連入２０１−施術区分       TO 参照Ｈ日−施術区分
027920     MOVE 連入２０１−患者コード     TO 参照Ｈ日−患者コード
027940     MOVE 連入２０１−施術和暦年月日 TO 参照Ｈ日−施術和暦年月日
027970*
028050     START 参照Ｈ日計データＦ KEY IS < 参照Ｈ日−施術区分
028060                                       参照Ｈ日−患者コード
028070                                       参照Ｈ日−施術和暦年月日
                                             REVERSED
028080     END-START
HILO***       DISPLAY "TOP-2 " 状態キー
028090     IF 状態キー = "00"
               MOVE SPACE TO 終了フラグ
               PERFORM 参照日計データＦ読込
021400         PERFORM UNTIL (連入２０１−患者コード   NOT = 参照Ｈ日−患者コード  ) OR
027940*                       (連入２０１−施術和暦年月 NOT = 参照Ｈ日−施術和暦年月) OR
                             (連入２０１−施術区分     NOT = 参照Ｈ日−施術区分    ) OR
                             (終了フラグ４             NOT = SPACE)
HILO  *             DISPLAY 参照Ｈ日−施術和暦年月日 " " 参照Ｈ日−患者コード
001107             IF 参照Ｈ日−突発往療区分 = 1 
                       MOVE 参照Ｈ日−施術区分       TO Ｈ往実−施術区分
002380                 MOVE 参照Ｈ日−施術和暦年月日 TO Ｈ往実−施術和暦年月日
002390                 MOVE 参照Ｈ日−患者コード     TO Ｈ往実−患者コード
                       READ Ｈ往療実績Ｆ
                       NOT INVALID KEY
                           IF Ｈ往実−往療金額 NOT = ZERO
                               MOVE 参照Ｈ日−施術和暦年月日 TO 計算和暦年月日Ｗ
014470                         PERFORM 日付絶対値計算
HILO***                           DISPLAY "TOP-3 絶対値Ｗ " 絶対値Ｗ 
                               COMPUTE 絶対値Ｗ = 当日絶対値Ｗ - 絶対値Ｗ
HILO***                        DISPLAY "TOP-3-1 差 " 連入２０１−施術和暦年月日 " " 参照Ｈ日−施術和暦年月日 " " 絶対値Ｗ 
                               IF 絶対値Ｗ <= 14
HILO***                               DISPLAY "TOP-7 エラー対象"
                                   MOVE "YES" TO 連入２０１−エラーフラグ 終了フラグ４
                               END-IF
                               MOVE "YES" TO 終了フラグ４
                           END-IF
                       END-READ
                   END-IF
                   PERFORM 参照日計データＦ読込
               END-PERFORM
           END-IF.
      *     IF 連入２０１−エラーフラグ NOT = SPACE
      *         DISPLAY "突発的な往療を行った日の翌日から起算して14日以内は往療料を算定できません。"
      *     END-IF.
007520************
007530*           *
007540* 終了処理  *
007550*           *
007560************
007570     PERFORM 終了処理.
007580     MOVE ZERO TO PROGRAM-STATUS.
007590     EXIT PROGRAM.
007600*
007610*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
007620*================================================================*
007630* 初期化 SECTION.
007640**
007840*================================================================*
007850 ファイルオープン SECTION.
007860*
008070     OPEN INPUT 参照Ｈ日計データＦ.
008080         MOVE NC"参照Ｈ日計データＦ" TO ファイル名.
008090         PERFORM オープンチェック.
008310*
018000     OPEN INPUT 元号マスタ.
018010         MOVE NC"元号" TO ファイル名.
018020         PERFORM オープンチェック.
008310*
008280     OPEN INPUT Ｈ往療実績Ｆ.
008290         MOVE NC"Ｈ往療実績Ｆ" TO ファイル名.
008300         PERFORM オープンチェック.
008350*================================================================*
008360 オープンチェック SECTION.
008370*
008380     IF 状態キー  NOT =  "00"
008390         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
008400         DISPLAY NC"状態キー：" 状態キー         UPON CONS
008410         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
008420                                                 UPON CONS
008430*-----------------------------------------*
008440         CALL "actcshm"  WITH C LINKAGE
008450*-----------------------------------------*
008460         ACCEPT  キー入力 FROM CONS
008470         PERFORM ファイル閉鎖
008480         MOVE 99 TO PROGRAM-STATUS
008490         EXIT PROGRAM.
008500*
008510*================================================================*
008520 ファイル閉鎖 SECTION.
008530*
           CLOSE 参照Ｈ日計データＦ 元号マスタ Ｈ往療実績Ｆ.
008580*================================================================*
008590 終了処理 SECTION.
008600*
008610     PERFORM ファイル閉鎖.
008620*================================================================*
028370 エラー表示 SECTION.
028380*
028390     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
028400     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
028410     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
028420     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"  UPON CONS.
028430*-----------------------------------------*
028440     CALL "actcshm"  WITH C LINKAGE.
028450*-----------------------------------------*
028460     ACCEPT  キー入力 FROM CONS.
028470*
028480*================================================================*
007550 西暦年取得 SECTION.
007560*
007570     MOVE 計算和暦Ｗ TO 元−元号区分.
007580     READ 元号マスタ
007590     INVALID KEY
007600         DISPLAY NC"指定和暦が登録されていません" UPON CONS
007610         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
007620                                                  UPON CONS
007630         ACCEPT  キー入力 FROM CONS
007640         PERFORM 終了処理
007650         EXIT PROGRAM
007660     NOT INVALID KEY
007670         COMPUTE 西暦年Ｗ = 元−開始西暦年 + 計算年Ｗ - 1
007680     END-READ.
007690*
007700*================================================================*
015550 日付絶対値計算 SECTION.
015560*
015570** 1989年を基準とし、当年まで閏年が何回あるかを計算
015580** 元号マスタの西暦開始年確認。
015590*     COMPUTE 西暦年差Ｗ   = 西暦年Ｗ - 1989 - 1.
015600*     DIVIDE 4 INTO 西暦年差Ｗ GIVING 閏年回数Ｗ
015610*                            REMAINDER 剰余Ｗ
015620*     END-DIVIDE.
014460     PERFORM 西暦年取得.
015630*/基準年1989年から当年までの閏年の回数を計算(当年は含まない)
015640*/始めのうるう年の1992年から数える
015650     COMPUTE 西暦年差Ｗ   = 西暦年Ｗ - 1989 - 1.
015660     MOVE 1992 TO 加算年Ｗ.
015670     MOVE ZERO TO 閏年回数Ｗ.
015680     PERFORM UNTIL 西暦年Ｗ <= 加算年Ｗ
015690         COMPUTE 加算年Ｗ   = 加算年Ｗ   + 4
015700         COMPUTE 閏年回数Ｗ = 閏年回数Ｗ + 1
015710     END-PERFORM.
015720*
015730     COMPUTE 通常年回数Ｗ     = 西暦年差Ｗ     - 閏年回数Ｗ.
015740     COMPUTE 通常年絶対値Ｗ   = 通常年回数Ｗ   * 365.
015750     COMPUTE 閏年絶対値Ｗ     = 閏年回数Ｗ     * 366.
015760     COMPUTE 年絶対値Ｗ       = 通常年絶対値Ｗ + 閏年絶対値Ｗ.
015770* 当年がうるう年かどうかを調べる（２月の末日計算）
015780*     COMPUTE 西暦年差２Ｗ     = 西暦年Ｗ + 1.
015790*     DIVIDE 4 INTO 西暦年差２Ｗ GIVING 閏年回数Ｗ
015800*                                REMAINDER 剰余Ｗ
015810*     END-DIVIDE.
015820     DIVIDE 4 INTO 西暦年Ｗ GIVING 閏年回数Ｗ
015830                            REMAINDER 剰余Ｗ
015840     END-DIVIDE.
015850*
015860     MOVE ZERO TO 絶対値月Ｗ.
015870*
015880* 前月までの絶対値を累計
015890     COMPUTE 絶対値月Ｗ２ = 計算月Ｗ * 30.
015900*
015910     EVALUATE 計算月Ｗ
015920     WHEN 3
015930         COMPUTE 絶対値月Ｗ   = 絶対値月Ｗ２ - 1
015940     WHEN 2
015950     WHEN 6
015960     WHEN 7
015970         COMPUTE 絶対値月Ｗ   = 絶対値月Ｗ２ + 1
015980     WHEN 8
015990         COMPUTE 絶対値月Ｗ   = 絶対値月Ｗ２ + 2
016000     WHEN 9
016010     WHEN 10
016020         COMPUTE 絶対値月Ｗ   = 絶対値月Ｗ２ + 3
016030     WHEN 11
016040     WHEN 12
016050         COMPUTE 絶対値月Ｗ   = 絶対値月Ｗ２ + 4
016060     WHEN OTHER
016070         MOVE 絶対値月Ｗ２ TO 絶対値月Ｗ
016080     END-EVALUATE.
016090*
016100     IF ( 剰余Ｗ = ZERO ) AND
016110        ( 計算月Ｗ > 2  )
016120         COMPUTE 絶対値月Ｗ = 絶対値月Ｗ + 1
016130     END-IF.
016140*
016150     COMPUTE 絶対値Ｗ   = 年絶対値Ｗ + 絶対値月Ｗ + 計算日Ｗ.
016160*
016170*================================================================*
013670 参照日計データＦ読込 SECTION.
013680*
013710     READ 参照Ｈ日計データＦ NEXT
013720     AT END
013730         MOVE "YES" TO 終了フラグ４
013740     END-READ.
013750*
013760*================================================================*
028510******************************************************************
028520 END PROGRAM TOPPATU.
028530******************************************************************
