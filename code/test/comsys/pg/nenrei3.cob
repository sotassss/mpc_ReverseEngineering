000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             NENREI3.
000060 AUTHOR.                 佐野 誠
000070*
000080*------------------------------------------------------------------*
000090*      年齢算出(施術日基準)   柔ｳｨﾝﾄﾞｳｽﾞ95版                       *
000100*------------------------------------------------------------------*
000110 DATE-WRITTEN.           2014-12-18
000120 DATE-COMPILED.          2014-12-18
000130*----------------------------------------------------------------*
000140******************************************************************
000150*            ENVIRONMENT         DIVISION                        *
000160******************************************************************
000170 ENVIRONMENT             DIVISION.
000180 CONFIGURATION           SECTION.
000190 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000200 OBJECT-COMPUTER.        FMV-DESKPOWER.
000210 SPECIAL-NAMES.          CONSOLE  IS  CONS
000220                         SYSERR   IS  MSGBOX.
000230 INPUT-OUTPUT            SECTION.
000240 FILE-CONTROL.
000250     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000260                             ORGANIZATION             IS  INDEXED
000270                             ACCESS MODE              IS  DYNAMIC
000280                             RECORD KEY               IS  元−元号区分
000290                             FILE STATUS              IS  状態キー
000300                             LOCK        MODE         IS  AUTOMATIC.
000310*
000320******************************************************************
000330*                      DATA DIVISION                             *
000340******************************************************************
000350 DATA                    DIVISION.
000360 FILE                    SECTION.
000370*                           ［ＲＬ＝  １２８］
000380 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
000390     COPY GENGOU          OF  XFDLIB  JOINING   元   AS  PREFIX.
000400*
000410*----------------------------------------------------------------*
000420******************************************************************
000430*                WORKING-STORAGE SECTION                         *
000440******************************************************************
000450 WORKING-STORAGE         SECTION.
000460 01 キー入力                           PIC X    VALUE SPACE.
000470 01 状態キー                           PIC X(2) VALUE SPACE.
000480 01 終了フラグ                         PIC X(3) VALUE SPACE.
000490 01 ファイル名                         PIC N(10) VALUE SPACE.
000500**
000510** 患者年齢用
000520 01 施術西暦年月日Ｗ.
000530    03 施術西暦年Ｗ                   PIC 9(4)  VALUE ZERO.
000540    03 施術西暦月Ｗ                   PIC 9(2)  VALUE ZERO.
000550    03 施術西暦日Ｗ                   PIC 9(2)  VALUE ZERO.
000560 01 患者年齢ＷＷ.
000570    03 患者和暦Ｗ                     PIC 9    VALUE ZERO.
000580    03 患者年Ｗ                       PIC 9(2) VALUE ZERO.
000590    03 患者月Ｗ                       PIC 9(2) VALUE ZERO.
000600    03 患者日Ｗ                       PIC 9(2) VALUE ZERO.
000610    03 患者年齢Ｗ                     PIC S9(3) VALUE ZERO.
000620    03 患者西暦年Ｗ                   PIC 9(4)  VALUE ZERO.
000630**
000640**********************************************************************
000650*
000660* 日付ＷＯＲＫ
000670 01 計算機西暦.
000680    03 計算機西暦年                    PIC 9(4).
000690    03 計算機西暦月日                  PIC 9(4).
000700 01 計算機西暦Ｒ REDEFINES 計算機西暦.
000710    03 計算機世紀                      PIC 9(2).
000720    03 計算機日付                      PIC 9(6).
000730    03 計算機日付Ｒ REDEFINES 計算機日付.
000740       05 計算機年月                   PIC 9(4).
000750       05 計算機年月Ｒ REDEFINES 計算機年月.
000760         07 計算機年                   PIC 9(2).
000770         07 計算機月                   PIC 9(2).
000780       05 計算機日                     PIC 9(2).
000790*
000800******************************************************************
000810*                          連結項目                              *
000820******************************************************************
000830*
000840* 年齢３用
000850 01 連年齢３−キー IS EXTERNAL.
000860    03 連年齢３−施術和暦年月日.
000870       05 連年齢３−施術和暦    PIC 9.
000880       05 連年齢３−施術年      PIC 9(2).
000890       05 連年齢３−施術月      PIC 9(2).
000900       05 連年齢３−施術日      PIC 9(2).
000910    03 連年齢３−和暦年月日.
000920       05 連年齢３−和暦        PIC 9.
000930       05 連年齢３−年          PIC 9(2).
000940       05 連年齢３−月          PIC 9(2).
000950       05 連年齢３−日          PIC 9(2).
000960    03 連年齢３−年齢           PIC 9(3).
000970*
000980******************************************************************
000990*                      PROCEDURE  DIVISION                       *
001000******************************************************************
001010 PROCEDURE               DIVISION.
001020************
001030*           *
001040* 初期処理   *
001050*           *
001060************
001070     PERFORM 初期化.
001080     PERFORM 施術年月日西暦化.
001090************
001100*           *
001110* 主処理     *
001120*           *
001130************
001140      PERFORM 年齢計算.
001150*
001160************
001170*           *
001180* 終了処理   *
001190*           *
001200************
001210     PERFORM 終了処理.
001220     EXIT PROGRAM.
001230*
001240*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
001250*================================================================*
001260 初期化 SECTION.
001270*
001280     PERFORM ファイルオープン.
001290* 連結項目の待避
001300     MOVE 連年齢３−和暦    TO 患者和暦Ｗ.
001310     MOVE 連年齢３−年      TO 患者年Ｗ.
001320     MOVE 連年齢３−月      TO 患者月Ｗ.
001330     MOVE 連年齢３−日      TO 患者日Ｗ.
001340*
001350*================================================================*
001360 ファイルオープン SECTION.
001370*
001380     OPEN INPUT 元号マスタ.
001390         MOVE NC"元号マスタ" TO ファイル名.
001400         PERFORM オープンチェック.
001410*================================================================*
001420 オープンチェック SECTION.
001430*
001440     IF 状態キー  NOT =  "00"
001450         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
001460         DISPLAY NC"状態キー：" 状態キー         UPON CONS
001470         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
001480                                                 UPON CONS
001490*-----------------------------------------*
001500         CALL "actcshm"  WITH C LINKAGE
001510*-----------------------------------------*
001520         ACCEPT  キー入力 FROM CONS
001530         PERFORM ファイル閉鎖
001540         MOVE 99 TO PROGRAM-STATUS
001550         EXIT PROGRAM.
001560*================================================================*
001570 ファイル閉鎖 SECTION.
001580*
001590     CLOSE 元号マスタ.
001600*================================================================*
001610 終了処理 SECTION.
001620*
001630     PERFORM ファイル閉鎖.
001640*================================================================*
001650*================================================================*
001660 施術年月日西暦化 SECTION.
001670**
001680     MOVE 連年齢３−施術和暦 TO 元−元号区分.
001690     READ 元号マスタ
001700     NOT INVALID KEY
001710         COMPUTE 施術西暦年Ｗ = 元−開始西暦年 + ( 連年齢３−施術年 - 1 )
001720     END-READ.
001730*
001740     MOVE 連年齢３−施術月 TO  施術西暦月Ｗ.
001750     MOVE 連年齢３−施術日 TO  施術西暦日Ｗ.
001760*
001770*================================================================*
001780*================================================================*
001790 年齢計算 SECTION.
001800*
001810*------- 施術年月日から年齢を算出---------------------*
001820* 
001830     MOVE ZERO TO 患者年齢Ｗ.
001840*
001850     IF ( 患者和暦Ｗ NOT = ZERO ) AND
001860        ( 患者年Ｗ   NOT = ZERO ) AND
001870        ( 患者月Ｗ   NOT = ZERO ) AND
001880        ( 患者日Ｗ   NOT = ZERO )
001890*
001900         MOVE 患者和暦Ｗ TO 元−元号区分
001910         READ 元号マスタ
001920         NOT INVALID KEY
001930             COMPUTE 患者西暦年Ｗ = 元−開始西暦年 + ( 患者年Ｗ - 1 )
001940             COMPUTE 患者年齢Ｗ   = 施術西暦年Ｗ - 患者西暦年Ｗ
001950*
001960             IF ( 施術西暦月Ｗ <= 患者月Ｗ )
001970                IF ( 施術西暦月Ｗ = 患者月Ｗ )
001980                   IF ( 施術西暦日Ｗ < 患者日Ｗ )
001990                      COMPUTE 患者年齢Ｗ = 患者年齢Ｗ - 1
002000                   END-IF
002010                ELSE
002020                   COMPUTE 患者年齢Ｗ = 患者年齢Ｗ - 1
002030                END-IF
002040             END-IF
002050*
002060         END-READ
002070*
002080         IF ( 患者年齢Ｗ < ZERO )
002090            MOVE ZERO TO 患者年齢Ｗ
002100         END-IF
002110*
002120     END-IF.
002130**
002140* / 年齢セット /
002150     MOVE 患者年齢Ｗ  TO 連年齢３−年齢.
002160*
002170*================================================================*
002180*================================================================*
002190******************************************************************
002200 END PROGRAM NENREI3.
002210******************************************************************
