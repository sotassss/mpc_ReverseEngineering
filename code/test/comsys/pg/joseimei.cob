000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             JOSEIMEI.
000060 AUTHOR.                 佐野 誠
000070*
000080*------------------------------------------------------------------*
000090* 助成の名称を返却
000110*------------------------------------------------------------------*
000120 DATE-WRITTEN.           2006-04-17
000130 DATE-COMPILED.          2006-04-17
      *********** 7210XXXX は 「子」(群馬県のその他)/151128
      */設定されていない場合は市町村マスタを読む/151128
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000810     SELECT  市町村マスタ    ASSIGN      TO        SITYOSNL
000820                             ORGANIZATION             IS  INDEXED
000830                             ACCESS MODE              IS  DYNAMIC
000840                             RECORD KEY               IS  市−公費種別
000850                                                          市−市町村番号
000860                             ALTERNATE RECORD KEY     IS  市−公費種別
000870                                                          市−市町村名称
000880                                                          市−市町村番号
000890                             FILE STATUS              IS  状態キー
000900                             LOCK        MODE         IS  AUTOMATIC.
001157*
001158******************************************************************
001159*                      DATA DIVISION                             *
001160******************************************************************
001170 DATA                    DIVISION.
001180 FILE                    SECTION.
002200*                           ［ＲＬ＝  ２５６］
002210 FD  市町村マスタ        BLOCK   CONTAINS   1   RECORDS.
002220     COPY SITYOSN         OF  XFDLIB  JOINING   市   AS  PREFIX.
001190*
002080*----------------------------------------------------------------*
002090******************************************************************
002100*                WORKING-STORAGE SECTION                         *
002110******************************************************************
002120 WORKING-STORAGE         SECTION.
002130 01 キー入力                           PIC X    VALUE SPACE.
002140 01 状態キー                           PIC X(2) VALUE SPACE.
002150 01 終了フラグ                         PIC X(3) VALUE SPACE.
002170 01 ファイル名                         PIC N(6) VALUE SPACE.
002173*
       01 略称Ｗ.
          03 略称ＷＰ                        PIC N(4) VALUE SPACE.
002271*
002590******************************************************************
002600*                          連結項目                              *
002610******************************************************************
002730*
002731* 助成名称
002740 01 連助成名称−キー IS EXTERNAL.
002750    03 連助成名称−助成種別             PIC 9(2).
002834    03 連助成名称−費用負担者番号助成   PIC X(10).
002836*   / OUT /
002837    03 連助成名称−名称集団.
002838       05 連助成名称−１文字            PIC N.
002839       05 連助成名称−略称              PIC N(4).
002840       05 連助成名称−正式名称          PIC N(10).
014070 01 連助成名称−会キー IS EXTERNAL.
014080    03 連助成名称−協会コード           PIC 9(2).
002850*
003481******************************************************************
003482*                      PROCEDURE  DIVISION                       *
003483******************************************************************
003490 PROCEDURE               DIVISION.
003500************
003510*           *
003520* 初期処理   *
003530*           *
003540************
003550     PERFORM 初期化.
003560************
003570*           *
003580* 主処理     *
003590*           *
003600************
003601*
           IF 連助成名称−協会コード = 14 OR 39 OR 40 OR 41
003618         PERFORM 判定処理１
           ELSE
003618         PERFORM 判定処理
           END-IF.
003619*
003620************
003630*           *
003640* 終了処理   *
003650*           *
003660************
003670     PERFORM 終了処理.
003690     EXIT PROGRAM.
003700*
003710*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003720*================================================================*
003730 初期化 SECTION.
003740*
014540     OPEN INPUT 市町村マスタ.
014550         MOVE NC"市町村" TO ファイル名.
014560         PERFORM オープンチェック.
003742* 初期化
003750     MOVE SPACE TO 連助成名称−名称集団.
           INITIALIZE 連助成名称−名称集団.
003862*
003874*================================================================*
014830 オープンチェック SECTION.
014840*
014850     IF ( 状態キー  NOT =  "00" )
014860        DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
014870        DISPLAY NC"状態キー：" 状態キー         UPON CONS
014880        DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
014890                                                UPON CONS
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
014900        ACCEPT  キー入力 FROM CONS
014910        PERFORM 終了処理
014920        EXIT PROGRAM.
004094*================================================================*
004104 終了処理 SECTION.
004114*
040410     CLOSE 市町村マスタ.
004124*
004125*================================================================*
004160*================================================================*
004161 判定処理 SECTION.
004211*
004221*
004235     EVALUATE 連助成名称−助成種別
004236*** 生保 (生保はその他扱いで、該当なし)
004237     WHEN  50
004238        MOVE SPACE         TO 連助成名称−１文字
004239        MOVE NC"生保"      TO 連助成名称−略称
004240        MOVE NC"生活保護"  TO 連助成名称−正式名称
004241*** 41老人
004242     WHEN  51
004243*********** 3〜4桁が "13"東京 "08"茨城 "32"島根 の時は、福。それ以外は老
004244        IF  ( 連助成名称−費用負担者番号助成(3:2) = "13" OR "08" OR "32" )
004245            MOVE NC"福"        TO 連助成名称−１文字
004246            MOVE NC"福"        TO 連助成名称−略称
004247            MOVE NC"老人医療（マル福）"  TO 連助成名称−正式名称
004248        ELSE
004249            MOVE NC"老"        TO 連助成名称−１文字
004250            MOVE NC"４１老人"  TO 連助成名称−略称
004251            MOVE NC"老人医療（４１老人）"  TO 連助成名称−正式名称
004252        END-IF
004253*** 母子
004254     WHEN  52
004255*********** 3〜4桁が 06山形 15新潟 20長野 22静岡 26京都 28兵庫 40福岡 の時は、母。それ以外は親
004256        IF  ( 連助成名称−費用負担者番号助成(3:2) = "06" OR "15" OR "20" OR "22" OR "26" OR "28" OR "40" )
004257            MOVE NC"母"         TO 連助成名称−１文字
004258            MOVE NC"母子"       TO 連助成名称−略称
004259            MOVE NC"母子"       TO 連助成名称−正式名称
004265        ELSE
004266            MOVE NC"親"         TO 連助成名称−１文字
004267            MOVE NC"ひとり親"   TO 連助成名称−略称
004268            MOVE NC"ひとり親"   TO 連助成名称−正式名称
004269        END-IF
004270*** 身障
004271     WHEN  53
004272            MOVE NC"障"         TO 連助成名称−１文字
004273            MOVE NC"障害"       TO 連助成名称−略称
004274            MOVE NC"障害"       TO 連助成名称−正式名称
004275*** 被爆
004276     WHEN  54
004277            MOVE NC"爆"         TO 連助成名称−１文字
004278            MOVE NC"被爆"       TO 連助成名称−略称
004279            MOVE NC"被爆"       TO 連助成名称−正式名称
      */ＪＢは"原"または"原爆"とする/20191211
                  IF 連助成名称−協会コード = 08
004277                MOVE NC"原"     TO 連助成名称−１文字
004278                MOVE NC"原爆"   TO 連助成名称−略称
004279                                   連助成名称−正式名称
                  END-IF
004280*** 乳幼児 
004281     WHEN  55
004282            MOVE NC"乳"         TO 連助成名称−１文字
004283            MOVE NC"乳幼児"     TO 連助成名称−略称
004284            MOVE NC"乳幼児"     TO 連助成名称−正式名称
004285*** その他
004286     WHEN  60
004287***********  8813XXXX は「子」
004288        IF  ( 連助成名称−費用負担者番号助成(1:4) = "8813"  )
004289            MOVE NC"子"         TO 連助成名称−１文字
004290            MOVE NC"子ども"     TO 連助成名称−略称
004291            MOVE NC"子ども医療助成"   TO 連助成名称−正式名称
004293        END-IF
      *********** 8923XXXX は「福」
004288        IF  ( 連助成名称−費用負担者番号助成(1:4) = "8923"  )
004289            MOVE NC"福"             TO 連助成名称−１文字
004290            MOVE NC"福"             TO 連助成名称−略称
004291            MOVE NC"高齢福祉給付金" TO 連助成名称−正式名称
004293        END-IF
004287***********  8322XXXX は「子」
004288        IF  ( 連助成名称−費用負担者番号助成(1:4) = "8322"  )
004289            MOVE NC"子"         TO 連助成名称−１文字
004290            MOVE NC"子ども"     TO 連助成名称−略称
004291            MOVE NC"子ども医療助成"   TO 連助成名称−正式名称
004293        END-IF
      *********** 8312XXXX は 「子」
004288        IF  ( 連助成名称−費用負担者番号助成(1:4) = "8312"  )
004289            MOVE NC"子"             TO 連助成名称−１文字
004290            MOVE NC"子ども"         TO 連助成名称−略称
004291            MOVE NC"子ども医療助成" TO 連助成名称−正式名称
004293        END-IF
      *********** 9907XXXX は 「子」(福島県内のその他)
004288        IF  ( 連助成名称−費用負担者番号助成(1:4) = "9907"  )
004289            MOVE NC"子"             TO 連助成名称−１文字
004290            MOVE NC"子ども"         TO 連助成名称−略称
004291            MOVE NC"子ども医療助成" TO 連助成名称−正式名称
004293        END-IF
      *********** 7210XXXX は 「子」(群馬県のその他)/151128
004288        IF  ( 連助成名称−費用負担者番号助成(1:4) = "7210"  )
004289            MOVE NC"子"             TO 連助成名称−１文字
004290            MOVE NC"子ども"         TO 連助成名称−略称
004291            MOVE NC"子ども医療助成" TO 連助成名称−正式名称
004293        END-IF
004287***********  8913XXXX は「青」
004288        IF  ( 連助成名称−費用負担者番号助成(1:4) = "8913"  )
004289            MOVE NC"青"         TO 連助成名称−１文字
004290            MOVE NC"マル青"     TO 連助成名称−略称
004291            MOVE NC"高校生等医療助成制度"   TO 連助成名称−正式名称
004293        END-IF
004296     WHEN  OTHER
004297        CONTINUE
004298     END-EVALUATE.
004299*
      */設定されていない場合は市町村マスタを読む/151128
           IF (連助成名称−１文字   = SPACE) AND
              (連助成名称−略称     = SPACE) AND
              (連助成名称−正式名称 = SPACE)
               PERFORM 判定処理１
           END-IF.
006645*================================================================*
004161 判定処理１ SECTION.
004211*
023280     MOVE 連助成名称−助成種別           TO 市−公費種別.
023290     MOVE 連助成名称−費用負担者番号助成 TO 市−市町村番号.
023300     READ 市町村マスタ
023330     NOT INVALID KEY
004289         MOVE 市−助成種類略称         TO 略称Ｗ
               MOVE 略称ＷＰ(1:1)            TO 連助成名称−１文字
004290         MOVE 略称ＷＰ                 TO 連助成名称−略称
004291         MOVE 市−助成種類名称         TO 連助成名称−正式名称
023380     END-READ.
010106*================================================================*
010200******************************************************************
010210 END PROGRAM JOSEIMEI.
010220******************************************************************
