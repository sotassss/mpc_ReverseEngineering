000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             HMSAIDO.
000060 AUTHOR.                 宮原 睦美
000070*
000080*----------------------------------------------------------------*
000090*   鍼灸マ   再同意リスト印刷【再同意予定日算出】
000100*----------------------------------------------------------------*
000110 DATE-WRITTEN.           2005-04-07
000120 DATE-COMPILED.          2005-04-07
000130*----------------------------------------------------------------*
000140* 2017/03/16 岡田憲和
000141* 　初療日基準であっても、H24/02以降は1か月以内初療日として判定する。
000142* 2018/07/31
000143* 　2018/10/01より、はりきゅう・マッサージは6ヶ月に延長、変形徒手は現行のまま1ヶ月とする
000145*----------------------------------------------------------------*
000146******************************************************************
000150*            ENVIRONMENT         DIVISION                        *
000160******************************************************************
000170 ENVIRONMENT             DIVISION.
000180 CONFIGURATION           SECTION.
000190 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000200 OBJECT-COMPUTER.        FMV-DESKPOWER.
000210 SPECIAL-NAMES.          CONSOLE  IS  CONS
000220                         SYSERR   IS  MSGBOX.
000230 INPUT-OUTPUT            SECTION.
000240 FILE-CONTROL.
000250     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000260                             ORGANIZATION             IS  INDEXED
000270                             ACCESS MODE              IS  DYNAMIC
000280                             RECORD KEY               IS  元−元号区分
000290                             FILE STATUS              IS  状態キー
000300                             LOCK        MODE         IS  AUTOMATIC.
000310     SELECT  Ｈ負傷データＦ  ASSIGN      TO        HHUSYOUL
000320                             ORGANIZATION             IS  INDEXED
000330                             ACCESS MODE              IS  DYNAMIC
000340                             RECORD KEY               IS  Ｈ負−主キー
000350                             ALTERNATE RECORD KEY     IS  Ｈ負−施術区分
000360                                                          Ｈ負−患者コード
000370                                                          Ｈ負−主キー
000380                             FILE STATUS              IS  状態キー
000390                             LOCK        MODE         IS  AUTOMATIC.
000400*
000410     SELECT  Ｈ制御マスタ    ASSIGN      TO        HSEIGYOL
000420                             ORGANIZATION             IS  INDEXED
000430                             ACCESS MODE              IS  DYNAMIC
000440                             RECORD KEY               IS  Ｈ制−制御区分
000450                             FILE STATUS              IS  状態キー
000460                             LOCK        MODE         IS  AUTOMATIC.
000470*
000480     SELECT  ＨレセプトＦ    ASSIGN      TO        HRECEL
000490                             ORGANIZATION             IS  INDEXED
000500                             ACCESS MODE              IS  DYNAMIC
000510                             RECORD KEY               IS  Ｈレセ−施術区分
000520                                                          Ｈレセ−施術和暦年月
000530                                                          Ｈレセ−患者コード
000540                                                          Ｈレセ−レセ種別
000550                             ALTERNATE RECORD KEY     IS  Ｈレセ−施術区分
000560                                                          Ｈレセ−患者コード
000570                                                          Ｈレセ−施術和暦年月
000580                                                          Ｈレセ−レセ種別
000590                             ALTERNATE RECORD KEY     IS  Ｈレセ−施術和暦年月
000600                                                          Ｈレセ−患者コード
000610                                                          Ｈレセ−レセ種別
000620                                                          Ｈレセ−施術区分
000630                             ALTERNATE RECORD KEY     IS  Ｈレセ−請求対象区分
000640                                                          Ｈレセ−請求和暦年月
000650                                                          Ｈレセ−施術区分
000660                                                          Ｈレセ−施術和暦年月
000670                                                          Ｈレセ−患者コード
000680                                                          Ｈレセ−レセ種別
000690                             FILE STATUS              IS  状態キー
000700                             LOCK        MODE         IS  AUTOMATIC.
000710*
000720******************************************************************
000730*                      DATA DIVISION                             *
000740******************************************************************
000750 DATA                    DIVISION.
000760 FILE                    SECTION.
000770*                           ［ＲＬ＝  １２８］
000780 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
000790     COPY GENGOU          OF  XFDLIB  JOINING   元   AS  PREFIX.
000800*                           ［ＲＬ＝  ２５６］
000810 FD  Ｈ負傷データＦ      BLOCK   CONTAINS   1   RECORDS.
000820     COPY H_HUSYOU   OF  XFDLIB  JOINING   Ｈ負 AS  PREFIX.
000830*                           ［ＲＬ＝  ３４８］
000840 FD  Ｈ制御マスタ        BLOCK   CONTAINS   1   RECORDS.
000850     COPY H_SEIGYO        OF  XFDLIB  JOINING   Ｈ制   AS  PREFIX.
000860*
000870 FD  ＨレセプトＦ        BLOCK   CONTAINS   1   RECORDS.
000880     COPY H_RECE     OF  XFDLIB  JOINING   Ｈレセ  AS  PREFIX.
000890*
000900******************************************************************
000910*                WORKING-STORAGE SECTION                         *
000920******************************************************************
000930 WORKING-STORAGE         SECTION.
000940 01 キー入力                           PIC X    VALUE SPACE.
000950 01 状態キー                           PIC X(2) VALUE SPACE.
000960 01 終了フラグ                         PIC X(3) VALUE SPACE.
000970 01 ファイル名                         PIC N(10) VALUE SPACE.
000980*
000990 01 カウンタ                           PIC 9(2).
001000*
001001 01 同意期限計算区分Ｗ                 PIC 9    VALUE ZERO.
001002*
001010*/ 日付計算用 /*
001020 01 計算和暦年月日Ｗ.
001030    03 計算和暦Ｗ                      PIC 9.
001040    03 計算年Ｗ                        PIC 9(2).
001050    03 計算月Ｗ                        PIC 9(2).
001060    03 計算日Ｗ                        PIC 9(2).
001070*
001080*/ 月末日取得用 /*
001090 01 月末日Ｗ                           PIC 9(2)  VALUE ZERO.
001100 01 施術西暦年Ｗ                       PIC 9(4)  VALUE ZERO.
001110 01 商Ｗ                               PIC 9(3)  VALUE ZERO.
001120 01 余Ｗ                               PIC 9(3)  VALUE ZERO.
001130*
001140*/ 変形徒手存在フラグ(YES:有)
001150 01 変形徒手フラグ                    PIC X(3)   VALUE SPACE.
001160*
001170*/ 連結項目退避 /*
001180 01 再同意和暦年月日ＷＲ.
001190    03 再同意和暦年月ＷＲ.
001200       05 再同意和暦ＷＲ               PIC 9.
001210       05 再同意年ＷＲ                 PIC 9(2).
001220       05 再同意月ＷＲ                 PIC 9(2).
001230    03 再同意日ＷＲ                    PIC 9(2).
001240 01 負傷主キーＷＲ                     PIC 9(8).
001250*
001260 01 施術区分ＷＲ                       PIC 9(1).
001270 01 施術内容区分ＷＲ                   PIC 9(2).
001280*
001290* 計算の基準日
001300 01 基準和暦年月日Ｗ.
001310    03 基準和暦年月Ｗ.
001311       05 基準和暦Ｗ                   PIC 9.
001320       05 基準年Ｗ                     PIC 9(2).
001330       05 基準月Ｗ                     PIC 9(2).
001340    03 基準日Ｗ                        PIC 9(2).
001350* 同意一ヶ月後日付
001360 01 一ヶ月和暦年月日Ｗ.
001370    03 一ヶ月和暦Ｗ                    PIC 9.
001380    03 一ヶ月年Ｗ                      PIC 9(2).
001390    03 一ヶ月月Ｗ                      PIC 9(2).
001400    03 一ヶ月日Ｗ                      PIC 9(2).
001410* 初回同意和暦年月日
001420 01 初回同意和暦年月日Ｗ.
001430    03 初回同意和暦Ｗ                  PIC 9.
001440    03 初回同意年Ｗ                    PIC 9(2).
001450    03 初回同意月Ｗ                    PIC 9(2).
001460    03 初回同意日Ｗ                    PIC 9(2).
001470
001480******************************************************************
001490*                          連結項目                              *
001500******************************************************************
001510********************
001520* 再同意予定日算出 *
001530********************
001540 01 連再同−キー IS EXTERNAL.
001550*/ in
001560    03 連再同−再同意和暦年月日      PIC 9(7).
001570    03 連再同−負傷主キー            PIC 9(8).
001580*
001590    03 連再同−施術区分              PIC 9(1).
001600    03 連再同−施術内容区分          PIC 9(2).
001610*/ out
001620    03 連再同−再同意予定和暦年月日.
001630       05 連再同−再同意予定和暦年月.
001640          07 連再同−再同意予定和暦  PIC 9.
001650          07 連再同−再同意予定年    PIC 9(2).
001660          07 連再同−再同意予定月    PIC 9(2).
001670       05 連再同−再同意予定日       PIC 9(2).
001680    03 連再同−初療和暦年月日.
001690       05 連再同−初療和暦年月.
001700          07 連再同−初療和暦        PIC 9.
001710          07 連再同−初療年          PIC 9(2).
001720          07 連再同−初療月          PIC 9(2).
001730       05 連再同−初療日             PIC 9(2).
001740*/  ( 1:変形徒手あり )
001750    03 連再同−変形徒手フラグ        PIC 9(1).
001760*
001770****************************
001780* 元号日付チェック用ワーク *
001790*****************************
001800 01 連元ＣＨＫ−キー IS EXTERNAL.
001810*/ in
001820    03 連元ＣＨＫ−和暦年月日.
001830       05 連元ＣＨＫ−和暦年月.
001840          07 連元ＣＨＫ−和暦           PIC 9.
001850          07 連元ＣＨＫ−年             PIC 9(2).
001860          07 連元ＣＨＫ−月             PIC 9(2).
001870       05 連元ＣＨＫ−日                PIC 9(2).
001880*   / 確認メッセージを使用する場合："YES" /
001890    03 連元ＣＨＫ−確認フラグ           PIC X(3).
001900*/ out
001910*   / 年月単位の年月日を作成 /
001920    03 連元ＣＨＫ−正式和暦年月日.
001930       05 連元ＣＨＫ−正式和暦年月.
001940          07 連元ＣＨＫ−正式和暦       PIC 9.
001950          07 連元ＣＨＫ−正式年         PIC 9(2).
001960          07 連元ＣＨＫ−正式月         PIC 9(2).
001970       05 連元ＣＨＫ−正式日            PIC 9(2).
001980*   / 存在しない場合エラー："YES" /
001990    03 連元ＣＨＫ−エラーフラグ         PIC X(3).
002000*
002010*   / 正式日付の年月日を作成（元号が変更されていない場合は上記年月日と同じ） /
002020    03 連元ＣＨＫ−正式和暦年月日２.
002030       05 連元ＣＨＫ−正式和暦年月２.
002040          07 連元ＣＨＫ−正式和暦２     PIC 9.
002050          07 連元ＣＨＫ−正式年２       PIC 9(2).
002060          07 連元ＣＨＫ−正式月２       PIC 9(2).
002070       05 連元ＣＨＫ−正式日２          PIC 9(2).
002080*   / 存在しない場合エラー："YES" /
002090    03 連元ＣＨＫ−エラーフラグ２       PIC X(3).
002100*
002110******************************************************************
002120*                      PROCEDURE  DIVISION                       *
002130******************************************************************
002140 PROCEDURE               DIVISION.
002150************
002160*           *
002170* 初期処理   *
002180*           *
002190************
002200     PERFORM ファイルオープン.
002210     PERFORM 連結項目待避.
002220************
002230*           *
002240* 主処理     *
002250*           *
002260************
002270     PERFORM Ｈ制御マスタ取得.
002280     PERFORM Ｈ負傷データＦ取得.
002290     PERFORM 初回同意日取得.
002300*
002310*    初療日基準であっても、H24/02以降は1か月以内初療日として判定する。
002312     IF (Ｈ制−同意期限計算区分 = ZERO) AND (Ｈ負−初療和暦年月日 >= 4240213)
002313         MOVE 1                        TO 同意期限計算区分Ｗ
002315     ELSE
002316         MOVE Ｈ制−同意期限計算区分   TO 同意期限計算区分Ｗ
002318     END-IF.
002319*
002320*    初回同意日の設定
002321     EVALUATE 同意期限計算区分Ｗ
002330     WHEN 1
002340*        同意日から１か月以内は初療日、それ以後は同意日
002350         MOVE 初回同意和暦年月日Ｗ TO 一ヶ月和暦年月日Ｗ
002360         IF 一ヶ月月Ｗ < 12
002370             ADD  1                TO 一ヶ月月Ｗ
002380         ELSE
002390             ADD  1                TO 一ヶ月年Ｗ
002400             MOVE 1                TO 一ヶ月月Ｗ
002410         END-IF
002420         IF Ｈ負−初療和暦年月日 >= 一ヶ月和暦年月日Ｗ
002430             MOVE 初回同意和暦年月日Ｗ TO 基準和暦年月日Ｗ
002440         ELSE
002450             MOVE Ｈ負−初療和暦年月日 TO 基準和暦年月日Ｗ
002460         END-IF
002470     WHEN 2
002480*        同意日起算
002490         MOVE 初回同意和暦年月日Ｗ TO 基準和暦年月日Ｗ
002500     WHEN OTHER
002510*        初療日起算
002520         MOVE Ｈ負−初療和暦年月日 TO 基準和暦年月日Ｗ
002530     END-EVALUATE.
002540*
002550     IF ( 再同意和暦年月日ＷＲ < 基準和暦年月日Ｗ )
002560        MOVE 基準和暦年月日Ｗ     TO 計算和暦年月日Ｗ
002570     ELSE
002580        MOVE 再同意和暦年月日ＷＲ TO 計算和暦年月日Ｗ
002590     END-IF.
002600*-----------------------------------------------------------------------*
002610     MOVE SPACE TO 変形徒手フラグ.
002620*
002630     IF ( 計算和暦年月日Ｗ NOT = ZERO )
002640*
002650        IF ( 施術区分ＷＲ = 2 ) AND
002660           ( 施術内容区分ＷＲ = 4 OR 5 OR 6 OR 7 OR 8 OR 9 )
002670           MOVE "YES" TO 変形徒手フラグ
002680        END-IF
002690*
002700        IF ( 変形徒手フラグ = "YES" )
002710           PERFORM 変形徒手再同意予定日算出
002720        ELSE
002730           PERFORM 再同意予定日算出
002740        END-IF
002750     END-IF.
002760*
002770************
002780*           *
002790* 終了処理   *
002800*           *
002810************
002820     PERFORM 連結項目設定.
002830     PERFORM 終了処理.
002840     MOVE ZERO TO PROGRAM-STATUS.
002850     EXIT PROGRAM.
002860*
002870*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002880*================================================================*
002890 ファイルオープン SECTION.
002900*================================================================*
002910     OPEN INPUT 元号マスタ
002920         MOVE NC"元号" TO ファイル名.
002930         PERFORM オープンチェック.
002940     OPEN INPUT Ｈ負傷データＦ.
002950         MOVE NC"Ｈ負傷データＦ" TO ファイル名.
002960         PERFORM オープンチェック.
002970     OPEN INPUT Ｈ制御マスタ.
002980         MOVE NC"Ｈ制御マスタ" TO ファイル名.
002990         PERFORM オープンチェック.
003000     OPEN INPUT ＨレセプトＦ.
003010         MOVE NC"ＨレセプトＦ" TO ファイル名.
003020         PERFORM オープンチェック.
003030*
003040*================================================================*
003050 オープンチェック SECTION.
003060*
003070     IF ( 状態キー  NOT =  "00" )
003080         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
003090         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003100         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003110                                                 UPON CONS
003120*-----------------------------------------*
003130         CALL "actcshm"  WITH C LINKAGE
003140*-----------------------------------------*
003150         ACCEPT  キー入力 FROM CONS
003160         PERFORM ファイル閉鎖
003170         MOVE 99 TO PROGRAM-STATUS
003180         EXIT PROGRAM.
003190*
003200*================================================================*
003210 連結項目待避 SECTION.
003220*================================================================*
003230* 連結項目の待避
003240     MOVE 連再同−再同意和暦年月日 TO 再同意和暦年月日ＷＲ.
003250     MOVE 連再同−負傷主キー       TO 負傷主キーＷＲ.
003260*
003270     MOVE 連再同−施術区分         TO 施術区分ＷＲ.
003280     MOVE 連再同−施術内容区分     TO 施術内容区分ＷＲ.
003290*
003300*================================================================*
003310 Ｈ負傷データＦ取得 SECTION.
003320*================================================================*
003330     MOVE 負傷主キーＷＲ TO Ｈ負−主キー.
003340*
003350     READ Ｈ負傷データＦ
003360     INVALID KEY
003370          MOVE SPACE TO Ｈ負−レコード
003380          INITIALIZE Ｈ負−レコード
003390     END-READ.
003400*
003410*================================================================*
003420 初回同意日取得 SECTION.
003430*================================================================*
003440     MOVE 連再同−施術区分         TO Ｈレセ−施術区分.
003450     MOVE Ｈ負−初療和暦年月       TO Ｈレセ−施術和暦年月.
003460     MOVE Ｈ負−患者コード         TO Ｈレセ−患者コード.
003470     MOVE 1                        TO Ｈレセ−レセ種別.
003480*
003490*    初療月のレセＦを読み、初回の同意日付を求める
003500*    レセ種別一般にて読込
003510     READ ＨレセプトＦ
003520     INVALID KEY
003530*        読めなかったらレセ種別後期高齢にて読込
003540         MOVE 連再同−施術区分         TO Ｈレセ−施術区分
003550         MOVE Ｈ負−初療和暦年月       TO Ｈレセ−施術和暦年月
003560         MOVE Ｈ負−患者コード         TO Ｈレセ−患者コード
003570         MOVE 2                        TO Ｈレセ−レセ種別
003580         READ ＨレセプトＦ
003590         INVALID KEY
003600              MOVE Ｈ負−初療和暦年月日   TO 初回同意和暦年月日Ｗ
003610         NOT INVALID KEY
003620              MOVE Ｈレセ−同意和暦年月日 TO 初回同意和暦年月日Ｗ
003630         END-READ
003640     NOT INVALID KEY
003650          MOVE Ｈレセ−同意和暦年月日 TO 初回同意和暦年月日Ｗ
003660     END-READ.
003670*
003680*================================================================*
003690 Ｈ制御マスタ取得 SECTION.
003700*================================================================*
003710     MOVE ZEROS          TO Ｈ制−制御区分.
003720*
003730     READ Ｈ制御マスタ
003740     INVALID KEY
003750          MOVE SPACE TO Ｈ制−レコード
003760          INITIALIZE Ｈ制−レコード
003770     END-READ.
003780*
003790*================================================================*
003800 再同意予定日算出 SECTION.
003810*================================================================*
003820*/ 2008.06.01の改正により修正
003830*/ 初療日または同意日が15日以前は翌々月の末日、
003840*/                     16日以降は３ヵ月後の末日を再同意予定日とする
003850*/ 2018.10.01より、はりきゅう・マッサージは6ヶ月に延長、変形徒手は現行のまま1ヶ月とする
003853*----------------------------------------------------------------*
003860*
003871     IF 連再同−再同意和暦年月日 < "4301000"
003874*       3カ月計算
003875        IF ( 計算日Ｗ < 16 )
003880           COMPUTE 計算月Ｗ = 計算月Ｗ + 2
003890        ELSE
003900           COMPUTE 計算月Ｗ = 計算月Ｗ + 3
003910        END-IF
003920     ELSE
003923*       6カ月計算
003924        IF ( 計算日Ｗ < 16 )
003925           COMPUTE 計算月Ｗ = 計算月Ｗ + 5
003926        ELSE
003927           COMPUTE 計算月Ｗ = 計算月Ｗ + 6
003928        END-IF
003929     END-IF.
003930**********     COMPUTE 計算月Ｗ = 計算月Ｗ + 3.
003931*
003940     IF ( 計算月Ｗ > 12 )
003950        COMPUTE 計算月Ｗ = 計算月Ｗ - 12
003960        ADD 1 TO 計算年Ｗ
003970     END-IF.
003980*
003990     PERFORM 月末日取得.
004000     MOVE 月末日Ｗ TO 計算日Ｗ.
004010**********     IF ( 計算日Ｗ > 27 )
004020**********        PERFORM 月末日取得
004030**********        IF ( 計算日Ｗ > 月末Ｗ )
004040**********           MOVE 月末日Ｗ TO 計算日Ｗ
004050**********        END-IF
004060**********     END-IF.
004070*
004081*================================================================*
004090 変形徒手再同意予定日算出 SECTION.
004100*================================================================*
004110*/ １ヶ月後を再同意予定日とする
004120*----------------------------------------------------------------*
004130*
004140     IF ( 計算日Ｗ = 1 )
004150        PERFORM 月末日取得
004160        MOVE 月末日Ｗ TO 計算日Ｗ
004170     ELSE
004180        COMPUTE 計算月Ｗ = 計算月Ｗ + 1
004190*
004200        IF ( 計算月Ｗ > 12 )
004210           COMPUTE 計算月Ｗ = 計算月Ｗ - 12
004220           ADD 1 TO 計算年Ｗ
004230        END-IF
004240*
004250        PERFORM 月末日取得
004260        IF ( 計算日Ｗ > 月末日Ｗ )
004270           MOVE 月末日Ｗ TO 計算日Ｗ
004280        ELSE
004290           COMPUTE 計算日Ｗ = 計算日Ｗ - 1
004300        END-IF
004310     END-IF.
004320*
004330*================================================================*
004340 月末日取得 SECTION.
004350*================================================================*
004360     MOVE 計算和暦Ｗ TO 元−元号区分.
004370     READ 元号マスタ
004380     NOT INVALID KEY
004390         MOVE 元−開始西暦年 TO 施術西暦年Ｗ
004400     END-READ.
004410     IF ( 施術西暦年Ｗ NOT = ZERO )
004420        COMPUTE 施術西暦年Ｗ = 施術西暦年Ｗ + 計算年Ｗ - 1
004430     END-IF.
004440*
004450     EVALUATE 計算月Ｗ
004460     WHEN 4
004470     WHEN 6
004480     WHEN 9
004490     WHEN 11
004500         MOVE 30 TO 月末日Ｗ
004510     WHEN 2
004520         DIVIDE 4 INTO 施術西暦年Ｗ GIVING    商Ｗ
004530                                    REMAINDER 余Ｗ
004540         END-DIVIDE
004550         IF ( 余Ｗ = ZERO )
004560             MOVE 29 TO 月末日Ｗ
004570         ELSE
004580             MOVE 28 TO 月末日Ｗ
004590         END-IF
004600     WHEN 1
004610     WHEN 3
004620     WHEN 5
004630     WHEN 7
004640     WHEN 8
004650     WHEN 10
004660     WHEN 12
004670         MOVE 31 TO 月末日Ｗ
004680     WHEN OTHER
004690          CONTINUE
004700     END-EVALUATE.
004710*
004720*================================================================*
004730 連結項目設定 SECTION.
004740*================================================================*
004750* 連結項目の設定
004760     MOVE 再同意和暦年月日ＷＲ TO 連再同−再同意和暦年月日.
004770     MOVE 負傷主キーＷＲ       TO 連再同−負傷主キー.
004780*
004790     MOVE 施術区分ＷＲ         TO 連再同−施術区分.
004800     MOVE 施術内容区分ＷＲ     TO 連再同−施術内容区分.
004810*/ out
004820*    / 2013.11.15 元号日付チェック・再設定 /
004830     INITIALIZE 連元ＣＨＫ−キー.
004840     MOVE 計算和暦年月日Ｗ TO 連元ＣＨＫ−和暦年月日.
004850     PERFORM 元号チェック.
004860     IF ( 連元ＣＨＫ−エラーフラグ２ NOT = SPACE )
004870        MOVE 連元ＣＨＫ−正式和暦年月日２ TO 計算和暦年月日Ｗ
004880     END-IF.
004890*
004900     MOVE 計算和暦年月日Ｗ     TO 連再同−再同意予定和暦年月日.
004910     MOVE Ｈ負−初療和暦年月日 TO 連再同−初療和暦年月日.
004920*
004930     IF ( 変形徒手フラグ = "YES" )
004940        MOVE 1                 TO 連再同−変形徒手フラグ
004950     ELSE
004960        MOVE ZERO              TO 連再同−変形徒手フラグ
004970     END-IF.
004980*
004990*================================================================*
005000 元号チェック SECTION.
005010*================================================================*
005020     CALL   "GENGOCHK".
005030     CANCEL "GENGOCHK".
005040*
005050*================================================================*
005060 終了処理 SECTION.
005070*================================================================*
005080     PERFORM ファイル閉鎖.
005090*
005100*================================================================*
005110 ファイル閉鎖 SECTION.
005120*
005130     CLOSE 元号マスタ Ｈ負傷データＦ Ｈ制御マスタ ＨレセプトＦ.
005140*
005150*================================================================*
005160******************************************************************
005170 END PROGRAM HMSAIDO.
005180******************************************************************
005190
