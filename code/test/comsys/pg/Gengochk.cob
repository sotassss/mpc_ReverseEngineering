000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             GENGOCHK.
000060 AUTHOR.                 宮原 睦美
000070*
000080*----------------------------------------------------------------*
000090*  ① IN-和暦の存在チェックを行う。                              *
000100*     存在しない場合はエラー。（OUT-エラーフラグ:"YES")          *
000110*  ② IN-和暦年月日の「日」がZERO の場合は、「年月チェック」     *
000120*     ZERO 以外は「年月日チェック」を行う。                      *
000130*  ③ 年月単位の日付の設定・チェック（OUT-正式和暦年月日）       *
000140*    （元号変更月は月末日までそのままの元号・年を使用する）      *
000150*     IN-和暦年月日の存在チェックを行う。                        *
000160*     存在しない場合はエラー。（OUT-エラーフラグ:"YES")          *
000170*     IN-和暦年月日からOUT-正式和暦年月日を設定。                *
000180*  ④ 正式日付の設定・チェック（OUT-正式和暦年月日２）           *
000190*    （カレンダーと同じ日付を設定）                              *
000200*     IN-和暦年月日の存在チェックを行う。                        *
000210*     存在しない場合はエラー。（OUT-エラーフラグ２:"YES")        *
000220*----------------------------------------------------------------*
000230 DATE-WRITTEN.           2013-06-28
000240 DATE-COMPILED.          2013-06-28
000250*----------------------------------------------------------------*
000260******************************************************************
000270*            ENVIRONMENT         DIVISION                        *
000280******************************************************************
000290 ENVIRONMENT             DIVISION.
000300 CONFIGURATION           SECTION.
000310 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000320 OBJECT-COMPUTER.        FMV-DESKPOWER.
000330 SPECIAL-NAMES.          CONSOLE  IS  CONS
000340                         SYSERR   IS  MSGBOX.
000350 INPUT-OUTPUT            SECTION.
000360 FILE-CONTROL.
000370     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000380                             ORGANIZATION             IS  INDEXED
000390                             ACCESS MODE              IS  DYNAMIC
000400                             RECORD KEY               IS  元－元号区分
000410                             FILE STATUS              IS  状態キー
000420                             LOCK        MODE         IS  AUTOMATIC.
000430******************************************************************
000440*                      DATA DIVISION                             *
000450******************************************************************
000460 DATA                    DIVISION.
000470 FILE                    SECTION.
000480*                           ［ＲＬ＝  １２８］
000490 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
000500     COPY GENGOU          OF  XFDLIB  JOINING   元   AS  PREFIX.
000510*
000520*----------------------------------------------------------------*
000530******************************************************************
000540*                WORKING-STORAGE SECTION                         *
000550******************************************************************
000560 WORKING-STORAGE         SECTION.
000570 01 キー入力                           PIC X    VALUE SPACE.
000580 01 状態キー                           PIC X(2) VALUE SPACE.
000590 01 ファイル名                         PIC N(10) VALUE SPACE.
000600*
000610 01 終了フラグ                         PIC X(3) VALUE SPACE.
000620*
000630 01 計算和暦年月日Ｗ.
000640    03 計算和暦年月Ｗ.
000650       05 計算和暦Ｗ     PIC 9.
000660       05 計算年Ｗ       PIC 9(2).
000670       05 計算月Ｗ       PIC 9(2).
000680    03 計算日Ｗ          PIC 9(2).
000690*
000700 01 計算西暦年Ｗ         PIC 9(4).
000710 01 月末日Ｗ             PIC 9(2).
000720 01 商Ｗ                 PIC 9(4).
000730 01 剰余Ｗ               PIC 9(2).
000740*
000750******************************************************************
000760*                          連結項目                              *
000770******************************************************************
000780*
000790 01 連元ＣＨＫ－キー IS EXTERNAL.
000800*/ in
000810    03 連元ＣＨＫ－和暦年月日.
000820       05 連元ＣＨＫ－和暦年月.
000830          07 連元ＣＨＫ－和暦           PIC 9.
000840          07 連元ＣＨＫ－年             PIC 9(2).
000850          07 連元ＣＨＫ－月             PIC 9(2).
000860       05 連元ＣＨＫ－日                PIC 9(2).
000870*   / 確認メッセージを使用する場合："YES" /
000880    03 連元ＣＨＫ－確認フラグ           PIC X(3).
000890*/ out
000900*   / 年月単位の年月日を作成 /
000910    03 連元ＣＨＫ－正式和暦年月日.
000920       05 連元ＣＨＫ－正式和暦年月.
000930          07 連元ＣＨＫ－正式和暦       PIC 9.
000940          07 連元ＣＨＫ－正式年         PIC 9(2).
000950          07 連元ＣＨＫ－正式月         PIC 9(2).
000960       05 連元ＣＨＫ－正式日            PIC 9(2).
000970*   / 存在しない場合エラー："YES" /
000980    03 連元ＣＨＫ－エラーフラグ         PIC X(3).
000990*
001000*   / 正式日付の年月日を作成（元号が変更されていない場合は上記年月日と同じ） /
001010    03 連元ＣＨＫ－正式和暦年月日２.
001020       05 連元ＣＨＫ－正式和暦年月２.
001030          07 連元ＣＨＫ－正式和暦２     PIC 9.
001040          07 連元ＣＨＫ－正式年２       PIC 9(2).
001050          07 連元ＣＨＫ－正式月２       PIC 9(2).
001060       05 連元ＣＨＫ－正式日２          PIC 9(2).
001070*   / 存在しない場合エラー："YES" /
001080    03 連元ＣＨＫ－エラーフラグ２       PIC X(3).
001090*
001100* 確認メッセージ用 (６行で 閉じる)
001110 01 連メ１０－キー IS EXTERNAL.
001120    03  連メ１０－メッセージ１          PIC X(40).
001130    03  連メ１０－メッセージ２          PIC X(40).
001140    03  連メ１０－メッセージ３          PIC X(40).
001150    03  連メ１０－メッセージ４          PIC X(40).
001160    03  連メ１０－メッセージ５          PIC X(40).
001170    03  連メ１０－メッセージ６          PIC X(40).
001180* 確認メッセージ用 (２０行で はい・いいえ)
001190 01 連メ８－キー IS EXTERNAL.
001200    03  連メ８－メッセージ              PIC N(20) OCCURS 20.
001210    03  連メ８－初期値                  PIC X.
001220    03  連メ８－返り値                  PIC X.
001230*
001240******************************************************************
001250*                      PROCEDURE  DIVISION                       *
001260******************************************************************
001270 PROCEDURE               DIVISION.
001280************
001290*           *
001300* 初期処理   *
001310*           *
001320************
001330     PERFORM 初期化.
001340*
001350*// out 初期化 //
001360     MOVE SPACE TO 連元ＣＨＫ－エラーフラグ
001370                   連元ＣＨＫ－エラーフラグ２.
001380*/   正式日付
001390     MOVE 連元ＣＨＫ－和暦年月日 TO 連元ＣＨＫ－正式和暦年月日
001400                                    連元ＣＨＫ－正式和暦年月日２.
001410*
001420************
001430*           *
001440* 主処理     *
001450*           *
001460************
001470*/ 年月単位の年月日設定
001480     MOVE 連元ＣＨＫ－和暦 TO 元－元号区分.
001490     READ 元号マスタ
001500     INVALID KEY
001510         MOVE "YES" TO 連元ＣＨＫ－エラーフラグ
001520     END-READ.
001530*
001540     IF ( 連元ＣＨＫ－エラーフラグ = SPACE )
001550        IF ( 元－正式開始和暦年月日 = ZERO OR SPACE )
001560           CONTINUE
001570        ELSE
001580           IF ( 連元ＣＨＫ－日 = ZERO )
001590              PERFORM 正式和暦年月セット
001600           ELSE
001610              PERFORM 正式和暦年月日セット
001620           END-IF
001630        END-IF
001640     END-IF.
001650*
001660*/ 正式日付の年月日設定
001670     IF ( 連元ＣＨＫ－日 = ZERO )
001680        CONTINUE
001690     ELSE
001700        MOVE 連元ＣＨＫ－和暦 TO 元－元号区分
001710        READ 元号マスタ
001720        NOT INVALID KEY
001730           IF ( 元－正式開始和暦年月日 = ZERO OR SPACE )
001740              CONTINUE
001750           ELSE
001760              PERFORM 正式和暦年月日２セット
001770           END-IF
001780        END-READ
001790     END-IF.
001800*
001810************
001820*           *
001830* 終了処理   *
001840*           *
001850************
001860     PERFORM 終了処理.
001870     MOVE ZERO TO PROGRAM-STATUS.
001880     EXIT PROGRAM.
001890*
001900*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
001910*================================================================*
001920 初期化 SECTION.
001930*
001940     PERFORM ファイルオープン.
001950*
001960*================================================================*
001970 ファイルオープン SECTION.
001980*
001990     OPEN INPUT 元号マスタ.
002000         MOVE NC"元号マスタ" TO ファイル名.
002010         PERFORM オープンチェック.
002020*================================================================*
002030 オープンチェック SECTION.
002040*
002050     IF ( 状態キー  NOT =  "00" )
002060         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
002070         DISPLAY NC"状態キー：" 状態キー         UPON CONS
002080         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
002090                                                 UPON CONS
002100         ACCEPT  キー入力 FROM CONS
002110         PERFORM ファイル閉鎖
002120         MOVE 99 TO PROGRAM-STATUS
002130         EXIT PROGRAM.
002140*
002150*================================================================*
002160 正式和暦年月セット SECTION.
002170*================================================================*
002180     EVALUATE TRUE
002190     WHEN ( 連元ＣＨＫ－和暦年月 = 元－正式開始和暦年月 )
002200          IF ( 元－正式開始日 = 01 )
002210             CONTINUE
002220          ELSE
002230             IF ( 連元ＣＨＫ－確認フラグ = "YES" )
002240                PERFORM 正式和暦年月確認－切替月
002250             ELSE
002260                PERFORM 正式和暦年月設定
002270             END-IF
002280          END-IF
002290     WHEN ( 連元ＣＨＫ－和暦年月 < 元－正式開始和暦年月 )
002300          IF ( 連元ＣＨＫ－確認フラグ = "YES" )
002310             PERFORM 正式和暦年月確認
002320          ELSE
002330             PERFORM 正式和暦年月設定
002340          END-IF
002350     WHEN OTHER
002360          IF ( 元－正式終了和暦年月日 = SPACE OR ZERO )
002370             CONTINUE
002380          ELSE
002390             EVALUATE TRUE
002400             WHEN ( 連元ＣＨＫ－和暦年月 = 元－正式終了和暦年月 )
002410                  CONTINUE
002420             WHEN ( 連元ＣＨＫ－和暦年月 > 元－正式終了和暦年月 )
002430                  IF ( 連元ＣＨＫ－確認フラグ = "YES" )
002440                     PERFORM 正式和暦年月確認－先入力
002450                  ELSE
002460                     PERFORM 正式和暦年月設定
002470                  END-IF
002480             END-EVALUATE
002490          END-IF
002500     END-EVALUATE.
002510*
002520*================================================================*
002530 正式和暦年月確認－切替月 SECTION.
002540*
002550     MOVE SPACE TO 連メ１０－キー.
002560     MOVE "施術年月に誤りがあります。"               TO 連メ１０－メッセージ１.
002570     MOVE "※ 年号の切り替え月は[旧和暦年月] で"     TO 連メ１０－メッセージ３.
002580     MOVE "    入力してください。"                   TO 連メ１０－メッセージ４.
002590     MOVE "正しい施術年月(旧和暦年月)に変更します。" TO 連メ１０－メッセージ６.
002600     CALL   "MSG010".
002610     CANCEL "MSG010".
002620*
002630     PERFORM 正式和暦年月設定.
002640*
002650*================================================================*
002660 正式和暦年月確認 SECTION.
002670*
002680     MOVE SPACE TO 連メ１０－キー.
002690     MOVE "施術年月に誤りがあります。"   TO 連メ１０－メッセージ１.
002700     MOVE "正しい施術年月に変更します。" TO 連メ１０－メッセージ３.
002710     CALL   "MSG010".
002720     CANCEL "MSG010".
002730*
002740     PERFORM 正式和暦年月設定.
002750*
002760*================================================================*
002770 正式和暦年月確認－先入力 SECTION.
002780*
002790     MOVE SPACE TO 連メ８－キー.
002800     MOVE NC"新しい元号に切り替わっています。"         TO 連メ８－メッセージ(1).
002810     MOVE SPACE                                        TO 連メ８－メッセージ(2).
002820     MOVE NC"新元号の施術年月に変更しますか？"         TO 連メ８－メッセージ(3).
002830     MOVE "Y"                                          TO 連メ８－初期値.
002840*
002850     CALL   "MSG008".
002860     CANCEL "MSG008".
002870*
002880     IF ( 連メ８－返り値 = "Y" OR "y" )
002890        PERFORM 正式和暦年月設定
002900     END-IF.
002910*
002920*================================================================*
002930 正式和暦年月設定 SECTION.
002940*
002950     MOVE "YES" TO 連元ＣＨＫ－エラーフラグ.
002960*
002970     IF ( 連元ＣＨＫ－和暦年月 <= 元－正式開始和暦年月 )
002980        PERFORM 正式日付再設定－前元号
002990     ELSE
003000        IF ( 連元ＣＨＫ－和暦年月 > 元－正式終了和暦年月 )
003010           PERFORM 正式日付再設定－次元号
003020        END-IF
003030     END-IF.
003040*
003050*================================================================*
003060 正式和暦年月日セット SECTION.
003070*================================================================*
003080     EVALUATE TRUE
003090     WHEN ( 連元ＣＨＫ－和暦年月 = 元－正式開始和暦年月 )
003100          IF ( 元－正式開始日 = 01 )
003110             CONTINUE
003120          ELSE
003130             MOVE "YES" TO 連元ＣＨＫ－エラーフラグ
003140             PERFORM 正式日付再設定－前元号
003150          END-IF
003160     WHEN ( 連元ＣＨＫ－和暦年月 < 元－正式開始和暦年月 )
003170          MOVE "YES" TO 連元ＣＨＫ－エラーフラグ
003180          PERFORM 正式日付再設定－前元号
003190     WHEN OTHER
003200          IF ( 元－正式終了和暦年月日 = SPACE OR ZERO )
003210             CONTINUE
003220          ELSE
003230             IF ( 連元ＣＨＫ－和暦年月 > 元－正式終了和暦年月 )
003240                IF ( 連元ＣＨＫ－確認フラグ = "YES" )
003250                   PERFORM 正式和暦年月日確認－先入力
003260                ELSE
003270                   MOVE "YES" TO 連元ＣＨＫ－エラーフラグ
003280                   PERFORM 正式日付再設定－次元号
003290                END-IF
003300             END-IF
003310          END-IF
003320     END-EVALUATE.
003330*
003340*================================================================*
003350 正式和暦年月日確認－先入力 SECTION.
003360*
003370     MOVE SPACE TO 連メ８－キー.
003380     MOVE NC"新しい元号に切り替わっています。"         TO 連メ８－メッセージ(1).
003390     MOVE SPACE                                        TO 連メ８－メッセージ(2).
003400     MOVE NC"新元号の施術日に変更しますか？"           TO 連メ８－メッセージ(3).
003410     MOVE "Y"                                          TO 連メ８－初期値.
003420*
003430     CALL   "MSG008".
003440     CANCEL "MSG008".
003450*
003460     IF ( 連メ８－返り値 = "Y" OR "y" )
003470*/ 次元号日付に再設定するが、エラーにはしない /*
003480*****        MOVE "YES" TO 連元ＣＨＫ－エラーフラグ
003490        PERFORM 正式日付再設定－次元号
003500     END-IF.
003510*
003520*================================================================*
003530 正式日付再設定－前元号 SECTION.
003540*================================================================*
003550     MOVE 連元ＣＨＫ－和暦年月日 TO 計算和暦年月日Ｗ.
003560*
003570     MOVE 連元ＣＨＫ－和暦 TO 元－元号区分.
003580*
003590     START 元号マスタ KEY IS < 元－元号区分
003600                               REVERSED
003610     END-START.
003620*
003630     IF ( 状態キー = "00" )
003640        MOVE SPACE TO 終了フラグ
003650        PERFORM 元号マスタ読込
003660        PERFORM UNTIL ( 終了フラグ NOT = SPACE )
003670*
003680*          / 前元号に置換え /
003690           MOVE 元－正式終了和暦 TO 計算和暦Ｗ
003700           MOVE 元－正式終了年   TO 計算年Ｗ
003710*
003720           EVALUATE TRUE
003730           WHEN ( 計算和暦年月Ｗ = 元－正式開始和暦年月 )
003740                IF ( 元－正式開始日 = 01 )
003750                   MOVE "YES" TO 終了フラグ
003760                ELSE
003770                   PERFORM 元号マスタ読込
003780                END-IF
003790          WHEN ( 計算和暦年月Ｗ < 元－正式開始和暦年月 )
003800               PERFORM 元号マスタ読込
003810          WHEN OTHER
003820               MOVE 計算和暦Ｗ TO 連元ＣＨＫ－正式和暦
003830               MOVE 計算年Ｗ   TO 連元ＣＨＫ－正式年
003840               MOVE "YES" TO 終了フラグ
003850          END-EVALUATE
003860        END-PERFORM
003870     END-IF.
003880*
003890*================================================================*
003900 正式日付再設定－次元号 SECTION.
003910*================================================================*
003920     MOVE 連元ＣＨＫ－和暦年月日 TO 計算和暦年月日Ｗ.
003930     IF ( 元－正式終了月 = 12 ) AND ( 元－正式終了日 = 31 )
003940        COMPUTE 計算年Ｗ = 連元ＣＨＫ－年 - 元－正式終了年
003950     ELSE
003960        COMPUTE 計算年Ｗ = 連元ＣＨＫ－年 - 元－正式終了年 + 1
003970     END-IF.
003980*
003990     MOVE 連元ＣＨＫ－和暦 TO 元－元号区分.
004000*
004010     START 元号マスタ KEY IS > 元－元号区分
004020     END-START.
004030*
004040     IF ( 状態キー = "00" )
004050        MOVE SPACE TO 終了フラグ
004060        PERFORM 元号マスタ読込
004070        PERFORM UNTIL ( 終了フラグ NOT = SPACE )
004080*
004090*          / 次元号に置換え /
004100           MOVE 元－元号区分 TO 計算和暦Ｗ
004110*
004120           IF ( 元－正式終了和暦年月日 = SPACE OR ZERO )
004130              MOVE 計算和暦Ｗ TO 連元ＣＨＫ－正式和暦
004140              MOVE 計算年Ｗ   TO 連元ＣＨＫ－正式年
004150              MOVE "YES" TO 終了フラグ
004160           ELSE
004170*             / 年月単位の年月日の設定用条件（年月） /
004180              IF ( 計算和暦年月Ｗ > 元－正式終了和暦年月 )
004190                 IF ( 元－正式終了月 = 12 ) AND ( 元－正式終了日 = 31 )
004200                    COMPUTE 計算年Ｗ = 計算年Ｗ - 元－正式終了年
004210                 ELSE
004220                    COMPUTE 計算年Ｗ = 計算年Ｗ - 元－正式終了年 + 1
004230                 END-IF
004240                 PERFORM 元号マスタ読込
004250              ELSE
004260                 MOVE 計算和暦Ｗ TO 連元ＣＨＫ－正式和暦
004270                 MOVE 計算年Ｗ   TO 連元ＣＨＫ－正式年
004280                 MOVE "YES" TO 終了フラグ
004290              END-IF
004300           END-IF
004310        END-PERFORM
004320     END-IF.
004330*
004340*================================================================*
004350 正式和暦年月日２セット SECTION.
004360*================================================================*
004370     IF ( 連元ＣＨＫ－和暦年月日 < 元－正式開始和暦年月日 )
004380        MOVE "YES" TO 連元ＣＨＫ－エラーフラグ２
004390        PERFORM 正式日付２再設定－前元号
004400     ELSE
004410        IF ( 元－正式終了和暦年月日 = SPACE OR ZERO )
004420           CONTINUE
004430        ELSE
004440           IF ( 連元ＣＨＫ－和暦年月日 > 元－正式終了和暦年月日 )
004450              MOVE "YES" TO 連元ＣＨＫ－エラーフラグ２
004460              PERFORM 正式日付２再設定－次元号
004470           END-IF
004480        END-IF
004490     END-IF.
004500*
004510*================================================================*
004520 正式日付２再設定－前元号 SECTION.
004530*================================================================*
004540     MOVE 連元ＣＨＫ－和暦年月日 TO 計算和暦年月日Ｗ.
004550*
004560     MOVE 連元ＣＨＫ－和暦 TO 元－元号区分.
004570*
004580     START 元号マスタ KEY IS < 元－元号区分
004590                               REVERSED
004600     END-START.
004610*
004620     IF ( 状態キー = "00" )
004630        MOVE SPACE TO 終了フラグ
004640        PERFORM 元号マスタ読込
004650        PERFORM UNTIL ( 終了フラグ NOT = SPACE )
004660*
004670*          / 前元号に置換え /
004680           MOVE 元－正式終了和暦 TO 計算和暦Ｗ
004690           MOVE 元－正式終了年   TO 計算年Ｗ
004700*
004710           IF ( 計算和暦年月日Ｗ < 元－正式開始和暦年月日 )
004720              PERFORM 元号マスタ読込
004730           ELSE
004740              MOVE 計算和暦Ｗ TO 連元ＣＨＫ－正式和暦２
004750              MOVE 計算年Ｗ   TO 連元ＣＨＫ－正式年２
004760              MOVE "YES" TO 終了フラグ
004770           END-IF
004780        END-PERFORM
004790     END-IF.
004800*
004810*================================================================*
004820 正式日付２再設定－次元号 SECTION.
004830*================================================================*
004840     MOVE 連元ＣＨＫ－和暦年月日 TO 計算和暦年月日Ｗ.
004850     IF ( 元－正式終了月 = 12 ) AND ( 元－正式終了日 = 31 )
004860        COMPUTE 計算年Ｗ = 連元ＣＨＫ－年 - 元－正式終了年
004870     ELSE
004880        COMPUTE 計算年Ｗ = 連元ＣＨＫ－年 - 元－正式終了年 + 1
004890     END-IF.
004900*
004910     MOVE 連元ＣＨＫ－和暦 TO 元－元号区分.
004920*
004930     START 元号マスタ KEY IS > 元－元号区分
004940     END-START.
004950*
004960     IF ( 状態キー = "00" )
004970        MOVE SPACE TO 終了フラグ
004980        PERFORM 元号マスタ読込
004990        PERFORM UNTIL ( 終了フラグ NOT = SPACE )
005000*
005010*          / 次元号に置換え /
005020           MOVE 元－元号区分 TO 計算和暦Ｗ
005030*
005040           IF ( 元－正式終了和暦年月日 = SPACE OR ZERO )
005050              MOVE 計算和暦Ｗ TO 連元ＣＨＫ－正式和暦２
005060              MOVE 計算年Ｗ   TO 連元ＣＨＫ－正式年２
005070              MOVE "YES" TO 終了フラグ
005080           ELSE
005090*             / 正式日付の年月日の設定用条件（年月日） /
005100              IF ( 計算和暦年月日Ｗ > 元－正式終了和暦年月日 )
005110                 IF ( 元－正式終了月 = 12 ) AND ( 元－正式終了日 = 31 )
005120                    COMPUTE 計算年Ｗ = 計算年Ｗ - 元－正式終了年
005130                 ELSE
005140                    COMPUTE 計算年Ｗ = 計算年Ｗ - 元－正式終了年 + 1
005150                 END-IF
005160                 PERFORM 元号マスタ読込
005170              ELSE
005180                 MOVE 計算和暦Ｗ TO 連元ＣＨＫ－正式和暦２
005190                 MOVE 計算年Ｗ   TO 連元ＣＨＫ－正式年２
005200                 MOVE "YES" TO 終了フラグ
005210              END-IF
005220           END-IF
005230        END-PERFORM
005240     END-IF.
005250*
005260*================================================================*
005270 元号マスタ読込 SECTION.
005280*================================================================*
005290     READ 元号マスタ NEXT
005300     AT END
005310        MOVE "YES" TO 終了フラグ
005320     END-READ.
005330*
005340*================================================================*
005350 ファイル閉鎖 SECTION.
005360*
005370     CLOSE 元号マスタ.
005380*================================================================*
005390 終了処理 SECTION.
005400*
005410     PERFORM ファイル閉鎖.
005420*
005430*================================================================*
005440******************************************************************
005450 END PROGRAM GENGOCHK.
005460******************************************************************
