000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             GETKYORI.
000060 AUTHOR.                 佐野 誠
000070*
000080*------------------------------------------------------------------*
000090*      ２点間の距離算出                                            *
000110*------------------------------------------------------------------*
000120 DATE-WRITTEN.           2009-09-09
000130 DATE-COMPILED.          2009-09-09
000141*----------------------------------------------------------------*
000150* 20171130 岡田憲和 距離の端数計算を切り捨て/切り上げ/四捨五入選択式に変更
000151*                 （快癒からしか呼ばれないのでH_SEIGYOにフラグ保管）
000152******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000266     SELECT  Ｈ制御情報マスタ  ASSIGN      TO      HSEIGYOL
000276                               ORGANIZATION             IS  INDEXED
000286                               ACCESS MODE              IS  DYNAMIC
000296                               RECORD KEY               IS  Ｈ制−制御区分
000306                               FILE STATUS              IS  状態キー
000316                               LOCK        MODE         IS  AUTOMATIC.
000326*
001020******************************************************************
001030*                      DATA DIVISION                             *
001040******************************************************************
001050 DATA                    DIVISION.
001060 FILE                    SECTION.
001190*                           ［ＲＬ＝  384］
001200 FD  Ｈ制御情報マスタ  BLOCK   CONTAINS   1   RECORDS.
001210     COPY H_SEIGYO    OF  XFDLIB  JOINING   Ｈ制   AS  PREFIX.
001220*
002080*----------------------------------------------------------------*
002090******************************************************************
002100*                WORKING-STORAGE SECTION                         *
002110******************************************************************
002120 WORKING-STORAGE         SECTION.
002130 01 キー入力                           PIC X    VALUE SPACE.
002140 01 状態キー                           PIC X(2) VALUE SPACE.
002150 01 終了フラグ                         PIC X(3) VALUE SPACE.
002170 01 ファイル名                         PIC N(10) VALUE SPACE.
002173**
002174** 固定定数
002175 01 円周率                             PIC 9V9999999999 VALUE 3.1415926535.
002183** 
002193 01 距離計算ＷＷ.
002194    03 緯度１Ｗ              PIC 9(3)V9(7).
002195    03 経度１Ｗ              PIC 9(3)V9(7).
002196    03 緯度２Ｗ              PIC 9(3)V9(7).
002197    03 経度２Ｗ              PIC 9(3)V9(7).
002200    03 平均緯度Ｗ            PIC 9(3)V9(7).
002201    03 緯度差Ｗ              PIC 9(3)V9(7).
002202    03 経度差Ｗ              PIC 9(3)V9(7).
002203    03 計算１Ｗ              PIC 9(11)V9(4).
002204    03 計算２Ｗ              PIC 9(11)V9(4).
002205    03 計算３Ｗ              PIC 9(11)V9(4).
002206    03 計算４Ｗ              PIC 9(11)V9(4).
002207    03 計算４１Ｗ            PIC 9(11)V9(4).
002208    03 計算５Ｗ              PIC 9(11)V9(4).
002209    03 計算６Ｗ              PIC 9(11)V9(4).
002210    03 計算７Ｗ              PIC 9(11)V9(4).
002211    03 計算８Ｗ              PIC 9(11)V9(4).
002212    03 計算９Ｗ              PIC 9(11)V9(4).
002213    03 計算１０Ｗ            PIC 9(11)V9(4).
002214    03 計算距離Ｗ            PIC 9(11)V9(4).
002273**
002580 01 距離端数区分Ｗ           PIC 9(1) VALUE ZERO.
002581 01 端数処理Ｗ               PIC 9(11)V9(4).
002583 01 端数処理分解Ｗ REDEFINES 端数処理Ｗ.
002584    03 整数１Ｗ              PIC 9(9).
002586    03 整数２Ｗ              PIC 9(1).
002587    03 整数３Ｗ              PIC 9(1).
002588    03 小数１Ｗ              PIC 9(4).
002589*
002590******************************************************************
002600*                          連結項目                              *
002610******************************************************************
002620*
002640 01 連距離−キー IS EXTERNAL.
002641    03 連距離−始点.
002642       05 連距離−始点緯度              PIC 9(3)V9(7).
002643       05 連距離−始点経度              PIC 9(3)V9(7).
002644    03 連距離−終点.
002645       05 連距離−終点緯度              PIC 9(3)V9(7).
002646       05 連距離−終点経度              PIC 9(3)V9(7).
002656    03 連距離−距離メートル             PIC 9(8)V9.
002710    03 連距離−距離キロ                 PIC 9(5)V9.
002730*
003481******************************************************************
003482*                      PROCEDURE  DIVISION                       *
003483******************************************************************
003490 PROCEDURE               DIVISION.
003500************
003510*           *
003520* 初期処理   *
003530*           *
003540************
003550     PERFORM 初期化.
003560************
003570*           *
003580* 主処理     *
003590*           *
003600************
003615*
003616     PERFORM 距離計算.
003617*
003620************
003630*           *
003640* 終了処理   *
003650*           *
003660************
003670     PERFORM 終了処理.
003690     EXIT PROGRAM.
003700*
003710*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003720*================================================================*
003730 初期化 SECTION.
003740*
003790     MOVE ZERO   TO 連距離−距離メートル 連距離−距離キロ.
003800*
004170     OPEN INPUT Ｈ制御情報マスタ
004171         MOVE NC"Ｈ制御情報" TO ファイル名
004172*****         PERFORM オープンチェック.
004173*
004174     MOVE ZERO TO Ｈ制−制御区分
004175     READ Ｈ制御情報マスタ
004176     NOT INVALID KEY
004177         MOVE Ｈ制−距離端数区分   TO 距離端数区分Ｗ
004178     END-READ.
004179*
004180     CLOSE Ｈ制御情報マスタ.
004181*
004182*================================================================*
004183 終了処理 SECTION.
004190*
004191*     DISPLAY "端数区分："  距離端数区分Ｗ UPON CONS.
004192*     DISPLAY "距離メートル："  連距離−距離メートル UPON CONS.
004202*     DISPLAY "距離キロ："  連距離−距離キロ UPON CONS.
004210*================================================================*
010002*================================================================*
010003 距離計算 SECTION.
010004*
010062     INITIALIZE 距離計算ＷＷ.
010063*
010069     COMPUTE 緯度１Ｗ =  連距離−始点緯度 *  円周率 / 180.
010070     COMPUTE 経度１Ｗ =  連距離−始点経度 *  円周率 / 180.
010071     COMPUTE 緯度２Ｗ =  連距離−終点緯度 *  円周率 / 180.
010072     COMPUTE 経度２Ｗ =  連距離−終点経度 *  円周率 / 180.
010073*
010074     COMPUTE 平均緯度Ｗ =  ( 緯度１Ｗ + 緯度２Ｗ ) / 2.
010079
010080     COMPUTE 緯度差Ｗ =  緯度１Ｗ  -  緯度２Ｗ.
010081     COMPUTE 経度差Ｗ =  経度１Ｗ  -  経度２Ｗ.
010083
010084
010085     COMPUTE 計算１Ｗ =  FUNCTION SIN(平均緯度Ｗ).
010086
010087
010088     COMPUTE 計算２Ｗ = 計算１Ｗ * 計算１Ｗ.
010091     COMPUTE 計算３Ｗ = 1.0 - ( 0.00669438 * 計算２Ｗ).
010092
010093     COMPUTE 計算４１Ｗ = 計算３Ｗ * 計算３Ｗ.
010094     COMPUTE 計算４Ｗ = 計算４１Ｗ * 計算３Ｗ.
010095
010096
010098     COMPUTE 計算５Ｗ = 6335439.0 /  FUNCTION SQRT(計算４Ｗ).
010099
010101     COMPUTE 計算６Ｗ = 6378137 / FUNCTION SQRT(計算３Ｗ).
010102
010103
010104     COMPUTE 計算７Ｗ = 計算５Ｗ * 緯度差Ｗ.
010105     COMPUTE 計算８Ｗ = 計算６Ｗ * FUNCTION COS(平均緯度Ｗ) * 経度差Ｗ.
010106
010107     COMPUTE 計算９Ｗ = 計算７Ｗ * 計算７Ｗ.
010108     COMPUTE 計算１０Ｗ = 計算８Ｗ * 計算８Ｗ.
010109
010111     COMPUTE 計算距離Ｗ = FUNCTION SQRT(計算９Ｗ + 計算１０Ｗ).
010112*
010113*
010115*---------------------------------------------------*
010123*    端数調整
010125     MOVE 計算距離Ｗ     TO 端数処理Ｗ.
010127     IF ((距離端数区分Ｗ = 1) AND (整数２Ｗ NOT = ZERO)) OR
010128        ((距離端数区分Ｗ = 2) AND (整数２Ｗ     > 4))
010129         ADD 100         TO 端数処理Ｗ
010135         MOVE ZERO       TO 整数３Ｗ
010136         MOVE ZERO       TO 小数１Ｗ
010140         MOVE 端数処理Ｗ TO 計算距離Ｗ 
010141     END-IF.
010142*
010143     MOVE 計算距離Ｗ TO 連距離−距離メートル.
010144*
010146     COMPUTE 連距離−距離キロ = 計算距離Ｗ / 1000
010147*
010148* 上記後、100M未満は、0.1KMとする。
010149     IF 計算距離Ｗ NOT = ZERO
010150        IF 連距離−距離キロ = ZERO
010151           MOVE 0.1 TO 連距離−距離キロ
010152        END-IF
010153     END-IF.
010154*---------------------------------------------------*
010155*
010156*================================================================*
010157*================================================================*
010200******************************************************************
010210 END PROGRAM GETKYORI.
010220******************************************************************
