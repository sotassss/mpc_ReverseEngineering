000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             KHTJYOS.
000060 AUTHOR.                 佐野 誠
000070*
000080*------------------------------------------------------------------*
000081* レセ用（月計）負担額助成、請求額助成の計算
000082*
000083* 助成の特殊負担は以下のみ対応。
000084* 負担区分:6,8,14 (毎月一定金額、一定回数を負担)
000085*          13  (毎月一定金額、一定回数を負担（１０円単位）)
000095* 負担区分:5,16,17
000110*------------------------------------------------------------------*
000120 DATE-WRITTEN.           2004-09-22
000130 DATE-COMPILED.          2004-09-22
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000260*
000873     SELECT  Ｈ日計データＦ  ASSIGN      TO        HNIKEIL
000874                             ORGANIZATION             IS  INDEXED
000875                             ACCESS MODE              IS  DYNAMIC
000876                             RECORD KEY               IS  Ｈ日−施術区分
000877                                                          Ｈ日−施術和暦年月日
000878                                                          Ｈ日−患者コード
000879                             ALTERNATE RECORD KEY     IS  Ｈ日−施術区分
000880                                                          Ｈ日−患者コード
000881                                                          Ｈ日−施術和暦年月日
000882                             ALTERNATE RECORD KEY     IS  Ｈ日−施術和暦年月日
000883                                                          Ｈ日−登録順
000884                             FILE STATUS              IS  状態キー
000885                             LOCK        MODE         IS  AUTOMATIC.
000886*
000887******************************************************************
000888*                      DATA DIVISION                             *
000889******************************************************************
000890 DATA                    DIVISION.
000891 FILE                    SECTION.
000901*
001083*                           ［ＲＬ＝  ５１２］
001084 FD  Ｈ日計データＦ      BLOCK   CONTAINS   1   RECORDS.
001085     COPY H_NIKEI    OF  XFDLIB  JOINING   Ｈ日   AS  PREFIX.
001086
001087*---------------------------------------------------------------*
002090******************************************************************
002100*                WORKING-STORAGE SECTION                         *
002110******************************************************************
002120 WORKING-STORAGE         SECTION.
002130 01 キー入力                           PIC X    VALUE SPACE.
002140 01 状態キー                           PIC X(2) VALUE SPACE.
002150 01 終了フラグ                         PIC X(3) VALUE SPACE.
002151 01 終了フラグ２                       PIC X(3) VALUE SPACE.
002170 01 ファイル名                         PIC N(10) VALUE SPACE.
002171*
002172*--------------------------------------------------------------*
002409*
002419*---------------------------------------------------------------------* 
002429 01 計算情報Ｗ.
002430     03 合計負担額助成Ｗ          PIC 9(6) VALUE ZERO.
002580*
002590******************************************************************
002600*                          連結項目                              *
002610******************************************************************
002620*
002639*--------------------------------------------------------
002640*
002650* 計算連携キー
002660 01 Ｈ連計算−キー IS EXTERNAL.
002670* / inパラメタ /
002680   03 Ｈ連計算−施術区分                   PIC 9.
002690   03 Ｈ連計算−施術和暦年月日.
002700      05 Ｈ連計算−施術和暦年月.
002710        07 Ｈ連計算−施術和暦              PIC 9.
002720        07 Ｈ連計算−施術年月.
002730          09 Ｈ連計算−施術年              PIC 9(2).
002740          09 Ｈ連計算−施術月              PIC 9(2).
002750      05 Ｈ連計算−施術日                  PIC 9(2).
002760   03 Ｈ連計算−患者コード.
002770      05 Ｈ連計算−患者番号                PIC 9(6).
002780      05 Ｈ連計算−枝番                    PIC X.
002790   03 Ｈ連計算−保険種別                   PIC 9(2).
002800* / 内部パラメタ/
002810   03 Ｈ連計算−内部パラメタ.
002820      05 Ｈ連計算−負担金計算区分          PIC 9.
002830      05 Ｈ連計算−費用額                  PIC 9(6).
002840*  初検日に"YES"(負担区分1,2,3,4)
002850      05 Ｈ連計算−初検日フラグ            PIC X(3).
002860*  当月の初回なら"YES"(負担区分5)
002870      05 Ｈ連計算−初診療フラグ            PIC X(3).
002880*  初検料の金額を転記(負担区分1)
002890      05 Ｈ連計算−初検料                  PIC 9(4).
002900*  当月の当日までの実日数(枝番も含む)(負担区分6,8,13)
002910      05 Ｈ連計算−実日数                  PIC 9(2).
002920*  日整静岡乳幼児用。当日の負担のありなし  ０：なし　１：あり (負担区分7)
002930      05 Ｈ連計算−その他計算区分１        PIC 9.
002940*  out
002950      05 Ｈ連計算−負担額                  PIC 9(6).
002960      05 Ｈ連計算−請求額                  PIC 9(6).
002970      05 Ｈ連計算−負担額助成              PIC 9(5).
002980      05 Ｈ連計算−請求額助成              PIC 9(5).
002990      05 Ｈ連計算−負担率                  PIC 9(3).
003000*
      */負担額切り上げ用/20221125
001660 01 Ｈ連計算３−キー IS EXTERNAL.
001950    03 Ｈ連計算３−負担額                  PIC 9(6).
001970    03 Ｈ連計算３−負担額助成              PIC 9(5).
003000*
003567*
003568*---------------------------------------------------------*
003569* 助成金額計算連携キー
003570 01 Ｈ連助成計算−キー IS EXTERNAL.
003571   03 Ｈ連助成計算−負担区分               PIC 9(2).
003572*
003573*---------------------------------------------------------*
003574* 助成月途中用
003575 01 Ｈ連計算助成月途中−キー IS EXTERNAL.
003576   03 Ｈ連計算助成月途中−助成月途中開始日   PIC 9(2).
003577*---------------------------------------------------------*
003578*
003579******************************************************************
003580*                      PROCEDURE  DIVISION                       *
003581******************************************************************
003582 PROCEDURE               DIVISION.
003583************
003584*           *
003585* 初期処理   *
003586*           *
003587************
003588     PERFORM 初期化.
003589************
003590*           *
003591* 主処理     *
003592*           *
003600************
003601*------------------------------------------------------------------*
003602*　負担区分:6,8,14 (毎月一定金額、一定回数を負担)
003603*           13  (毎月一定金額、一定回数を負担（１０円単位）)
003604*           5,16,17
003605*
      */負担区分２２(大阪府平成30年4月〜)/20180306
003606     IF Ｈ連助成計算−負担区分 = 6 OR 8 OR 13 OR 14 OR 5 OR 16 OR 17 OR 22
003611        PERFORM 日計データ処理
003612     END-IF.
003613*------------------------------------------------------------------*
003614*
003620************
003630*           *
003640* 終了処理   *
003650*           *
003660************
003670     PERFORM 終了処理.
003690     EXIT PROGRAM.
003700*
003710*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003720*================================================================*
003730 初期化 SECTION.
003740*
003750     PERFORM ファイルオープン.
003844*
003855*
003857*================================================================*
003860 ファイルオープン SECTION.
003870*
004030     OPEN INPUT Ｈ日計データＦ.
004031         MOVE NC"Ｈ日計データＦ" TO ファイル名.
004032         PERFORM オープンチェック.
004036*
004037*================================================================*
004038 オープンチェック SECTION.
004039*
004040     IF 状態キー  NOT =  "00"
004041         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
004050         DISPLAY NC"状態キー：" 状態キー         UPON CONS
004060         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004070                                                 UPON CONS
004071*-----------------------------------------*
004072         CALL "actcshm"  WITH C LINKAGE
004073*-----------------------------------------*
004080         ACCEPT  キー入力 FROM CONS
004090         PERFORM ファイル閉鎖
004100         MOVE 99 TO PROGRAM-STATUS
004110         EXIT PROGRAM.
004111*
004120*================================================================*
004130 ファイル閉鎖 SECTION.
004140*
004173     CLOSE Ｈ日計データＦ.
004174*================================================================*
004180 終了処理 SECTION.
004190*
004200     PERFORM ファイル閉鎖.
004210*================================================================*
005610*================================================================*
006522 日計データ処理 SECTION.
006524*------------------------------------------------------------------*
006525*　負担区分:6,8,14 (毎月一定金額、一定回数を負担)
006526*           13  (毎月一定金額、一定回数を負担（１０円単位）)
006527*           5,16,17
006528*
006529*  日計の負担額を累計してレセ用金額を算出する。
006530*------------------------------------------------------------------*
006531*
006532*   / 初期化 /
006533     INITIALIZE 計算情報Ｗ.
006534*
006538     MOVE Ｈ連計算−施術区分  TO  Ｈ日−施術区分.
006539     MOVE Ｈ連計算−患者番号  TO  Ｈ日−患者番号.
006540     MOVE Ｈ連計算−枝番      TO  Ｈ日−枝番.
006543     MOVE Ｈ連計算−施術和暦  TO  Ｈ日−施術和暦.
006544     MOVE Ｈ連計算−施術年    TO  Ｈ日−施術年.
006545     MOVE Ｈ連計算−施術月    TO  Ｈ日−施術月.
006546     MOVE 1                   TO  Ｈ日−施術日.
006548*
006549     START Ｈ日計データＦ KEY IS >= Ｈ日−施術区分
006550                                    Ｈ日−患者コード
006551                                    Ｈ日−施術和暦年月日
006552     END-START.
006553*
006554     IF 状態キー = "00"
006555         MOVE SPACE TO 終了フラグ
006556         PERFORM Ｈ日計データＦ読込
006577*        繰り返し（当月）
006578         PERFORM UNTIL ( Ｈ日−施術区分   NOT = Ｈ連計算−施術区分   ) OR
006579                       ( Ｈ日−患者コード NOT = Ｈ連計算−患者コード ) OR
006580                       ( Ｈ日−施術和暦   NOT = Ｈ連計算−施術和暦   ) OR
006581                       ( Ｈ日−施術年     NOT = Ｈ連計算−施術年     ) OR
006582                       ( Ｈ日−施術月     NOT = Ｈ連計算−施術月     ) OR
006583                       ( 終了フラグ       NOT = SPACE )
006593*
006594*           / 助成月途中開始を考慮 /
006595            IF ( Ｈ連計算助成月途中−助成月途中開始日 = ZERO ) OR
006596               (( Ｈ連計算助成月途中−助成月途中開始日 NOT = ZERO ) AND ( Ｈ日−施術日 >= Ｈ連計算助成月途中−助成月途中開始日 ))
006599*
      */負担区分２２(大阪府平成30年4月〜)/20180306
006601                IF Ｈ連助成計算−負担区分 = 6 OR 8 OR 14 OR 22
006602*                  / Ｈ日−例外助成計算負担額(1円単位)を合計する /
006603                   COMPUTE 合計負担額助成Ｗ = 合計負担額助成Ｗ + Ｈ日−例外助成計算負担額
006604                ELSE
006605                   IF Ｈ連助成計算−負担区分 = 13 OR 5 OR 16 OR 17
006606*                     / Ｈ日−例外一部負担金(10円単位)を合計する /
006607                      COMPUTE 合計負担額助成Ｗ = 合計負担額助成Ｗ + Ｈ日−例外一部負担金
006608                   END-IF
006609                END-IF
006610            END-IF
006616*
006617            PERFORM Ｈ日計データＦ読込
006618         END-PERFORM
006619**
006620*       / 連結の負担額助成、請求額助成セット /
006621         IF 合計負担額助成Ｗ > Ｈ連計算−負担額
006622            MOVE Ｈ連計算−負担額 TO Ｈ連計算−負担額助成
      */         */負担額を切り上げるバージョンに対応/20221125
006622            MOVE Ｈ連計算３−負担額 TO Ｈ連計算３−負担額助成
006623         ELSE
006624            MOVE 合計負担額助成Ｗ TO Ｈ連計算−負担額助成
      */         */負担額を切り上げるバージョンに対応/20221125
006624            MOVE 合計負担額助成Ｗ TO Ｈ連計算３−負担額助成
006625         END-IF
006626         COMPUTE Ｈ連計算−請求額助成 = Ｈ連計算−負担額 - Ｈ連計算−負担額助成
006627**
006637*
006638     END-IF.
006639*
009790*================================================================*
009800 Ｈ日計データＦ読込 SECTION.
009810*
009820     READ Ｈ日計データＦ NEXT
009830     AT END
009840         MOVE "YES"  TO 終了フラグ
009850     END-READ.
009860*
009861*================================================================*
010841*================================================================*
010856*================================================================*
010857*================================================================*
010858******************************************************************
010859 END PROGRAM KHTJYOS.
010860******************************************************************
