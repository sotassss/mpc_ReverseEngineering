000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YAW9222.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*         受診者ラベル印刷
000100*         MED = YAW922 YAW9222P
      *         PowerCOBOL用に変更/09.08.20
      */2014.07.14 受診者カナ順印刷追加
000110*----------------------------------------------------------------*
000120 DATE-WRITTEN.           2009-08-20
000130 DATE-COMPILED.          2009-08-20
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000260     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\HMOBJ\TEMP\H9221L.DAT"
000270                             ORGANIZATION             IS  INDEXED
000280                             ACCESS                   IS  DYNAMIC
000290                             RECORD      KEY          IS  作１−施術和暦年月Ｎ
000300                                                          作１−患者コード
000310                             ALTERNATE RECORD KEY     IS  作１−印刷区分
000320                                                          作１−施術和暦年月Ｎ
000330                                                          作１−患者コード
000340*/                                                       */並び順の変更/0404
000350                             ALTERNATE RECORD KEY     IS  作１−患者郵便番号
000360                                                          作１−患者カナ
000370                                                          作１−患者コード
000380                                                          作１−施術和暦年月Ｎ
000390                             ALTERNATE RECORD KEY     IS  作１−印刷区分
000400                                                          作１−患者郵便番号
000410                                                          作１−患者カナ
000420                                                          作１−患者コード
000430                                                          作１−施術和暦年月Ｎ
000930                             ALTERNATE RECORD KEY     IS  作１−印刷区分
000400                                                          作１−患者カナ
000950                                                          作１−患者コード
000960                                                          作１−施術和暦年月Ｎ
000440                             FILE        STATUS       IS  状態キー
000450                             LOCK        MODE         IS  AUTOMATIC.
000460     SELECT  印刷ファイル    ASSIGN      TO     GS-PRTF001
000470                             SYMBOLIC    DESTINATION  IS "PRT"
000480                             FORMAT                   IS  定義体名Ｐ
000490                             GROUP                    IS  項目群名Ｐ
000500                             PROCESSING  MODE         IS  処理種別Ｐ
000510                             UNIT        CONTROL      IS  拡張制御Ｐ
000520                             FILE        STATUS       IS  通知情報Ｐ.
000530******************************************************************
000540*                      DATA DIVISION                             *
000550******************************************************************
000560 DATA                    DIVISION.
000570 FILE                    SECTION.
000580*                           ［ＲＬ＝  ３２０］
001240 FD  作業ファイル１ RECORD  CONTAINS 320 CHARACTERS.
001250 01  作１−レコード.
001260     03  作１−レコードキー.
001270       05 作１−施術和暦年月Ｎ.
001280         07 作１−施術和暦Ｎ                 PIC 9.
001290         07 作１−施術年月Ｎ.
001300           09 作１−施術年Ｎ                 PIC 9(2).
001310           09 作１−施術月Ｎ                 PIC 9(2).
001320       05 作１−患者コード.
001330         07 作１−患者番号                   PIC 9(6).
001340         07 作１−枝番                       PIC X.
001350     03  作１−レコードデータ.
001360       05 作１−患者カナ                     PIC X(50).
001370       05 作１−患者氏名                     PIC X(50).
001380       05 作１−患者郵便番号.
001390         07 作１−患者郵便番号１             PIC X(3).
001400         07 作１−患者郵便番号２             PIC X(4).
001410       05 作１−患者住所１                   PIC X(50).
001420       05 作１−患者住所２                   PIC X(50).
001430       05 作１−患者生年月日.
001440         07 作１−患者和暦                   PIC 9.
001450         07 作１−患者年                     PIC 9(2).
001460         07 作１−患者月                     PIC 9(2).
001470         07 作１−患者日                     PIC 9(2).
001480       05 作１−患者電話番号                 PIC X(30).
001490       05 作１−患者性別                     PIC 9.
001500       05 作１−印刷区分                     PIC 9.
001510       05 作１−インデックス                 PIC 9(7).
001520       05 FILLER                             PIC X(55).
000880*
000890 FD  印刷ファイル.
000900     COPY YAW9222P         OF  XMDLIB.
000910*
000920*----------------------------------------------------------------*
000930******************************************************************
000940*                WORKING-STORAGE SECTION                         *
000950******************************************************************
000960 WORKING-STORAGE         SECTION.
000970 01 キー入力                           PIC X    VALUE SPACE.
000980 01 状態キー                           PIC X(2) VALUE SPACE.
000990 01 終了フラグ                         PIC X(3) VALUE SPACE.
001000 01 ファイル名                         PIC N(10) VALUE SPACE.
001010 01 オープンフラグ                     PIC X(3) VALUE SPACE.
001020***
001030 01 開始位置Ｗ                         PIC 9(2) VALUE ZERO.
001040 01 位置カウンタ                       PIC 9(2) VALUE 0.
001370 01 患者氏名Ｗ                         PIC X(50).
001050***
001060 01 印刷制御.
001070     03 定義体名Ｐ                     PIC X(8) VALUE SPACE.
001080     03 項目群名Ｐ                     PIC X(8) VALUE SPACE.
001090     03 処理種別Ｐ                     PIC X(2) VALUE SPACE.
001100     03 拡張制御Ｐ.
001110         05 端末制御Ｐ.
001120             07 移動方向Ｐ             PIC X(1).
001130             07 移動行数Ｐ             PIC 9(3).
001140         05 詳細制御Ｐ                 PIC X(2).
001150     03 通知情報Ｐ                     PIC X(2) VALUE SPACE.
001160     03 ユニット名Ｐ                   PIC X(8) VALUE SPACE.
001170*
001180******************************************************************
001190*                          連結項目                              *
001200******************************************************************
001210************************************
001220* プリンタファイル作成用           *
001230************************************
001240 01 Ｈ連ＰＲＴＦ−作成データ IS EXTERNAL.
001250     03 Ｈ連ＰＲＴＦ−ファイル名           PIC X(8).
001260     03 Ｈ連ＰＲＴＦ−プレビュー区分       PIC 9.
000600     03 Ｈ連ＰＲＴＦ−帳票プログラム名     PIC X(8).
001270     03 Ｈ連ＰＲＴＦ−オーバレイ名         PIC X(8).
001280*
001290*
001300********************
001310* メッセージ表示キー *
001320********************
001330 01 連メ−キー IS EXTERNAL.
001340    03  連メ−メッセージ               PIC N(20).
001350*
001360****************
001370* 画面入力情報 *
001380****************
       01 連入−入力区分９２２ IS EXTERNAL.
          03 連入−表示区分                PIC 9(1).
      */0:受診者住所を出力、1:被保険者住所を出力。
          03 連入−住所区分                PIC 9(1).
      */0:ラベルを出力、1:リストを出力、3:ＣＳＶ。
          03 連入−印刷形式                PIC 9(1).
      */ラベルの開始位置
          03 連入−ラベル開始位置          PIC 9(2).
      *
      */0:郵便番号順 1:受診者カナ順
       01 連入−印刷区分９２２ IS EXTERNAL.
          03 連入−印刷順区分              PIC 9(1).
001700*
001710******************************************************************
001720*                      PROCEDURE  DIVISION                       *
001730******************************************************************
001740 PROCEDURE               DIVISION.
001750************
001760*           *
001770* 初期処理   *
001780*           *
001790************
001800     PERFORM プリンタファイル作成.
001810     PERFORM ファイルオープン.
001820     PERFORM 連結項目退避.
001830************
001840*           *
001850* 主処理     *
001860*           *
001870************
001880     PERFORM 印刷処理.
001890************
001900*           *
001910* 終了処理   *
001920*           *
001930************
001940     PERFORM 終了処理.
001950     EXIT PROGRAM.
001960*
001970*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
001980*================================================================*
001990 プリンタファイル作成 SECTION.
002000*
002010*   / 初期化 /
002020     MOVE SPACE TO Ｈ連ＰＲＴＦ−作成データ.
002030     INITIALIZE Ｈ連ＰＲＴＦ−作成データ.
002040*
002050*
002060*--↓↓ 変更箇所 ↓↓--------------------------------------*
002070*   使用するプリンタファイル名セット
002080     MOVE "PRTF001"             TO Ｈ連ＰＲＴＦ−ファイル名.
000680*
000690*   使用する帳票プログラム名セット
000700     MOVE "YAW9222P"              TO Ｈ連ＰＲＴＦ−帳票プログラム名.
002090*
002100*   / プレビュー区分セット /
002110     MOVE ZERO TO Ｈ連ＰＲＴＦ−プレビュー区分.
002120*****     MOVE Ｈ連入−プレビュー区分  TO Ｈ連ＰＲＴＦ−プレビュー区分.
002130*
002140*--↑↑-----------------------------------------------------*
002150*
002160     CALL   "CRTPRTF".
002170     CANCEL "CRTPRTF".
002180*
002190*================================================================*
002200 ファイルオープン SECTION.
002210*
002220     OPEN INPUT 作業ファイル１.
002230         MOVE NC"作業" TO ファイル名.
002240         PERFORM オープンチェック.
002250* プレビューの時、OPENしてWRITEせずにCLOSEすると、エラーがでるので、データがあったらOPENする
002260*     OPEN I-O  印刷ファイル.
002270*         PERFORM エラー処理Ｐ.
002280*================================================================*
002290 オープンチェック SECTION.
002300*
002310     IF ( 状態キー  NOT =  "00" )
002320         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
002330         DISPLAY NC"状態キー：" 状態キー         UPON CONS
002340         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
002350                                                 UPON CONS
000090         CALL "actcshm"  WITH C LINKAGE
002360         ACCEPT  キー入力 FROM CONS
002370         PERFORM ファイル閉鎖
002380         EXIT PROGRAM.
002390*================================================================*
002400 連結項目退避 SECTION.
002410*
002440     MOVE 連入−ラベル開始位置  TO 開始位置Ｗ.
002520*
002530*================================================================*
002540 ファイル閉鎖 SECTION.
002550*
002560     IF ( オープンフラグ = "YES" )
002570         CLOSE 印刷ファイル
002580     ELSE
002590         MOVE  NC"　　データが０件です。確認して下さい。" TO 連メ−メッセージ
002600         CALL   "MSG001"
002610         CANCEL "MSG001"
002620     END-IF.
002630*
002640     CLOSE 作業ファイル１.
002650*================================================================*
002660 終了処理 SECTION.
002670*
002680     PERFORM ファイル閉鎖.
002690*================================================================*
002700 エラー表示Ｒ SECTION.
002710*
002720     DISPLAY ファイル名   NC"ファイル読込エラー"   UPON CONS.
002730     DISPLAY NC"状態キー：" 状態キー               UPON CONS.
002740     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
002750     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
002760                                                   UPON CONS.
000240     CALL "actcshm"  WITH C LINKAGE.
002770     ACCEPT  キー入力 FROM CONS.
002780     PERFORM ファイル閉鎖.
002790     EXIT PROGRAM.
002800*================================================================*
002810 エラー表示 SECTION.
002820*
002830     DISPLAY NC"状態キー" 状態キー  UPON CONS.
002840     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
002850     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
002860     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
002870                                                   UPON CONS.
000240     CALL "actcshm"  WITH C LINKAGE.
002880     ACCEPT  キー入力 FROM CONS.
002890     PERFORM ファイル閉鎖.
002900     EXIT PROGRAM.
002910*================================================================*
002920 印刷処理 SECTION.
002930*
002940     MOVE  開始位置Ｗ   TO 位置カウンタ.
003080*/並び順を郵便番号患者カナ順にする。/0404
003090     MOVE  1    TO 作１−印刷区分.
003100     MOVE SPACE TO 作１−患者郵便番号.
003110     MOVE SPACE TO 作１−患者カナ.
003120     MOVE ZERO  TO 作１−施術和暦Ｎ.
003130     MOVE ZERO  TO 作１−施術年Ｎ.
003140     MOVE ZERO  TO 作１−施術月Ｎ.
003150     MOVE ZERO  TO 作１−患者番号.
003160     MOVE SPACE TO 作１−枝番.
      */2014.07.14 受診者カナ順印刷追加
           IF 連入−印刷順区分 = ZERO
003170         START 作業ファイル１  KEY IS >= 作１−印刷区分
003180                                         作１−患者郵便番号
003190                                         作１−患者カナ
003200                                         作１−患者コード
003210                                         作１−施術和暦年月Ｎ
003220         END-START
           ELSE
003170         START 作業ファイル１  KEY IS >= 作１−印刷区分
003190                                         作１−患者カナ
003200                                         作１−患者コード
003210                                         作１−施術和暦年月Ｎ
003220         END-START
           END-IF.
003230*
003240     IF ( 状態キー = "00" )
003250        MOVE SPACE  TO 終了フラグ
003260        PERFORM 作業ファイル１読込
003270        IF ( 終了フラグ NOT = "YES" )
003280            MOVE "YES" TO オープンフラグ
003290            OPEN I-O  印刷ファイル
003300            PERFORM エラー処理Ｐ
003310        END-IF
003320        PERFORM UNTIL ( 終了フラグ = "YES" ) OR ( 作１−印刷区分 NOT = 1 )
003330*
003640             INITIALIZE YAW9222P
003350             PERFORM UNTIL ( 終了フラグ = "YES" )      OR
003360                           ( 作１−印刷区分 NOT = 1 )  OR ( 位置カウンタ > 12 )  
003370                 PERFORM 明細部セット
003380                 PERFORM 作業ファイル１読込
003390                 COMPUTE 位置カウンタ = 位置カウンタ + 1
003400             END-PERFORM
003410             PERFORM 印字処理
003420             PERFORM 改頁処理
003430             MOVE 1  TO 位置カウンタ
003440*
003450        END-PERFORM
003460     END-IF.
003470*
003480*================================================================*
003490 作業ファイル１読込 SECTION.
003500*
003510     READ 作業ファイル１ NEXT
003520     AT END
003530         MOVE "YES" TO 終了フラグ
003540     END-READ.
003550*
003560*================================================================*
003570 印字処理  SECTION.
003580*
003590     MOVE "YAW9222P"  TO  定義体名Ｐ.
003600     MOVE SPACE     TO  処理種別Ｐ.
003610     MOVE "GRP001"  TO  項目群名Ｐ.
003720     WRITE YAW9222P.
003630     PERFORM エラー処理Ｐ.
003640     INITIALIZE YAW9222P.
003650*================================================================*
003660 改頁処理  SECTION.
003670
003680     MOVE "YAW9222P"  TO  定義体名Ｐ.
003690     MOVE "CT"      TO  処理種別Ｐ.
003700     MOVE "PAGE"    TO  拡張制御Ｐ.
003710     MOVE SPACE     TO  項目群名Ｐ.
003720     WRITE YAW9222P.
003730     PERFORM エラー処理Ｐ.
003740     MOVE SPACE     TO  拡張制御Ｐ.
003750*
003760     CLOSE  印刷ファイル.
003770     OPEN I-O   印刷ファイル.
003780     PERFORM エラー処理Ｐ.
003790*
003800*================================================================*
003810 明細部セット SECTION.
003820*
003830     MOVE 作１−患者住所１     TO 住所１(位置カウンタ).
003840     MOVE 作１−患者住所２     TO 住所２(位置カウンタ).
003850     MOVE NC"〒"               TO 郵便マーク(位置カウンタ).
003860     MOVE 作１−患者郵便番号１ TO 郵便番号１(位置カウンタ).
003870     MOVE 作１−患者郵便番号２ TO 郵便番号２(位置カウンタ).
003880     IF ( 作１−患者郵便番号２ = SPACE )
003890         MOVE SPACE            TO 区切(位置カウンタ)
003900     ELSE
003910         MOVE "-"              TO 区切(位置カウンタ)
003920     END-IF.
           MOVE 作１−患者氏名       TO 患者氏名Ｗ.
           INSPECT 患者氏名Ｗ REPLACING ALL "　" BY "  " .
           INSPECT 患者氏名Ｗ REPLACING FIRST "   " BY " 様" .
003930     MOVE 患者氏名Ｗ           TO 患者氏名(位置カウンタ).
003950*
003960*================================================================*
003970 エラー処理Ｐ SECTION.
003980*
003990     IF ( 通知情報Ｐ NOT = "00" )
004000         DISPLAY NC"帳票エラー"              UPON CONS
004010         DISPLAY NC"項目群名Ｐ：" 項目群名Ｐ UPON CONS
004020         DISPLAY NC"通知情報Ｐ：" 通知情報Ｐ UPON CONS
004030         DISPLAY NC"拡張制御Ｐ：" 拡張制御Ｐ UPON CONS
004040         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004050                                             UPON CONS
000420         CALL "actcshm"  WITH C LINKAGE
004060         ACCEPT  キー入力 FROM CONS
004070         PERFORM ファイル閉鎖
004080         EXIT PROGRAM
004090     END-IF.
004100*================================================================*
004110******************************************************************
004120 END PROGRAM YAW9222.
004130******************************************************************
