000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             SYOKEN.
000060 AUTHOR.                 上田  育正
000070*
000080*----------------------------------------------------------------*
000090*         初検日取得（柔）
000100*         MED = 
000110*----------------------------------------------------------------*
000120 DATE-WRITTEN.           1997-10-17
000130 DATE-COMPILED.          1997-10-17
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000260     SELECT  負傷データＦ    ASSIGN      TO        HUSYOUL
000270                             ORGANIZATION             IS  INDEXED
000280                             ACCESS MODE              IS  DYNAMIC
000290                             RECORD KEY               IS 負−施術和暦年月
000300                                                         負−患者コード
000310                             ALTERNATE RECORD KEY     IS 負−患者コード
000320                                                         負−施術和暦年月
000330                             FILE STATUS              IS  状態キー
000340                             LOCK        MODE         IS  AUTOMATIC.
000410******************************************************************
000420*                      DATA DIVISION                             *
000430******************************************************************
000440 DATA                    DIVISION.
000450 FILE                    SECTION.
000460*                           ［ＲＬ＝  １２８］
000470 FD  負傷データＦ        BLOCK   CONTAINS   1   RECORDS.
000480     COPY HUSYOU          OF  XFDLIB  JOINING   負   AS  PREFIX.
000520*---------------------------------------------------------------*
000530******************************************************************
000540*                WORKING-STORAGE SECTION                         *
000550******************************************************************
000560 WORKING-STORAGE         SECTION.
000570 01 キー入力                           PIC X    VALUE SPACE.
000580 01 状態キー                           PIC X(2) VALUE SPACE.
000590 01 終了フラグ                         PIC X(3) VALUE SPACE.
000600 01 終了フラグ２                       PIC X(3) VALUE SPACE.
000610 01 終了フラグ３                       PIC X(3) VALUE SPACE.
000620 01 書込フラグ                         PIC X(4) VALUE SPACE.
000630 01 参照フラグ                         PIC X(3) VALUE SPACE.
000640 01 書込許可フラグ                     PIC X(3) VALUE SPACE.
000650 01 年月フラグ                         PIC X(3) VALUE SPACE.
000660 01 追加フラグ                         PIC X(3) VALUE SPACE.
000670 01 初検日フラグ                       PIC X(3) VALUE SPACE.
000690 01 ファイル名                         PIC N(4) VALUE SPACE.
000740 01 負傷ＣＮＴ                         PIC 9    VALUE 0.
000820 01 初検カウンタ                       PIC 9(2) VALUE ZERO.
000830 01 次部位カウンタ                     PIC 9(2) VALUE ZERO.
000840 01 部位カウンタ                       PIC 9(2) VALUE ZERO.
000850 01 部位ＣＮＴ                         PIC 9(2) VALUE ZERO.
000860 01 最大登録数Ｗ                       PIC 9 VALUE ZERO.
000870 01 遅延フラグ                         PIC X(3) VALUE SPACE.
000880 01 遅延回数Ｗ                         PIC 9(4) VALUE ZERO.
000890 01 遅延ＣＮＴ                         PIC 9(5) VALUE ZERO.
000900*
000910 01 部位番号ＲＷ                       PIC 9.
000920 01 部位番号ＲＷ１ REDEFINES 部位番号ＲＷ.
000930   03 部位番号ＭＷ                     PIC X.
000940*
000950 01 部位番号ＣＭＷ                     PIC X.
000960 01 部位番号ＣＭＷ１ REDEFINES 部位番号ＣＭＷ.
000970   03 部位番号ＣＲＷ                   PIC 9.
000980*
000990 01 次部位番号ＣＭＷ                   PIC X.
001000 01 次部位番号ＣＭＷ１ REDEFINES 次部位番号ＣＭＷ.
001010   03 次部位番号ＣＲＷ                 PIC 9.
001020*
001030 01 開始和暦年月日ＣＷ.
001040   03 開始和暦ＣＷ                     PIC 9.
001050   03 開始年月ＣＷ.
001060     05 開始年ＣＷ                     PIC 9(2).
001070     05 開始月ＣＷ                     PIC 9(2).
001080   03 開始日ＣＷ                       PIC 9(2).
001090*
001091*
001092 01 終了和暦年月日ＣＷ.
001093   03 終了和暦ＣＷ                     PIC 9.
001094   03 終了年月ＣＷ.
001095     05 終了年ＣＷ                     PIC 9(2).
001096     05 終了月ＣＷ                     PIC 9(2).
001097   03 終了日ＣＷ                       PIC 9(2).
001098**
001210*
001520*
001530 01 並替部位情報Ｗ.
001540   03 部位情報Ｗ OCCURS 9.
001550     05 開始和暦年月日Ｗ               PIC X(7).
001560     05 部位番号Ｗ                     PIC X.
001570 01 部位数Ｗ                           PIC 9 COMP-5.
001580*
001590* 01 並替部位情報.
001600*   03 開始和暦年月日                    PIC X(8) OCCURS 9.
001610* 01 部位数                              PIC 9 COMP-5.
001620*
001630* ファイル識別名
001640*****************************************************************
001650*                          連結項目                             *
001660*****************************************************************
001670* キー項目
001680* 施術記録レコード構築ＰＧ（ＳＥＫＩＲＥＣ）連携キー
001690 01 連施−キー IS EXTERNAL.
001700   03 連施−施記構築区分               PIC 9.
001710   03 連施−施術和暦年月日.
001720     05 連施−施術和暦                 PIC 9.
001730     05 連施−施術年月.
001740       07 連施−施術年                 PIC 9(2).
001750       07 連施−施術月                 PIC 9(2).
001760     05 連施−施術日                   PIC 9(2).
001770   03 連施−患者コード.
001780     05 連施−患者番号                 PIC 9(6).
001790     05 連施−枝番                     PIC X.
001800   03 連施−初検情報 OCCURS 5.
001810     05 連施−初検和暦年月日.
001820       07 連施−初検和暦               PIC 9.
001830       07 連施−初検年月.
001840         09 連施−初検年               PIC 9(2).
001850         09 連施−初検月               PIC 9(2).
001860       07 連施−初検日                 PIC 9(2).
001870*
001880******************************************************************
001890*                      PROCEDURE  DIVISION                       *
001900******************************************************************
001910 PROCEDURE               DIVISION.
001920************
001930*           *
001940* 初期処理   *
001950*           *
001960************
001970     PERFORM 初期化.
001990************
002000*           *
002010* 主処理     *
002020*           *
002030************
002040*
002050     MOVE 連施−施術和暦 TO 負−施術和暦.
002060     MOVE 連施−施術年   TO 負−施術年.
002070     MOVE 連施−施術月   TO 負−施術月.
002080     MOVE 連施−患者番号 TO 負−患者番号.
002090     MOVE 連施−枝番     TO 負−枝番.
002100     READ 負傷データＦ
002110     INVALID KEY
002120         DISPLAY NC"指定月の患者負傷情報がありません" UPON MSGBOX
002130         MOVE NC"負傷" TO ファイル名
002140         PERFORM エラー表示Ｒ
002150     NOT INVALID KEY
002170         PERFORM 初検日チェック
002180*
002190         PERFORM 初検診療日取得１
002200         PERFORM 初検診療日取得２
002210     END-READ.
002220*
002230************
002240*           *
002250* 終了処理   *
002260*           *
002270************
002280     PERFORM 終了処理.
002290     EXIT PROGRAM.
002300*
002310*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002320*================================================================*
002330 初期化 SECTION.
002340*
002350     OPEN INPUT 負傷データＦ.
002360         MOVE NC"負傷" TO ファイル名.
002370         PERFORM オープンチェック.
002410*
002420     PERFORM VARYING 部位カウンタ FROM 1 BY 1
002430             UNTIL 部位カウンタ > 9
002440         MOVE SPACE TO 開始和暦年月日Ｗ(部位カウンタ)
002450         MOVE SPACE TO 部位番号Ｗ(部位カウンタ)
002460     END-PERFORM.
002470*================================================================*
002480 オープンチェック SECTION.
002490*
002500     IF 状態キー  NOT =  "00"
002510         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
002520         DISPLAY NC"状態キー：" 状態キー         UPON CONS
002530         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
002540                                                 UPON CONS
002541*-----------------------------------------*
002542         CALL "actcshm"  WITH C LINKAGE
002543*-----------------------------------------*
002550         ACCEPT  キー入力 FROM CONS
002560         PERFORM ファイル閉鎖
002570         EXIT PROGRAM.
002580*================================================================*
002590 エラー表示Ｒ SECTION.
002600*
002610     DISPLAY NC"ファイル読込エラー" ファイル名     UPON CONS.
002620     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
002630     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
002631*-----------------------------------------*
002632     CALL "actcshm"  WITH C LINKAGE.
002633*-----------------------------------------*
002640     ACCEPT  キー入力 FROM CONS.
002650     PERFORM ファイル閉鎖.
002660     EXIT PROGRAM.
002670*================================================================*
002680 ファイル閉鎖 SECTION.
002690*
002700     CLOSE 負傷データＦ.
002710*================================================================*
002720 終了処理 SECTION.
002730*
002740     PERFORM ファイル閉鎖.
002750*================================================================*
002860*================================================================*
002870 初検日チェック SECTION.
002880*------------------------------------------------------------------------*
002890* 負傷ファイルの初検年月日は、正しいので、施術年月と初検年月が同じ時は、
002900* 初検日とする。
002940*------------------------------------------------------------------------*
002941     MOVE SPACE TO 初検日フラグ.
002942*
002943     IF  ( 負−施術和暦   = 負−初検和暦 ) AND
002944         ( 負−施術年     = 負−初検年   ) AND
002945         ( 負−施術月     = 負−初検月   )
002946          MOVE "YES" TO 初検日フラグ
002947     END-IF.
002948*
002953*
002954*     MOVE 99    TO 開始日ＣＷ.
002955*     MOVE "YES" TO 初検日フラグ.
002956*
002957*     PERFORM VARYING 部位ＣＮＴ FROM 1 BY 1
002960*             UNTIL ( 部位ＣＮＴ       > 負−部位数 ) OR 
002970*                   ( 終了フラグ３ NOT = SPACE  )
002980*
002990*         IF ( 負−施術和暦 NOT = 負−開始和暦(部位ＣＮＴ) ) OR
003000*            ( 負−施術年   NOT = 負−開始年(部位ＣＮＴ) )   OR
003010*            ( 負−施術月   NOT = 負−開始月(部位ＣＮＴ) )
003020*             MOVE SPACE TO 初検日フラグ
003030*             MOVE "YES" TO 終了フラグ３
003040*         ELSE
003050*             IF 負−開始日(部位ＣＮＴ) < 開始日ＣＷ
003060*                 MOVE 負−開始和暦(部位ＣＮＴ) TO 開始和暦ＣＷ
003070*                 MOVE 負−開始年(部位ＣＮＴ)   TO 開始年ＣＷ
003080*                 MOVE 負−開始月(部位ＣＮＴ)   TO 開始月ＣＷ
003090*                 MOVE 負−開始日(部位ＣＮＴ)   TO 開始日ＣＷ
003100*             END-IF
003110*         END-IF
003120*     END-PERFORM.
003130*
003260*
003270*================================================================*
003280 初検診療日取得１ SECTION.
003290*
003300* 負−初検年月日が指定月の初検日かの判定
003310*
003320     MOVE 1 TO 初検カウンタ.
003330*
003340* 初検日チェックにて初検日の正当性確認済
003350* 画面の初検日が当月の場合、初検日の１つとして退避しておく
003360* DEBUG
003370*     DISPLAY NC"負−患者番号" 負−患者番号 UPON MSGBOX.
003380*     DISPLAY NC"負−施術和暦年月" 負−施術和暦年月 UPON MSGBOX.
003390*     DISPLAY NC"負−初検和暦年月日" 負−初検和暦年月日 UPON MSGBOX.
003400*
003410     IF ( 初検日フラグ NOT = SPACE        ) AND
003420        (( 負−施術和暦    = 負−初検和暦 ) AND
003430         ( 負−施術年      = 負−初検年   ) AND
003440         ( 負−施術月      = 負−初検月   ))
003450         MOVE 負−初検和暦 TO 連施−初検和暦(1)
003460         MOVE 負−初検年   TO 連施−初検年(1)
003470         MOVE 負−初検月   TO 連施−初検月(1)
003480         MOVE 負−初検日   TO 連施−初検日(1)
003490         COMPUTE 初検カウンタ = 初検カウンタ + 1
003500     END-IF.
003510*
003520*================================================================*
003530*================================================================*
003540 初検診療日取得２ SECTION.
003541*
003542     INITIALIZE 終了和暦年月日ＣＷ.
003543**
003550* 開始日ソート（作業ファイル）
003560* 負−初検日以外に初検日に当てはまる初検日があるかを判定。
003590     PERFORM VARYING 部位カウンタ FROM 1 BY 1
003600             UNTIL 部位カウンタ > 負−部位数
003610*
003620         MOVE 負−開始和暦年月日(部位カウンタ) TO 開始和暦年月日Ｗ(部位カウンタ)
003630         MOVE 部位カウンタ                     TO 部位番号ＲＷ
003640         MOVE 部位番号ＭＷ                     TO 部位番号Ｗ(部位カウンタ)
003650     END-PERFORM.
003660*
003670     MOVE 負−部位数 TO 部位数Ｗ.
003680*
003690* 開始日＋部位番号による負傷情報のソート処理
003700     CALL "husort" WITH C LINKAGE USING BY REFERENCE 並替部位情報Ｗ
003710                                        BY VALUE     部位数Ｗ.
003720*
003730     MOVE SPACE TO 終了フラグ２.
003740     PERFORM VARYING 部位カウンタ FROM 1 BY 1
003750               UNTIL ( 部位カウンタ > 負−部位数 ) OR 
003760                     ( 終了フラグ２ NOT = SPACE  )
003770*
003780         MOVE 部位番号Ｗ(部位カウンタ) TO 部位番号ＣＭＷ
003790*
003800         IF ( 負−転帰区分(部位番号ＣＲＷ) = 9 ) AND
003810            (( 負−終了和暦(部位番号ＣＲＷ) = ZERO ) OR
003820            ( 負−終了年(部位番号ＣＲＷ)   = ZERO ) OR
003830            ( 負−終了月(部位番号ＣＲＷ)   = ZERO ) OR
003840            ( 負−終了日(部位番号ＣＲＷ)   = ZERO ))
003850             MOVE "YES" TO 終了フラグ２
003860         ELSE
003861* 終了年月日大きい日をセット
003862             IF  負−終了和暦年月日(部位番号ＣＲＷ) > 終了和暦年月日ＣＷ
003863                 MOVE  負−終了和暦年月日(部位番号ＣＲＷ) TO 終了和暦年月日ＣＷ
003864             END-IF
003865*
003870             COMPUTE 次部位カウンタ = 部位カウンタ + 1
003880             MOVE 部位番号Ｗ(次部位カウンタ) TO 次部位番号ＣＭＷ
003890*
003891             IF 終了和暦年月日ＣＷ NOT = ZERO
      */それまでの部位の終了日より追加した部位の開始日が大きい場合は初検算定可/121105
      */(当日の施術後に負傷し翌日来院などの場合は初検にする)
003900*                IF 終了和暦年月日ＣＷ <  負−負傷和暦年月日(次部位番号ＣＲＷ) 
003900                IF 終了和暦年月日ＣＷ <  負−開始和暦年月日(次部位番号ＣＲＷ) 
003920                    IF ( 負−施術和暦 = 負−開始和暦(次部位番号ＣＲＷ) ) AND
003930                       ( 負−施術年   = 負−開始年(次部位番号ＣＲＷ)   ) AND
003940                       ( 負−施術月   = 負−開始月(次部位番号ＣＲＷ)   )
003941** / カウンタ1の初検日と同じ時は、転記なし /
003942                        IF ( 初検カウンタ = 2 ) AND
003951                           ( 負−開始和暦(次部位番号ＣＲＷ) = 連施−初検和暦(1) ) AND
003960                           ( 負−開始年(次部位番号ＣＲＷ)   = 連施−初検年(1)   ) AND
003970                           ( 負−開始月(次部位番号ＣＲＷ)   = 連施−初検月(1)   ) AND
003980                           ( 負−開始日(次部位番号ＣＲＷ)   = 連施−初検日(1)   )
003981* 終了年月日ワーククリアー
003982                            INITIALIZE 終了和暦年月日ＣＷ
003983                        ELSE
003984                            MOVE 負−開始和暦(次部位番号ＣＲＷ) TO 連施−初検和暦(初検カウンタ)
003985                            MOVE 負−開始年(次部位番号ＣＲＷ)   TO 連施−初検年(初検カウンタ)
003986                            MOVE 負−開始月(次部位番号ＣＲＷ)   TO 連施−初検月(初検カウンタ)
003987                            MOVE 負−開始日(次部位番号ＣＲＷ)   TO 連施−初検日(初検カウンタ)
003990                            COMPUTE 初検カウンタ = 初検カウンタ + 1
003991* 終了年月日ワーククリアー
003992                            INITIALIZE 終了和暦年月日ＣＷ
004000                        END-IF
004001                    END-IF
004010                END-IF
004011             END-IF
004020         END-IF
004030     END-PERFORM.
004040*
004050*================================================================*
004090*================================================================*
004091*******************************************************************
004092 END PROGRAM SYOKEN.
004093*******************************************************************
