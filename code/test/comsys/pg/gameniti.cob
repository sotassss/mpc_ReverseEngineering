000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             GAMENITI.
000060 AUTHOR.                 宮原 睦美
000070*
000080*----------------------------------------------------------------*
000090*    鍼灸マ　画面位置調整
000100*----------------------------------------------------------------*
000110 DATE-WRITTEN.           2009-04-06
000120 DATE-COMPILED.          2009-04-06
000130*----------------------------------------------------------------*
000140* 2013/10/01 岡田憲和
000141* 画面最大化・最小化・異常画面サイズへ対応
000142*----------------------------------------------------------------*
000143******************************************************************
000150*            ENVIRONMENT         DIVISION                        *
000160******************************************************************
000170 ENVIRONMENT             DIVISION.
000180 CONFIGURATION           SECTION.
000190 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000200 OBJECT-COMPUTER.        FMV-DESKPOWER.
000210 SPECIAL-NAMES.          CONSOLE  IS  CONS
000220                         SYSERR   IS  MSGBOX.
000230 INPUT-OUTPUT            SECTION.
000240 FILE-CONTROL.
000250     SELECT 画面位置情報Ｆ   ASSIGN      TO         画面位置情報ＦＷ
000260                             ORGANIZATION             IS LINE SEQUENTIAL
000270                             ACCESS MODE              IS SEQUENTIAL
000280                             FILE STATUS              IS 状態キー
000290                             LOCK      MODE           IS AUTOMATIC.
000300*
000310******************************************************************
000320*                      DATA DIVISION                             *
000330******************************************************************
000340 DATA                    DIVISION.
000350 FILE                    SECTION.
000360*                           ［ＲＬ＝  １９］
000370 FD  画面位置情報Ｆ RECORD  CONTAINS 19 CHARACTERS.
000380 01  画面位置−レコード.
000390     03  画面位置−位置データ.
000400         05  画面位置−左                    PIC 9(4).
000410         05  画面位置−区切１                PIC X(1).
000420         05  画面位置−上                    PIC 9(4).
000430         05  画面位置−区切２                PIC X(1).
000440         05  画面位置−幅                    PIC 9(4).
000450         05  画面位置−区切３                PIC X(1).
000460         05  画面位置−高さ                  PIC 9(4).
000470*
000480******************************************************************
000490*                WORKING-STORAGE SECTION                         *
000500******************************************************************
000510 WORKING-STORAGE         SECTION.
000520 01 キー入力                           PIC X    VALUE SPACE.
000530 01 状態キー                           PIC X(2) VALUE SPACE.
000540 01 終了フラグ                         PIC X(3) VALUE SPACE.
000550 01 ファイル名                         PIC N(10).
000560 01 エラーフラグ                       PIC X(3) VALUE SPACE.
000561 01 プログラム名Ｗ                     PIC X(8) VALUE SPACE.
000562*
000570 01 画面位置情報ＦＷ                   PIC X(80) VALUE SPACE.
000580*
000590 01 解像度横Ｗ                         PIC S9(9) VALUE ZERO.
000591 01 解像度縦Ｗ                         PIC S9(9) VALUE ZERO.
000593 01 解像度横Ｗ２                       PIC S9(9) VALUE ZERO.
000594 01 解像度縦Ｗ２                       PIC S9(9) VALUE ZERO.
000595******************************************************************
000600*                          連結項目                              *
000610******************************************************************
000620********************
000630* メッセージ表示キー *
000640********************
000650 01 連メ−キー IS EXTERNAL.
000660    03  連メ−メッセージ             PIC N(20).
000670*
000680*****************
000690* 画面 位置調整 *
000700*****************
000710 01 連画面−位置調整情報 IS EXTERNAL.
000720    03 連画面−ファイル名            PIC X(8).
000730    03 連画面−処理区分              PIC X(5).
000740    03 連画面−左                    PIC 9(4).
000750    03 連画面−上                    PIC 9(4).
000760    03 連画面−幅                    PIC 9(4).
000770    03 連画面−高さ                  PIC 9(4).
000780*
000790* 画面解像度用ワーク
000791 01 パラメタ１Ｗ                     PIC S9(9) COMP-5.
000792 01 パラメタ２Ｗ                     PIC S9(9) COMP-5.
000793*
000794******************************************************************
000800*                      PROCEDURE  DIVISION                       *
000810******************************************************************
000820 PROCEDURE               DIVISION.
000830************
000840*           *
000850* 初期処理   *
000860*           *
000870************
000881     PERFORM 連結項目待避.
000890     PERFORM 画面解像度取得.
000891************
000900*           *
000910* 主処理     *
000920*           *
000930************
000940*
000950     STRING "C:\makishisys\data\adjust\" DELIMITED BY SIZE
000960             連画面−ファイル名          DELIMITED BY SPACE
000970             INTO 画面位置情報ＦＷ
000980     END-STRING.
000990*
001000     EVALUATE 連画面−処理区分
001010     WHEN "OPEN"
001020         PERFORM 画面位置情報取得
001030     WHEN "CLOSE"
001043         PERFORM 登録エラーチェック
001044         IF エラーフラグ = SPACE
001045             PERFORM 画面位置情報更新
001046         END-IF
001050     END-EVALUATE.
001060*
001070************
001080*           *
001090* 終了処理   *
001100*           *
001110************
001120     PERFORM 終了処理.
001130     EXIT PROGRAM.
001140*
001150*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
001160*================================================================*
001170 画面位置情報取得 SECTION.
001180*================================================================*
001190     OPEN INPUT 画面位置情報Ｆ.
001200     IF ( 状態キー = "35" )
001210        OPEN OUTPUT 画面位置情報Ｆ
001220        CLOSE       画面位置情報Ｆ
001230        OPEN INPUT  画面位置情報Ｆ
001240     ELSE
001250        MOVE NC"画面位置情報Ｆ" TO ファイル名
001260        PERFORM オープンチェック
001270     END-IF.
001280*----------------------------------------------------------------*
001290     MOVE SPACE TO 終了フラグ.
001300*
001310     READ 画面位置情報Ｆ
001320     AT END
001330         MOVE "YES" TO 終了フラグ
001340     END-READ.
001350*
001360     IF ( 終了フラグ = SPACE )
001370        IF ( 画面位置−左 NOT = ZERO ) AND
001380           ( 画面位置−左 NOT = SPACE )
001390           MOVE 画面位置−左   TO 連画面−左
001400        END-IF
001410        IF ( 画面位置−上 NOT = ZERO ) AND
001420           ( 画面位置−上 NOT = SPACE )
001430           MOVE 画面位置−上   TO 連画面−上
001440        END-IF
001450        IF ( 画面位置−幅 NOT = ZERO ) AND
001460           ( 画面位置−幅 NOT = SPACE )
001470           MOVE 画面位置−幅   TO 連画面−幅
001480        END-IF
001490        IF ( 画面位置−高さ NOT = ZERO ) AND
001500           ( 画面位置−高さ NOT = SPACE )
001510           MOVE 画面位置−高さ TO 連画面−高さ
001520        END-IF
001530*
001531*       表示位置が画面サイズの80％より大きい場合は調整（右端・下端ぎりぎり配置はさせない）
001541*       複数画面の場合にプライマリ画面に強制セットされてしまうので中止
001542*        COMPUTE 解像度横Ｗ２ = 解像度横Ｗ * 0.8
001543*        COMPUTE 解像度縦Ｗ２ = 解像度縦Ｗ * 0.8
001544*        IF ( 連画面−左         > 解像度横Ｗ２ )
001547*           MOVE 解像度横Ｗ２   TO 連画面−左
001548*        END-IF
001549*        IF ( 連画面−上         > 解像度縦Ｗ２ )
001552*           MOVE 解像度縦Ｗ２   TO 連画面−上
001553*        END-IF
001555*
001556*       画面サイズより大きい場合は調整(9999は最大化状態)
001557        IF ( 画面位置−幅   NOT = 9999 ) AND
001558           ( 画面位置−幅       > 解像度横Ｗ )
001559           MOVE 1              TO 連画面−左
001560           MOVE 解像度横Ｗ     TO 連画面−幅
001561        END-IF
001562        IF ( 画面位置−高さ NOT = 9999 ) AND
001563           ( 画面位置−高さ     > 解像度縦Ｗ )
001564           MOVE 1              TO 連画面−上
001565           MOVE 解像度縦Ｗ     TO 連画面−高さ
001566        END-IF
001567     END-IF.
001568*
001569*================================================================*
001570 画面位置情報更新 SECTION.
001571*================================================================*
001580     OPEN OUTPUT 画面位置情報Ｆ.
001590        MOVE NC"画面位置情報Ｆ" TO ファイル名.
001600        PERFORM オープンチェック.
001610*----------------------------------------------------------------*
001620     MOVE SPACE  TO 画面位置−レコード.
001630     INITIALIZE 画面位置−レコード.
001640*
001665     MOVE 連画面−左         TO 画面位置−左.
001666     MOVE 連画面−上         TO 画面位置−上.
001670     MOVE 連画面−幅         TO 画面位置−幅.
001680     MOVE 連画面−高さ       TO 画面位置−高さ.
001690     MOVE ","                TO 画面位置−区切１ 画面位置−区切２ 画面位置−区切３.
001700*
001711     WRITE 画面位置−レコード.
001720*
001730*================================================================*
001740 連結項目待避 SECTION.
001750*================================================================*
001760*
001770*================================================================*
001780 オープンチェック SECTION.
001790*================================================================*
001800     IF ( 状態キー NOT = "00" )
001810         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
001820         DISPLAY NC"状態キー：" 状態キー         UPON CONS
001830         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
001840                                                 UPON CONS
001850*-----------------------------------------*
001860         CALL "actcshm"  WITH C LINKAGE
001870*-----------------------------------------*
001880         ACCEPT  キー入力 FROM CONS
001890         PERFORM ファイル閉鎖
001900         EXIT PROGRAM.
001910*
001911*================================================================*
001912 画面解像度取得 SECTION.
001913*------------------------------------------------------------------------*--
001914* パラメタ１Ｗ:横、パラメタ２Ｗ:縦
001915* (例：1024*768の時、パラメタ１Ｗ:1024、パラメタ２Ｗ:768　がセットされる）
001916*-------------------------------------------------------------------------* 
001917      MOVE "getscren" TO プログラム名Ｗ.
001918      MOVE ZERO TO パラメタ１Ｗ.
001919      MOVE ZERO TO パラメタ２Ｗ.
001921      CALL プログラム名Ｗ WITH  C  LINKAGE
001922                          USING BY REFERENCE パラメタ１Ｗ
001923                                BY REFERENCE パラメタ２Ｗ.
001925*
001926      MOVE パラメタ１Ｗ   TO 解像度横Ｗ.
001927      MOVE パラメタ２Ｗ   TO 解像度縦Ｗ.
001930*
001931**================================================================*
001932 登録エラーチェック SECTION.
001933*------------------------------------------------------------------------*--
001936*
001937     MOVE SPACE             TO エラーフラグ.
001938
001939*    画面サイズがZEROの場合は登録しない
001941     IF ( 連画面−幅     = ZERO )
001942        MOVE "YES"          TO エラーフラグ
001943     END-IF.
001944     IF ( 連画面−高さ   = ZERO )
001945        MOVE "YES"          TO エラーフラグ
001946     END-IF.
001947
001948*    画面サイズより大きい場合は登録しない
001949     IF ( 連画面−幅 NOT = 9999 ) AND
001950        ( 連画面−幅     > 解像度横Ｗ )
001951        MOVE "YES"          TO エラーフラグ
001952     END-IF.
001953     IF ( 連画面−高さ NOT = 9999 ) AND
001954        ( 連画面−高さ     > 解像度縦Ｗ )
001955        MOVE "YES"          TO エラーフラグ
001956     END-IF.
001957*
001958*================================================================*
001959 終了処理 SECTION.
001960*================================================================*
001961     PERFORM ファイル閉鎖.
001962*
001970*================================================================*
001980 ファイル閉鎖 SECTION.
001990*
002000     CLOSE 画面位置情報Ｆ.
002010*
002020*================================================================*
002030******************************************************************
002040 END PROGRAM GAMENITI.
002050******************************************************************
