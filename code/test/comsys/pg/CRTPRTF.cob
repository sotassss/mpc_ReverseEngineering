000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             CRTPRTF.
000060 AUTHOR.                 佐野 誠
000070*
000080*----------------------------------------------------------------*
000090*  柔整・鍼灸マ用    プリンタファイル作成
000100*----------------------------------------------------------------*
000110 DATE-WRITTEN.           2010-03-05
000120 DATE-COMPILED.          2010-03-05
      *
      */2014.11.18 印字調整基準値4ミリ、9.9ミリ作成
      */ PRTF003 4ミリ基準  PRTF004 9.9ミリ基準
      * 2015/01/06 岡田　憲和
      * 用紙サイズ・印字方向など詳細設定を追加
      */2024.09.05 印字調整基準値3ミリ作成
000130*----------------------------------------------------------------*
000140******************************************************************
000150*            ENVIRONMENT         DIVISION                        *
000160******************************************************************
000170 ENVIRONMENT             DIVISION.
000180 CONFIGURATION           SECTION.
000190 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000200 OBJECT-COMPUTER.        FMV-DESKPOWER.
000210 SPECIAL-NAMES.          CONSOLE  IS  CONS
000220                         SYSERR   IS  MSGBOX.
000230 INPUT-OUTPUT            SECTION.
000240 FILE-CONTROL.
000250**
000260     SELECT  制御情報マスタ  ASSIGN      TO        SEIGYOL
000270                             ORGANIZATION             IS  INDEXED
000280                             ACCESS MODE              IS  DYNAMIC
000290                             RECORD KEY               IS  制０２−制御区分
000300                             FILE STATUS              IS  状態キー
000310                             LOCK        MODE         IS  AUTOMATIC.
000320     SELECT  Ｈ制御情報マスタ  ASSIGN      TO      HSEIGYOL
000330                               ORGANIZATION             IS  INDEXED
000340                               ACCESS MODE              IS  DYNAMIC
000350                               RECORD KEY               IS  Ｈ制−制御区分
000360                               FILE STATUS              IS  状態キー
000370                               LOCK        MODE         IS  AUTOMATIC.
000380     SELECT  ユーザ名称マスタ   ASSIGN      TO     UMEISYOL
000390                                ORGANIZATION             IS  INDEXED
000400                                ACCESS MODE              IS  DYNAMIC
000410                                RECORD KEY               IS  ユ名−区分コード
000420                                                             ユ名−名称コード
000430                                FILE STATUS              IS  状態キー
000440                                LOCK        MODE         IS  AUTOMATIC.
000450**
000460     SELECT  元印刷ファイル    ASSIGN      TO      元印刷ファイル名
000470                              ORGANIZATION             IS  LINE SEQUENTIAL
000480                              ACCESS MODE              IS  SEQUENTIAL
000490                              FILE        STATUS       IS  状態キー
000500                              LOCK        MODE         IS  AUTOMATIC.
000510**
000520     SELECT  作成印刷ファイル    ASSIGN      TO    作成ファイル名
000530                              ORGANIZATION             IS  LINE SEQUENTIAL
000540                              ACCESS MODE              IS  SEQUENTIAL
000550                              FILE        STATUS       IS  状態キー
000560                              LOCK        MODE         IS  AUTOMATIC.
000570*
000580******************************************************************
000590*                      DATA DIVISION                             *
000600******************************************************************
000610 DATA                    DIVISION.
000620 FILE                    SECTION.
000630*
000640 FD  制御情報マスタ    BLOCK   CONTAINS   1   RECORDS.
000650     COPY SEIGYO02    OF  XFDLIB  JOINING   制０２ AS  PREFIX.
000660*                           ［ＲＬ＝  384］
000670 FD  Ｈ制御情報マスタ  BLOCK   CONTAINS   1   RECORDS.
000680     COPY H_SEIGYO    OF  XFDLIB  JOINING   Ｈ制   AS  PREFIX.
000690*
000700 FD  ユーザ名称マスタ      BLOCK   CONTAINS   1   RECORDS.
000710     COPY UMEISYO          OF  XFDLIB  JOINING   ユ名   AS  PREFIX.
000720*
000730*
000740*----------------------------------------------------------------------*
000750* 可変長
000760 FD  元印刷ファイル
000770      RECORD IS VARYING IN SIZE FROM  1 TO 100 CHARACTERS DEPENDING ON レコード長.
000780 01 入−レコード.
000790	  02 文字列.
000800      03 文字 PIC X OCCURS 1 TO 100 TIMES DEPENDING ON レコード長.
000810**
000820* 固定長
000830 FD  作成印刷ファイル RECORD  CONTAINS 100 CHARACTERS.
000840 01  作成−レコード.
000850    03 作成−内容       PIC X(100).
000860*
000870*---------------------------------------------------------------*
000880******************************************************************
000890*                WORKING-STORAGE SECTION                         *
000900******************************************************************
000910 WORKING-STORAGE         SECTION.
000920 01 キー入力                           PIC X    VALUE SPACE.
000930 01 状態キー                           PIC X(2) VALUE SPACE.
000940 01 終了フラグ                         PIC X(3) VALUE SPACE.
000950*
000960 01 レコード長                         PIC 9(3) BINARY VALUE 100.
000970 01 作成ファイル名                     PIC X(40) VALUE SPACE.
000980 01 元印刷ファイル名                   PIC X(40) VALUE SPACE.
000990
001000*
001010 01 カウンタ                           PIC 9(3) VALUE 0.
001020 01 ファイル名                         PIC N(10) VALUE SPACE.
001030*
001040 01 元内容Ｗ                           PIC X(100) VALUE SPACE.
001050 01 印字左右調整行Ｗ                   PIC X(100) VALUE SPACE.
001060 01 印字上下調整行Ｗ                   PIC X(100) VALUE SPACE.
001070 01 レシ印字方向左数Ｗ                 PIC 9(3)   VALUE ZERO.
001080*
001090** 印字調整用 ----------------------------------------------------------*
001100 01 印字調整用Ｗ.
001110    03 印字方向左右Ｗ                  PIC 9 VALUE ZERO.
001120    03 印字方向上下Ｗ                  PIC 9 VALUE ZERO.
001130    03 印字方向左右数Ｗ                PIC 9V9 VALUE ZERO.
001140    03 印字方向上下数Ｗ                PIC 9V9 VALUE ZERO.
001150    03 印字方向左右数Ｗ２              PIC 9(3) VALUE ZERO.
001160    03 印字方向上下数Ｗ２              PIC 9(3) VALUE ZERO.
001170*
001180    03 最終左右調整数Ｗ                PIC 9(4) VALUE ZERO.
001190    03 最終左右調整数文字Ｗ            PIC X(4) VALUE SPACE.
001200    03 最終上下調整数Ｗ                PIC 9(4) VALUE ZERO.
001210    03 最終上下調整数文字Ｗ            PIC X(4) VALUE SPACE.
001220*-----------------------------------------------------------------------*
001230*
001240*
001250** プリンタ名行 --------------------------------------------------------*
001260 01 プリンタ名行Ｗ.
001270    03 プリンタ名固定パラ              PIC X(6) VALUE "PRTDRV".
001280    03 FILLER                          PIC X    VALUE SPACE.
001290    03 プリンタ名Ｗ                    PIC X(52) VALUE SPACE.
001300    03 FILLER                          PIC X(21) VALUE " * プリンタデバイス名".
001310*-----------------------------------------------------------------------*
001320*
001330** プレビュー行(固定）--------------------------------------------------*
001340 01 プレビュー行Ｗ                     PIC X(60) VALUE "PREVIEW 3                    * 印刷プレビュー".
001350*-----------------------------------------------------------------------*
001360*
001370** オーバレイ行 --------------------------------------------------------*
001380 01 オーバレイ行Ｗ.
001390    03 オーバレイ固定パラ              PIC X(8) VALUE "OVLPNAME".
001400    03 FILLER                          PIC X    VALUE SPACE.
001410    03 オーバレイ名Ｗ                  PIC X(8) VALUE SPACE.
001420    03 FILLER                          PIC X(26) VALUE "            * オーバレイ名".
001430*-----------------------------------------------------------------------*
001440*
001450* プリンタ名編集用 50+1バイト
001460 01 プリンタ名編集Ｗ.
001470    03 プリンタ名１Ｗ                  PIC X OCCURS 51.
001480*
001490***
001500* 特殊用紙用
001510 01 特殊用紙種類Ｗ                     PIC X(8) VALUE SPACE.
001520*
      * 印刷詳細制御用
       01 存在カウンタ１                     PIC 9(3) VALUE ZERO.
       01 存在カウンタ２                     PIC 9(3) VALUE ZERO.
      
      * 連結項目待避
       01 印刷詳細ＷＲ.
           03 用紙方向ＷＲ                   PIC 9(1).
           03 用紙サイズＷＲ                 PIC X(2).
           03 任意ＸＷＲ                     PIC 9(4).
           03 任意ＹＷＲ                     PIC 9(4).
           03 給紙トレイＷＲ                 PIC X(2).

001530******************************************************************
001540*                          連結項目                              *
001550******************************************************************
001560*
001570 01  Ｈ連メイン−施術区分情報 IS EXTERNAL.
001580     03 Ｈ連メイン−施術区分         PIC 9.
001590*
001600************************************
001610* プリンタファイル作成用           *
001620************************************
001630 01 Ｈ連ＰＲＴＦ−作成データ IS EXTERNAL.
001640     03 Ｈ連ＰＲＴＦ−ファイル名           PIC X(8).
001650     03 Ｈ連ＰＲＴＦ−プレビュー区分       PIC 9.
001660     03 Ｈ連ＰＲＴＦ−帳票プログラム名     PIC X(8).
001670     03 Ｈ連ＰＲＴＦ−オーバレイ名         PIC X(8).
001680*
001690*
001700************************************
001710* プリンタファイル作成特殊用       *
001720************************************
001730 01 Ｈ連特殊ＰＲＴＦ−作成データ IS EXTERNAL.
001740     03 Ｈ連特殊ＰＲＴＦ−用紙種類         PIC X(8).
001750*
001760*
001770*****************
001780* 位置調整 *
001790*****************
001800 01 連印刷−位置調整情報 IS EXTERNAL.
001810    03 連印刷−帳票プログラム名      PIC X(8).
001820    03 連印刷−処理区分              PIC X(5).
001830    03 連印刷−調整データ.
001840       05 連印刷−印字方向左右       PIC 9.
001850       05 連印刷−印字方向左右数     PIC 9V9.
001860       05 連印刷−印字方向上下       PIC 9.
001870       05 連印刷−印字方向上下数     PIC 9V9.
001880*
      ************************************
      * プリンタファイル詳細設定用       *
      ************************************
       01 Ｈ連詳細ＰＲＴＦ−作成データ IS EXTERNAL.
      *    用紙方向 1:縦 2:横
           03 Ｈ連詳細ＰＲＴＦ−用紙方向   PIC 9(1).
      *    用紙サイズ 
      *      A3 A4 A5 A6 B4 B5 PS(ハガキ) LT(レター=A4) LG(リーガル) 
      *      OT(その他 任意サイズＸＹの指定必須)
           03 Ｈ連詳細ＰＲＴＦ−用紙サイズ PIC X(2).
      *    任意サイズＸＹ 1/10mm単位指定
           03 Ｈ連詳細ＰＲＴＦ−任意Ｘ     PIC 9(4).
           03 Ｈ連詳細ＰＲＴＦ−任意Ｙ     PIC 9(4).
      *    給紙トレイ 
      *      CF(カットシートフィーダ) MA(手差し) AT(自動) NO 指定なし)
      *      H1(ホッパ1) H2(ホッパ2) H3(ホッパ3) H4(ホッパ4)
           03 Ｈ連詳細ＰＲＴＦ−給紙トレイ PIC X(2).
      *
001890******************************************************************
001900*                      PROCEDURE  DIVISION                       *
001910******************************************************************
001920 PROCEDURE               DIVISION.
001930************
001940*           *
001950* 初期処理   *
001960*           *
001970************
001980     PERFORM 作成ファイル名決定.
001990     PERFORM 初期化.
002000*************
002010*           *
002020* 主処理    *
002030*           *
002040************
002050     PERFORM ファイル作成処理.
002060************
002070*           *
002080* 終了処理   *
002090*           *
002100************
002110     PERFORM 終了処理.
002120     EXIT PROGRAM.
002130*
002140*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002150*================================================================*
002160 作成ファイル名決定 SECTION.
002170*
002180     MOVE SPACE TO 作成ファイル名 元印刷ファイル名.
002190*
002200     IF Ｈ連メイン−施術区分 = ZERO
002210        STRING "C:\MAKISHISYS\YAWOBJ\"    DELIMITED BY SIZE
002220               Ｈ連ＰＲＴＦ−ファイル名   DELIMITED BY SIZE
002230               INTO 作成ファイル名
002240        END-STRING
002250        MOVE "C:\MAKISHISYS\YAWOBJ\PRTF000"  TO 元印刷ファイル名
002260     ELSE
002270        STRING "C:\MAKISHISYS\HMOBJ\"     DELIMITED BY SIZE
002280               Ｈ連ＰＲＴＦ−ファイル名   DELIMITED BY SIZE
002290               INTO 作成ファイル名
002300        END-STRING
002310        MOVE "C:\MAKISHISYS\HMOBJ\PRTF000"  TO 元印刷ファイル名
002320     END-IF.
002330*
002340*----------------------------------------------------*
002350* Ｈ連特殊ＰＲＴＦ−用紙種類を退避し、クリアー
002360     MOVE SPACE TO 特殊用紙種類Ｗ.
002370     MOVE Ｈ連特殊ＰＲＴＦ−用紙種類  TO 特殊用紙種類Ｗ
002380*
002390     MOVE SPACE TO Ｈ連特殊ＰＲＴＦ−用紙種類.
002400     MOVE SPACE TO Ｈ連特殊ＰＲＴＦ−作成データ.
002410*----------------------------------------------------*
      *----------------------------------------------------*
      * Ｈ連詳細ＰＲＴＦ−作成データを退避し、クリアー
           INITIALIZE 印刷詳細ＷＲ.
           MOVE Ｈ連詳細ＰＲＴＦ−作成データ  TO 印刷詳細ＷＲ.
      *
           INITIALIZE Ｈ連詳細ＰＲＴＦ−作成データ.
002410*----------------------------------------------------*
002420*
002430*================================================================*
002440 初期化 SECTION.
002450*
002460* 制御マスタにあるプリンタ名を読み込む。
002470*
002480     MOVE SPACE TO プリンタ名編集Ｗ.
002490*
002500     IF Ｈ連メイン−施術区分 = ZERO
002510***
002520        OPEN INPUT 制御情報マスタ
002530            MOVE NC"制御情報" TO ファイル名
002540            PERFORM オープンチェック
002550*
002560        MOVE 02 TO 制０２−制御区分
002570        READ 制御情報マスタ
002580        NOT INVALID KEY
002590            MOVE 制０２−柔整プリンタ名 TO プリンタ名編集Ｗ
002600        END-READ
002610*
002620        CLOSE 制御情報マスタ
002630***
002640     ELSE
002650***
002660        OPEN INPUT Ｈ制御情報マスタ
002670            MOVE NC"Ｈ制御情報" TO ファイル名
002680            PERFORM オープンチェック
002690*
002700        MOVE ZERO TO Ｈ制−制御区分
002710        READ Ｈ制御情報マスタ
002720        NOT INVALID KEY
002730            MOVE Ｈ制−プリンタ名   TO プリンタ名編集Ｗ
002740        END-READ
002750*
002760        CLOSE Ｈ制御情報マスタ
002770***
002780     END-IF.
002790*
002800*-----------------------------------------------------------------------*
002810* 上記後、Ｈ連特殊ＰＲＴＦ−用紙種類により、プリンタ名編集Ｗを上書き
002820* ユーザ名称マスタ READ
002830*   区分コード:703
002840*   名称コード:1 領収証
002850*   名称コード:2 カルテ
002860*   名称コード:3 レセプト
002870*   名称コード:4 レシート
002871*   名称コード:5 PDF
002880*
002900*
002901*　Ｈ連特殊ＰＲＴＦ−用紙種類：RYOSYU(領収証)、KARUTE(カルテ)、RECE(レセプト)、REJI(レシート)、PDF
002902*
002903     IF 特殊用紙種類Ｗ = "RYOSYU" OR "KARUTE" OR "RECE" OR "REJI" OR "PDF"
002920*
002930        OPEN INPUT ユーザ名称マスタ
002940            MOVE NC"ユーザ名称" TO ファイル名
002950            PERFORM オープンチェック
002960*
002970        MOVE 703 TO ユ名−区分コード
002980*
002990        EVALUATE 特殊用紙種類Ｗ
003000        WHEN "RYOSYU"
003010           MOVE 1   TO ユ名−名称コード
003020        WHEN "KARUTE"
003030           MOVE 2   TO ユ名−名称コード
003040        WHEN "RECE"
003050           MOVE 3   TO ユ名−名称コード
003060        WHEN "REJI"
003070           MOVE 4   TO ユ名−名称コード
003071        WHEN "PDF"
003072           MOVE 5   TO ユ名−名称コード
003080        WHEN OTHER
003090           MOVE ZERO TO ユ名−名称コード
003100        END-EVALUATE
003110*
003120        READ ユーザ名称マスタ
003130        NOT INVALID KEY
003140            IF ユ名−名称 NOT = SPACE
003150               MOVE ユ名−名称 TO プリンタ名編集Ｗ
003160            END-IF
003170            IF ユ名−名称コード = 4
003180               MOVE ユ名−数字パラ１ TO レシ印字方向左数Ｗ
003190            END-IF
003200        END-READ
003210*
003220        CLOSE ユーザ名称マスタ
003230*
003240     END-IF.
003250*
003260*-----------------------------------------------------------------------*
003270*
003280*================================================================*
003290 オープンチェック SECTION.
003300*
003310     IF 状態キー  NOT =  "00"
003320         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
003330         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003340         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003350                                                 UPON CONS
003360*-----------------------------------------*
003370         CALL "actcshm"  WITH C LINKAGE
003380*-----------------------------------------*
003390         ACCEPT  キー入力 FROM CONS
003400         PERFORM ファイル閉鎖
003410         EXIT PROGRAM.
003420*================================================================*
003430 ファイル閉鎖 SECTION.
003440*
003450*================================================================*
003460 終了処理 SECTION.
003470*
003480     PERFORM ファイル閉鎖.
003490*================================================================*
003500*================================================================*
003510 ファイル作成処理 SECTION.
003520*
003530*************************************************************************
003540* プリンタファイル PRTFXXX を作成する。
003550*
003560* PRTF001=\MAKISHISYS\HMOBJ\PRTF001
003570* PRTFWAKU=\MAKISHISYS\HMOBJ\PRTFWAKU
003580* 上記以外（ワクに合わせる帳票）
003590* PRTF002=\MAKISHISYS\HMOBJ\PRTF002
003600*
003610**
003620* ・PRTF001以外、PRINTITIプログラムにより印字調整数の2行を書き込む。
003630* ・プレビューの時は、プレビュー行を書き込む。
003640* ・オーバレイがある時は、その情報を書き込む。
003650* ・元印刷ファイル PRTF000 を読み込んで、同じ内容をプリンタファイルに書き込む。
003660*************************************************************************
003670*
003680     OPEN OUTPUT 作成印刷ファイル.
003690         MOVE NC"作成印刷ファイル" TO ファイル名.
003700         PERFORM オープンチェック.
003710     OPEN INPUT  元印刷ファイル.
003720         MOVE NC"元印刷" TO ファイル名.
003730         PERFORM オープンチェック.
003740*
003750     INITIALIZE  印字調整用Ｗ.
003760*
003770**----------------------------------------------------------------**
003780*
003790     EVALUATE Ｈ連ＰＲＴＦ−ファイル名
003800     WHEN "PRTF001"
003810            CONTINUE
003820*
003830*    / PRTFWAKU はワクのみ印刷用 基準値500にする /
003840     WHEN "PRTFWAKU"
003850            MOVE ZERO                    TO  印字方向左右Ｗ
003860            MOVE ZERO                    TO  印字方向左右数Ｗ
003870            MOVE ZERO                    TO  印字方向上下Ｗ
003880            MOVE ZERO                    TO  印字方向上下数Ｗ
003890     WHEN OTHER
003900*    / PRINTITI サブにて調整数を取得 /
003910            IF Ｈ連ＰＲＴＦ−帳票プログラム名 NOT = SPACE
003920               INITIALIZE 連印刷−位置調整情報
003930               MOVE Ｈ連ＰＲＴＦ−帳票プログラム名  TO 連印刷−帳票プログラム名
003940               MOVE "READ"                          TO 連印刷−処理区分
003950               CALL   "PRINTITI"
003960               CANCEL "PRINTITI"
003970               MOVE 連印刷−印字方向左右    TO  印字方向左右Ｗ
003980               MOVE 連印刷−印字方向左右数  TO  印字方向左右数Ｗ
003990               MOVE 連印刷−印字方向上下    TO  印字方向上下Ｗ
004000               MOVE 連印刷−印字方向上下数  TO  印字方向上下数Ｗ
004010            END-IF
004020     END-EVALUATE.
004030**
004040**----------------------------------------------------------------**
004050* プリンタ名の編集→""で囲んで書き込む。
004060     IF プリンタ名編集Ｗ NOT = SPACE
004070*
004080        PERFORM VARYING カウンタ FROM 51 BY -1  UNTIL ( カウンタ < 1 )
004090             IF プリンタ名１Ｗ(カウンタ) NOT = SPACE
004100                MOVE '"'   TO  プリンタ名１Ｗ(カウンタ + 1)
004110                MOVE 1 TO カウンタ
004120             END-IF
004130        END-PERFORM
004140*
004150        MOVE SPACE TO プリンタ名Ｗ
004160        STRING '"'              DELIMITED BY SIZE
004170              プリンタ名編集Ｗ  DELIMITED BY SIZE
004180              INTO プリンタ名Ｗ
004190        END-STRING
004200*      １行書込み
004210        MOVE SPACE TO 作成−レコード
004220        INITIALIZE    作成−レコード
004230        MOVE プリンタ名行Ｗ    TO 作成−レコード
004240        WRITE 作成−レコード
004250        IF 状態キー NOT = "00"
004260            MOVE NC"作成印刷" TO ファイル名
004270            PERFORM エラー表示
004280        END-IF
004290*
004300     END-IF.
004310*
004320**----------------------------------------------------------------**
004330* PRTF001以外、上記取得の印字調整数の行を書き込む。（２行）
004340     IF (Ｈ連ＰＲＴＦ−ファイル名 NOT = "PRTF001") AND
004350        (Ｈ連ＰＲＴＦ−ファイル名 NOT = "PRTFREC")
004360*
               EVALUATE Ｈ連ＰＲＴＦ−ファイル名
               WHEN "PRTF003"
004370             PERFORM 印字調整行作成４
               WHEN "PRTF004"
004370             PERFORM 印字調整行作成９９
               WHEN "PRTF005"
004370             PERFORM 印字調整行作成３
               WHEN OTHER
004370             PERFORM 印字調整行作成
               END-EVALUATE
004380*       ２行書込み
004390         MOVE SPACE TO 作成−レコード
004400         INITIALIZE    作成−レコード
004410         MOVE 印字左右調整行Ｗ    TO 作成−レコード
004420         WRITE 作成−レコード
004430         IF 状態キー NOT = "00"
004440            MOVE NC"作成印刷" TO ファイル名
004450            PERFORM エラー表示
004460         END-IF
004470*
004480         MOVE SPACE TO 作成−レコード
004490         INITIALIZE    作成−レコード
004500         MOVE 印字上下調整行Ｗ    TO 作成−レコード
004510         WRITE 作成−レコード
004520         IF 状態キー NOT = "00"
004530            MOVE NC"作成印刷" TO ファイル名
004540            PERFORM エラー表示
004550         END-IF
004560     END-IF.
004570**
004580* PRTFRECの時、印字調整数の行を書き込む。（２行）
004590     IF (Ｈ連ＰＲＴＦ−ファイル名 = "PRTFREC")
004600         COMPUTE 最終左右調整数Ｗ = レシ印字方向左数Ｗ * 10
004610         STRING "PRTMPOSX"        DELIMITED BY SIZE
004620                " "               DELIMITED BY SIZE
004630                最終左右調整数Ｗ  DELIMITED BY SIZE
004640                "                   *   印刷開始桁位置(1/100mm単位の整数値)"  DELIMITED BY SIZE
004650                INTO 印字左右調整行Ｗ
004660         END-STRING
004670         MOVE SPACE TO 作成−レコード
004680         INITIALIZE    作成−レコード
004690         MOVE 印字左右調整行Ｗ    TO 作成−レコード
004700         WRITE 作成−レコード
004710         IF 状態キー NOT = "00"
004720            MOVE NC"作成印刷" TO ファイル名
004730            PERFORM エラー表示
004740         END-IF
004750*
004760         MOVE ZERO            TO 最終上下調整数文字Ｗ
004770         STRING "PRTMPOSY"        DELIMITED BY SIZE
004780                " "               DELIMITED BY SIZE
004790                最終上下調整数文字Ｗ  DELIMITED BY SIZE
004800                "                   *   印刷開始行位置(1/100mm単位の整数値)"  DELIMITED BY SIZE
004810                INTO 印字上下調整行Ｗ
004820         END-STRING
004830         MOVE SPACE TO 作成−レコード
004840         INITIALIZE    作成−レコード
004850         MOVE 印字上下調整行Ｗ    TO 作成−レコード
004860         WRITE 作成−レコード
004870         IF 状態キー NOT = "00"
004880            MOVE NC"作成印刷" TO ファイル名
004890            PERFORM エラー表示
004900         END-IF
004910     END-IF.
004920**----------------------------------------------------------------**
004930* プレビュー時、プレビュー行を書き込む。
004940     IF ( Ｈ連ＰＲＴＦ−プレビュー区分 = 1 ) AND ( 特殊用紙種類Ｗ NOT = "PDF" )
004950*      １行書込み
004960        MOVE SPACE TO 作成−レコード
004970        INITIALIZE    作成−レコード
004980        MOVE プレビュー行Ｗ  TO 作成−レコード
004990        WRITE 作成−レコード
005000        IF 状態キー NOT = "00"
005010            MOVE NC"作成印刷" TO ファイル名
005020            PERFORM エラー表示
005030        END-IF
005040     END-IF.
005050*
005060**----------------------------------------------------------------**
005070* オーバレイ指定時、オーバレイ行を書き込む。
005080     IF Ｈ連ＰＲＴＦ−オーバレイ名 NOT = SPACE
005090        MOVE Ｈ連ＰＲＴＦ−オーバレイ名  TO オーバレイ名Ｗ
005100*      １行書込み
005110        MOVE SPACE TO 作成−レコード
005120        INITIALIZE    作成−レコード
005130        MOVE オーバレイ行Ｗ  TO 作成−レコード
005140        WRITE 作成−レコード
005150        IF 状態キー NOT = "00"
005160            MOVE NC"作成印刷" TO ファイル名
005170            PERFORM エラー表示
005180        END-IF
005190     END-IF.
005200*
005210**----------------------------------------------------------------**
005220* 元印刷ファイル PRTF000 を読み込んで、同じ内容をプリンタファイルに書き込む。
005230     MOVE SPACE TO 終了フラグ.
005240     PERFORM  元印刷ファイル読込.
005250*
005260     PERFORM UNTIL 終了フラグ = "YES"
005270         MOVE SPACE TO 作成−レコード
005280         INITIALIZE    作成−レコード
005290         MOVE SPACE        TO 元内容Ｗ
005300         MOVE 入−レコード TO 元内容Ｗ
005310         MOVE 元内容Ｗ     TO 作成−レコード
      *
      *        印刷詳細設定が指定されている場合は単純コピーから除く
               PERFORM コピーチェック
               IF 存在カウンタ１ = ZERO
                   PERFORM 印刷ファイル書込
               END-IF
      *
005370         PERFORM  元印刷ファイル読込
005380     END-PERFORM.
005390*
           PERFORM  印刷詳細設定追加.
005400**----------------------------------------------------------------**
005410*
005420     CLOSE 作成印刷ファイル 元印刷ファイル.
005430*
      *================================================================*
       コピーチェック SECTION.
      *    印刷詳細設定が指定されている場合は同じパラメータをコピーしないようにする
           MOVE ZERO              TO 存在カウンタ１.
      *    用紙方向
           IF 用紙方向ＷＲ = 1 OR 2
               MOVE ZERO          TO 存在カウンタ２
               INSPECT 元内容Ｗ TALLYING 存在カウンタ２ FOR ALL "PRTFORM "
               ADD 存在カウンタ２ TO 存在カウンタ１
           END-IF.

      *    用紙サイズ
           IF 用紙サイズＷＲ = "A3" OR "A4" OR "A5" OR "A6" OR "B4" OR "B5" OR "PS" OR "LT" OR "LG" OR "OT"
               MOVE ZERO          TO 存在カウンタ２
               INSPECT 元内容Ｗ TALLYING 存在カウンタ２ FOR ALL "FORMSIZE "
               ADD 存在カウンタ２ TO 存在カウンタ１
           END-IF.

      *    任意サイズＸＹ(用紙サイズが"OT"の場合のみ有効)
           IF (用紙サイズＷＲ = "OT") AND (任意ＸＷＲ NOT = ZERO) AND (任意ＹＷＲ NOT = ZERO)
               MOVE ZERO          TO 存在カウンタ２
               INSPECT 元内容Ｗ TALLYING 存在カウンタ２ FOR ALL "PAPERSIZE"
               ADD 存在カウンタ２ TO 存在カウンタ１
           END-IF.

      *    給紙トレイ
           IF 給紙トレイＷＲ = "CF" OR "MA" OR "AT" OR "NO" OR "H1" OR "H2" OR "H3" OR "H4"
               MOVE ZERO          TO 存在カウンタ２
               INSPECT 元内容Ｗ TALLYING 存在カウンタ２ FOR ALL "SUPLY "
               ADD 存在カウンタ２ TO 存在カウンタ１
           END-IF.

      *    フォント
           IF Ｈ連ＰＲＴＦ−帳票プログラム名 = "YNS6125" OR "YAW823" OR "YAW8231" OR "YAW8232" OR "YAW8233"
               MOVE ZERO          TO 存在カウンタ２
               INSPECT 元内容Ｗ TALLYING 存在カウンタ２ FOR ALL "FONTFACE "
               ADD 存在カウンタ２ TO 存在カウンタ１
           END-IF.

      *================================================================*
       印刷詳細設定追加 SECTION.
      *    印刷詳細設定パラメータを追加する
      *    用紙方向
           EVALUATE 用紙方向ＷＲ
           WHEN 1
               MOVE "PRTFORM PO"  TO 作成−レコード
               PERFORM 印刷ファイル書込
           WHEN 2
               MOVE "PRTFORM LA"  TO 作成−レコード
               PERFORM 印刷ファイル書込
           END-EVALUATE.

      *    用紙サイズ
           IF 用紙サイズＷＲ = "A3" OR "A4" OR "A5" OR "A6" OR "B4" OR "B5" OR "PS" OR "LT" OR "LG" OR "OT"
               MOVE SPACE        TO 作成−レコード
               STRING "FORMSIZE "        DELIMITED BY SIZE
                      用紙サイズＷＲ     DELIMITED BY SIZE
                 INTO 作成−レコード
               END-STRING
               PERFORM 印刷ファイル書込
           END-IF.

      *    任意サイズＸＹ(用紙サイズが"OT"の場合のみ有効)
           IF (用紙サイズＷＲ = "OT") AND (任意ＸＷＲ NOT = ZERO) AND (任意ＹＷＲ NOT = ZERO)
               MOVE SPACE        TO 作成−レコード
               STRING "PAPERSIZEX "      DELIMITED BY SIZE
                      任意ＸＷＲ         DELIMITED BY SIZE
                 INTO 作成−レコード
               END-STRING
               PERFORM 印刷ファイル書込

               MOVE SPACE        TO 作成−レコード
               STRING "PAPERSIZEY "      DELIMITED BY SIZE
                      任意ＹＷＲ         DELIMITED BY SIZE
                 INTO 作成−レコード
               END-STRING
               PERFORM 印刷ファイル書込
           END-IF.

      *    給紙トレイ
           IF 給紙トレイＷＲ = "CF" OR "MA" OR "AT" OR "NO" OR "H1" OR "H2" OR "H3" OR "H4"
               MOVE SPACE        TO 作成−レコード
               STRING "SUPLY "           DELIMITED BY SIZE
                      給紙トレイＷＲ     DELIMITED BY SIZE
                 INTO 作成−レコード
               END-STRING
               PERFORM 印刷ファイル書込
           END-IF.

      *    フォント
           IF Ｈ連ＰＲＴＦ−帳票プログラム名 = "YNS6125" OR "YAW823" OR "YAW8231" OR "YAW8232" OR "YAW8233"
               MOVE SPACE        TO 作成−レコード
               STRING "FONTFACE "         DELIMITED BY SIZE
                      """ＭＳ ゴシック"""     DELIMITED BY SIZE
                 INTO 作成−レコード
               END-STRING
               PERFORM 印刷ファイル書込
           END-IF.

      *================================================================*
       印刷ファイル書込 SECTION.
      *
           WRITE 作成−レコード.
           IF 状態キー NOT = "00"
               MOVE NC"作成印刷" TO ファイル名
               PERFORM エラー表示
           END-IF.

005440*================================================================*
005450 元印刷ファイル読込 SECTION.
005460*
005470     READ 元印刷ファイル NEXT
005480     AT END
005490         MOVE "YES" TO 終了フラグ
005500     END-READ.
005510*
005520*================================================================*
005530*================================================================*
005540 印字調整行作成 SECTION.
005550*
005560* 基準500
005570* //--- 左右 ----------------------------------//
005580* 調整ありの時 ( 印字調整 は 5より大きい数字は無い。)
005590     IF 印字方向左右Ｗ = 1 OR 2
005600        IF 印字方向左右数Ｗ NOT = ZERO
005610            COMPUTE 印字方向左右数Ｗ２ = 印字方向左右数Ｗ * 100
005620            EVALUATE 印字方向左右Ｗ
005630*         / 右 /
005640            WHEN 1
005650               COMPUTE 最終左右調整数Ｗ = 500 + 印字方向左右数Ｗ２
005660*         / 左 /
005670            WHEN 2
005680               COMPUTE 最終左右調整数Ｗ = 500 - 印字方向左右数Ｗ２
005690            END-EVALUATE
005700        ELSE
005710            MOVE 500 TO  最終左右調整数Ｗ
005720        END-IF
005730* 調整なしの時は、基準の 500 をセット
005740     ELSE
005750        MOVE 500 TO  最終左右調整数Ｗ
005760     END-IF.
005770*
005780     MOVE 最終左右調整数Ｗ TO 最終左右調整数文字Ｗ.
005790     MOVE SPACE            TO 印字左右調整行Ｗ.
005800*
005810     STRING "PRTMPOSX"        DELIMITED BY SIZE
005820            " "               DELIMITED BY SIZE
005830            最終左右調整数文字Ｗ  DELIMITED BY SIZE
005840            "                   *   印刷開始桁位置(1/100mm単位の整数値)"  DELIMITED BY SIZE
005850            INTO 印字左右調整行Ｗ
005860     END-STRING.
005870*
005880* //--- 上下 ----------------------------------//
005890* 調整ありの時 ( 印字調整 は 5より大きい数字は無い。)
005900     IF 印字方向上下Ｗ = 1 OR 2
005910        IF 印字方向上下数Ｗ NOT = ZERO
005920            COMPUTE 印字方向上下数Ｗ２ = 印字方向上下数Ｗ * 100
005930            EVALUATE 印字方向上下Ｗ
005940*         / 上 /
005950            WHEN 1
005960               COMPUTE 最終上下調整数Ｗ = 500 - 印字方向上下数Ｗ２
005970*         / 下 /
005980            WHEN 2
005990               COMPUTE 最終上下調整数Ｗ = 500 + 印字方向上下数Ｗ２
006000            END-EVALUATE
006010        ELSE
006020            MOVE 500 TO  最終上下調整数Ｗ
006030        END-IF
006040* 調整なしの時は、基準の 500 をセット
006050     ELSE
006060        MOVE 500 TO  最終上下調整数Ｗ
006070     END-IF.
006080*
006090     MOVE 最終上下調整数Ｗ TO 最終上下調整数文字Ｗ.
006100     MOVE SPACE            TO 印字上下調整行Ｗ.
006110*
006120     STRING "PRTMPOSY"        DELIMITED BY SIZE
006130            " "               DELIMITED BY SIZE
006140            最終上下調整数文字Ｗ  DELIMITED BY SIZE
006150            "                   *   印刷開始行位置(1/100mm単位の整数値)"  DELIMITED BY SIZE
006160            INTO 印字上下調整行Ｗ
006170     END-STRING.
006180*
005530*================================================================*
005540 印字調整行作成４ SECTION.
005550*
005560* 基準400
005570* //--- 左右 ----------------------------------//
005580* 調整ありの時 ( 印字調整 は 4より大きい数字は無い。)
005590     IF 印字方向左右Ｗ = 1 OR 2
005600        IF 印字方向左右数Ｗ NOT = ZERO
005610            COMPUTE 印字方向左右数Ｗ２ = 印字方向左右数Ｗ * 100
005620            EVALUATE 印字方向左右Ｗ
005630*         / 右 /
005640            WHEN 1
005650               COMPUTE 最終左右調整数Ｗ = 400 + 印字方向左右数Ｗ２
005660*         / 左 /
005670            WHEN 2
005680               COMPUTE 最終左右調整数Ｗ = 400 - 印字方向左右数Ｗ２
005690            END-EVALUATE
005700        ELSE
005710            MOVE 400 TO  最終左右調整数Ｗ
005720        END-IF
005730* 調整なしの時は、基準の 400 をセット
005740     ELSE
005750        MOVE 400 TO  最終左右調整数Ｗ
005760     END-IF.
005770*
005780     MOVE 最終左右調整数Ｗ TO 最終左右調整数文字Ｗ.
005790     MOVE SPACE            TO 印字左右調整行Ｗ.
005800*
005810     STRING "PRTMPOSX"        DELIMITED BY SIZE
005820            " "               DELIMITED BY SIZE
005830            最終左右調整数文字Ｗ  DELIMITED BY SIZE
005840            "                   *   印刷開始桁位置(1/100mm単位の整数値)"  DELIMITED BY SIZE
005850            INTO 印字左右調整行Ｗ
005860     END-STRING.
005870*
005880* //--- 上下 ----------------------------------//
005890* 調整ありの時 ( 印字調整 は 4より大きい数字は無い。)
005900     IF 印字方向上下Ｗ = 1 OR 2
005910        IF 印字方向上下数Ｗ NOT = ZERO
005920            COMPUTE 印字方向上下数Ｗ２ = 印字方向上下数Ｗ * 100
005930            EVALUATE 印字方向上下Ｗ
005940*         / 上 /
005950            WHEN 1
005960               COMPUTE 最終上下調整数Ｗ = 400 - 印字方向上下数Ｗ２
005970*         / 下 /
005980            WHEN 2
005990               COMPUTE 最終上下調整数Ｗ = 400 + 印字方向上下数Ｗ２
006000            END-EVALUATE
006010        ELSE
006020            MOVE 400 TO  最終上下調整数Ｗ
006030        END-IF
006040* 調整なしの時は、基準の 400 をセット
006050     ELSE
006060        MOVE 400 TO  最終上下調整数Ｗ
006070     END-IF.
006080*
006090     MOVE 最終上下調整数Ｗ TO 最終上下調整数文字Ｗ.
006100     MOVE SPACE            TO 印字上下調整行Ｗ.
006110*
006120     STRING "PRTMPOSY"        DELIMITED BY SIZE
006130            " "               DELIMITED BY SIZE
006140            最終上下調整数文字Ｗ  DELIMITED BY SIZE
006150            "                   *   印刷開始行位置(1/100mm単位の整数値)"  DELIMITED BY SIZE
006160            INTO 印字上下調整行Ｗ
006170     END-STRING.
006180*
005530*================================================================*
005540 印字調整行作成３ SECTION.
005550*
005560* 基準300
005570* //--- 左右 ----------------------------------//
005580* 調整ありの時 ( 印字調整 は 3より大きい数字は無い。)
005590     IF 印字方向左右Ｗ = 1 OR 2
005600        IF 印字方向左右数Ｗ NOT = ZERO
005610            COMPUTE 印字方向左右数Ｗ２ = 印字方向左右数Ｗ * 100
005620            EVALUATE 印字方向左右Ｗ
005630*         / 右 /
005640            WHEN 1
005650               COMPUTE 最終左右調整数Ｗ = 300 + 印字方向左右数Ｗ２
005660*         / 左 /
005670            WHEN 2
005680               COMPUTE 最終左右調整数Ｗ = 300 - 印字方向左右数Ｗ２
005690            END-EVALUATE
005700        ELSE
005710            MOVE 300 TO  最終左右調整数Ｗ
005720        END-IF
005730* 調整なしの時は、基準の 400 をセット
005740     ELSE
005750        MOVE 300 TO  最終左右調整数Ｗ
005760     END-IF.
005770*
005780     MOVE 最終左右調整数Ｗ TO 最終左右調整数文字Ｗ.
005790     MOVE SPACE            TO 印字左右調整行Ｗ.
005800*
005810     STRING "PRTMPOSX"        DELIMITED BY SIZE
005820            " "               DELIMITED BY SIZE
005830            最終左右調整数文字Ｗ  DELIMITED BY SIZE
005840            "                   *   印刷開始桁位置(1/100mm単位の整数値)"  DELIMITED BY SIZE
005850            INTO 印字左右調整行Ｗ
005860     END-STRING.
005870*
005880* //--- 上下 ----------------------------------//
005890* 調整ありの時 ( 印字調整 は 3より大きい数字は無い。)
005900     IF 印字方向上下Ｗ = 1 OR 2
005910        IF 印字方向上下数Ｗ NOT = ZERO
005920            COMPUTE 印字方向上下数Ｗ２ = 印字方向上下数Ｗ * 100
005930            EVALUATE 印字方向上下Ｗ
005940*         / 上 /
005950            WHEN 1
005960               COMPUTE 最終上下調整数Ｗ = 300 - 印字方向上下数Ｗ２
005970*         / 下 /
005980            WHEN 2
005990               COMPUTE 最終上下調整数Ｗ = 300 + 印字方向上下数Ｗ２
006000            END-EVALUATE
006010        ELSE
006020            MOVE 300 TO  最終上下調整数Ｗ
006030        END-IF
006040* 調整なしの時は、基準の 300 をセット
006050     ELSE
006060        MOVE 300 TO  最終上下調整数Ｗ
006070     END-IF.
006080*
006090     MOVE 最終上下調整数Ｗ TO 最終上下調整数文字Ｗ.
006100     MOVE SPACE            TO 印字上下調整行Ｗ.
006110*
006120     STRING "PRTMPOSY"        DELIMITED BY SIZE
006130            " "               DELIMITED BY SIZE
006140            最終上下調整数文字Ｗ  DELIMITED BY SIZE
006150            "                   *   印刷開始行位置(1/100mm単位の整数値)"  DELIMITED BY SIZE
006160            INTO 印字上下調整行Ｗ
006170     END-STRING.
006180*
005530*================================================================*
005540 印字調整行作成９９ SECTION.
005550*
005560* 基準990
005570* //--- 左右 ----------------------------------//
005580* 調整ありの時 ( 印字調整 は 9.9より大きい数字は無い。)
005590     IF 印字方向左右Ｗ = 1 OR 2
005600        IF 印字方向左右数Ｗ NOT = ZERO
005610            COMPUTE 印字方向左右数Ｗ２ = 印字方向左右数Ｗ * 100
005620            EVALUATE 印字方向左右Ｗ
005630*         / 右 /
005640            WHEN 1
005650               COMPUTE 最終左右調整数Ｗ = 990 + 印字方向左右数Ｗ２
005660*         / 左 /
005670            WHEN 2
005680               COMPUTE 最終左右調整数Ｗ = 990 - 印字方向左右数Ｗ２
005690            END-EVALUATE
005700        ELSE
005710            MOVE 990 TO  最終左右調整数Ｗ
005720        END-IF
005730* 調整なしの時は、基準の 990 をセット
005740     ELSE
005750        MOVE 990 TO  最終左右調整数Ｗ
005760     END-IF.
005770*
005780     MOVE 最終左右調整数Ｗ TO 最終左右調整数文字Ｗ.
005790     MOVE SPACE            TO 印字左右調整行Ｗ.
005800*
005810     STRING "PRTMPOSX"        DELIMITED BY SIZE
005820            " "               DELIMITED BY SIZE
005830            最終左右調整数文字Ｗ  DELIMITED BY SIZE
005840            "                   *   印刷開始桁位置(1/100mm単位の整数値)"  DELIMITED BY SIZE
005850            INTO 印字左右調整行Ｗ
005860     END-STRING.
005870*
005880* //--- 上下 ----------------------------------//
005890* 調整ありの時 ( 印字調整 は 5より大きい数字は無い。)
005900     IF 印字方向上下Ｗ = 1 OR 2
005910        IF 印字方向上下数Ｗ NOT = ZERO
005920            COMPUTE 印字方向上下数Ｗ２ = 印字方向上下数Ｗ * 100
005930            EVALUATE 印字方向上下Ｗ
005940*         / 上 /
005950            WHEN 1
005960               COMPUTE 最終上下調整数Ｗ = 990 - 印字方向上下数Ｗ２
005970*         / 下 /
005980            WHEN 2
005990               COMPUTE 最終上下調整数Ｗ = 990 + 印字方向上下数Ｗ２
006000            END-EVALUATE
006010        ELSE
006020            MOVE 990 TO  最終上下調整数Ｗ
006030        END-IF
006040* 調整なしの時は、基準の 990 をセット
006050     ELSE
006060        MOVE 990 TO  最終上下調整数Ｗ
006070     END-IF.
006080*
006090     MOVE 最終上下調整数Ｗ TO 最終上下調整数文字Ｗ.
006100     MOVE SPACE            TO 印字上下調整行Ｗ.
006110*
006120     STRING "PRTMPOSY"        DELIMITED BY SIZE
006130            " "               DELIMITED BY SIZE
006140            最終上下調整数文字Ｗ  DELIMITED BY SIZE
006150            "                   *   印刷開始行位置(1/100mm単位の整数値)"  DELIMITED BY SIZE
006160            INTO 印字上下調整行Ｗ
006170     END-STRING.
006180*
006190*================================================================*
006200 エラー表示 SECTION.
006210*
006220     DISPLAY NC"状態キー" 状態キー  UPON CONS.
006230     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
006240     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
006250     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"   UPON CONS.
006260*-----------------------------------------*
006270     CALL "actcshm"  WITH C LINKAGE.
006280*-----------------------------------------*
006290     ACCEPT  キー入力 FROM CONS.
006300     PERFORM ファイル閉鎖.
006310     MOVE 99 TO PROGRAM-STATUS.
006320     EXIT PROGRAM.
006330*================================================================*
006340*================================================================*
006350*******************************************************************
006360 END PROGRAM CRTPRTF.
006370*******************************************************************
