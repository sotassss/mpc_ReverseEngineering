000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             HUTANRIT.
000060 AUTHOR.                 佐野　誠
000070*
000080*----------------------------------------------------------------*
000090*         負担率取得プログラム
000110* 平成１４年１０月分請求から使用開始
000120* ３歳未満２割負担、高齢者１，２割負担の対応。
000130*----------------------------------------------------------------*
000140 DATE-WRITTEN.           2009-04-01
000150 DATE-COMPILED.          2009-04-01
000160*----------------------------------------------------------------*
000170******************************************************************
000180*            ENVIRONMENT         DIVISION                        *
000190******************************************************************
000200 ENVIRONMENT             DIVISION.
000210 CONFIGURATION           SECTION.
000220 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000230 OBJECT-COMPUTER.        FMV-DESKPOWER.
000240 SPECIAL-NAMES.          CONSOLE  IS  CONS
000250                         SYSERR   IS  MSGBOX.
000260 INPUT-OUTPUT            SECTION.
000270 FILE-CONTROL.
000280     SELECT  保険者マスタ    ASSIGN      TO        HOKENSL
000290                             ORGANIZATION             IS  INDEXED
000300                             ACCESS MODE              IS  DYNAMIC
000310                             RECORD KEY               IS  保－保険種別
000320                                                          保－保険者番号
000330                             ALTERNATE RECORD KEY     IS  保－保険種別
000340                                                          保－保険者名称
000350                                                          保－保険者番号
000360                             FILE STATUS              IS  状態キー
000370                             LOCK        MODE         IS  AUTOMATIC.
000380     SELECT  負担率マスタ    ASSIGN      TO        HUTANRIL
000390                             ORGANIZATION             IS  INDEXED
000400                             ACCESS MODE              IS  DYNAMIC
000410                             RECORD KEY               IS  負率－保険種別
000420                                                          負率－開始和暦年月
000430                             FILE STATUS              IS  状態キー
000440                             LOCK        MODE         IS  AUTOMATIC.
000450     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000460                             ORGANIZATION             IS  INDEXED
000470                             ACCESS MODE              IS  DYNAMIC
000480                             RECORD KEY               IS  受－施術和暦年月
000490                                                          受－患者コード
000500                             ALTERNATE RECORD KEY     IS  受－施術和暦年月
000510                                                          受－患者カナ
000520                                                          受－患者コード
000530                             ALTERNATE RECORD KEY     IS  受－患者コード
000540                                                          受－施術和暦年月
000550                             ALTERNATE RECORD KEY     IS  受－施術和暦年月
000560                                                          受－保険種別
000570                                                          受－保険者番号
000580                                                          受－患者コード
000590                             ALTERNATE RECORD KEY     IS  受－施術和暦年月
000600                                                          受－公費種別
000610                                                          受－費用負担者番号
000620                                                          受－患者コード
000630                             ALTERNATE RECORD KEY     IS  受－施術和暦年月
000640                                                          受－助成種別
000650                                                          受－費用負担者番号助成
000660                                                          受－患者コード
000670                             ALTERNATE RECORD KEY     IS  受－請求和暦年月
000680                                                          受－施術和暦年月
000690                                                          受－患者コード
000700                             FILE STATUS              IS  状態キー
000710                             LOCK        MODE         IS  AUTOMATIC.
000720     SELECT  市町村マスタ    ASSIGN      TO        SITYOSNL
000730                             ORGANIZATION             IS  INDEXED
000740                             ACCESS MODE              IS  DYNAMIC
000750                             RECORD KEY               IS  市－公費種別
000760                                                          市－市町村番号
000770                             ALTERNATE RECORD KEY     IS  市－公費種別
000780                                                          市－市町村名称
000790                                                          市－市町村番号
000800                             FILE STATUS              IS  状態キー
000810                             LOCK        MODE         IS  AUTOMATIC.
000811**
000812     SELECT  保険者特別負担マスタ   ASSIGN      TO        HOKENTKL
000813                                    ORGANIZATION          IS  INDEXED
000814                                    ACCESS MODE           IS  DYNAMIC
000815                                    RECORD KEY            IS  保特－保険種別
000816                                                              保特－保険者番号
000817                                                              保特－開始和暦年月
000818                                    FILE STATUS           IS  状態キー
000819                                    LOCK        MODE      IS  AUTOMATIC.
000820**
000821******************************************************************
000830*                      DATA DIVISION                             *
000840******************************************************************
000850 DATA                    DIVISION.
000860 FILE                    SECTION.
000870*
000881 FD  保険者マスタ        BLOCK   CONTAINS   1   RECORDS.
000890     COPY HOKENS     OF  XFDLIB  JOINING   保   AS  PREFIX.
000900*
000910 FD  負担率マスタ        BLOCK   CONTAINS   1   RECORDS.
000920     COPY HUTANRI    OF  XFDLIB  JOINING   負率 AS  PREFIX.
000940*
000941 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
000950     COPY JUSINJ     OF  XFDLIB  JOINING   受   AS  PREFIX.
000960*
000970 FD  市町村マスタ        BLOCK   CONTAINS   1   RECORDS.
000980     COPY SITYOSN         OF  XFDLIB  JOINING   市   AS  PREFIX.
000981*
000982 FD  保険者特別負担マスタ  BLOCK   CONTAINS   1   RECORDS.
000983     COPY HOKENTK         OF  XFDLIB  JOINING   保特 AS PREFIX.
000984*
000990*----------------------------------------------------------------*
001000******************************************************************
001010*                WORKING-STORAGE SECTION                         *
001020******************************************************************
001030 WORKING-STORAGE         SECTION.
001040 01 キー入力                           PIC X    VALUE SPACE.
001050 01 状態キー                           PIC X(2) VALUE SPACE.
001060 01 終了フラグ                         PIC X(3) VALUE SPACE.
001070 01 終了フラグ２                       PIC X(3) VALUE SPACE.
001080*
001090 01 ファイル名Ｗ                       PIC N(4)  VALUE SPACE.
001100 01 エラー内容Ｗ                       PIC N(18) VALUE SPACE.
001110*
001120 01 負担率Ｗ                           PIC 9(3) VALUE ZERO.
001130 01 甲乙区分Ｗ                         PIC 9    VALUE ZERO.
001140 01 本人負担率Ｗ                       PIC 9(3) VALUE ZERO.
001150 01 家族負担率Ｗ                       PIC 9(3) VALUE ZERO.
001160 01 負担率区分Ｗ                       PIC 9    VALUE ZERO.
001170 01 負担区分Ｗ                         PIC 9(2) VALUE ZERO.
001180*
001190*/愛知県の乳幼児の対応0109
001200 01 助成負担率Ｗ                       PIC 9(3) VALUE ZERO.
001210*/年月毎持つ保険者データのワーク
001220 01 保負担率区分Ｗ                     PIC 9    VALUE ZERO.
001230 01 保本人負担率Ｗ                     PIC 9(3) VALUE ZERO.
001240 01 保家族負担率Ｗ                     PIC 9(3) VALUE ZERO.
001250 01 保本人負担率乙Ｗ                   PIC 9(3) VALUE ZERO.
001260 01 保家族負担率乙Ｗ                   PIC 9(3) VALUE ZERO.
001270*
001280 01 施術和暦年月日ＣＷ.
001290   03 施術和暦年月ＣＷ.
001300     05 施術和暦ＣＷ                   PIC 9.
001310     05 施術年月ＣＷ.
001320        07 施術年ＣＷ                  PIC 9(2).
001330        07 施術月ＣＷ                  PIC 9(2).
001340   03 施術日ＣＷ                       PIC 9(2).
001350*/保険種別も判定する/0609
001360  01 保険種別ＣＷ                      PIC 9(2) VALUE ZERO.
001370*
001380*/エラー表示の修正
001390 01 エラー表示Ｆ                       PIC 9(1)  VALUE ZERO.
001400*
001401**助成特別負担用
001402 01 助成特別負担内容Ｗ.
001403    03 助成特別負担率１Ｗ              PIC 9(3) VALUE ZERO.
001404    03 助成特別負担率２Ｗ              PIC 9(3) VALUE ZERO.
001405    03 助成特別負担一定金額Ｗ          PIC 9(5) VALUE ZERO.
001407    03 助成特別事務手数料Ｗ            PIC 9(4) VALUE ZERO.
001409    03 助成特別助成負担回数Ｗ          PIC 9(2) VALUE ZERO.
001411    03 助成特別助成負担回数２Ｗ        PIC 9(2) VALUE ZERO.
001412***
001414**
001415* 日付ＷＯＲＫ
001420 01 和暦終了年Ｗ                       PIC 9(4) VALUE ZERO.
001430 01 計算機和暦年Ｗ                     PIC 9(2) VALUE ZERO.
001440*
001450 01 計算機西暦.
001460    03 計算機西暦年                    PIC 9(4).
001470    03 計算機西暦月日                  PIC 9(4).
001480 01 計算機西暦Ｒ REDEFINES 計算機西暦.
001490    03 計算機世紀                      PIC 9(2).
001500    03 計算機日付                      PIC 9(6).
001510    03 計算機日付Ｒ REDEFINES 計算機日付.
001520       05 計算機年月                   PIC 9(4).
001530       05 計算機年月Ｒ REDEFINES 計算機年月.
001540         07 計算機年                   PIC 9(2).
001550         07 計算機月                   PIC 9(2).
001560       05 計算機日                     PIC 9(2).
001570*
001610*****************************************************************
001620*                          連結項目                             *
001630*****************************************************************
001640*
001650 01 連率－負担率取得キー IS EXTERNAL.
001660    03 連率－施術和暦年月.
001670       05 連率－施術和暦               PIC 9.
001680       05 連率－施術年月.
001690          07 連率－施術年              PIC 9(2).
001700          07 連率－施術月              PIC 9(2).
001710    03 連率－患者コード.
001720       05 連率－患者番号               PIC 9(6).
001730       05 連率－枝番                   PIC X.
001740    03 連率－実際負担率                PIC 9(3).
001750    03 連率－実際本体負担率            PIC 9(3).
001760    03 連率－健保負担率                PIC 9(3).
001770    03 連率－２７老負担率              PIC 9(3).
001780    03 連率－助成負担率                PIC 9(3).
001790    03 連率－特別用負担率              PIC 9(3).
001800*
001810* 確認メッセージ用 (２行)
001820 01 連メ７－キー IS EXTERNAL.
001830    03  連メ７－メッセージ１             PIC X(40).
001840    03  連メ７－メッセージ２             PIC X(40).
001850*
001860******************************************************************
001870*                      PROCEDURE  DIVISION                       *
001880******************************************************************
001890 PROCEDURE               DIVISION.
001900************
001910*           *
001920* 初期処理   *
001930*           *
001940************
001950     PERFORM 初期化.
001960*
001970************
001980*           *
001990* 主処理     *
002000*           *
002010************
002020*
002030     PERFORM 負担率取得.
002040*
002050************
002060*           *
002070* 終了処理   *
002080*           *
002090************
002100     PERFORM 終了処理.
002110     MOVE 1 TO PROGRAM-STATUS.
002120     EXIT PROGRAM.
002130*
002140*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002150*================================================================*
002160 初期化 SECTION.
002170*
002180     OPEN INPUT 保険者マスタ.
002190         MOVE NC"保険" TO ファイル名Ｗ.
002200         PERFORM オープンチェック.
002210     OPEN INPUT 負担率マスタ.
002220         MOVE NC"負率" TO ファイル名Ｗ.
002230         PERFORM オープンチェック.
002240     OPEN INPUT 受診者情報Ｆ.
002250         MOVE NC"受診" TO ファイル名Ｗ.
002260         PERFORM オープンチェック.
002270     OPEN INPUT 市町村マスタ.
002280         MOVE NC"市町" TO ファイル名Ｗ.
002290         PERFORM オープンチェック.
002291     OPEN INPUT 保険者特別負担マスタ.
002292         MOVE NC"保特別" TO ファイル名Ｗ.
002293         PERFORM オープンチェック.
002300*
002310*================================================================*
002320 オープンチェック SECTION.
002330*
002340     IF 状態キー  NOT =  "00"
002350         DISPLAY ファイル名Ｗ NC"Ｆオープンエラー" UPON CONS
002360         DISPLAY NC"状態キー：" 状態キー         UPON CONS
002370         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
002380                                                 UPON CONS
002381*-----------------------------------------*
002382         CALL "actcshm"  WITH C LINKAGE
002383*-----------------------------------------*
002390         ACCEPT  キー入力 FROM CONS
002400         PERFORM ファイル閉鎖
002410         MOVE ZERO TO PROGRAM-STATUS
002420         EXIT PROGRAM.
002430*================================================================*
002440 ファイル閉鎖 SECTION.
002450*
002460     CLOSE 保険者マスタ 負担率マスタ 受診者情報Ｆ 市町村マスタ 保険者特別負担マスタ.
002470*================================================================*
002480 終了処理 SECTION.
002490*
002500     PERFORM ファイル閉鎖.
002510*================================================================*
002520 エラー表示 SECTION.
002530*
002540     DISPLAY ファイル名Ｗ NC"ファイル書込エラー"   UPON CONS.
002550     DISPLAY NC"状態キー：" 状態キー               UPON CONS.
002560     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
002570     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
002580                                                   UPON CONS.
002581*-----------------------------------------*
002582     CALL "actcshm"  WITH C LINKAGE.
002583*-----------------------------------------*
002590     ACCEPT  キー入力 FROM CONS.
002600     PERFORM ファイル閉鎖.
002610     MOVE ZERO TO PROGRAM-STATUS
002620     EXIT PROGRAM.
002630*================================================================*
002640 負担率取得 SECTION.
002650*
002660     MOVE ZERO TO 連率－実際負担率
002670                  連率－健保負担率
002680                  連率－２７老負担率
002690                  連率－助成負担率
002700                  連率－特別用負担率.
002710     INITIALIZE   連率－実際負担率
002720                  連率－健保負担率
002730                  連率－２７老負担率
002740                  連率－助成負担率
002750                  連率－特別用負担率.
002760*
002770     MOVE 連率－施術和暦 TO 受－施術和暦.
002780     MOVE 連率－施術年   TO 受－施術年.
002790     MOVE 連率－施術月   TO 受－施術月.
002800     MOVE 連率－患者番号 TO 受－患者番号.
002810     MOVE 連率－枝番     TO 受－枝番.
002820     READ 受診者情報Ｆ
002830     INVALID KEY
002840         CONTINUE
002850     NOT INVALID KEY
002851*----------------------------------------------------------*
002852*
002853* 保険分類　6:保険証忘れ（保険種別91）は、91で負担率マスタ読んで終了する
002854* (保険者番号など入力されているとエラーになっていしまうので）
002855      IF 受－保険分類 = 6
002856         PERFORM 負担率マスタより取得
002857      ELSE
002858*
002870*/連率－健保負担率のセット
002880*/後期高齢時、健保負担率の算定をしない/0801
002890         IF (受－保険種別 = 05) AND (受－施術和暦年月 >= 42004)
002900             CONTINUE
002910         ELSE
002920             PERFORM 健保負担率取得
002930         END-IF
002940*/連率－２７老負担率のセット
002950         IF 受－公費種別 = 05
002960             PERFORM ２７老人負担率取得
002970         END-IF
002980*/連率－特別用負担率のセット高齢者、３歳未満
002990*/後期高齢時、特別用負担率(前期高齢、３歳未満等)の算定をしない/0801
003000         IF (受－保険種別 = 05) AND (受－施術和暦年月 >= 42004)
003010             CONTINUE
003020         ELSE
003030             IF 受－特別区分 NOT = ZERO AND SPACE
003040                 PERFORM 特別用負担率取得
003050             END-IF
003060         END-IF
003061***
003070*/連率－実際本体負担率のセット、助成負担区分１２の対応、先に実際本体負担率を求める。/0412
003080         EVALUATE TRUE
003090*/      */健保のみ
003100         WHEN (受－保険種別 NOT = ZERO) AND
003110              (受－公費種別     = ZERO) AND
003120              (受－助成種別     = ZERO) AND
003130              (受－特別区分     = ZERO)
003140             MOVE 連率－健保負担率 TO 連率－実際本体負担率
003150*/      */資格証明
003160         WHEN 受－資格証明区分 = 1
003170             MOVE 連率－健保負担率 TO 連率－実際本体負担率
003180*/      */助成
003190         WHEN 受－助成種別 NOT = ZERO
003200             EVALUATE TRUE
003210             WHEN 受－資格証明区分 = 1
003220                 MOVE 連率－健保負担率   TO 連率－実際本体負担率
003230             WHEN 受－公費種別 NOT = ZERO
003240                 MOVE 連率－２７老負担率 TO 連率－実際本体負担率
003250             WHEN 受－特別区分 NOT = ZERO
003260                 MOVE 連率－特別用負担率 TO 連率－実際本体負担率
003270             WHEN OTHER
003280                 MOVE 連率－健保負担率   TO 連率－実際本体負担率
003290             END-EVALUATE
003300*/      */老人
003310         WHEN 受－公費種別 NOT = ZERO
003320             MOVE 連率－２７老負担率 TO 連率－実際本体負担率
003330*/      */特別区分が入っている場合
003340         WHEN 受－特別区分 NOT = ZERO
003350             MOVE 連率－特別用負担率 TO 連率－実際本体負担率
003360         END-EVALUATE
003361**
003370*/連率－助成負担率のセット
003380         IF 受－助成種別 NOT = ZERO
003390             PERFORM 助成負担区分取得
003400             EVALUATE TRUE
003410             WHEN 受－助成種別 = ZERO
003420                 CONTINUE
003430             WHEN 受－助成種別 = 51
003440                 PERFORM ４１老人負担率取得
003450             WHEN (受－助成種別 >= 52) AND
003460                  (受－助成種別 <= 60) AND
003470                  (負担区分Ｗ    = 09 OR 10)
003480                 IF 受－助成負担金免除 NOT = 1
003490                     PERFORM ４１老人負担率取得
003500                 END-IF
003510             WHEN OTHER
003520                 IF 受－助成負担金免除 NOT = 1
003530                     PERFORM 助成負担率取得
003540                 END-IF
003550             END-EVALUATE
003560         END-IF
003570*/連率－実際負担率、連率－実際本体負担率のセット
003580         EVALUATE TRUE
003590*/      */健保のみ
003600         WHEN (受－保険種別 NOT = ZERO) AND
003610              (受－公費種別     = ZERO) AND
003620              (受－助成種別     = ZERO) AND
003630              (受－特別区分     = ZERO)
003640             MOVE 連率－健保負担率 TO 連率－実際負担率
003650             MOVE 連率－健保負担率 TO 連率－実際本体負担率
003660*/      */資格証明
003670         WHEN 受－資格証明区分 = 1
003680*/後高＋資格証明/080410
003690             IF (受－保険種別 = 05) AND (受－施術和暦年月 >= 42004)
003700                 MOVE 連率－２７老負担率 TO 連率－実際負担率
003710                 MOVE 連率－２７老負担率 TO 連率－実際本体負担率
003720             ELSE
003730                 MOVE 連率－健保負担率 TO 連率－実際負担率
003740                 MOVE 連率－健保負担率 TO 連率－実際本体負担率
003750             END-IF
003760*/      */助成
003770         WHEN 受－助成種別 NOT = ZERO
003780             MOVE 連率－助成負担率 TO 連率－実際負担率
003790             EVALUATE TRUE
003800             WHEN 受－資格証明区分 = 1
003810                 MOVE 連率－健保負担率 TO 連率－実際本体負担率
003820             WHEN 受－公費種別 NOT = ZERO
003830                 MOVE 連率－２７老負担率 TO 連率－実際本体負担率
003840             WHEN 受－特別区分 NOT = ZERO
003850                 MOVE 連率－特別用負担率 TO 連率－実際本体負担率
003860             WHEN OTHER
003870                 MOVE 連率－健保負担率 TO 連率－実際本体負担率
003880             END-EVALUATE
003890*/      */老人
003900         WHEN 受－公費種別 NOT = ZERO
003910             MOVE 連率－２７老負担率 TO 連率－実際負担率
003920             MOVE 連率－２７老負担率 TO 連率－実際本体負担率
003930*/      */特別区分が入っている場合
003940         WHEN 受－特別区分 NOT = ZERO
003950             MOVE 連率－特別用負担率 TO 連率－実際負担率
003960             MOVE 連率－特別用負担率 TO 連率－実際本体負担率
003970         END-EVALUATE
003971      END-IF
003980     END-READ.
003981*
003990*================================================================*
004000 特別用負担率取得 SECTION.
004010*
004020     MOVE ZERO TO 負担率区分Ｗ.
004030     MOVE ZERO TO 本人負担率Ｗ.
004040     MOVE ZERO TO 家族負担率Ｗ.
004041*
004042     MOVE ZERO TO 保負担率区分Ｗ.
004043     MOVE ZERO TO 保本人負担率Ｗ.
004044     MOVE ZERO TO 保家族負担率Ｗ.
004045     MOVE ZERO TO 保本人負担率乙Ｗ.
004046     MOVE ZERO TO 保家族負担率乙Ｗ.
004047*
004060     MOVE 連率－施術和暦 TO 受－施術和暦.
004070     MOVE 連率－施術年   TO 受－施術年.
004080     MOVE 連率－施術月   TO 受－施術月.
004090     MOVE 連率－患者番号 TO 受－患者番号.
004100     MOVE 連率－枝番     TO 受－枝番.
004110     READ 受診者情報Ｆ
004120     INVALID KEY
004130*/当月を登録する前は計算しないで抜ける。
004140**/エラー表示の修正
004150*         PERFORM コンソールエラー確認
004160*         DISPLAY "施術月の受診者情報がありません"
004170*                 " 受診者№=" 連率－患者コード
004180*                 " 施術年月=" 連率－施術年 連率－施術月 UPON CONS
004190         PERFORM ファイル閉鎖
004200         MOVE ZERO TO PROGRAM-STATUS
004210         EXIT PROGRAM
004220     NOT INVALID KEY
004230         MOVE 受－甲乙区分 TO 甲乙区分Ｗ
004240     END-READ.
004250*
004260     MOVE 受－保険種別   TO 保－保険種別.  
004270     MOVE 受－保険者番号 TO 保－保険者番号.
004280     READ 保険者マスタ
004290     INVALID KEY
004300*/エラー表示の修正
004310         PERFORM コンソールエラー確認
004320         DISPLAY "保険者番号が登録されていません"
004330                 " 受診者№="   受－患者コード
004340                 " 施術年月="   受－施術年 受－施術月
004350                 " 保険者番号=" 受－保険者番号  UPON CONS
004351*-----------------------------------------*
004352         CALL "actcshm"  WITH C LINKAGE
004353*-----------------------------------------*
004360         PERFORM ファイル閉鎖
004370         MOVE ZERO TO PROGRAM-STATUS
004380         EXIT PROGRAM
004390     NOT INVALID KEY
004723*******
004724* 特別負担
004725         MOVE 受－保険種別   TO 保特－保険種別
004726         MOVE 受－保険者番号 TO 保特－保険者番号
004727         MOVE 連率－施術和暦 TO 保特－開始和暦
004728         MOVE 連率－施術年   TO 保特－開始年
004729         MOVE 連率－施術月   TO 保特－開始月
004730         START 保険者特別負担マスタ KEY IS <= 保特－保険種別 
004731                                              保特－保険者番号
004732                                              保特－開始和暦年月
004733                                              REVERSED
004734         END-START
004735         IF 状態キー = "00" 
004736             READ 保険者特別負担マスタ NEXT
004737             NOT AT END
004738                 IF ( 保特－保険種別   = 受－保険種別 ) AND
004739                    ( 保特－保険者番号 = 受－保険者番号 )
004740                    MOVE 保特－負担率区分   TO 保負担率区分Ｗ
004741                    MOVE 保特－本人負担率   TO 保本人負担率Ｗ
004742                    MOVE 保特－家族負担率   TO 保家族負担率Ｗ
004743                    MOVE 保特－本人負担率乙 TO 保本人負担率乙Ｗ
004744                    MOVE 保特－家族負担率乙 TO 保家族負担率乙Ｗ
004745                 END-IF
004746             END-READ
004747         END-IF
004748********
004754* 船員公傷扱い
004755         IF ( 受－保険種別     = 07 ) AND
004756            ( 受－本人家族区分 = 1  ) AND
004760            ( 甲乙区分Ｗ         = 1  )
004770              MOVE ZERO   TO 本人負担率Ｗ
004780              MOVE 1      TO 負担率区分Ｗ
004790*
004800         ELSE
004810*/資格証明の対応/を含む↓負担率区分=特殊負担率が設定されていても資格証明の時は全額負担
004820*/市町村国保か退職で資格証明区分が１の時、以外は資格証明区分の対象外にする
004830             IF (((受－保険種別 = 01)  AND (受－保険者番号(3:1) NOT = "3")) OR
004840                 ( 受－保険種別 = 08)) AND
004850                 ( 受－資格証明区分 = 1)
004860                 CONTINUE
004870             ELSE
004880                 MOVE ZERO TO 受－資格証明区分
004890             END-IF
004900             IF (( 保負担率区分Ｗ NOT = ZERO )      OR
004910                 ( 甲乙区分Ｗ     NOT = ZERO ))    AND
004920                 ( 受－資格証明区分    NOT = 1)
004930*/1410高齢者、３才未満の対応0210
004940             AND (受－特別区分 = ZERO)
004950                 IF 甲乙区分Ｗ NOT = 2
004960                     MOVE 保本人負担率Ｗ   TO 本人負担率Ｗ
004970                     MOVE 保家族負担率Ｗ   TO 家族負担率Ｗ
004980                 ELSE
004990                     MOVE 保本人負担率乙Ｗ TO 本人負担率Ｗ
005000                     MOVE 保家族負担率乙Ｗ TO 家族負担率Ｗ
005010                 END-IF
005020                 MOVE 1 TO 負担率区分Ｗ
005030             ELSE
005040*/資格証明の対応/市町村国保退職＆資格証明区分１の時は全額負担(91が資格証明)
005050                 IF (((受－保険種別 = 01)  AND (受－保険者番号(3:1) NOT = "3")) OR
005060                     ( 受－保険種別 = 08)) AND
005070                     ( 受－資格証明区分 = 1)
005080                     MOVE 91           TO 負率－保険種別
005090                 ELSE
005100                     MOVE 受－保険種別 TO 負率－保険種別
005110                 END-IF
005120*/1410高齢者、３才未満の対応0210↓↓↓↓↓↓
005130                 EVALUATE 受－特別区分
005140                 WHEN ZERO
005150                     CONTINUE
005160                 WHEN 1
005170                     MOVE 21 TO 負率－保険種別
005180                 WHEN 2
005190                     MOVE 22 TO 負率－保険種別
005200*/３歳未満２割を６に変更して高齢者３割を３で追加/0608
005210*                 WHEN 3
005220                 WHEN 6
005230                     MOVE 23 TO 負率－保険種別
005240*/高齢者３割の対応/0608
005250                 WHEN 3
005260                     MOVE 24 TO 負率－保険種別
005270                 WHEN OTHER
005280                     CONTINUE
005290                 END-EVALUATE
005300*/1410高齢者、３才未満の対応0210↑↑↑↑↑↑
005310*                 MOVE 連計－保険種別 TO 負率－保険種別
005320*/保険種別も判定する/0609
005330                 MOVE 負率－保険種別 TO 保険種別ＣＷ
005340                 MOVE 連率－施術和暦 TO 負率－開始和暦 施術和暦ＣＷ
005350                 MOVE 連率－施術年   TO 負率－開始年   施術年ＣＷ
005360                 MOVE 連率－施術月   TO 負率－開始月   施術月ＣＷ
005370                 START 負担率マスタ KEY IS <= 負率－保険種別 
005380                                              負率－開始和暦年月
005390                                              REVERSED
005400                 END-START
005410                 IF 状態キー = "00" 
005420                     READ 負担率マスタ NEXT
005430                     AT END
005440*/エラー表示の修正
005450                        PERFORM コンソールエラー確認
005460                        DISPLAY "施術日に合った負担率がみつかりません"
005470                           " 受診者№="   連率－患者コード
005480                           " 施術年月="   連率－施術年 連率－施術月 UPON CONS
005490*-----------------------------------------*
005491                           CALL "actcshm"  WITH C LINKAGE
005492*-----------------------------------------*
005493                           PERFORM ファイル閉鎖
005500                           MOVE ZERO TO PROGRAM-STATUS
005510                           EXIT PROGRAM
005520                     NOT AT END
005530                         IF ( 施術和暦年月ＣＷ >= 負率－開始和暦年月 ) AND
005540                            ( 施術和暦年月ＣＷ <= 負率－終了和暦年月 )
005550*/保険種別も判定する/0609
005560                         AND (負率－保険種別 = 保険種別ＣＷ)
005570                             MOVE 負率－本人負担率 TO 本人負担率Ｗ
005580                             MOVE 負率－家族負担率 TO 家族負担率Ｗ
005590                         ELSE
005600*/不具合の修正(メッセージ追加)/0609
005610                             PERFORM コンソールエラー確認
005620                             DISPLAY "施術日に合った負担率がみつかりません"
005630                                     " 受診者№="   連率－患者コード
005640                                     " 施術年月="   連率－施術年 連率－施術月 UPON CONS
005641*-----------------------------------------*
005642                             CALL "actcshm"  WITH C LINKAGE
005643*-----------------------------------------*
005650                             PERFORM ファイル閉鎖
005660                             MOVE ZERO TO PROGRAM-STATUS
005670                             EXIT PROGRAM
005680                         END-IF
005690                     END-READ
005700                 END-IF
005710             END-IF
005720         END-IF
005730     END-READ.
005740*
005750     EVALUATE 受－本人家族区分
005760     WHEN 1
005770         MOVE 本人負担率Ｗ TO 負担率Ｗ
005780     WHEN 2
005790         MOVE 家族負担率Ｗ TO 負担率Ｗ
005800     WHEN OTHER
005810*/エラー表示の修正
005820         PERFORM コンソールエラー確認
005830         DISPLAY "本人家族区分が設定されていません"
005840                 " 受診者№="   受－患者コード
005850                 " 施術年月="   受－施術年 受－施術月 UPON CONS
005851*-----------------------------------------*
005852         CALL "actcshm"  WITH C LINKAGE
005853*-----------------------------------------*
005860         PERFORM ファイル閉鎖
005870         MOVE ZERO TO PROGRAM-STATUS
005880         EXIT PROGRAM
005890     END-EVALUATE.
005900*
005910*/国保組合で負担なしの対応。特別区分/0501
005920     IF 甲乙区分Ｗ = 9
005930         MOVE ZERO TO 負担率Ｗ
005940     END-IF.
005950*/9:０割の対応/0609
005960     IF 受－特別区分 = 9
005970         MOVE ZERO TO 負担率Ｗ
005980     END-IF.
005990*
006000     MOVE 負担率Ｗ TO 連率－特別用負担率.
006001*
006010*================================================================*
006020 コンソールエラー確認 SECTION.
006030*
006040     IF エラー表示Ｆ = ZERO
006050         MOVE  "エラーが発生したのでコンソール画面の内容" TO 連メ７－メッセージ１
006060         MOVE  "を確認し、修正してください。"             TO 連メ７－メッセージ２
006070         CALL   "MSG007"
006080         CANCEL "MSG007"
006090     END-IF.
006100     MOVE 1 TO エラー表示Ｆ.
006110*================================================================*
006120 健保負担率取得 SECTION.
006130*
006140     MOVE ZERO TO 負担率区分Ｗ.
006150     MOVE ZERO TO 本人負担率Ｗ.
006160     MOVE ZERO TO 家族負担率Ｗ.
006161*
006162     MOVE ZERO TO 保負担率区分Ｗ.
006163     MOVE ZERO TO 保本人負担率Ｗ.
006164     MOVE ZERO TO 保家族負担率Ｗ.
006165     MOVE ZERO TO 保本人負担率乙Ｗ.
006166     MOVE ZERO TO 保家族負担率乙Ｗ.
006170*
006180     MOVE 連率－施術和暦 TO 受－施術和暦.
006190     MOVE 連率－施術年   TO 受－施術年.
006200     MOVE 連率－施術月   TO 受－施術月.
006210     MOVE 連率－患者番号 TO 受－患者番号.
006220     MOVE 連率－枝番     TO 受－枝番.
006230     READ 受診者情報Ｆ
006240     INVALID KEY
006250*/エラー表示の修正
006260         PERFORM コンソールエラー確認
006270         DISPLAY "施術月の受診者情報がありません"
006280                 " 受診者№=" 連率－患者コード
006290                 " 施術年月=" 連率－施術年 連率－施術月 UPON CONS
006291*-----------------------------------------*
006292         CALL "actcshm"  WITH C LINKAGE
006293*-----------------------------------------*
006300         PERFORM ファイル閉鎖
006310         MOVE ZERO TO PROGRAM-STATUS
006320         EXIT PROGRAM
006330     NOT INVALID KEY
006340         MOVE 受－甲乙区分 TO 甲乙区分Ｗ
006350     END-READ.
006360*
006370     MOVE 受－保険種別   TO 保－保険種別.  
006380     MOVE 受－保険者番号 TO 保－保険者番号.
006390     READ 保険者マスタ
006400     INVALID KEY
006410*/エラー表示の修正
      */保険証画面からコンソールエラー表示を削除する↓↓↓/120928
006420*         PERFORM コンソールエラー確認
006430*         DISPLAY "保険者番号が登録されていません"
006440*                 " 受診者№="   受－患者コード
006450*                 " 施術年月="   受－施術年 受－施術月
006460*                 " 保険者番号=" 受－保険者番号  UPON CONS
006461**-----------------------------------------*
006462*         CALL "actcshm"  WITH C LINKAGE
006463**-----------------------------------------*
               MOVE  "保険者番号が登録されていません" TO 連メ７－メッセージ１
               MOVE  SPACE                            TO 連メ７－メッセージ２
               CALL   "MSG007"
               CANCEL "MSG007"
      */保険証画面からコンソールエラー表示を削除する↑↑↑/120928
006470         PERFORM ファイル閉鎖
006480         MOVE ZERO TO PROGRAM-STATUS
006490         EXIT PROGRAM
006500     NOT INVALID KEY
006831*******
006832* 特別負担
006843         MOVE 受－保険種別   TO 保特－保険種別
006844         MOVE 受－保険者番号 TO 保特－保険者番号
006845         MOVE 連率－施術和暦 TO 保特－開始和暦
006846         MOVE 連率－施術年   TO 保特－開始年
006847         MOVE 連率－施術月   TO 保特－開始月
006848         START 保険者特別負担マスタ KEY IS <= 保特－保険種別 
006849                                              保特－保険者番号
006850                                              保特－開始和暦年月
006851                                              REVERSED
006852         END-START
006853         IF 状態キー = "00" 
006854             READ 保険者特別負担マスタ NEXT
006863             NOT AT END
006865                 IF ( 保特－保険種別   = 受－保険種別 ) AND
006866                    ( 保特－保険者番号 = 受－保険者番号 )
006887                    MOVE 保特－負担率区分   TO 保負担率区分Ｗ
006888                    MOVE 保特－本人負担率   TO 保本人負担率Ｗ
006889                    MOVE 保特－家族負担率   TO 保家族負担率Ｗ
006890                    MOVE 保特－本人負担率乙 TO 保本人負担率乙Ｗ
006891                    MOVE 保特－家族負担率乙 TO 保家族負担率乙Ｗ
006893                 END-IF
006894             END-READ
006895         END-IF
006915********
006916* 船員公傷扱い
006917         IF ( 受－保険種別     = 07 ) AND
006918            ( 受－本人家族区分 = 1  ) AND
006919            ( 甲乙区分Ｗ         = 1  )
006920              MOVE ZERO   TO 本人負担率Ｗ
006921              MOVE 1      TO 負担率区分Ｗ
006922*
006923         ELSE
006924*/資格証明の対応/を含む↓負担率区分=特殊負担率が設定されていても資格証明の時は全額負担
006930*/市町村国保か退職で資格証明区分が１の時、以外は資格証明区分の対象外にする
006940             IF (((受－保険種別 = 01)  AND (受－保険者番号(3:1) NOT = "3")) OR
006950                 ( 受－保険種別 = 08)) AND
006960                 ( 受－資格証明区分 = 1)
006970                 CONTINUE
006980             ELSE
006990                 MOVE ZERO TO 受－資格証明区分
007000             END-IF
007010             IF (( 保負担率区分Ｗ NOT = ZERO )      OR
007020                 ( 甲乙区分Ｗ     NOT = ZERO ))    AND
007030                 ( 受－資格証明区分    NOT = 1)
007040**/1410高齢者、３才未満の対応0210
007050*             AND (受－特別区分 = ZERO)
007060                 IF 甲乙区分Ｗ NOT = 2
007070                     MOVE 保本人負担率Ｗ   TO 本人負担率Ｗ
007080                     MOVE 保家族負担率Ｗ   TO 家族負担率Ｗ
007090                 ELSE
007100                     MOVE 保本人負担率乙Ｗ TO 本人負担率Ｗ
007110                     MOVE 保家族負担率乙Ｗ TO 家族負担率Ｗ
007120                 END-IF
007130                 MOVE 1 TO 負担率区分Ｗ
007140             ELSE
007150*/資格証明の対応/市町村国保退職＆資格証明区分１の時は全額負担(91が資格証明)
007160                 IF (((受－保険種別 = 01)  AND (受－保険者番号(3:1) NOT = "3")) OR
007170                     ( 受－保険種別 = 08)) AND
007180                     ( 受－資格証明区分 = 1)
007190                     MOVE 91           TO 負率－保険種別
007200                 ELSE
007210                     MOVE 受－保険種別 TO 負率－保険種別
007220                 END-IF
007230**/1410高齢者、３才未満の対応0210↓↓↓↓↓↓
007240*                 EVALUATE 受－特別区分
007250*                 WHEN ZERO
007260*                     CONTINUE
007270*                 WHEN 1
007280*                     MOVE 21 TO 負率－保険種別
007290*                 WHEN 2
007300*                     MOVE 22 TO 負率－保険種別
007310*                 WHEN 3
007320*                     MOVE 23 TO 負率－保険種別
007330*                 WHEN OTHER
007340*                     CONTINUE
007350*                 END-EVALUATE
007360*/1410高齢者、３才未満の対応0210↑↑↑↑↑↑
007370*                 MOVE 連計－保険種別 TO 負率－保険種別
007371                 MOVE 負率－保険種別 TO 保険種別ＣＷ
007380                 MOVE 連率－施術和暦 TO 負率－開始和暦 施術和暦ＣＷ
007390                 MOVE 連率－施術年   TO 負率－開始年   施術年ＣＷ
007400                 MOVE 連率－施術月   TO 負率－開始月   施術月ＣＷ
007410                 START 負担率マスタ KEY IS <= 負率－保険種別 
007420                                              負率－開始和暦年月
007430                                              REVERSED
007440                 END-START
007450                 IF 状態キー = "00" 
007460                     READ 負担率マスタ NEXT
007470                     AT END
007480*/エラー表示の修正
007490                        PERFORM コンソールエラー確認
007500                        DISPLAY "施術日に合った負担率がみつかりません"
007510                           " 受診者№="   連率－患者コード
007520                           " 施術年月="   連率－施術年 連率－施術月 UPON CONS
007521*-----------------------------------------*
007522                           CALL "actcshm"  WITH C LINKAGE
007523*-----------------------------------------*
007530                           PERFORM ファイル閉鎖
007540                           MOVE ZERO TO PROGRAM-STATUS
007550                           EXIT PROGRAM
007560                     NOT AT END
007561                         IF ( 施術和暦年月ＣＷ >= 負率－開始和暦年月 ) AND
007562                            ( 施術和暦年月ＣＷ <= 負率－終了和暦年月 ) AND
007563                            ( 負率－保険種別 = 保険種別ＣＷ )
007590                             MOVE 負率－本人負担率 TO 本人負担率Ｗ
007600                             MOVE 負率－家族負担率 TO 家族負担率Ｗ
007610                         END-IF
007620                     END-READ
007630                 END-IF
007640             END-IF
007650         END-IF
007660     END-READ.
007670*
007680     EVALUATE 受－本人家族区分
007690     WHEN 1
007700         MOVE 本人負担率Ｗ TO 連率－健保負担率
007710     WHEN 2
007720         MOVE 家族負担率Ｗ TO 連率－健保負担率
007730     WHEN OTHER
007740*/エラー表示の修正
      */保険証画面からコンソールエラー表示を削除する↓↓↓/120928
007750*         PERFORM コンソールエラー確認
007760*         DISPLAY "本人家族区分が設定されていません"
007770*                 " 受診者№="   受－患者コード
007780*                 " 施術年月="   受－施術年 受－施術月 UPON CONS
007781**-----------------------------------------*
007782*         CALL "actcshm"  WITH C LINKAGE
007783**-----------------------------------------*
               MOVE  "本人家族区分が設定されていません" TO 連メ７－メッセージ１
               MOVE  SPACE                              TO 連メ７－メッセージ２
      */コンソール画面に年月患者コードを表示する/130601
               STRING "受診者№="       DELIMITED BY SIZE
                       受－患者コード   DELIMITED BY SIZE
                     " 施術年月="       DELIMITED BY SIZE
                       受－施術年       DELIMITED BY SIZE
                       受－施術月       DELIMITED BY SIZE
                  INTO 連メ７－メッセージ２
               END-STRING
               CALL   "MSG007"
               CANCEL "MSG007"
      */保険証画面からコンソールエラー表示を削除する↑↑↑/120928
007790         PERFORM ファイル閉鎖
007800         MOVE ZERO TO PROGRAM-STATUS
007810         EXIT PROGRAM
007820     END-EVALUATE.
007830*
007840*/仕様変更0401↓↓↓
007850*/埼玉県医師国保組合(113019)埼玉県歯科医師国保組合(113027)関東信越税理士国保組合(113043)
007860*/埼玉県建設国保組合(113050)埼玉県土建国保組合(113068)
007870*/で埼玉県内の41老の場合、負担率を国保の標準の物にする。(３割)
007880     IF 受－保険者番号 = "113019" OR "113027" OR "113043" OR 
007890                         "113050" OR "113068"
007900         IF (受－助成種別                =  51 ) AND
007910            (受－費用負担者番号助成(3:2) = "11")
007920             MOVE 受－保険種別   TO 負率－保険種別
007930             MOVE 連率－施術和暦 TO 負率－開始和暦 施術和暦ＣＷ
007940             MOVE 連率－施術年   TO 負率－開始年   施術年ＣＷ
007950             MOVE 連率－施術月   TO 負率－開始月   施術月ＣＷ
007960             START 負担率マスタ KEY IS <= 負率－保険種別 
007970                                          負率－開始和暦年月
007980                                          REVERSED
007990             END-START
008000             IF 状態キー = "00" 
008010                 READ 負担率マスタ NEXT
008020                 AT END
008030*/エラー表示の修正
008040                    PERFORM コンソールエラー確認
008050                    DISPLAY "施術日に合った負担率がみつかりません"
008060                       " 受診者№="   連率－患者コード
008070                       " 施術年月="   連率－施術年 連率－施術月 UPON CONS
008071*-----------------------------------------*
008072                       CALL "actcshm"  WITH C LINKAGE
008073*-----------------------------------------*
008080                       PERFORM ファイル閉鎖
008090                       MOVE ZERO TO PROGRAM-STATUS
008100                       EXIT PROGRAM
008110                 NOT AT END
008111                     IF ( 施術和暦年月ＣＷ >= 負率－開始和暦年月 ) AND
008112                        ( 施術和暦年月ＣＷ <= 負率－終了和暦年月 ) AND
008113                        ( 負率－保険種別 = 受－保険種別 )
008140                         MOVE 負率－本人負担率 TO 本人負担率Ｗ
008150                         MOVE 負率－家族負担率 TO 家族負担率Ｗ
008160*
008170                         EVALUATE 受－本人家族区分
008180                         WHEN 1
008190                             MOVE 本人負担率Ｗ TO 連率－健保負担率
008200                         WHEN 2
008210                             MOVE 家族負担率Ｗ TO 連率－健保負担率
008220                         WHEN OTHER
008230*                    /エラー表示の修正
008240                             PERFORM コンソールエラー確認
008250                             DISPLAY "本人家族区分が設定されていません"
008260                                     " 受診者№="   受－患者コード
008270                                     " 施術年月="   受－施術年 受－施術月 UPON CONS
008271*-----------------------------------------*
008272                             CALL "actcshm"  WITH C LINKAGE
008273*-----------------------------------------*
008280                             PERFORM ファイル閉鎖
008290                             MOVE ZERO TO PROGRAM-STATUS
008300                             EXIT PROGRAM
008310                         END-EVALUATE
008320*
008330                     END-IF
008340                 END-READ
008350             END-IF
008360         END-IF
008370     END-IF.
008380*/仕様変更0401↑↑↑
008390*/自費学校共済の対応/0409
008400     IF (受－保険種別   = 90) AND
008410        (受－保険外区分 = 1 )
008420         MOVE ZERO TO 連率－健保負担率
008430     END-IF.
008440*/国保組合で負担なしの対応。健保/0501
008450     IF 甲乙区分Ｗ = 9
008460         MOVE ZERO TO 連率－健保負担率
008470     END-IF.
008480*================================================================*
008490 ２７老人負担率取得 SECTION.
008500*
008510     MOVE 受－公費種別 TO 負率－保険種別.
008520*/後高＋資格証明/080410
008530     IF (受－資格証明区分 = 1) AND (受－施術和暦年月 >= 42004)
008540         MOVE 91       TO 負率－保険種別
008550     END-IF.
008551
008552
008553
008554     MOVE 負率－保険種別 TO 保険種別ＣＷ.
008570     MOVE 受－施術和暦   TO 負率－開始和暦 施術和暦ＣＷ.
008580     MOVE 受－施術年     TO 負率－開始年   施術年ＣＷ.
008590     MOVE 受－施術月     TO 負率－開始月   施術月ＣＷ.
008600     START 負担率マスタ KEY IS <= 負率－保険種別
008610                                  負率－開始和暦年月
008620                                  REVERSED
008630     END-START.
008640     READ 負担率マスタ NEXT
008650     AT END
008660*/エラー表示の修正
008670         PERFORM コンソールエラー確認
008680         DISPLAY "施術日に合った負担率がみつかりません"
008690                 " 受診者№="   受－患者コード
008700                 " 施術年月="   受－施術年 受－施術月 UPON CONS
008701*-----------------------------------------*
008702         CALL "actcshm"  WITH C LINKAGE
008703*-----------------------------------------*
008710         PERFORM ファイル閉鎖
008720         MOVE ZERO TO PROGRAM-STATUS
008730         EXIT PROGRAM
008740     NOT AT END
008741         IF ( 施術和暦年月ＣＷ >= 負率－開始和暦年月 ) AND
008742            ( 施術和暦年月ＣＷ <= 負率－終了和暦年月 ) AND
008743            ( 負率－保険種別 = 保険種別ＣＷ )
008770             MOVE 負率－本人負担率 TO 本人負担率Ｗ
008780             MOVE 負率－家族負担率 TO 家族負担率Ｗ
008790         END-IF
008800     END-READ.
008810*
008820     EVALUATE 受－本人家族区分
008830     WHEN 1
008840         MOVE 本人負担率Ｗ TO 負担率Ｗ
008850     WHEN 2
008860         MOVE 家族負担率Ｗ TO 負担率Ｗ
008870     WHEN OTHER
008880*/エラー表示の修正
008890*         PERFORM コンソールエラー確認
008900*         DISPLAY "本人家族区分が設定されていません"
008910*                 " 受診者№="   受－患者コード
008920*                 " 施術年月="   受－施術年 受－施術月 UPON CONS
008921**-----------------------------------------*
008922*         CALL "actcshm"  WITH C LINKAGE
008923**-----------------------------------------*
               MOVE  "本人家族区分が設定されていません" TO 連メ７－メッセージ１
               MOVE  SPACE                              TO 連メ７－メッセージ２
      */コンソール画面に年月患者コードを表示する/130601
               STRING "受診者№="       DELIMITED BY SIZE
                       受－患者コード   DELIMITED BY SIZE
                     " 施術年月="       DELIMITED BY SIZE
                       受－施術年       DELIMITED BY SIZE
                       受－施術月       DELIMITED BY SIZE
                  INTO 連メ７－メッセージ２
               END-STRING
               CALL   "MSG007"
               CANCEL "MSG007"
      */保険証画面からコンソールエラー表示を削除する↑↑↑/120928
008930         PERFORM ファイル閉鎖
008940         MOVE ZERO TO PROGRAM-STATUS
008950         EXIT PROGRAM
008960     END-EVALUATE.
008970*
008980*/後高＋資格証明/080410
008990     IF (受－資格証明区分 = 1) AND (受－施術和暦年月 >= 42004)
009000         CONTINUE
009010     ELSE
009020         IF 受－老人負担金免除 >= 2
009030             COMPUTE 負担率Ｗ = 受－老人負担金免除 * 10
009040         END-IF
009050     END-IF.
009060*
009070*/国保組合で負担なしの対応。老人/0501
009080     IF 甲乙区分Ｗ = 9
009090         MOVE ZERO TO 負担率Ｗ
009100     END-IF.
009110*/9:０割の対応/0609
009120     IF 受－特別区分 = 9
009130         MOVE ZERO TO 負担率Ｗ
009140     END-IF.
009150*
009160     MOVE 負担率Ｗ TO 連率－２７老負担率.
009170*================================================================*
009180 助成負担区分取得 SECTION.
009190*
009191* 初期化
009192     MOVE ZERO  TO 負担区分Ｗ.
009193     MOVE ZERO  TO 助成特別負担内容Ｗ.
009202**
009206*/負担区分が８の場合負担額助成をゼロにして請求額側に足し込む
009210     MOVE 受－助成種別           TO 市－公費種別.
009220     MOVE 受－費用負担者番号助成 TO 市－市町村番号.
009230     READ 市町村マスタ
009240     INVALID KEY
009250*/エラー表示の修正
      */保険証画面からコンソールエラー表示を削除する↓↓↓/120928
009260*         PERFORM コンソールエラー確認
009270*         DISPLAY "市町村番号が登録されていません"
009280*                 " 受診者№="   受－患者コード
009290*                 " 施術年月="   受－施術年 受－施術月
009300*                 " 助成種別="   受－助成種別
009310*                 " 市町村番号=" 受－費用負担者番号助成 UPON CONS
009311**-----------------------------------------*
009312*         CALL "actcshm"  WITH C LINKAGE
009313**-----------------------------------------*
               MOVE  "市町村番号が登録されていません" TO 連メ７－メッセージ１
               MOVE  SPACE                            TO 連メ７－メッセージ２
               CALL   "MSG007"
               CANCEL "MSG007"
      */保険証画面からコンソールエラー表示を削除する↑↑↑/120928
009320         PERFORM ファイル閉鎖
009330         MOVE ZERO TO PROGRAM-STATUS
009340         EXIT PROGRAM
009350     NOT INVALID KEY
009514*******
009516* 特別負担
009519         MOVE 受－助成種別           TO 保特－保険種別
009520         MOVE 受－費用負担者番号助成 TO 保特－保険者番号
009521         MOVE 連率－施術和暦 TO 保特－開始和暦
009522         MOVE 連率－施術年   TO 保特－開始年
009523         MOVE 連率－施術月   TO 保特－開始月
009524         START 保険者特別負担マスタ KEY IS <= 保特－保険種別 
009525                                              保特－保険者番号
009526                                              保特－開始和暦年月
009527                                              REVERSED
009528         END-START
009529         IF 状態キー = "00" 
009530             READ 保険者特別負担マスタ NEXT
009531             NOT AT END
009532                 IF ( 保特－保険種別   = 受－助成種別 ) AND
009533                    ( 保特－保険者番号 = 受－費用負担者番号助成 )
009534                    MOVE 保特－負担区分       TO 負担区分Ｗ
009537                    MOVE 保特－負担率１       TO 助成特別負担率１Ｗ
009538                    MOVE 保特－負担率２       TO 助成特別負担率２Ｗ
009539                    MOVE 保特－負担一定金額   TO 助成特別負担一定金額Ｗ
009540                    MOVE 保特－事務手数料     TO 助成特別事務手数料Ｗ
009541                    MOVE 保特－助成負担回数   TO 助成特別助成負担回数Ｗ
009542                    MOVE 保特－助成負担回数２ TO 助成特別助成負担回数２Ｗ
009559                 END-IF
009560             END-READ
009561         END-IF
009562*******
009563     END-READ.
009564*
009565*================================================================*
009566 ４１老人負担率取得 SECTION.
009567*
009570*     MOVE 連計－助成種別 TO 負率－保険種別.
009580     MOVE 51             TO 負率－保険種別.
009590     MOVE 受－施術和暦   TO 負率－開始和暦 施術和暦ＣＷ.
009600     MOVE 受－施術年     TO 負率－開始年   施術年ＣＷ.
009610     MOVE 受－施術月     TO 負率－開始月   施術月ＣＷ.
009620     START 負担率マスタ KEY IS <= 負率－保険種別
009630                                  負率－開始和暦年月
009640                                  REVERSED
009650     END-START.
009660     READ 負担率マスタ NEXT
009670     AT END
009680*/エラー表示の修正
009690         PERFORM コンソールエラー確認
009700         DISPLAY "施術日に合った負担率がみつかりません"
009710                 " 受診者№="   受－患者コード
009720                 " 施術年月="   受－施術年 受－施術月 UPON CONS
009721*-----------------------------------------*
009722         CALL "actcshm"  WITH C LINKAGE
009723*-----------------------------------------*
009740         PERFORM ファイル閉鎖
009750         MOVE ZERO TO PROGRAM-STATUS
009760         EXIT PROGRAM
009770     NOT AT END
009771         IF ( 施術和暦年月ＣＷ >= 負率－開始和暦年月 ) AND
009772            ( 施術和暦年月ＣＷ <= 負率－終了和暦年月 ) AND
009773            ( 負率－保険種別 = 51 )
009800             MOVE 負率－本人負担率 TO 本人負担率Ｗ
009810             MOVE 負率－家族負担率 TO 家族負担率Ｗ
009820         END-IF
009830     END-READ.
009840*
009850     EVALUATE 受－本人家族区分
009860     WHEN 1
009870         MOVE 本人負担率Ｗ TO 負担率Ｗ
009880     WHEN 2
009890         MOVE 家族負担率Ｗ TO 負担率Ｗ
009900     WHEN OTHER
009910*/エラー表示の修正
009920         PERFORM コンソールエラー確認
009930         DISPLAY "本人家族区分が設定されていません"
009940                 " 受診者№="   受－患者コード
009950                 " 施術年月="   受－施術年 受－施術月 UPON CONS
009951*-----------------------------------------*
009952         CALL "actcshm"  WITH C LINKAGE
009953*-----------------------------------------*
009970         PERFORM ファイル閉鎖
009980         MOVE ZERO TO PROGRAM-STATUS
009990         EXIT PROGRAM
010000     END-EVALUATE.
010010*
010020*/老人料金改正の対応0210(４１で２割は無いとの説もあり流動的だが対応する)HILO
010030     IF 受－助成負担金免除 >= 2
010040         COMPUTE 負担率Ｗ = 受－助成負担金免除 * 10
010050     END-IF.
010060*
010070     MOVE 負担率Ｗ TO 連率－助成負担率.
010071*
010080*================================================================*
010090 助成負担率取得 SECTION.
010100*
010110     MOVE ZERO TO 負担率Ｗ.
010120     INITIALIZE   負担率Ｗ.
010130* 助成分
010140     EVALUATE 負担区分Ｗ
010150*　０：通常請求（全額負担）
010160     WHEN ZERO
010170*　１：初検料(初検料が発生する日)に対する負担率分
010180     WHEN 1
010190*　２：初検料(初検料が発生する日)に対する一定金額
010200     WHEN 2
010210*　３：初検日(初検料の発生する日)の費用額の本体負担率分に対する負担率
010220     WHEN 3
010230*　４：初検日(初検料の発生する日)の費用額の本体負担率分に対する一定金額
010240     WHEN 4
010250*　５：毎月一定金額を負担（窓口会計では月の初回に換算？）
010260     WHEN 5
010270*　６：毎月一定金額、一定回数を負担
010280     WHEN 6
010290     WHEN 8
010300*　７：指定した日の費用額の本体負担率分に対する一定金額
010310*
010320     WHEN 7
010330         MOVE ZERO TO 負担率Ｗ
010340*/愛知県の乳幼児の対応0109
010350* １１：毎日の費用額に対する負担率
010360     WHEN 11
010370*/0709東京都マル子の対応191001より
010380* １５：指定負担率
010390     WHEN 15
010391*/愛知県の乳幼児の対応0109
010393         MOVE 助成特別負担率１Ｗ  TO 助成負担率Ｗ
010394**
010700         MOVE 助成負担率Ｗ   TO 負担率Ｗ
010710* １２：本体負担額を全額負担030717
010720     WHEN 12
010730*/実際本体負担率を使うべき。(老人、特別、資格証明を考慮した本体負担率)/0412
010740*         MOVE 連率－健保負担率 TO 負担率Ｗ
010750         MOVE 連率－実際本体負担率 TO 負担率Ｗ
010760     END-EVALUATE.
010770*
010780     MOVE 負担率Ｗ TO 連率－助成負担率.
010781*
010782*================================================================*
010783*================================================================*
010784 負担率マスタより取得 SECTION.
010785*
010786* 保険分類　6:保険証忘れ（保険種別91）の時用
010788*
010789* 念のため100
010790     MOVE 100 TO 本人負担率Ｗ.
010791     MOVE 100 TO 家族負担率Ｗ.
010792*
010793     MOVE 受－保険種別   TO 負率－保険種別.
010794*
010795     MOVE 負率－保険種別 TO 保険種別ＣＷ.
010796     MOVE 受－施術和暦   TO 負率－開始和暦 施術和暦ＣＷ.
010797     MOVE 受－施術年     TO 負率－開始年   施術年ＣＷ.
010798     MOVE 受－施術月     TO 負率－開始月   施術月ＣＷ.
010799     START 負担率マスタ KEY IS <= 負率－保険種別
010800                                  負率－開始和暦年月
010801                                  REVERSED
010802     END-START.
010803     READ 負担率マスタ NEXT
010804     AT END
010805*/エラー表示の修正
010806         PERFORM コンソールエラー確認
010807         DISPLAY "施術日に合った負担率がみつかりません"
010808                 " 受診者№="   受－患者コード
010809                 " 施術年月="   受－施術年 受－施術月 UPON CONS
010810*-----------------------------------------*
010811         CALL "actcshm"  WITH C LINKAGE
010812*-----------------------------------------*
010813         PERFORM ファイル閉鎖
010814         MOVE ZERO TO PROGRAM-STATUS
010815         EXIT PROGRAM
010816     NOT AT END
010817         IF ( 施術和暦年月ＣＷ >= 負率－開始和暦年月 ) AND
010818            ( 施術和暦年月ＣＷ <= 負率－終了和暦年月 ) AND
010819            ( 負率－保険種別 = 保険種別ＣＷ )
010820             MOVE 負率－本人負担率 TO 本人負担率Ｗ
010821             MOVE 負率－家族負担率 TO 家族負担率Ｗ
010822         END-IF
010823     END-READ.
010824*
010843*    本人家族区分未設定かものため、本人採用
010844     MOVE 本人負担率Ｗ TO 連率－健保負担率.
010846*
010847     MOVE 連率－健保負担率 TO 連率－実際負担率.
010848     MOVE 連率－健保負担率 TO 連率－実際本体負担率.
010849*
010856*================================================================*
010871*================================================================*
010872******************************************************************
010873 END PROGRAM HUTANRIT.
010874******************************************************************
