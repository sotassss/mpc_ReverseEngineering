000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YAW9224.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*    鍼灸マ　患者データ ＣＳＶ出力
000100*         MED = YAW922
000110*----------------------------------------------------------------*
000120 DATE-WRITTEN.           2009-08-20
000130 DATE-COMPILED.          2009-08-20
      *
      */2014.07.14 受診者カナ順印刷追加
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000260     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\HMOBJ\TEMP\H9221L.DAT"
000270                             ORGANIZATION             IS  INDEXED
000280                             ACCESS                   IS  DYNAMIC
000290                             RECORD      KEY          IS  作１−施術和暦年月Ｎ
000300                                                          作１−患者コード
000310                             ALTERNATE RECORD KEY     IS  作１−印刷区分
000320                                                          作１−施術和暦年月Ｎ
000330                                                          作１−患者コード
000340*/                                                       */並び順の変更/0404
000350                             ALTERNATE RECORD KEY     IS  作１−患者郵便番号
000360                                                          作１−患者カナ
000370                                                          作１−患者コード
000380                                                          作１−施術和暦年月Ｎ
000390                             ALTERNATE RECORD KEY     IS  作１−印刷区分
000400                                                          作１−患者郵便番号
000410                                                          作１−患者カナ
000420                                                          作１−患者コード
000430                                                          作１−施術和暦年月Ｎ
000930                             ALTERNATE RECORD KEY     IS  作１−印刷区分
000400                                                          作１−患者カナ
000950                                                          作１−患者コード
000960                                                          作１−施術和暦年月Ｎ
000440                             FILE        STATUS       IS  状態キー
000450                             LOCK        MODE         IS  AUTOMATIC.
000460     SELECT  他ソフト連係Ｆ  ASSIGN      TO        保存ファイル名Ｗ
000460*     SELECT  他ソフト連係Ｆ  ASSIGN      TO        "C:\MAKISHISYS\HMOBJ\TEMP\JUSYO.TXT"
000470                             ORGANIZATION             IS LINE SEQUENTIAL
000480                             ACCESS MODE              IS SEQUENTIAL
000490                             FILE STATUS              IS 状態キー
000500                             LOCK      MODE           IS AUTOMATIC.
000510*
000520******************************************************************
000530*                      DATA DIVISION                             *
000540******************************************************************
000550 DATA                    DIVISION.
000560 FILE                    SECTION.
001240 FD  作業ファイル１ RECORD  CONTAINS 320 CHARACTERS.
001250 01  作１−レコード.
001260     03  作１−レコードキー.
001270       05 作１−施術和暦年月Ｎ.
001280         07 作１−施術和暦Ｎ                 PIC 9.
001290         07 作１−施術年月Ｎ.
001300           09 作１−施術年Ｎ                 PIC 9(2).
001310           09 作１−施術月Ｎ                 PIC 9(2).
001320       05 作１−患者コード.
001330         07 作１−患者番号                   PIC 9(6).
001340         07 作１−枝番                       PIC X.
001350     03  作１−レコードデータ.
001360       05 作１−患者カナ                     PIC X(50).
001370       05 作１−患者氏名                     PIC X(50).
001380       05 作１−患者郵便番号.
001390         07 作１−患者郵便番号１             PIC X(3).
001400         07 作１−患者郵便番号２             PIC X(4).
001410       05 作１−患者住所１                   PIC X(50).
001420       05 作１−患者住所２                   PIC X(50).
001430       05 作１−患者生年月日.
001440         07 作１−患者和暦                   PIC 9.
001450         07 作１−患者年                     PIC 9(2).
001460         07 作１−患者月                     PIC 9(2).
001470         07 作１−患者日                     PIC 9(2).
001480       05 作１−患者電話番号                 PIC X(30).
001490       05 作１−患者性別                     PIC 9.
001500       05 作１−印刷区分                     PIC 9.
001510       05 作１−インデックス                 PIC 9(7).
001520       05 FILLER                             PIC X(55).
000860*
000870* 可変長
000880 FD  他ソフト連係Ｆ
000890      RECORD IS VARYING IN SIZE FROM  1 TO 261 CHARACTERS
000900                                             DEPENDING ON レコード長.
000910 01 他−レコード.
000920   02 他−文字列.
000930     03 他−文字 PIC X OCCURS 1 TO 261 TIMES DEPENDING ON レコード長.
000940*
000950******************************************************************
000960*                WORKING-STORAGE SECTION                         *
000970******************************************************************
000980 WORKING-STORAGE         SECTION.
000990 01 キー入力                           PIC X    VALUE SPACE.
001000 01 状態キー                           PIC X(2) VALUE SPACE.
001010 01 終了フラグ                         PIC X(3) VALUE SPACE.
001020 01 ファイル名                         PIC N(10).
001180 01 オープンフラグ                     PIC X(3) VALUE SPACE.
001030*
001040 01 レコード長                         PIC 9(3) BINARY VALUE 166.
001050*
001060 01 全角ＴＢＬ.
001070   03 全角Ｗ                           PIC N(1) OCCURS 25.
001080 01 半角ＴＢＬ REDEFINES 全角ＴＢＬ.
001090   03 半角Ｗ                           PIC X(1) OCCURS 50.
001100 01 最大文字数                         PIC 9(3) VALUE ZERO.
001110 01 有効文字数                         PIC 9(3) VALUE ZERO.
001120 01 文字数Ｗ                           PIC 9(3) VALUE ZERO.
001130 01 他文字数                           PIC 9(3) VALUE ZERO.
001140**
001150 01 全角空白                           PIC X(2)  VALUE X"8140".
001160 01 半角空白                           PIC X(2)  VALUE X"2020".
001200 01 有効文字数Ｗ                       PIC 9(3) VALUE ZERO.
001210*
001220*-------------------------------------------------------------------*
001230* ファイル保存用
001240 01 保存パラメタ.
001250   03 リターンコード                   PIC S9(4) COMP-5.
001260   03 元ファイル名Ｗ                   PIC X(80).
001270 01 保存ファイル名Ｗ                   PIC X(80).
001280*
001290******************************************************************
001300*                          連結項目                              *
001310******************************************************************
001320********************
001330* メッセージ表示キー *
001340********************
001350 01 連メ−キー IS EXTERNAL.
001360    03  連メ−メッセージ               PIC N(20).
      *
       01 連入−連携データ９２２ IS EXTERNAL.
          03 連入−保存ファイル名            PIC X(80).
          03 連入−終了フラグ                PIC X(3).
      *
      */0:郵便番号順 1:受診者カナ順
       01 連入−印刷区分９２２ IS EXTERNAL.
          03 連入−印刷順区分              PIC 9(1).
001370*
001380******************************************************************
001390*                      PROCEDURE  DIVISION                       *
001400******************************************************************
001410 PROCEDURE               DIVISION.
001420************
001430*           *
001440* 初期処理   *
001450*           *
001460************
001470     PERFORM ファイルオープン.
001490************
001500*           *
001510* 主処理     *
001520*           *
001530************
            MOVE SPACE   TO 連入−終了フラグ
           IF 連入−保存ファイル名 = SPACE
              MOVE "C:\MAKISHISYS\HMOBJ\TEMP\JUSYO.TXT" TO 保存ファイル名Ｗ
           ELSE
              MOVE 連入−保存ファイル名 TO 保存ファイル名Ｗ
           END-IF.
001540     PERFORM 患者データＣＳＶ作成.
003610     IF ( オープンフラグ = "YES" ) AND
              ( 連入−保存ファイル名 = SPACE )
001550         PERFORM 保存呼出
           ELSE
               IF ( オープンフラグ = SPACE ) 
                  MOVE "YES"   TO 連入−終了フラグ
003640            MOVE  NC"　　データが０件です。確認して下さい。" TO 連メ−メッセージ
003650            CALL   "MSG001"
003660            CANCEL "MSG001"
               END-IF
           END-IF.
001560************
001570*           *
001580* 終了処理   *
001590*           *
001600************
001610     PERFORM 終了処理.
001620     EXIT PROGRAM.
001630*
001640*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
001650*================================================================*
001660 ファイルオープン SECTION.
001670*================================================================*
001680     OPEN INPUT   作業ファイル１.
001690         MOVE NC"作業" TO ファイル名.
001700         PERFORM オープンチェック.
001710*
001720*================================================================*
001730 オープンチェック SECTION.
001740*
001750     IF ( 状態キー NOT = "00" )
001760         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
001770         DISPLAY NC"状態キー：" 状態キー         UPON CONS
001780         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
001790                                                 UPON CONS
000090         CALL "actcshm"  WITH C LINKAGE
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
001800         ACCEPT  キー入力 FROM CONS
001810         PERFORM ファイル閉鎖
001820         EXIT PROGRAM.
001830*
001880*================================================================*
001890 患者データＣＳＶ作成 SECTION.
001900*================================================================*
001910     OPEN OUTPUT  他ソフト連係Ｆ.
001920         MOVE NC"他連携" TO ファイル名.
001930         PERFORM オープンチェック.
001940*
003090     MOVE  1    TO 作１−印刷区分.
003100     MOVE SPACE TO 作１−患者郵便番号.
003110     MOVE SPACE TO 作１−患者カナ.
003120     MOVE ZERO  TO 作１−施術和暦Ｎ.
003130     MOVE ZERO  TO 作１−施術年Ｎ.
003140     MOVE ZERO  TO 作１−施術月Ｎ.
003150     MOVE ZERO  TO 作１−患者番号.
003160     MOVE SPACE TO 作１−枝番.
      */2014.07.14 受診者カナ順印刷追加
           IF 連入−印刷順区分 = ZERO
003170         START 作業ファイル１  KEY IS >= 作１−印刷区分
003180                                         作１−患者郵便番号
003190                                         作１−患者カナ
003200                                         作１−患者コード
003210                                         作１−施術和暦年月Ｎ
003220         END-START
           ELSE
003170         START 作業ファイル１  KEY IS >= 作１−印刷区分
003190                                         作１−患者カナ
003200                                         作１−患者コード
003210                                         作１−施術和暦年月Ｎ
003220         END-START
           END-IF.
003230*
003240     IF ( 状態キー = "00" )
001950         MOVE SPACE  TO 終了フラグ
001960*
001970         PERFORM 作業ファイル１読込
004260         IF ( 終了フラグ NOT = "YES" )
004270             MOVE "YES" TO オープンフラグ
               END-IF
001980*
001990         PERFORM UNTIL ( 終了フラグ = "YES" )
002000             PERFORM レコードセット
002010             PERFORM 患者データＣＳＶ書込
002020             PERFORM 作業ファイル１読込
002030         END-PERFORM
           END-IF.
002040*
002050     CLOSE 他ソフト連係Ｆ.
002060*
002070*================================================================*
002080 作業ファイル１読込 SECTION.
002090*================================================================*
002100     READ 作業ファイル１ NEXT
002110     AT END
002120         MOVE "YES" TO 終了フラグ
002130     END-READ.
002140*
002150*================================================================*
002160 レコードセット SECTION.
002170*================================================================*
002180     MOVE SPACE                TO  他−レコード.
002190     MOVE ZERO                 TO  他文字数.
002200*
002210     MOVE 作１−患者カナ       TO  半角ＴＢＬ.
002220     MOVE 50                   TO  最大文字数.
002230     PERFORM 半角文字数セット.
002240     PERFORM 半角データセット.
002250     PERFORM カンマデータセット.
002260*
002310     MOVE 作１−患者氏名       TO  半角ＴＢＬ.
002390     MOVE 50                   TO  最大文字数.
           PERFORM 全角空白変換.
002400     PERFORM 半角文字数セット.
           PERFORM 半角空白変換.
002450     PERFORM 半角データセット.
002460     PERFORM カンマデータセット.
002470*
002480     MOVE 作１−患者郵便番号   TO  半角ＴＢＬ.
002490     MOVE 7                    TO  有効文字数.
002500     PERFORM 半角データセット.
002510     PERFORM カンマデータセット.
002520*
002530     MOVE 作１−患者住所１     TO  半角ＴＢＬ.
002540     MOVE 50                   TO  最大文字数.
           PERFORM 全角空白変換.
002550     PERFORM 半角文字数セット.
           PERFORM 半角空白変換.
002560     PERFORM 半角データセット.
002570     PERFORM カンマデータセット.
002580*
002590     MOVE 作１−患者住所２     TO  半角ＴＢＬ.
002600     MOVE 50                   TO  最大文字数.
           PERFORM 全角空白変換.
002610     PERFORM 半角文字数セット.
           PERFORM 半角空白変換.
002620     PERFORM 半角データセット.
002630     PERFORM カンマデータセット.
002640*
002650     MOVE 作１−患者生年月日   TO  半角ＴＢＬ.
002660     MOVE 7                    TO  有効文字数.
002670     PERFORM 半角データセット.
002680     PERFORM カンマデータセット.
002690*
002700     MOVE 作１−患者電話番号   TO  半角ＴＢＬ.
002710     MOVE 30                   TO  最大文字数.
002720     PERFORM 半角文字数セット.
002730     PERFORM 半角データセット.
002740     PERFORM カンマデータセット.
002750*
002760     EVALUATE 作１−患者性別
002770     WHEN 1
002780         MOVE "男"             TO  半角ＴＢＬ
002790     WHEN 2
002800         MOVE "女"             TO  半角ＴＢＬ
002810     WHEN OTHER 
002820         MOVE SPACE            TO  半角ＴＢＬ
002830     END-EVALUATE.
002840     MOVE 2                    TO  有効文字数.
002850     PERFORM 半角データセット.
002860     PERFORM カンマデータセット.
002870*
002880     MOVE 作１−患者コード     TO  半角ＴＢＬ.
002890     MOVE 7                    TO  有効文字数.
002900     PERFORM 半角データセット.
002910*
002920*================================================================*
002930 半角文字数セット SECTION.
002940*
002950     PERFORM VARYING 有効文字数 FROM 最大文字数 BY -1
002960             UNTIL ( 有効文字数 = ZERO ) OR
002970                   ( 半角Ｗ(有効文字数) NOT = " " )
002980             CONTINUE
002990     END-PERFORM.
003000*
003010*================================================================*
003020 全角文字数セット SECTION.
003030*
003040     PERFORM VARYING 有効文字数 FROM 最大文字数 BY -1
003050             UNTIL ( 有効文字数 = ZERO ) OR
003060                   ( 全角Ｗ(有効文字数) NOT = NC"　" )
003070             CONTINUE
003080     END-PERFORM.
003090     COMPUTE 有効文字数 = 有効文字数 * 2.
003100*
003110*================================================================*
003120 半角データセット SECTION.
003130*
003140     PERFORM VARYING 文字数Ｗ FROM 1 BY 1
003150             UNTIL ( 文字数Ｗ > 有効文字数 )
003160*
003170        COMPUTE 他文字数 = 他文字数 + 1
003180        MOVE 半角Ｗ(文字数Ｗ)   TO  他−文字(他文字数)
003190*
003200     END-PERFORM.
003210*
      *================================================================*
       全角空白変換 SECTION.
      *================================================================*
      *
           INSPECT 半角ＴＢＬ REPLACING ALL 全角空白 BY 半角空白.
      *
      *================================================================*
       半角空白変換 SECTION.
      *================================================================*
      *
           INSPECT 半角ＴＢＬ REPLACING ALL 半角空白 BY 全角空白.
      *
003220*================================================================*
003230 カンマデータセット SECTION.
003240*
003250     COMPUTE 他文字数 = 他文字数 + 1.
003260     MOVE ","           TO  他−文字(他文字数).
003270*
003280*================================================================*
003290 患者データＣＳＶ書込 SECTION.
003300*================================================================*
003310     MOVE 他文字数 TO レコード長.
003320     WRITE 他−レコード.
003330
003340*================================================================*
003350 保存呼出 SECTION.
003360*================================================================*
003370     MOVE SPACE        TO 元ファイル名Ｗ.
003380     MOVE SPACE        TO 保存ファイル名Ｗ.
003390     MOVE ZERO         TO リターンコード.
003400     MOVE "C:\MAKISHISYS\HMOBJ\TEMP\JUSYO.TXT" TO 元ファイル名Ｗ.
003410     STRING "C:\MAKISHISYS\CSV\" DELIMITED BY SIZE
                  "住所"       DELIMITED BY SIZE
003420            ".CSV"       DELIMITED BY SIZE
003430             INTO 保存ファイル名Ｗ
003440     END-STRING.
003450*
003460     CALL "savedlg"  WITH C LINKAGE
003470                     USING BY REFERENCE  保存パラメタ 保存ファイル名Ｗ.
003480*
003490     EVALUATE リターンコード 
003500*      WHEN 1
003510*          MOVE  NC"　キャンセルしました。" TO 連メ−メッセージ
003520*          CALL   "MSG001"
003530*          CANCEL "MSG001"
003540     WHEN 9
003550         MOVE  NC"　　　　　　失敗しました。" TO 連メ−メッセージ
003560         CALL   "MSG001"
003570         CANCEL "MSG001"
003580     END-EVALUATE.
003590*
003600*================================================================*
003610 終了処理 SECTION.
003620*================================================================*
003630     PERFORM ファイル閉鎖.
003640*
003650*================================================================*
003660 ファイル閉鎖 SECTION.
003670*
003680     CLOSE 作業ファイル１.
003690*
003700*================================================================*
003710******************************************************************
003720 END PROGRAM YAW9224.
003730******************************************************************
