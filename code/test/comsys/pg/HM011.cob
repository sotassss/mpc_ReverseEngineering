000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             HM011.
000060 AUTHOR.                 佐野 誠
000070*
000081*----------------------------------------------------------*
000082* ＨレセプトＦの該当レコードを削除して、WRITEしなおす。
000083* 保険証変更時の処理（実際はＦ１２登録時）
000084*----------------------------------------------------------*
000110*------------------------------------------------------------------*
000120 DATE-WRITTEN.           2004-09-08
000130 DATE-COMPILED.          2004-09-08
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000260*
000270     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000271                             ORGANIZATION             IS  INDEXED
000272                             ACCESS MODE              IS  DYNAMIC
000273                             RECORD KEY               IS  受−施術和暦年月
000274                                                          受−患者コード
000275                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000276                                                          受−患者カナ
000277                                                          受−患者コード
000278                             ALTERNATE RECORD KEY     IS  受−患者コード
000279                                                          受−施術和暦年月
000280                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000281                                                          受−保険種別
000282                                                          受−保険者番号
000283                                                          受−患者コード
000284                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000285                                                          受−公費種別
000286                                                          受−費用負担者番号
000287                                                          受−患者コード
000288                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000289                                                          受−助成種別
000290                                                          受−費用負担者番号助成
000291                                                          受−患者コード
000292                             ALTERNATE RECORD KEY     IS  受−請求和暦年月
000293                                                          受−施術和暦年月
000294                                                          受−患者コード
000295                             FILE STATUS              IS  状態キー
000296                             LOCK        MODE         IS  AUTOMATIC.
000297**
000307     SELECT  ＨレセプトＦ    ASSIGN      TO        HRECEL
000317                             ORGANIZATION             IS  INDEXED
000327                             ACCESS MODE              IS  DYNAMIC
000337                             RECORD KEY               IS  Ｈレセ−施術区分
000347                                                          Ｈレセ−施術和暦年月
000357                                                          Ｈレセ−患者コード
000367                                                          Ｈレセ−レセ種別
000377                             ALTERNATE RECORD KEY     IS  Ｈレセ−施術区分
000387                                                          Ｈレセ−患者コード
000397                                                          Ｈレセ−施術和暦年月
000407                                                          Ｈレセ−レセ種別
000417                             ALTERNATE RECORD KEY     IS  Ｈレセ−施術和暦年月
000427                                                          Ｈレセ−患者コード
000437                                                          Ｈレセ−レセ種別
000447                                                          Ｈレセ−施術区分
000457                             ALTERNATE RECORD KEY     IS  Ｈレセ−請求対象区分
000467                                                          Ｈレセ−請求和暦年月
000477                                                          Ｈレセ−施術区分
000487                                                          Ｈレセ−施術和暦年月
000497                                                          Ｈレセ−患者コード
000507                                                          Ｈレセ−レセ種別
000517                             FILE STATUS              IS  状態キー
000527                             LOCK        MODE         IS  AUTOMATIC.
000867*
000885******************************************************************
000886*                      DATA DIVISION                             *
000887******************************************************************
000888 DATA                    DIVISION.
000890 FILE                    SECTION.
000891*
001056*                           ［ＲＬ＝  ３２０］
001057 FD  受診者情報Ｆ    BLOCK   CONTAINS   1   RECORDS.
001058     COPY JUSINJ     OF  XFDLIB  JOINING   受   AS  PREFIX.
001059*
001060*-----------------------------------------------------------------------//
001065*                           ［ＲＬ＝  ７６８］
001066 FD  ＨレセプトＦ        BLOCK   CONTAINS   1   RECORDS.
001067     COPY H_RECE     OF  XFDLIB  JOINING   Ｈレセ  AS  PREFIX.
001073*
001074*---------------------------------------------------------------*
002090******************************************************************
002100*                WORKING-STORAGE SECTION                         *
002110******************************************************************
002120 WORKING-STORAGE         SECTION.
002130 01 キー入力                           PIC X    VALUE SPACE.
002140 01 状態キー                           PIC X(2) VALUE SPACE.
002150 01 終了フラグ                         PIC X(3) VALUE SPACE.
002151 01 終了フラグ２                       PIC X(3) VALUE SPACE.
002170 01 ファイル名                         PIC N(10) VALUE SPACE.
002171 01 レセ存在フラグ                     PIC X(3)  VALUE SPACE.
002172 01 存在フラグ                         PIC X(3)  VALUE SPACE.
002173 01 はり存在フラグ                     PIC X(3)  VALUE SPACE.
002174 01 マッサージ存在フラグ               PIC X(3)  VALUE SPACE.
002175*
002176*--------------------------------------------------------------*
002513*
002522* キーレセ種別用
002523 01 レセ種別Ｗ                         PIC 9(2) VALUE ZERO.
002532 01 レセプト種類Ｗ                     PIC X(4) VALUE SPACE.
002533 01 助成カウンタ                       PIC 9    VALUE ZERO.
002534*
002535 01 施術区分Ｗ                         PIC 9 VALUE ZERO.
002536 01 保険種別Ｗ                         PIC 9(2) VALUE ZERO.
002538*
002539**
002540 01 助成レコード退避Ｗ.
002541    03 助成レセ回数Ｗ                  PIC 9(3) VALUE ZERO.
002544    03 助成請求年月情報Ｗ.
002546        05 助成請求区分Ｗ              PIC 9 VALUE ZERO.
002547        05 助成請求和暦年月Ｗ.
002548           07 助成請求和暦Ｗ           PIC 9 VALUE ZERO.
002549           07 助成請求年月Ｗ.
002550             09 助成請求年Ｗ           PIC 9(2) VALUE ZERO.
002551             09 助成請求月Ｗ           PIC 9(2) VALUE ZERO.
002552    03 助成署名区分Ｗ                  PIC 9 VALUE ZERO.
002553    03 助成レセ印刷区分Ｗ              PIC 9 VALUE ZERO.
002563    03 助成レセ印刷対象区分Ｗ          PIC 9 VALUE ZERO.
002564    03 助成会総括表印刷対象区分Ｗ      PIC 9 VALUE ZERO.
002565**
002566*
002567**
002568* レセＦ退避用
002569     COPY H_RECE   OF  XFDLIB  JOINING   退避レセ  AS  PREFIX.
002580*
002590******************************************************************
002600*                          連結項目                              *
002610******************************************************************
002620*
002639*--------------------------------------------------------
002640*
002641 01 連メ−キー IS EXTERNAL.
002650    03 連メ−メッセージ                 PIC N(20).
002660*
002670* 保険証変更連携キー
002680 01 Ｈ連保変更−キー IS EXTERNAL.
002720   03 Ｈ連保変更−施術和暦年月.
002730      05 Ｈ連保変更−施術和暦              PIC 9.
002740      05 Ｈ連保変更−施術年月.
002750        07 Ｈ連保変更−施術年              PIC 9(2).
002760        07 Ｈ連保変更−施術月              PIC 9(2).
002780   03 Ｈ連保変更−患者コード.
002790     05 Ｈ連保変更−患者番号               PIC 9(6).
002800     05 Ｈ連保変更−枝番                   PIC X.
002810*
002811* 計算連携キー
002812 01 Ｈ連計算−キー IS EXTERNAL.
002813* / inパラメタ /
002814   03 Ｈ連計算−施術区分                   PIC 9.
002815   03 Ｈ連計算−施術和暦年月日.
002816      05 Ｈ連計算−施術和暦年月.
002817        07 Ｈ連計算−施術和暦              PIC 9.
002818        07 Ｈ連計算−施術年月.
002819          09 Ｈ連計算−施術年              PIC 9(2).
002820          09 Ｈ連計算−施術月              PIC 9(2).
002821      05 Ｈ連計算−施術日                  PIC 9(2).
002822   03 Ｈ連計算−患者コード.
002823      05 Ｈ連計算−患者番号                PIC 9(6).
002824      05 Ｈ連計算−枝番                    PIC X.
002825   03 Ｈ連計算−保険種別                   PIC 9(2).
002826* / 内部パラメタ/
002827   03 Ｈ連計算−内部パラメタ.
002828      05 Ｈ連計算−負担金計算区分          PIC 9.
002829      05 Ｈ連計算−費用額                  PIC 9(6).
002830*  初検日に"YES"(負担区分1,2,3,4)
002831      05 Ｈ連計算−初検日フラグ            PIC X(3).
002832*  当月の初回なら"YES"(負担区分5)
002833      05 Ｈ連計算−初診療フラグ            PIC X(3).
002834*  初検料の金額を転記(負担区分1)
002835      05 Ｈ連計算−初検料                  PIC 9(4).
002836*  当月の当日までの実日数(枝番も含む)(負担区分6,8,13)
002837      05 Ｈ連計算−実日数                  PIC 9(2).
002838*  日整静岡乳幼児用。当日の負担のありなし  ０：なし　１：あり (負担区分7)
002839      05 Ｈ連計算−その他計算区分１        PIC 9.
002840*  out
002841      05 Ｈ連計算−負担額                  PIC 9(6).
002842      05 Ｈ連計算−請求額                  PIC 9(6).
002843      05 Ｈ連計算−負担額助成              PIC 9(5).
002844      05 Ｈ連計算−請求額助成              PIC 9(5).
002845      05 Ｈ連計算−負担率                  PIC 9(3).
002846*
003050*
003532******************************************************************
003533*                      PROCEDURE  DIVISION                       *
003534******************************************************************
003535 PROCEDURE               DIVISION.
003536************
003537*           *
003538* 初期処理   *
003539*           *
003540************
003550     PERFORM 初期化.
003560************
003570*           *
003580* 主処理     *
003590*           *
003600************
003601*    施術区分 1:鍼灸、2:あんま・マッサージ
003610     MOVE 1 TO 施術区分Ｗ.
003614     PERFORM 保険証変更時の処理.
003615     IF レセ存在フラグ = "YES"
003616        MOVE "YES" TO はり存在フラグ
003617     END-IF.
003618*
003619     MOVE 2 TO 施術区分Ｗ.
003620     PERFORM 保険証変更時の処理.
003621     IF レセ存在フラグ = "YES"
003622        MOVE "YES" TO マッサージ存在フラグ
003623     END-IF.
003624************
003630*           *
003640* 終了処理   *
003650*           *
003660************
003670     PERFORM 終了処理.
003680*
003681     PERFORM 計算呼び出し処理.
003685*
003690     EXIT PROGRAM.
003700*
003710*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003720*================================================================*
003730 初期化 SECTION.
003740*
003750     PERFORM ファイルオープン.
003844*
003855*
003857*================================================================*
003860 ファイルオープン SECTION.
003870*
004006     OPEN INPUT 受診者情報Ｆ.
004007         MOVE NC"受診" TO ファイル名.
004008         PERFORM オープンチェック.
004018     OPEN I-O ＨレセプトＦ.
004019         MOVE NC"ＨレセプトＦ" TO ファイル名.
004020         PERFORM オープンチェック.
004032*
004033*================================================================*
004034 オープンチェック SECTION.
004035*
004036     IF 状態キー  NOT =  "00"
004040         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
004050         DISPLAY NC"状態キー：" 状態キー         UPON CONS
004060         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004070                                                 UPON CONS
004071*-----------------------------------------*
004072         CALL "actcshm"  WITH C LINKAGE
004073*-----------------------------------------*
004080         ACCEPT  キー入力 FROM CONS
004090         PERFORM ファイル閉鎖
004100         MOVE 99 TO PROGRAM-STATUS
004110         EXIT PROGRAM.
004111*
004120*================================================================*
004130 ファイル閉鎖 SECTION.
004140*
004170     CLOSE 受診者情報Ｆ ＨレセプトＦ.
004171*================================================================*
004180 終了処理 SECTION.
004190*
004200     PERFORM ファイル閉鎖.
004210*================================================================*
010422*================================================================*
010703*================================================================*
010704 保険証変更時の処理 SECTION.
010705*----------------------------------------------------------*
010706* ＨレセプトＦの該当レコードを削除して、WRITEしなおす。
010708*----------------------------------------------------------*
010709*
010787* 現行レコード退避
010788     MOVE SPACE TO レセ存在フラグ.
010789     MOVE 施術区分Ｗ            TO  Ｈレセ−施術区分.
010790     MOVE Ｈ連保変更−施術和暦  TO  Ｈレセ−施術和暦.
010791     MOVE Ｈ連保変更−施術年    TO  Ｈレセ−施術年.
010792     MOVE Ｈ連保変更−施術月    TO  Ｈレセ−施術月.
010793     MOVE Ｈ連保変更−患者番号  TO  Ｈレセ−患者番号.
010794     MOVE Ｈ連保変更−枝番      TO  Ｈレセ−枝番.
010795     MOVE ZERO                  TO  Ｈレセ−レセ種別.
010796*
010797     START ＨレセプトＦ KEY IS >= Ｈレセ−施術区分
010798                                  Ｈレセ−施術和暦年月
010799                                  Ｈレセ−患者コード
010800                                  Ｈレセ−レセ種別
010801     END-START.
010802*
010803     IF 状態キー = "00"
010804         MOVE SPACE TO 終了フラグ
010805         PERFORM ＨレセプトＦ読込
010806         IF ( Ｈレセ−施術区分   = 施術区分Ｗ   ) AND
010807            ( Ｈレセ−施術和暦   = Ｈ連保変更−施術和暦   ) AND
010808            ( Ｈレセ−施術年     = Ｈ連保変更−施術年     ) AND
010809            ( Ｈレセ−施術月     = Ｈ連保変更−施術月     ) AND
010810            ( Ｈレセ−患者コード = Ｈ連保変更−患者コード ) AND
010811            ( 終了フラグ         = SPACE )
010814*            / レコード退避 /
010815              INITIALIZE 退避レセ−レコード
010816              MOVE Ｈレセ−レコード TO 退避レセ−レコード
010817              MOVE "YES" TO レセ存在フラグ
010824         END-IF
010833     END-IF.
010834*
010835*
010837****** 助成レセレコードがあれば、助成レコード固有の情報を退避する。
010838     INITIALIZE 助成レコード退避Ｗ.
010853     MOVE 施術区分Ｗ            TO Ｈレセ−施術区分.
010854     MOVE Ｈ連保変更−施術和暦  TO Ｈレセ−施術和暦.
010855     MOVE Ｈ連保変更−施術年    TO Ｈレセ−施術年.
010856     MOVE Ｈ連保変更−施術月    TO Ｈレセ−施術月.
010857     MOVE Ｈ連保変更−患者番号  TO Ｈレセ−患者番号.
010858     MOVE Ｈ連保変更−枝番      TO Ｈレセ−枝番.
010859     MOVE 3                     TO Ｈレセ−レセ種別.
010860     READ ＨレセプトＦ
010861     NOT INVALID KEY
010871          MOVE Ｈレセ−レセ回数      TO 助成レセ回数Ｗ
010876          MOVE Ｈレセ−請求年月情報  TO 助成請求年月情報Ｗ
010877          MOVE Ｈレセ−署名区分      TO 助成署名区分Ｗ
010878          MOVE Ｈレセ−レセ印刷区分  TO 助成レセ印刷区分Ｗ
010889          MOVE Ｈレセ−レセ印刷対象区分      TO 助成レセ印刷対象区分Ｗ
010890          MOVE Ｈレセ−会総括表印刷対象区分  TO 助成会総括表印刷対象区分Ｗ
010895     END-READ.
010896*****
010897*
010898* 現行レコード削除
010899     IF レセ存在フラグ = "YES"
010900        MOVE 施術区分Ｗ            TO  Ｈレセ−施術区分
010901        MOVE Ｈ連保変更−施術和暦  TO  Ｈレセ−施術和暦
010902        MOVE Ｈ連保変更−施術年    TO  Ｈレセ−施術年
010903        MOVE Ｈ連保変更−施術月    TO  Ｈレセ−施術月
010904        MOVE Ｈ連保変更−患者番号  TO  Ｈレセ−患者番号
010905        MOVE Ｈ連保変更−枝番      TO  Ｈレセ−枝番
010906        MOVE ZERO                  TO  Ｈレセ−レセ種別
010907*
010908        START ＨレセプトＦ KEY IS >= Ｈレセ−施術区分
010909                                     Ｈレセ−施術和暦年月
010910                                     Ｈレセ−患者コード
010911                                     Ｈレセ−レセ種別
010912        END-START
010913*
010914        IF 状態キー = "00"
010915            MOVE SPACE TO 終了フラグ
010916            PERFORM ＨレセプトＦ読込
010917            PERFORM UNTIL ( Ｈレセ−施術区分   NOT = 施術区分Ｗ   ) OR
010918                          ( Ｈレセ−施術和暦   NOT = Ｈ連保変更−施術和暦   ) OR
010919                          ( Ｈレセ−施術年     NOT = Ｈ連保変更−施術年     ) OR
010920                          ( Ｈレセ−施術月     NOT = Ｈ連保変更−施術月     ) OR
010921                          ( Ｈレセ−患者コード NOT = Ｈ連保変更−患者コード ) OR
010922                          ( 終了フラグ         NOT = SPACE )
010923**
010924                DELETE  ＨレセプトＦ
010925                IF 状態キー NOT = "00"
010926                     MOVE NC"ＨレセプトＦ" TO ファイル名
010927                     PERFORM エラー表示Ｄ
010928                END-IF
010929**
010930                PERFORM ＨレセプトＦ読込
010931            END-PERFORM
010932        END-IF
010933     END-IF.
010934*
010935*--------------------------------------------------------------*
010936* 新規WRITE
010937     IF レセ存在フラグ = "YES"
010938        MOVE Ｈ連保変更−施術和暦    TO 受−施術和暦
010939        MOVE Ｈ連保変更−施術年      TO 受−施術年
010940        MOVE Ｈ連保変更−施術月      TO 受−施術月
010941        MOVE Ｈ連保変更−患者番号    TO 受−患者番号
010942        MOVE Ｈ連保変更−枝番        TO 受−枝番
010943        READ 受診者情報Ｆ
010944        INVALID KEY
010945*       / ありえない /
010946            MOVE SPACE TO 存在フラグ
010947        NOT INVALID KEY
010948            PERFORM レセプト種別取得
010949*           / 計算用退避 /
010950            MOVE 受−保険種別 TO 保険種別Ｗ
010951            MOVE "YES" TO 存在フラグ
010952        END-READ
010953*
010954        IF 存在フラグ = "YES"
010955           MOVE ZERO TO 助成カウンタ
010956           PERFORM レコードセット
010957           PERFORM ファイル書込
010958*         / 助成の時は、２件作成 /
010959           IF レセプト種類Ｗ = "JYOS"
010960               MOVE 1  TO 助成カウンタ
010961               PERFORM レコードセット
010962*             /---助成レセ退避情報復元---------------------------------------------------/
010963               MOVE 助成レセ回数Ｗ     TO Ｈレセ−レセ回数
010964*               / 助成請求年月情報ＷZEROは転帰なし（本体のデータになる）
010965               IF 助成請求年月情報Ｗ NOT = ZERO
010966                  MOVE 助成請求年月情報Ｗ TO Ｈレセ−請求年月情報
010967               END-IF
010968               MOVE 助成署名区分Ｗ     TO Ｈレセ−署名区分
010969               MOVE 助成レセ印刷区分Ｗ TO Ｈレセ−レセ印刷区分
010970               MOVE 助成レセ印刷対象区分Ｗ     TO Ｈレセ−レセ印刷対象区分
010971               MOVE 助成会総括表印刷対象区分Ｗ TO Ｈレセ−会総括表印刷対象区分
010977*             /-------------------------------------------------------------------------/
010978               PERFORM ファイル書込
010979           END-IF
010980        END-IF
010981     END-IF.
010982*
010983*================================================================*
010984 レセプト種別取得 SECTION.
010985*
010986     EVALUATE 受−保険種別 
010987     WHEN 70
010988          MOVE "ROUS"    TO レセプト種類Ｗ
010989     WHEN 80
010990          MOVE "JIBA"    TO レセプト種類Ｗ
010991     WHEN 85
010994          MOVE "SEIH"    TO レセプト種類Ｗ
010996     WHEN 90
010997          MOVE "JIHI"    TO レセプト種類Ｗ
010998     WHEN 91
010999          MOVE "HUME"   TO レセプト種類Ｗ
011001     WHEN OTHER
011002**      / 助成がある時は、助成用（助成金額取得）で計算する /
011003          IF 受−助成種別  = ZERO
011004             EVALUATE TRUE
011005             WHEN (受−保険種別 = 01) AND (受−公費種別 = ZERO)
011006                MOVE "KOKU"    TO レセプト種類Ｗ
011007             WHEN (受−保険種別 = 02 OR 06 OR 07) AND (受−本人家族区分 = 1) AND (受−公費種別 = ZERO)
011008                MOVE "SYAH"    TO レセプト種類Ｗ
011009             WHEN (受−保険種別 = 02 OR 06 OR 07) AND (受−本人家族区分 = 2) AND (受−公費種別 = ZERO)
011010                MOVE "SYAK"    TO レセプト種類Ｗ
011011             WHEN (受−保険種別 = 03) AND (受−本人家族区分 = 1) AND (受−公費種別 = ZERO)
011012                MOVE "KUMH"    TO レセプト種類Ｗ
011013             WHEN (受−保険種別 = 03) AND (受−本人家族区分 = 2) AND (受−公費種別 = ZERO)
011014                MOVE "KUMK"    TO レセプト種類Ｗ
011015             WHEN (受−保険種別 = 04) AND (受−本人家族区分 = 1) AND (受−公費種別 = ZERO)
011016                MOVE "KYOH"    TO レセプト種類Ｗ
011017             WHEN (受−保険種別 = 04) AND (受−本人家族区分 = 2) AND (受−公費種別 = ZERO)
011018                MOVE "KYOK"    TO レセプト種類Ｗ
011019             WHEN 受−公費種別 = 05
011020                MOVE "ROUJ"    TO レセプト種類Ｗ
011021             WHEN (受−保険種別 = 08) AND (受−公費種別 = ZERO)
011022                MOVE "TAIS"    TO レセプト種類Ｗ
011023             WHEN (受−保険種別 = 09) AND (受−公費種別 = ZERO)
011024                MOVE "JIEI"    TO レセプト種類Ｗ
011025             WHEN OTHER
011026                MOVE  NC"　　　　　保険種別が不正です。" TO 連メ−メッセージ
011027                CALL   "MSG001"
011028                CANCEL "MSG001"
011029                PERFORM ファイル閉鎖
011030                EXIT PROGRAM
011031             END-EVALUATE
011032          ELSE
011033**    / 助成がある時 /
011037             MOVE "JYOS"    TO レセプト種類Ｗ
011038          END-IF
011039     END-EVALUATE.
011040*
011041*================================================================*
011042*================================================================*
011043 レコードセット SECTION.
011044*
011045     PERFORM キー用レセ種別取得.
011046*
011047     MOVE SPACE TO Ｈレセ−レコード.
011048     INITIALIZE    Ｈレセ−レコード.
011049*
011050*  退避レコードをセット
011051     MOVE 退避レセ−レコード TO Ｈレセ−レコード.
011052*
011053*  / キー用レセ種別を上書きセット /
011054     MOVE レセ種別Ｗ   TO Ｈレセ−レセ種別.
011055*
011056*  / ファイルの金額欄初期化 /
011057     INITIALIZE Ｈレセ−金額合計欄.
011058     INITIALIZE Ｈレセ−はり金額欄.
011059     INITIALIZE Ｈレセ−マッサージ金額欄.
011060     INITIALIZE Ｈレセ−例外レセ金額合計欄.
011061     INITIALIZE Ｈレセ−例外本体枝番まとめ単独金額合計欄.
011070*
011071*---------------------------------------------------------------------------------*
011072*---------------------------------------------------------------------------------*
011073* ※※  レセＦ追加項目（償還払いなどの区分）の再設定  ※※
011074**
011083*/ 償還払い区分 /
011084     IF 助成カウンタ = ZERO
011085*       0:通常、1:鍼灸のみ償還払い、2:柔整のみ償還払い、3:両方償還払い
011093        IF ( 受−保険証償還払い区分 = 1 OR 3 )
011094           MOVE 1 TO Ｈレセ−償還払い区分
011095        ELSE
011097           MOVE 0 TO Ｈレセ−償還払い区分
011098        END-IF
011099     ELSE
011101*       0:通常、1:鍼灸のみ償還払い、2:柔整のみ償還払い、3:両方償還払い
011104        IF ( 受−助成償還払い区分 = 1 OR 3 )
011105           MOVE 1 TO Ｈレセ−償還払い区分
011106        ELSE
011107           MOVE 0 TO Ｈレセ−償還払い区分
011108        END-IF
011109*
011110*/ レセ印刷対象区分（助成のみ） /
011111*       0:対象、1:鍼灸のみ本体まとめ、2:柔整のみ本体まとめ、3:両方本体まとめ、4:鍼灸のみレセ印刷なし、5:柔整のみレセ印刷なし、6:両方印刷なし（0以外はレセ印刷なし）
011112        IF ( 受−助成レセ印刷対象区分 = 1 OR 3 OR 4 OR 6 )
011113           MOVE 1 TO Ｈレセ−レセ印刷対象区分
011114        ELSE
011115           MOVE 0 TO Ｈレセ−レセ印刷対象区分
011116        END-IF
011118*
011119*/ 会総括表印刷対象区分 （助成のみ）/
011120*       0:対象、1:鍼灸のみ印刷なし、2:柔整のみ印刷なし、3:両方印刷なし（0以外は印刷なし）
011121        IF ( 受−助成レセ会総括表印刷対象区分 = 1 OR 3 )
011122           MOVE 1 TO Ｈレセ−会総括表印刷対象区分
011123        ELSE
011124           MOVE 0 TO Ｈレセ−会総括表印刷対象区分
011125        END-IF
011126*
011130     END-IF.
011131
011132* 本体・助成レコードともにセット
011133* 0:対象、1:鍼灸のみ本体まとめ、2:柔整のみ本体まとめ、3:両方本体まとめ、4:鍼灸のみレセ印刷なし、5:柔整のみレセ印刷なし、6:両方印刷なし（0以外はレセ印刷なし）
011134     IF ( 受−助成レセ印刷対象区分 = 1 OR 3 )
011135        MOVE 1 TO Ｈレセ−本体まとめ区分
011136     ELSE
011137        MOVE 0 TO Ｈレセ−本体まとめ区分
011138     END-IF.
011140*
011141*---------------------------------------------------------------------------------*
011144*
011154*================================================================*
011171*================================================================*
011172 キー用レセ種別取得 SECTION.
011173*
011174* / レセプトＦのキー：レセ種別を決定する /
011175*
011176*   / 1:一般,2:老人,3:助成,4:労災,5:自賠責,6:自費,7:生保単独,8:保険証忘れ /
011177*
011178     EVALUATE レセプト種類Ｗ
011179     WHEN "ROUJ"
011180         MOVE  2   TO レセ種別Ｗ
011181     WHEN "JYOS"
011182        IF 助成カウンタ = ZERO
011183           IF 受−公費種別 = ZERO
011184              MOVE  1   TO レセ種別Ｗ
011185           ELSE
011186              MOVE  2   TO レセ種別Ｗ
011187           END-IF
011188        ELSE
011189           MOVE  3   TO レセ種別Ｗ
011190        END-IF
011191     WHEN "ROUS"
011192         MOVE  4   TO レセ種別Ｗ
011193     WHEN "JIBA"
011194         MOVE  5   TO レセ種別Ｗ
011195     WHEN "JIHI"
011196         MOVE  6   TO レセ種別Ｗ
011197     WHEN "SEIH"
011198         MOVE  7   TO レセ種別Ｗ
011199     WHEN "HUME"
011200         MOVE  8   TO レセ種別Ｗ
011201     WHEN OTHER
011202         MOVE  1   TO レセ種別Ｗ
011203     END-EVALUATE.
011207*
011208*================================================================*
011209 ＨレセプトＦ読込 SECTION.
011210*
011211     READ ＨレセプトＦ NEXT
011212     AT END
011213         MOVE "YES"  TO 終了フラグ
011214     END-READ.
011215*
011216*================================================================*
011217 ファイル書込 SECTION.
011218*
011219     WRITE Ｈレセ−レコード.
011220     IF 状態キー NOT = "00"
011221         MOVE NC"ＨレセプトＦ" TO ファイル名
011222         PERFORM エラー表示
011223     END-IF.
011224*
011225*================================================================*
011226*================================================================*
011227 計算呼び出し処理 SECTION.
011228*
011229     IF はり存在フラグ = "YES"
011230        PERFORM 計算連結項目セット
011231        MOVE 1 TO Ｈ連計算−施術区分
011232        CALL   "KEISAN"
011233        CANCEL "KEISAN"
011234     END-IF.
011235*
011236     IF マッサージ存在フラグ = "YES"
011237        PERFORM 計算連結項目セット
011238        MOVE 2 TO Ｈ連計算−施術区分
011239        CALL   "KEISAN"
011240        CANCEL "KEISAN"
011241     END-IF.
011242*
011243*================================================================*
011244 計算連結項目セット SECTION.
011245*
011246     MOVE SPACE TO Ｈ連計算−キー.
011247     INITIALIZE Ｈ連計算−キー.
011248*
011249     MOVE Ｈ連保変更−施術和暦    TO Ｈ連計算−施術和暦.
011250     MOVE Ｈ連保変更−施術年      TO Ｈ連計算−施術年.
011251     MOVE Ｈ連保変更−施術月      TO Ｈ連計算−施術月.
011252     MOVE Ｈ連保変更−患者番号    TO Ｈ連計算−患者番号.
011253     MOVE Ｈ連保変更−枝番        TO Ｈ連計算−枝番.
011254     MOVE 保険種別Ｗ              TO Ｈ連計算−保険種別.
011255     MOVE ZERO                    TO Ｈ連計算−施術日.
011256*
011257*================================================================*
011258 エラー表示 SECTION.
011259*
011260     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
011261     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
011262     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
011263     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"  UPON CONS.
011264*-----------------------------------------*
011265     CALL "actcshm"  WITH C LINKAGE.
011266*-----------------------------------------*
011267     ACCEPT  キー入力 FROM CONS.
011268*
011269*================================================================*
011270 エラー表示Ｄ SECTION.
011271*
011272     DISPLAY NC"ファイル削除エラー：" ファイル名   UPON CONS.
011273     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
011274     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
011275     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"  UPON CONS.
011276*-----------------------------------------*
011277     CALL "actcshm"  WITH C LINKAGE.
011278*-----------------------------------------*
011279     ACCEPT  キー入力 FROM CONS.
011280*================================================================*
011281*================================================================*
011282*================================================================*
011283******************************************************************
011284 END PROGRAM HM011.
011285******************************************************************
