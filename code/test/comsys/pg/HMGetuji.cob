000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             HMGETUJI.
000060 AUTHOR.                 佐野　誠
000070*
000080*----------------------------------------------------------------*
000090*  はり用    月次更新　（柔）
000100*  柔のGETUJIK よりいらない処理を削除
000110*  受診者情報Ｆとキーのみの負傷データＦ作成
000120*
000130* 2008/02/26 後期高齢対応
000140*----------------------------------------------------------------*
000150 DATE-WRITTEN.           2004-09-16
000160 DATE-COMPILED.          2004-09-16
000170*----------------------------------------------------------------*
000180******************************************************************
000190*            ENVIRONMENT         DIVISION                        *
000200******************************************************************
000210 ENVIRONMENT             DIVISION.
000220 CONFIGURATION           SECTION.
000230 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000240 OBJECT-COMPUTER.        FMV-DESKPOWER.
000250 SPECIAL-NAMES.          CONSOLE  IS  CONS
000260                         SYSERR   IS  MSGBOX.
000270 INPUT-OUTPUT            SECTION.
000280 FILE-CONTROL.
000290     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000300                             ORGANIZATION             IS  INDEXED
000310                             ACCESS MODE              IS  DYNAMIC
000320                             RECORD KEY               IS  受−施術和暦年月
000330                                                          受−患者コード
000340                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000350                                                          受−患者カナ
000360                                                          受−患者コード
000370                             ALTERNATE RECORD KEY     IS  受−患者コード
000380                                                          受−施術和暦年月
000390                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000400                                                          受−保険種別
000410                                                          受−保険者番号
000420                                                          受−患者コード
000430                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000440                                                          受−公費種別
000450                                                          受−費用負担者番号
000460                                                          受−患者コード
000470                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000480                                                          受−助成種別
000490                                                          受−費用負担者番号助成
000500                                                          受−患者コード
000510                             ALTERNATE RECORD KEY     IS  受−請求和暦年月
000520                                                          受−施術和暦年月
000530                                                          受−患者コード
000540                             FILE STATUS              IS  状態キー
000550                             LOCK        MODE         IS  AUTOMATIC.
000560*
000570     SELECT  受診者情報２Ｆ  ASSIGN      TO        JUSINJ2L
000580                             ORGANIZATION             IS INDEXED
000590                             ACCESS MODE              IS DYNAMIC
000600                             RECORD KEY               IS 受２−施術和暦年月
000610                                                         受２−患者コード
000620                             ALTERNATE RECORD KEY     IS 受２−請求対象区分
000630                                                         受２−請求和暦年月
000640                                                         受２−施術和暦年月
000650                                                         受２−患者コード
000660                             ALTERNATE RECORD KEY     IS 受２−助成請求対象区分
000670                                                         受２−助成請求和暦年月
000680                                                         受２−施術和暦年月
000690                                                         受２−患者コード
000700                             FILE STATUS              IS  状態キー
000710                             LOCK        MODE         IS  AUTOMATIC.
000720*
000730     SELECT  負傷データＦ    ASSIGN      TO        HUSYOUL
000740                             ORGANIZATION             IS  INDEXED
000750                             ACCESS MODE              IS  DYNAMIC
000760                             RECORD KEY               IS  負−施術和暦年月
000770                                                          負−患者コード
000780                             ALTERNATE RECORD KEY     IS  負−患者コード
000790                                                          負−施術和暦年月
000800                             FILE STATUS              IS  状態キー
000810                             LOCK        MODE         IS  AUTOMATIC.
000820*
000830******************************************************************
000840*                      DATA DIVISION                             *
000850******************************************************************
000860 DATA                    DIVISION.
000870 FILE                    SECTION.
000880*
000890 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
000900     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
000910*
000920 FD  受診者情報２Ｆ      BLOCK   CONTAINS   1   RECORDS.
000930     COPY JUSINJ2         OF  XFDLIB  JOINING   受２   AS  PREFIX.
000940*
000950 FD  負傷データＦ        BLOCK   CONTAINS   1   RECORDS.
000960     COPY HUSYOU          OF  XFDLIB  JOINING   負   AS  PREFIX.
000970*
000980*----------------------------------------------------------------*
000990******************************************************************
001000*                WORKING-STORAGE SECTION                         *
001010******************************************************************
001020 WORKING-STORAGE         SECTION.
001030 01 キー入力                           PIC X    VALUE SPACE.
001040 01 状態キー                           PIC X(2) VALUE SPACE.
001050 01 終了フラグ                         PIC X(3) VALUE SPACE.
001060 01 更新フラグ                         PIC X(3) VALUE SPACE.
001070 01 ファイル名                         PIC N(6) VALUE SPACE.
001080 01 施術和暦年月ＮＷ.
001090   03 施術和暦ＮＷ                     PIC 9.
001100   03 施術年ＮＷ                       PIC 9(2).
001110   03 施術月ＮＷ                       PIC 9(2).
001120*
001130*****
001140** 受診者レコード退避用
001150     COPY JUSINJ    OF  XFDLIB  JOINING   作業受    AS  PREFIX.
001160     COPY JUSINJ2   OF  XFDLIB  JOINING   作業受２  AS  PREFIX.
001170**
001180*****************************************************************
001190*                          連結項目                             *
001200*****************************************************************
001210*
001220* 月次更新連携キー
001230 01 Ｈ連月−キー IS EXTERNAL.
001240    03  Ｈ連月−施術和暦年月.
001250      05  Ｈ連月−施術和暦               PIC 9.
001260      05  Ｈ連月−施術年月.
001270        07  Ｈ連月−施術年               PIC 9(2).
001280        07  Ｈ連月−施術月               PIC 9(2).
001290    03  Ｈ連月−患者コード.
001300      05  Ｈ連月−患者番号               PIC 9(6).
001310      05  Ｈ連月−枝番                   PIC X.
001320*
001330 01 Ｈ連メ−キー IS EXTERNAL.
001340    03  Ｈ連メ−メッセージ               PIC X(60).
001350*
001360*----------------------------------------------------------*
001370* 平成20年4月からの後期高齢者用保険証データ変換
001380 01 連後期高齢ＣＮＶ−キー IS EXTERNAL.
001390    03 連後期高齢ＣＮＶ−施術和暦年月.
001400       05 連後期高齢ＣＮＶ−施術和暦            PIC 9.
001410       05 連後期高齢ＣＮＶ−施術年月.
001420          07 連後期高齢ＣＮＶ−施術年           PIC 9(2).
001430          07 連後期高齢ＣＮＶ−施術月           PIC 9(2).
001440    03 連後期高齢ＣＮＶ−患者コード.
001450       05 連後期高齢ＣＮＶ−患者番号            PIC 9(6).
001460       05 連後期高齢ＣＮＶ−枝番                PIC X.
001470    03 連後期高齢ＣＮＶ−変換データ.
001480       05 連後期高齢ＣＮＶ−変換対象フラグ      PIC X(3).
001490       05 連後期高齢ＣＮＶ−患者氏名            PIC X(50).
001500       05 連後期高齢ＣＮＶ−患者カナ            PIC X(50).
001510       05 連後期高齢ＣＮＶ−患者性別            PIC 9.
001520       05 連後期高齢ＣＮＶ−患者生年月日.
001530          07 連後期高齢ＣＮＶ−患者和暦         PIC 9.
001540          07 連後期高齢ＣＮＶ−患者年           PIC 9(2).
001550          07 連後期高齢ＣＮＶ−患者月           PIC 9(2).
001560          07 連後期高齢ＣＮＶ−患者日           PIC 9(2).
001570       05 連後期高齢ＣＮＶ−患者郵便番号.
001580          07 連後期高齢ＣＮＶ−患者郵便番号１   PIC X(3).
001590          07 連後期高齢ＣＮＶ−患者郵便番号２   PIC X(4).
001600       05 連後期高齢ＣＮＶ−患者住所１          PIC X(50).
001610       05 連後期高齢ＣＮＶ−患者住所２          PIC X(50).
001620       05 連後期高齢ＣＮＶ−患者電話番号        PIC X(30).
001630       05 連後期高齢ＣＮＶ−本人家族区分        PIC 9.
001640       05 連後期高齢ＣＮＶ−保険種別            PIC 9(2).
001650       05 連後期高齢ＣＮＶ−保険者番号          PIC X(10).
001660       05 連後期高齢ＣＮＶ−公費種別            PIC 9(2).
001670       05 連後期高齢ＣＮＶ−費用負担者番号      PIC X(10).
001680       05 連後期高齢ＣＮＶ−老人負担金免除      PIC 9.
001690       05 連後期高齢ＣＮＶ−特別区分            PIC 9.
001700*
001710*
001720*-------------------------------------------------------------------*
001730* 労災・自賠責・生保ファイルの月次更新連携キー
001740 01 連月労災等−キー IS EXTERNAL.
001750    03  連月労災等−施術和暦年月.
001760      05  連月労災等−施術和暦               PIC 9.
001770      05  連月労災等−施術年月.
001780        07  連月労災等−施術年               PIC 9(2).
001790        07  連月労災等−施術月               PIC 9(2).
001800    03  連月労災等−患者コード.
001810      05  連月労災等−患者番号               PIC 9(6).
001820      05  連月労災等−枝番                   PIC X.
001830*   / 1:健康保険、2:労災、3:自賠責、4:生保、5:自費 /
001840    03  連月労災等−保険分類                 PIC 9(2).
001850    03  連月労災等−最新施術和暦年月.
001860      05  連月労災等−最新施術和暦           PIC 9.
001870      05  連月労災等−最新施術年月.
001880        07  連月労災等−最新施術年           PIC 9(2).
001890        07  連月労災等−最新施術月           PIC 9(2).
001900*
001910*
001920******************************************************************
001930*                      PROCEDURE  DIVISION                       *
001940******************************************************************
001950 PROCEDURE               DIVISION.
001960************
001970*           *
001980* 初期処理   *
001990*           *
002000************
002010     PERFORM 初期化.
002020************
002030*           *
002040* 主処理     *
002050*           *
002060************
002070     PERFORM 月次更新.
002080************
002090*           *
002100* 終了処理   *
002110*           *
002120************
002130     PERFORM 終了処理.
002140     EXIT PROGRAM.
002150*
002160*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002170*================================================================*
002180 初期化 SECTION.
002190*
002200     OPEN I-O 受診者情報Ｆ.
002210         MOVE NC"受診" TO ファイル名.
002220         PERFORM オープンチェック.
002230     OPEN I-O 受診者情報２Ｆ.
002240         MOVE NC"受診２" TO ファイル名.
002250         PERFORM オープンチェック.
002260     OPEN I-O 負傷データＦ.
002270         MOVE NC"負傷" TO ファイル名.
002280         PERFORM オープンチェック.
002290*
002300*================================================================*
002310 オープンチェック SECTION.
002320*
002330     IF 状態キー  NOT =  "00"
002340         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
002350         DISPLAY NC"状態キー：" 状態キー         UPON CONS
002360         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
002370                                                 UPON CONS
002380*-----------------------------------------*
002390         CALL "actcshm"  WITH C LINKAGE
002400*-----------------------------------------*
002410         ACCEPT  キー入力 FROM CONS
002420         PERFORM ファイル閉鎖
002430         EXIT PROGRAM.
002440*================================================================*
002450 ファイル閉鎖 SECTION.
002460*
002470     CLOSE 受診者情報Ｆ 受診者情報２Ｆ 負傷データＦ.
002480*================================================================*
002490 終了処理 SECTION.
002500*
002510     PERFORM ファイル閉鎖.
002520     MOVE 1 TO PROGRAM-STATUS.
002530*================================================================*
002540 エラー表示 SECTION.
002550*
002560     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
002570     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
002580     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
002590     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
002600*-----------------------------------------*
002610     CALL "actcshm"  WITH C LINKAGE.
002620*-----------------------------------------*
002630     ACCEPT  キー入力 FROM CONS.
002640     PERFORM 終了処理.
002650     MOVE ZERO TO PROGRAM-STATUS.
002660     EXIT PROGRAM.
002670*
002680*================================================================*
002690 エラー表示Ｒ SECTION.
002700*
002710     DISPLAY NC"ファイル読込エラー" ファイル名     UPON CONS.
002720     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
002730     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
002740*-----------------------------------------*
002750     CALL "actcshm"  WITH C LINKAGE.
002760*-----------------------------------------*
002770     ACCEPT  キー入力 FROM CONS.
002780     PERFORM 終了処理.
002790     MOVE ZERO TO PROGRAM-STATUS.
002800     EXIT PROGRAM.
002810*
002820*================================================================*
002830*================================================================*
002840 月次更新 SECTION.
002850*
002860     MOVE SPACE TO 更新フラグ.
002870*
002880     MOVE Ｈ連月−施術和暦     TO 受−施術和暦.
002890     MOVE Ｈ連月−施術年       TO 受−施術年.
002900     MOVE Ｈ連月−施術月       TO 受−施術月.
002910     MOVE Ｈ連月−患者番号     TO 受−患者番号.
002920     MOVE Ｈ連月−枝番         TO 受−枝番.
002930     READ 受診者情報Ｆ
002940     INVALID KEY
002950         MOVE "YES" TO 更新フラグ
002960     NOT INVALID KEY
002970         CONTINUE
002980     END-READ.
002990*
003000     IF 更新フラグ NOT = SPACE
003010         MOVE ZERO             TO 受−施術和暦
003020         MOVE ZERO             TO 受−施術年
003030         MOVE ZERO             TO 受−施術月
003040         MOVE Ｈ連月−患者番号 TO 受−患者番号
003050         MOVE Ｈ連月−枝番     TO 受−枝番
003060         READ 受診者情報Ｆ
003070         INVALID KEY
003080             DISPLAY NC"月次更新ができません" UPON CONS
003090             DISPLAY NC"システム管理者に連絡してください" UPON CONS
003100             MOVE NC"受診" TO ファイル名
003110             PERFORM エラー表示Ｒ
003120         NOT INVALID KEY
003130*
003140*-------------------------------------------------------------------*
003150             INITIALIZE  作業受２−レコード
003160             MOVE ZERO             TO 受２−施術和暦
003170             MOVE ZERO             TO 受２−施術年
003180             MOVE ZERO             TO 受２−施術月
003190             MOVE Ｈ連月−患者番号 TO 受２−患者番号
003200             MOVE Ｈ連月−枝番     TO 受２−枝番
003210             READ 受診者情報２Ｆ
003220             INVALID KEY
003230                DISPLAY NC"月次更新ができません" UPON CONS
003240                DISPLAY NC"システム管理者に連絡してください" UPON CONS
003250                MOVE NC"受診２" TO ファイル名
003260                PERFORM エラー表示Ｒ
003270             NOT INVALID KEY
003280                MOVE 受２−レコード   TO 作業受２−レコード
003290             END-READ
003300*-------------------------------------------------------------------*
003310*
003320**           / 最新レコード退避 /
003330             INITIALIZE  作業受−レコード
003340             MOVE 受−レコード   TO 作業受−レコード
003350**
003360             MOVE 受−施術和暦Ｎ TO 施術和暦ＮＷ
003370             MOVE 受−施術年Ｎ   TO 施術年ＮＷ
003380             MOVE 受−施術月Ｎ   TO 施術月ＮＷ
003390*
003400             MOVE Ｈ連月−施術和暦 TO 受−施術和暦 受−請求和暦
003410             MOVE Ｈ連月−施術年   TO 受−施術年   受−請求年  
003420             MOVE Ｈ連月−施術月   TO 受−施術月   受−請求月  
003430* 初期化する
003440             MOVE ZERO           TO 受−継続区分 受−請求区分
003450             MOVE ZERO           TO 受−レセ印刷区分
003460             MOVE ZERO           TO 受−レセ印刷区分助成
003470*
003480             MOVE ZERO           TO 受−老人負担回数
003490             MOVE ZERO           TO 受−老人負担額累計
003500             MOVE ZERO           TO 受−老人最終負担日
003510             MOVE ZERO           TO 受−老人負担額端数
003520             MOVE ZERO           TO 受−老人負担無し
003530             MOVE SPACE          TO 受−枝番作成時枝番
003540             MOVE ZERO           TO 受−はり区分
003550             MOVE ZERO           TO 受−あんま区分
003560             MOVE ZERO           TO 受−柔整有区分
003570             MOVE ZERO           TO 受−鍼灸有区分
003580             MOVE ZERO           TO 受−マッサージ有区分
003590             MOVE ZERO           TO 受−併用区分
003600             INITIALIZE             受−保険変更情報
003610             MOVE ZERO           TO 受−助成月途中開始日
003620*
003630*
      *令和４年１０月より特別区分２割を繰り越さない対応をやめる↓↓↓/20221031
003640**---/ 平成18年10月以降、18年4月〜9月から繰り越す場合のみ、2割を3割に変換する。それ以外は初期値に戻す/0609
003650*             IF Ｈ連月−施術和暦年月 >= 41810
003660*                 IF 受−特別区分 = 2
003670*                     IF (41804 <= 受−施術和暦年月Ｎ) AND (受−施術和暦年月Ｎ <= 41809)
003680*                         MOVE 3 TO 受−特別区分
003690*                     ELSE
003700*                         MOVE 1 TO 受−特別区分
003710*                     END-IF
003720*                 END-IF
003730*                 IF 受−老人負担金免除 = 2
003740*                     IF (41804 <= 受−施術和暦年月Ｎ) AND (受−施術和暦年月Ｎ <= 41809)
003750*                         MOVE 3    TO 受−老人負担金免除
003760*                     ELSE
003770*                         MOVE ZERO TO 受−老人負担金免除
003780*                     END-IF
003790*                 END-IF
003800*             END-IF
      *令和４年１０月より特別区分２割を繰り越さない対応をやめる↑↑↑/20221031
003810***            / 上記後、平成22年4月以降+後期高齢者でない時は、特別区分1→2 /
003820***            / 上記後、時期未定+後期高齢者でない時は、特別区分1→2 /
003830*            / 上記後、平成26年5月以降+後期高齢者でない+生年月日が昭和19年4月2日以降の時は、特別区分1→2 (2014.04) /
003840             IF ( Ｈ連月−施術和暦年月 >= "42605" ) AND ( 受−保険種別 NOT = 05 )
003850                 IF ( 受−特別区分 = 1 )
003860                    IF ( 受−患者生年月日 >= "3190402" )
003870                       MOVE 2 TO 受−特別区分
003880                    END-IF
003890                 END-IF
003900             END-IF
003910*---*
003920*---------------------------------------------------------------------------------------------*
003930* 最後に実施！
003940* 平成20年4月以降の後期高齢者データの変換
003950             MOVE SPACE TO 連後期高齢ＣＮＶ−キー
003960             INITIALIZE    連後期高齢ＣＮＶ−キー
003970             IF ( Ｈ連月−施術和暦年月 >= 42004 ) AND ( 受−保険種別 NOT = 05 ) AND ( 受−保険分類 = 01 )
003980                MOVE Ｈ連月−施術和暦年月 TO 連後期高齢ＣＮＶ−施術和暦年月
003990                MOVE Ｈ連月−患者コード   TO 連後期高齢ＣＮＶ−患者コード
004000                CALL   "KKCNV"
004010                CANCEL "KKCNV"
004020*
004030                IF 連後期高齢ＣＮＶ−変換対象フラグ = "YES"
004040**                 / レコード初期化 /
004050                   INITIALIZE  受−レコード
004060**
004070                   MOVE Ｈ連月−施術和暦 TO 受−施術和暦 受−請求和暦
004080                   MOVE Ｈ連月−施術年   TO 受−施術年   受−請求年  
004090                   MOVE Ｈ連月−施術月   TO 受−施術月   受−請求月  
004100                   MOVE Ｈ連月−患者番号 TO 受−患者番号
004110                   MOVE Ｈ連月−枝番     TO 受−枝番
004120*
004130                   MOVE 連後期高齢ＣＮＶ−保険種別       TO 受−保険種別
004140                   MOVE 連後期高齢ＣＮＶ−保険者番号     TO 受−保険者番号
004150                   MOVE 連後期高齢ＣＮＶ−公費種別       TO 受−公費種別
004160                   MOVE 連後期高齢ＣＮＶ−費用負担者番号 TO 受−費用負担者番号
004170                   MOVE 連後期高齢ＣＮＶ−老人負担金免除 TO 受−老人負担金免除
004180                   MOVE 連後期高齢ＣＮＶ−特別区分       TO 受−特別区分
004190*
004200                   MOVE 連後期高齢ＣＮＶ−本人家族区分   TO 受−本人家族区分
004210                   MOVE 連後期高齢ＣＮＶ−患者氏名       TO 受−患者氏名 受−被保険者氏名
004220                   MOVE 連後期高齢ＣＮＶ−患者カナ       TO 受−患者カナ 受−被保険者カナ
004230                   MOVE 連後期高齢ＣＮＶ−患者性別       TO 受−患者性別 受−被保険者性別
004240                   MOVE 連後期高齢ＣＮＶ−患者生年月日   TO 受−患者生年月日 受−被保険者生年月日
004250                   MOVE 連後期高齢ＣＮＶ−患者郵便番号   TO 受−患者郵便番号 受−郵便番号
004260                   MOVE 連後期高齢ＣＮＶ−患者住所１     TO 受−患者住所１ 受−住所１
004270                   MOVE 連後期高齢ＣＮＶ−患者住所２     TO 受−患者住所２ 受−住所２
004280                   MOVE 連後期高齢ＣＮＶ−患者電話番号   TO 受−患者電話番号 受−電話番号
004290*                 / 退避レコードより /
004300                   MOVE 作業受−助成種別                 TO 受−助成種別
004310                   MOVE 作業受−費用負担者番号助成       TO 受−費用負担者番号助成
004320                   MOVE 作業受−受益者番号助成           TO 受−受益者番号助成
004330                   MOVE 作業受−有効年月日助成           TO 受−有効年月日助成
004340                   MOVE 作業受−助成負担金免除           TO 受−助成負担金免除
004350***                   MOVE 作業受−備考                     TO 受−備考
004360                   MOVE 作業受−カルテ裏印刷行数         TO 受−カルテ裏印刷行数
004370                   MOVE 作業受−カルテ一般裏印刷行数     TO 受−カルテ一般裏印刷行数
004380                   MOVE 作業受−カルテ裏印刷列数         TO 受−カルテ裏印刷列数
004390*
004400**                  / 新システム追加項目 /
004410                   MOVE 作業受−保険分類                 TO 受−保険分類
004420                   MOVE 作業受−助成償還払い区分         TO 受−助成償還払い区分
004430                   MOVE 作業受−助成レセ印刷対象区分     TO 受−助成レセ印刷対象区分
004440                   MOVE 作業受−資格取得年月日助成       TO 受−資格取得年月日助成
004450                   MOVE 作業受−助成レセ会総括表印刷対象区分   TO 受−助成レセ会総括表印刷対象区分
004460*
004470                   MOVE 作業受−柔整郵便電話番号印刷     TO 受−柔整郵便電話番号印刷
004480                   MOVE 作業受−鍼灸郵便電話番号印刷     TO 受−鍼灸郵便電話番号印刷
004490*
004500**
004510                END-IF
004520             END-IF
004530*---------------------------------------------------------------------------------------------*
004540*
004550             WRITE 受−レコード
004560             INVALID KEY
004570                 MOVE NC"受診" TO ファイル名
004580                 PERFORM エラー表示
004590             END-WRITE
004600*
004610*---------------------------------------------------------------------------------------------*
004620* 受診者２
004630             MOVE Ｈ連月−施術和暦 TO 受２−施術和暦 受２−請求和暦  受２−助成請求和暦
004640             MOVE Ｈ連月−施術年   TO 受２−施術年   受２−請求年    受２−助成請求年
004650             MOVE Ｈ連月−施術月   TO 受２−施術月   受２−請求月    受２−助成請求月
004660
004670* 初期化する
004680             MOVE ZERO             TO 受２−請求区分      受２−助成請求区分
004690             MOVE ZERO             TO 受２−請求対象区分  受２−助成請求対象区分
004700             MOVE ZERO             TO 受２−来院動機区分
004710             MOVE ZERO             TO 受２−紹介者患者番号
004720***
004730* 受診者Ｆより編集
004740*             IF 作業受−保険証償還払い区分 = 2 OR 3
004750*                MOVE 1    TO 受２−柔整本体償還払い区分
004760*             ELSE
004770*                MOVE ZERO TO 受２−柔整本体償還払い区分
004780*             END-IF
004790*
004800*             IF 作業受−助成償還払い区分 = 2 OR 3
004810*                MOVE 1    TO 受２−柔整助成償還払い区分
004820*             ELSE
004830*                MOVE ZERO TO 受２−柔整助成償還払い区分
004840*             END-IF
004850*
004860*             IF 作業受−助成レセ印刷対象区分 = 2 OR 3 OR 5 OR 6
004870*                MOVE 1 TO 受２−柔整助成レセ印刷対象区分
004880*                IF 作業受−助成レセ印刷対象区分 = 2 OR 3
004890*                   MOVE 1    TO 受２−柔整本体まとめ区分
004900*                ELSE
004910*                   MOVE ZERO TO 受２−柔整本体まとめ区分
004920*                END-IF
004930*             ELSE
004940*                MOVE ZERO TO 受２−柔整助成レセ印刷対象区分
004950*             END-IF
004960*
004970*             IF 作業受−助成レセ会総括表印刷対象区分 = 2 OR 3
004980*                MOVE 1    TO 受２−柔整助成レセ会総括表印刷対象区分
004990*             ELSE
005000*                MOVE ZERO TO 受２−柔整助成レセ会総括表印刷対象区分
005010*             END-IF
005020***
005030*
005040* 上記の区分は使用しなくなった（receptへ移動）ので、初期化する
005050             MOVE ZERO           TO 受２−柔整本体償還払い区分
005060             MOVE ZERO           TO 受２−柔整本体まとめ区分
005070             MOVE ZERO           TO 受２−柔整助成償還払い区分
005080             MOVE ZERO           TO 受２−柔整助成レセ印刷対象区分
005090             MOVE ZERO           TO 受２−柔整助成レセ会総括表印刷対象区分
005100             MOVE ZERO           TO 受２−柔整本体枝番まとめ区分
005110***
005120*------------------------------------------------------*
005130             IF 連後期高齢ＣＮＶ−変換対象フラグ = "YES"
005140                MOVE SPACE TO 受２−事業所名称
005150                MOVE SPACE TO 受２−事業所郵便番号
005160                MOVE SPACE TO 受２−事業所住所１
005170                MOVE SPACE TO 受２−事業所住所２
005180             END-IF
005190*------------------------------------------------------*
005200*
005210             WRITE 受２−レコード
005220             INVALID KEY
005230                 MOVE NC"受診２" TO ファイル名
005240                 PERFORM エラー表示
005250             END-WRITE
005260*---------------------------------------------------------------------------------------------*
005270*---------------------------------------------------------------------------------------------*
005280* 最新レコード
005290* 処理年月が最新レコードより過去の場合は更新しない
005300***             IF 受−施術和暦年月Ｎ < Ｈ連月−施術和暦年月 
005310             IF 施術和暦年月ＮＷ < Ｈ連月−施術和暦年月 
005320*---------------------*
005330*
005340                 MOVE ZERO           TO 受−施術和暦
005350                 MOVE ZERO           TO 受−施術年  
005360                 MOVE ZERO           TO 受−施術月  
005370                 MOVE Ｈ連月−施術和暦 TO 受−施術和暦Ｎ
005380                 MOVE Ｈ連月−施術年   TO 受−施術年Ｎ
005390                 MOVE Ｈ連月−施術月   TO 受−施術月Ｎ
005400                 MOVE ZERO           TO 受−請求和暦
005410                 MOVE ZERO           TO 受−請求年  
005420                 MOVE ZERO           TO 受−請求月  
005430                 MOVE ZERO           TO 受−継続区分
005440                 MOVE ZERO           TO 受−請求区分
005450                 MOVE ZERO           TO 受−レセ印刷区分
005460                 MOVE ZERO           TO 受−レセ印刷区分助成
005470                 MOVE ZERO           TO 受−老人負担回数
005480                 MOVE ZERO           TO 受−老人負担額累計
005490                 MOVE ZERO           TO 受−老人最終負担日
005500                 MOVE ZERO           TO 受−老人負担額端数
005510                 MOVE ZERO           TO 受−老人負担無し
005520                 MOVE SPACE          TO 受−枝番作成時枝番
005530                 MOVE ZERO           TO 受−はり区分
005540                 MOVE ZERO           TO 受−あんま区分
005550                 MOVE ZERO           TO 受−柔整有区分
005560                 MOVE ZERO           TO 受−鍼灸有区分
005570                 MOVE ZERO           TO 受−マッサージ有区分
005580                 MOVE ZERO           TO 受−併用区分
005590                 INITIALIZE             受−保険変更情報
005600                 MOVE ZERO           TO 受−助成月途中開始日
005610*
005620                 REWRITE 受−レコード
005630                 INVALID KEY
005640                     MOVE NC"受診" TO ファイル名
005650                     PERFORM エラー表示
005660                 END-REWRITE
005670*
005680*---------------------*
005690* 受診者２
005700                 MOVE ZERO           TO 受２−施術和暦
005710                 MOVE ZERO           TO 受２−施術年  
005720                 MOVE ZERO           TO 受２−施術月  
005730                 MOVE ZERO           TO 受２−請求和暦
005740                 MOVE ZERO           TO 受２−請求年  
005750                 MOVE ZERO           TO 受２−請求月  
005760                 MOVE ZERO           TO 受２−請求区分
005770                 MOVE ZERO           TO 受２−請求対象区分
005780                 MOVE ZERO           TO 受２−助成請求和暦
005790                 MOVE ZERO           TO 受２−助成請求年  
005800                 MOVE ZERO           TO 受２−助成請求月  
005810                 MOVE ZERO           TO 受２−助成請求区分
005820                 MOVE ZERO           TO 受２−助成請求対象区分
005830                 MOVE ZERO           TO 受２−来院動機区分
005840                 MOVE ZERO           TO 受２−紹介者患者番号
005850                 MOVE ZERO           TO 受２−柔整本体償還払い区分
005860                 MOVE ZERO           TO 受２−柔整本体まとめ区分
005870                 MOVE ZERO           TO 受２−柔整助成償還払い区分
005880                 MOVE ZERO           TO 受２−柔整助成レセ印刷対象区分
005890                 MOVE ZERO           TO 受２−柔整助成レセ会総括表印刷対象区分
005900                 MOVE ZERO           TO 受２−柔整本体枝番まとめ区分
005910*
005920                 REWRITE 受２−レコード
005930                 INVALID KEY
005940                     MOVE NC"受診２" TO ファイル名
005950                     PERFORM エラー表示
005960                 END-REWRITE
005970*---------------------*
005980             END-IF
005990*
006000*-----------------------------------------------------------*
006010*            労災・自賠責・生保ファイルの月次更新
006020             IF 作業受−保険分類 = 2 OR 3 OR 4
006030                INITIALIZE 連月労災等−キー
006040                MOVE Ｈ連月−施術和暦年月 TO 連月労災等−施術和暦年月
006050                MOVE Ｈ連月−患者コード   TO 連月労災等−患者コード
006060                MOVE 作業受−保険分類     TO 連月労災等−保険分類
006070                MOVE 施術和暦年月ＮＷ     TO 連月労災等−最新施術和暦年月
006080                CALL   "GETUJIK2"
006090                CANCEL "GETUJIK2"
006100             END-IF
006110*-----------------------------------------------------------*
006120*
006130             PERFORM 負傷データ生成
006140         END-READ
006150     END-IF.
006160*
006170*================================================================*
006180 負傷データ生成 SECTION.
006190*
006200* キーのみセット
006210     PERFORM レコードセット.
006220     WRITE 負−レコード.
006230     IF 状態キー NOT = "00"
006240        IF 状態キー NOT = "22"
006250           MOVE NC"負傷" TO ファイル名
006260           PERFORM エラー表示
006270        END-IF
006280     END-IF.
006290*
006300*================================================================*
006310*================================================================*
006320 レコードセット SECTION.
006330*
006340     MOVE SPACE TO 負−レコード.
006350     INITIALIZE    負−レコード.
006360*
006370     MOVE Ｈ連月−施術和暦     TO 負−施術和暦
006380     MOVE Ｈ連月−施術年       TO 負−施術年
006390     MOVE Ｈ連月−施術月       TO 負−施術月
006400     MOVE Ｈ連月−患者番号     TO 負−患者番号.
006410     MOVE Ｈ連月−枝番         TO 負−枝番.
006420*
006430*================================================================*
006440*================================================================*
006450******************************************************************
006460 END PROGRAM HMGETUJI.
006470******************************************************************
