000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             NENREI.
000060 AUTHOR.                 佐野 誠
000070*
000080*------------------------------------------------------------------*
000090*      年齢算出   柔ｳｨﾝﾄﾞｳｽﾞ95版                                   *
000100*------------------------------------------------------------------*
000110 DATE-WRITTEN.           2001-12-18
000120 DATE-COMPILED.          2001-12-18
000130*----------------------------------------------------------------*
000140******************************************************************
000150*            ENVIRONMENT         DIVISION                        *
000160******************************************************************
000170 ENVIRONMENT             DIVISION.
000180 CONFIGURATION           SECTION.
000190 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000200 OBJECT-COMPUTER.        FMV-DESKPOWER.
000210 SPECIAL-NAMES.          CONSOLE  IS  CONS
000220                         SYSERR   IS  MSGBOX.
000230 INPUT-OUTPUT            SECTION.
000240 FILE-CONTROL.
000250     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000260                             ORGANIZATION             IS  INDEXED
000270                             ACCESS MODE              IS  DYNAMIC
000280                             RECORD KEY               IS  元−元号区分
000290                             FILE STATUS              IS  状態キー
000300                             LOCK        MODE         IS  AUTOMATIC.
000310*
000320******************************************************************
000330*                      DATA DIVISION                             *
000340******************************************************************
000350 DATA                    DIVISION.
000360 FILE                    SECTION.
000370*                           ［ＲＬ＝  １２８］
000380 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
000390     COPY GENGOU          OF  XFDLIB  JOINING   元   AS  PREFIX.
000400*
000410*----------------------------------------------------------------*
000420******************************************************************
000430*                WORKING-STORAGE SECTION                         *
000440******************************************************************
000450 WORKING-STORAGE         SECTION.
000460 01 キー入力                           PIC X    VALUE SPACE.
000470 01 状態キー                           PIC X(2) VALUE SPACE.
000480 01 終了フラグ                         PIC X(3) VALUE SPACE.
000490 01 ファイル名                         PIC N(10) VALUE SPACE.
000500**
000510** 患者年齢用
000520 01 患者年齢ＷＷ.
000530    03 患者和暦Ｗ                     PIC 9    VALUE ZERO.
000540    03 患者年Ｗ                       PIC 9(2) VALUE ZERO.
000550    03 患者月Ｗ                       PIC 9(2) VALUE ZERO.
000560    03 患者日Ｗ                       PIC 9(2) VALUE ZERO.
000570    03 患者年齢Ｗ                     PIC S9(3) VALUE ZERO.
000580    03 患者西暦年Ｗ                   PIC 9(4)  VALUE ZERO.
000590** 西暦比較用
000600 01 現在西暦年月日Ｗ.
000610    03 現在西暦年月Ｗ                 PIC 9(6)  VALUE ZERO.
000620    03 現在西暦日Ｗ                   PIC 9(2)  VALUE ZERO.
000630 01 施術西暦年月Ｗ.
000640    03 施術西暦年Ｗ                   PIC 9(4)  VALUE ZERO.
000650    03 施術西暦月Ｗ                   PIC 9(2)  VALUE ZERO.
000660**
000670**********************************************************************
000680*
000690* 日付ＷＯＲＫ
000700 01 計算機西暦.
000710    03 計算機西暦年                    PIC 9(4).
000720    03 計算機西暦月日                  PIC 9(4).
000730 01 計算機西暦Ｒ REDEFINES 計算機西暦.
000740    03 計算機世紀                      PIC 9(2).
000750    03 計算機日付                      PIC 9(6).
000760    03 計算機日付Ｒ REDEFINES 計算機日付.
000770       05 計算機年月                   PIC 9(4).
000780       05 計算機年月Ｒ REDEFINES 計算機年月.
000790         07 計算機年                   PIC 9(2).
000800         07 計算機月                   PIC 9(2).
000810       05 計算機日                     PIC 9(2).
000820*
000830******************************************************************
000840*                          連結項目                              *
000850******************************************************************
000860*
000870* 年齢用
000880 01 連年齢−キー IS EXTERNAL.
000890    03 連年齢−施術和暦年月.
000900       05 連年齢−施術和暦      PIC 9.
000910       05 連年齢−施術年月.
000920         07 連年齢−施術年      PIC 9(2).
000930         07 連年齢−施術月      PIC 9(2).
000940    03 連年齢−和暦年月日.
000950       05 連年齢−和暦          PIC 9.
000960       05 連年齢−年            PIC 9(2).
000970       05 連年齢−月            PIC 9(2).
000980       05 連年齢−日            PIC 9(2).
000990    03 連年齢−年齢             PIC 9(3).
001000*
001010******************************************************************
001020*                      PROCEDURE  DIVISION                       *
001030******************************************************************
001040 PROCEDURE               DIVISION.
001050************
001060*           *
001070* 初期処理   *
001080*           *
001090************
001100     PERFORM 初期化.
001110     PERFORM 施術年月西暦化.
001120************
001130*           *
001140* 主処理     *
001150*           *
001160************
001170** 廃止2009/03  復活2014/12
001180     IF  施術西暦年月Ｗ  >=  現在西暦年月Ｗ
001190         PERFORM 年齢計算１
001200     ELSE
001210         PERFORM 年齢計算２
001220     END-IF.
001230**
001240* 上記処理を復活、廃止2014/12
001250* 常に保険年齢
001260*****     PERFORM 年齢計算２.
001270*
001280************
001290*           *
001300* 終了処理   *
001310*           *
001320************
001330     PERFORM 終了処理.
001340     EXIT PROGRAM.
001350*
001360*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
001370*================================================================*
001380 初期化 SECTION.
001390*
001400     PERFORM ファイルオープン.
001410* 連結項目の待避
001420     MOVE 連年齢−和暦    TO 患者和暦Ｗ.
001430     MOVE 連年齢−年      TO 患者年Ｗ.
001440     MOVE 連年齢−月      TO 患者月Ｗ.
001450     MOVE 連年齢−日      TO 患者日Ｗ.
001460*
001470*    /* 現在日付取得 */
001480     ACCEPT 計算機日付 FROM DATE.
001490*    /* 1980〜2079年の間で設定 */
001500     IF 計算機年 > 80
001510         MOVE 19 TO 計算機世紀
001520     ELSE
001530         MOVE 20 TO 計算機世紀
001540     END-IF.
001550*
001560     MOVE 計算機西暦Ｒ  TO  現在西暦年月日Ｗ.
001570*
001580*================================================================*
001590 ファイルオープン SECTION.
001600*
001610     OPEN INPUT 元号マスタ.
001620         MOVE NC"元号マスタ" TO ファイル名.
001630         PERFORM オープンチェック.
001640*================================================================*
001650 オープンチェック SECTION.
001660*
001670     IF 状態キー  NOT =  "00"
001680         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
001690         DISPLAY NC"状態キー：" 状態キー         UPON CONS
001700         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
001710                                                 UPON CONS
001720*-----------------------------------------*
001730         CALL "actcshm"  WITH C LINKAGE
001740*-----------------------------------------*
001750         ACCEPT  キー入力 FROM CONS
001760         PERFORM ファイル閉鎖
001770         MOVE 99 TO PROGRAM-STATUS
001780         EXIT PROGRAM.
001790*================================================================*
001800 ファイル閉鎖 SECTION.
001810*
001820     CLOSE 元号マスタ.
001830*================================================================*
001840 終了処理 SECTION.
001850*
001860     PERFORM ファイル閉鎖.
001870*================================================================*
001880*================================================================*
001890 施術年月西暦化 SECTION.
001900**
001910     MOVE 連年齢−施術和暦 TO 元−元号区分.
001920     READ 元号マスタ
001930     NOT INVALID KEY
001940         COMPUTE 施術西暦年Ｗ = 元−開始西暦年 + ( 連年齢−施術年 - 1 )
001950     END-READ.
001960*
001970     MOVE 連年齢−施術月 TO  施術西暦月Ｗ.
001980*
001990*================================================================*
002000*================================================================*
002010 年齢計算１ SECTION.
002020*
002030*------- 単純にシステム日付から年齢を算出---------------------*
002040* 
002050     MOVE ZERO TO 患者年齢Ｗ.
002060*
002070     IF ( 患者和暦Ｗ NOT = ZERO ) AND
002080        ( 患者年Ｗ   NOT = ZERO ) AND
002090        ( 患者月Ｗ   NOT = ZERO ) AND
002100        ( 患者日Ｗ   NOT = ZERO )
002110*
002120         MOVE 患者和暦Ｗ TO 元−元号区分
002130         READ 元号マスタ
002140         NOT INVALID KEY
002150             COMPUTE 患者西暦年Ｗ = 元−開始西暦年 + ( 患者年Ｗ - 1 )
002160             COMPUTE 患者年齢Ｗ   = 計算機西暦年 - 患者西暦年Ｗ
002170*
002180             IF 計算機月 <= 患者月Ｗ
002190                IF 計算機月 = 患者月Ｗ
002200                   IF 計算機日 < 患者日Ｗ
002210                      COMPUTE 患者年齢Ｗ = 患者年齢Ｗ - 1
002220                   END-IF
002230                ELSE
002240                   COMPUTE 患者年齢Ｗ = 患者年齢Ｗ - 1
002250                END-IF
002260             END-IF
002270*
002280         END-READ
002290*
002300         IF 患者年齢Ｗ < ZERO
002310            MOVE ZERO TO 患者年齢Ｗ
002320         END-IF
002330*
002340     END-IF.
002350**
002360* / 年齢セット /
002370     MOVE 患者年齢Ｗ  TO 連年齢−年齢.
002380*
002390*================================================================*
002400*================================================================*
002410 年齢計算２ SECTION.
002420*
002430*- 施術年月から年齢を算出（保険年齢。ただし1日生まれは先月生まれ扱い）-*
002440* 
002450     MOVE ZERO TO 患者年齢Ｗ.
002460*
002470     IF ( 患者和暦Ｗ NOT = ZERO ) AND
002480        ( 患者年Ｗ   NOT = ZERO ) AND
002490        ( 患者月Ｗ   NOT = ZERO ) AND
002500        ( 患者日Ｗ   NOT = ZERO )
002510**
002520         MOVE 患者和暦Ｗ TO 元−元号区分
002530         READ 元号マスタ
002540         NOT INVALID KEY
002550             COMPUTE 患者西暦年Ｗ = 元−開始西暦年 + ( 患者年Ｗ - 1 )
002560             COMPUTE 患者年齢Ｗ   = 施術西暦年Ｗ - 患者西暦年Ｗ
002570
002580             IF 施術西暦月Ｗ <= 患者月Ｗ
002590                IF 施術西暦月Ｗ = 患者月Ｗ
002600                   IF 患者日Ｗ NOT = 1
002610                      COMPUTE 患者年齢Ｗ = 患者年齢Ｗ - 1
002620                   END-IF
002630                ELSE
002640                   COMPUTE 患者年齢Ｗ = 患者年齢Ｗ - 1
002650                END-IF
002660             END-IF
002670*
002680         END-READ
002690*
002700         IF 患者年齢Ｗ < ZERO
002710            MOVE ZERO TO 患者年齢Ｗ
002720         END-IF
002730*
002740     END-IF.
002750**
002760* / 年齢セット /
002770     MOVE 患者年齢Ｗ  TO 連年齢−年齢.
002780*
002790*================================================================*
002800*================================================================*
002810******************************************************************
002820 END PROGRAM NENREI.
002830******************************************************************
