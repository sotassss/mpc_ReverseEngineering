000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YKJIHI.
000060 AUTHOR.                 佐野 誠
000070*
000080*------------------------------------------------------------------*
000090* 計算プログラム：柔整自費用ＰＧ
000091*
000110*------------------------------------------------------------------*
000120 DATE-WRITTEN.           2011-09-22
000130 DATE-COMPILED.          2011-09-22
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000260*
000865     SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
000866                             ORGANIZATION             IS  INDEXED
000867                             ACCESS MODE              IS  DYNAMIC
000868                             RECORD KEY               IS  レセ−施術和暦年月
000869                                                          レセ−患者コード
000870                                                          レセ−レセ種別
000871                             ALTERNATE RECORD KEY     IS  レセ−患者コード
000872                                                          レセ−施術和暦年月
000873                                                          レセ−レセ種別
000874                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000875                                                          レセ−施術和暦年月
000876                                                          レセ−患者コード
000877                                                          レセ−レセ種別
000878                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000879                                                          レセ−レセ種別
000880                                                          レセ−請求保険者番号
000881                                                          レセ−患者コード
000882                                                          レセ−施術和暦年月
000883                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000884                                                          レセ−請求保険者番号
000885                                                          レセ−患者コード
000886                                                          レセ−レセ種別
000887                                                          レセ−施術和暦年月
000888                             FILE STATUS              IS  状態キー
000889                             LOCK        MODE         IS  AUTOMATIC.
000890*
000904     SELECT  会計データＦ    ASSIGN      TO        KAIKEIL
000905                             ORGANIZATION             IS  INDEXED
000906                             ACCESS MODE              IS  DYNAMIC
000907                             RECORD KEY               IS  会−施術和暦年月日
000908                                                          会−患者コード
000909                             ALTERNATE RECORD KEY     IS  会−患者コード
000910                                                          会−施術和暦年月日
000911                             FILE STATUS              IS  状態キー
000912                             LOCK        MODE         IS  AUTOMATIC.
000913*
000914     SELECT  施術記録Ｆ      ASSIGN      TO        SEKIROKL
000915                             ORGANIZATION             IS  INDEXED
000916                             ACCESS MODE              IS  DYNAMIC
000917                             RECORD KEY               IS  施記−施術和暦年月日
000918                                                          施記−患者コード
000919                             ALTERNATE RECORD KEY     IS  施記−患者コード
000920                                                          施記−施術和暦年月日
000921                             FILE STATUS              IS  状態キー
000922                             LOCK        MODE         IS  AUTOMATIC.
000923*
000924******************************************************************
000925*                      DATA DIVISION                             *
000926******************************************************************
000927 DATA                    DIVISION.
000928 FILE                    SECTION.
000929*
000930 FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS.
000939     COPY RECEPT     OF  XFDLIB  JOINING   レセ   AS  PREFIX.
000949*
001089 FD  会計データＦ        BLOCK   CONTAINS   1   RECORDS.
001090     COPY KAIKEI     OF  XFDLIB  JOINING   会   AS  PREFIX.
001094*
001095 FD  施術記録Ｆ          BLOCK   CONTAINS   1   RECORDS.
001096     COPY SEKIROK         OF  XFDLIB  JOINING   施記 AS  PREFIX.
001097*
001098*---------------------------------------------------------------*
002090******************************************************************
002100*                WORKING-STORAGE SECTION                         *
002110******************************************************************
002120 WORKING-STORAGE         SECTION.
002130 01 キー入力                           PIC X    VALUE SPACE.
002140 01 状態キー                           PIC X(2) VALUE SPACE.
002150 01 終了フラグ                         PIC X(3) VALUE SPACE.
002170 01 ファイル名                         PIC N(10) VALUE SPACE.
002171 01 カウンタ                           PIC 9(2) VALUE ZERO.
002180*
002419*---------------------------------------------------------------------* 
002429 01 計算合計額Ｗ                       PIC 9(6) VALUE ZERO.
002446*
002449 01 自費額Ｗ                           PIC 9(6) VALUE ZERO.
002450 01 実日数Ｗ                           PIC 9(2) VALUE ZERO.
002453*---------------------------------------------------------------------* 
002524*
002580*
002590******************************************************************
002600*                          連結項目                              *
002610******************************************************************
002620*
003568*
003569*/新計算連結
003570 01 連Ｙ計算−キー IS EXTERNAL.
003571   03 連Ｙ計算−施術和暦年月.
003572     05 連Ｙ計算−施術和暦           PIC 9.
003573     05 連Ｙ計算−施術年月.
003574       07 連Ｙ計算−施術年           PIC 9(2).
003575       07 連Ｙ計算−施術月           PIC 9(2).
003576   03 連Ｙ計算−患者コード.
003577     05 連Ｙ計算−患者番号           PIC 9(6).
003578     05 連Ｙ計算−枝番               PIC X.
003579*
003580**
003586*
003587******************************************************************
003588*                      PROCEDURE  DIVISION                       *
003589******************************************************************
003590 PROCEDURE               DIVISION.
003591************
003592*           *
003593* 初期処理   *
003594*           *
003595************
003596     PERFORM 初期化.
003600************
003601*           *
003602* 主処理     *
003603*           *
003604************
003611     PERFORM 日計データ処理.
003612*   上記の後
003616     PERFORM レセプト処理.
003620************
003630*           *
003640* 終了処理   *
003650*           *
003660************
003670     PERFORM 終了処理.
003680     MOVE ZERO TO PROGRAM-STATUS.
003690     EXIT PROGRAM.
003700*
003710*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003720*================================================================*
003730 初期化 SECTION.
003740*
003746*
003750     PERFORM ファイルオープン.
003844*
003855*
003857*================================================================*
003860 ファイルオープン SECTION.
003870*
004025*
004026     OPEN I-O 会計データＦ.
004027         MOVE NC"会計" TO ファイル名.
004028         PERFORM オープンチェック.
004029     OPEN I-O レセプトＦ.
004030         MOVE NC"レセ" TO ファイル名.
004031         PERFORM オープンチェック.
004032     OPEN INPUT 施術記録Ｆ.
004033         MOVE NC"施術記録Ｆ" TO ファイル名.
004034         PERFORM オープンチェック.
004040*
004042*================================================================*
004043 オープンチェック SECTION.
004044*
004045     IF 状態キー  NOT =  "00"
004046         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
004050         DISPLAY NC"状態キー：" 状態キー         UPON CONS
004060         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004070                                                 UPON CONS
004071*-----------------------------------------*
004072         CALL "actcshm"  WITH C LINKAGE
004073*-----------------------------------------*
004080         ACCEPT  キー入力 FROM CONS
004090         PERFORM ファイル閉鎖
004100         MOVE 99 TO PROGRAM-STATUS
004110         EXIT PROGRAM.
004111*
004120*================================================================*
004130 ファイル閉鎖 SECTION.
004140*
004173     CLOSE 会計データＦ レセプトＦ 施術記録Ｆ.
004174*================================================================*
004180 終了処理 SECTION.
004190*
004200     PERFORM ファイル閉鎖.
004210*================================================================*
005610*================================================================*
006522 日計データ処理 SECTION.
006523*
006524** 会計データＦの自費額の累計を算出し、レコードデータの自費額・入金額以外初期化する。
006525*
006527     MOVE ZERO TO 計算合計額Ｗ.
006535*
006565     MOVE 連Ｙ計算−患者コード     TO 会−患者コード.
006566     MOVE 連Ｙ計算−施術和暦       TO 会−施術和暦.
006567     MOVE 連Ｙ計算−施術年         TO 会−施術年.
006568     MOVE 連Ｙ計算−施術月         TO 会−施術月.
006569     MOVE ZERO                     TO 会−施術日.
006570     START 会計データＦ KEY IS >= 会−患者コード 会−施術和暦年月日
006571     END-START.
006574*
006575     IF 状態キー = "00"
006576         MOVE SPACE TO 終了フラグ
006578         PERFORM 会計データＦ読込
006579*
006631*        繰り返し（当月）
006632         PERFORM UNTIL ( 会−患者コード NOT = 連Ｙ計算−患者コード ) OR
006634                       ( 会−施術和暦   NOT = 連Ｙ計算−施術和暦   ) OR
006635                       ( 会−施術年     NOT = 連Ｙ計算−施術年     ) OR
006636                       ( 会−施術月     NOT = 連Ｙ計算−施術月     ) OR
006637                       ( 終了フラグ     NOT = SPACE )
006640*
006667             COMPUTE 計算合計額Ｗ = 計算合計額Ｗ + 会−自費額
006668*
006670             MOVE 会−自費額 TO 自費額Ｗ
006677             INITIALIZE 会−レコードデータ
006679             MOVE 自費額Ｗ   TO 会−入金額  会−自費額
006680*
006682             REWRITE 会−レコード
006683             IF 状態キー NOT = "00"
006684                MOVE NC"自費会計データ更新" TO ファイル名
006685                PERFORM エラー表示
006686             END-IF
006689*
006690             PERFORM 会計データＦ読込
006691         END-PERFORM
006692     END-IF.
006693*
006694*================================================================*
006695 会計データＦ読込 SECTION.
006703*
006713     READ 会計データＦ NEXT
006723     AT END
006743         MOVE "YES" TO 終了フラグ
006753     END-READ.
009790*================================================================*
009861*================================================================*
010392*================================================================*
010393*================================================================*
010394 レセプト処理 SECTION.
010395*
010396* / レセ種別　6:自費 /
010397* 会計データＦの自費額の累計を、費用額・負担額にセット。請求額0
010398*
010408     MOVE 連Ｙ計算−施術和暦      TO レセ−施術和暦.
010409     MOVE 連Ｙ計算−施術年        TO レセ−施術年.
010410     MOVE 連Ｙ計算−施術月        TO レセ−施術月.
010411     MOVE 連Ｙ計算−患者番号      TO レセ−患者番号.
010412     MOVE 連Ｙ計算−枝番          TO レセ−枝番.
010414     MOVE 6                       TO レセ−レセ種別.
010415     READ レセプトＦ
010420     NOT INVALID KEY
010428*
010435**    / ファイルの金額欄初期化 /
010436         INITIALIZE レセ−合計欄
010437         INITIALIZE レセ−印刷レセ金額合計欄
010438         INITIALIZE レセ−例外本体枝番まとめ単独金額合計欄
010439         INITIALIZE レセ−料金１
010440         INITIALIZE レセ−部位１
010441         INITIALIZE レセ−部位２
010442         INITIALIZE レセ−部位３８
010443         INITIALIZE レセ−部位３０
010444         INITIALIZE レセ−部位４５
010445         INITIALIZE レセ−部位４８
010446         INITIALIZE レセ−部位４０
010447         INITIALIZE レセ−部位５２
010448         INITIALIZE レセ−部位５５
010449         INITIALIZE レセ−部位５８
010450         INITIALIZE レセ−部位５０
010451**
010454         MOVE 計算合計額Ｗ  TO レセ−合計
010455         MOVE 計算合計額Ｗ  TO レセ−一部負担金 レセ−印刷レセ負担額
010456         MOVE ZERO          TO レセ−請求金額   レセ−印刷レセ請求額
010457         MOVE ZERO          TO レセ−受給者負担額 レセ−印刷レセ受給者負担額
010458         MOVE ZERO          TO レセ−助成請求金額 レセ−印刷レセ助成請求金額
010459*
010492*（負担割合10）
010493         MOVE 10 TO  レセ−負担割合
010494*
010495         PERFORM 実日数セット
010497**
010498         REWRITE レセ−レコード
010499         IF 状態キー NOT = "00"
010500            MOVE NC"自費レセ更新" TO ファイル名
010501            PERFORM エラー表示
010502         END-IF
010508**
010509     END-READ.
010510*
010511*================================================================*
010512*================================================================*
010513 実日数セット SECTION.
010514*
010515     MOVE ZERO      TO  実日数Ｗ.
010516*
010532     MOVE 連Ｙ計算−患者番号    TO 施記−患者番号.
010533     MOVE 連Ｙ計算−枝番        TO 施記−枝番.
010534     MOVE 連Ｙ計算−施術和暦    TO 施記−施術和暦.
010535     MOVE 連Ｙ計算−施術年      TO 施記−施術年.
010536     MOVE 連Ｙ計算−施術月      TO 施記−施術月.
010537     MOVE ZERO                  TO 施記−施術日.
010538     START 施術記録Ｆ   KEY IS >= 施記−患者コード
010539                                  施記−施術和暦年月日
010540     END-START.
010541     IF 状態キー = "00"
010542         MOVE SPACE TO 終了フラグ
010543         PERFORM 施術記録Ｆ読込
010544         PERFORM UNTIL ( 施記−施術和暦   NOT = 連Ｙ計算−施術和暦 ) OR
010545                       ( 施記−施術年     NOT = 連Ｙ計算−施術年   ) OR
010546                       ( 施記−施術月     NOT = 連Ｙ計算−施術月   ) OR
010547                       ( 施記−患者コード NOT = 連Ｙ計算−患者コード ) OR
010548                       ( 終了フラグ       NOT = SPACE )
010549*
010550             COMPUTE 実日数Ｗ = 実日数Ｗ + 1
010551*
010560             PERFORM 施術記録Ｆ読込
010561         END-PERFORM
010562     END-IF.
010563*
010564*
010565     MOVE 実日数Ｗ    TO  レセ−レセ実日数.
010566*
010568** 上記後
010569*----------------------------------------------------------------------------*
010570     IF レセ−請求区分 = 9
010571        MOVE ZERO TO レセ−請求対象区分
010572     ELSE
010573        IF ( 実日数Ｗ NOT = ZERO )
010574           MOVE 1    TO レセ−請求対象区分
010575        ELSE
010576           MOVE ZERO TO レセ−請求対象区分
010577        END-IF
010578     END-IF.
010579*----------------------------------------------------------------------------*
010580*
010581*================================================================*
010586 施術記録Ｆ読込 SECTION.
010596*
010606     READ 施術記録Ｆ NEXT
010616     AT END
010626         MOVE "YES" TO 終了フラグ
010636     END-READ.
010646*
010869*================================================================*
010870 エラー表示 SECTION.
010871*
010872     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
010873     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
010874     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
010875     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"  UPON CONS.
010876*-----------------------------------------*
010877     CALL "actcshm"  WITH C LINKAGE.
010878*-----------------------------------------*
010879     ACCEPT  キー入力 FROM CONS.
010880*
010881*================================================================*
010882*================================================================*
010883*================================================================*
010884******************************************************************
010885 END PROGRAM YKJIHI.
010886******************************************************************
