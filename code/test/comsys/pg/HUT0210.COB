000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             HUT0210.
000060 AUTHOR.                 山田  浩之
000070*
000080*----------------------------------------------------------------*
000090*         負担額計算プログラム　日計処理［健康保険用］（柔）
000100*         MED = 
000110* 平成１４年１０月分請求から使用開始
000120* ３歳未満２割負担、高齢者１，２割負担の対応。
000130*----------------------------------------------------------------*
000140 DATE-WRITTEN.           1997-12-21
000150 DATE-COMPILED.          1997-12-21
000160*----------------------------------------------------------------*
000170******************************************************************
000180*            ENVIRONMENT         DIVISION                        *
000190******************************************************************
000200 ENVIRONMENT             DIVISION.
000210 CONFIGURATION           SECTION.
000220 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000230 OBJECT-COMPUTER.        FMV-DESKPOWER.
000240 SPECIAL-NAMES.          CONSOLE  IS  CONS
000250                         SYSERR   IS  MSGBOX.
000260 INPUT-OUTPUT            SECTION.
000270 FILE-CONTROL.
000271* EXTERNAL用
000280     SELECT  ＥＸ保険者マスタ    ASSIGN      TO        HOKENSL
000290                             ORGANIZATION             IS  INDEXED
000300                             ACCESS MODE              IS  DYNAMIC
000310                             RECORD KEY               IS  保－保険種別
000320                                                          保－保険者番号
000330                             ALTERNATE RECORD KEY     IS  保－保険種別
000340                                                          保－保険者名称
000350                                                          保－保険者番号
000360                             FILE STATUS              IS  状態キー
000370                             LOCK        MODE         IS  AUTOMATIC.
000380     SELECT  ＥＸ負担率マスタ    ASSIGN      TO        HUTANRIL
000390                             ORGANIZATION             IS  INDEXED
000400                             ACCESS MODE              IS  DYNAMIC
000410                             RECORD KEY               IS  負率－保険種別
000420                                                          負率－開始和暦年月
000430                             FILE STATUS              IS  状態キー
000440                             LOCK        MODE         IS  AUTOMATIC.
000450     SELECT  ＥＸ受診者情報Ｆ    ASSIGN      TO        JUSINJL
000460                             ORGANIZATION             IS  INDEXED
000470                             ACCESS MODE              IS  DYNAMIC
000480                             RECORD KEY               IS  受－施術和暦年月
000490                                                          受－患者コード
000500                             ALTERNATE RECORD KEY     IS  受－施術和暦年月
000510                                                          受－患者カナ
000520                                                          受－患者コード
000530                             ALTERNATE RECORD KEY     IS  受－患者コード
000540                                                          受－施術和暦年月
000550                             ALTERNATE RECORD KEY     IS  受－施術和暦年月
000560                                                          受－保険種別
000570                                                          受－保険者番号
000580                                                          受－患者コード
000590                             ALTERNATE RECORD KEY     IS  受－施術和暦年月
000600                                                          受－公費種別
000610                                                          受－費用負担者番号
000620                                                          受－患者コード
000630                             ALTERNATE RECORD KEY     IS  受－施術和暦年月
000640                                                          受－助成種別
000650                                                          受－費用負担者番号助成
000660                                                          受－患者コード
000670                             ALTERNATE RECORD KEY     IS  受－請求和暦年月
000680                                                          受－施術和暦年月
000690                                                          受－患者コード
000700                             FILE STATUS              IS  状態キー
000710                             LOCK        MODE         IS  AUTOMATIC.
000910**
000920     SELECT  ＥＸ保険者特別負担マスタ   ASSIGN      TO        HOKENTKL
000930                                    ORGANIZATION          IS  INDEXED
000940                                    ACCESS MODE           IS  DYNAMIC
000950                                    RECORD KEY            IS  保特－保険種別
000960                                                              保特－保険者番号
000970                                                              保特－開始和暦年月
000980                                    FILE STATUS           IS  状態キー
000990                                    LOCK        MODE      IS  AUTOMATIC.
001000**
001010******************************************************************
001020*                      DATA DIVISION                             *
001030******************************************************************
001040 DATA                    DIVISION.
001050 FILE                    SECTION.
001051* EXTERNAL用
001060*                           ［ＲＬ＝  ２５６］
001070 FD  ＥＸ保険者マスタ  IS EXTERNAL      BLOCK   CONTAINS   1   RECORDS.
001080     COPY HOKENS     OF  XFDLIB  JOINING   保   AS  PREFIX.
001090*                           ［ＲＬ＝  ２５６］
001100 FD  ＥＸ負担率マスタ  IS EXTERNAL      BLOCK   CONTAINS   1   RECORDS.
001110     COPY HUTANRI    OF  XFDLIB  JOINING   負率 AS  PREFIX.
001120*                           ［ＲＬ＝  ３２０］
001130 FD  ＥＸ受診者情報Ｆ  IS EXTERNAL       BLOCK   CONTAINS   1   RECORDS.
001140     COPY JUSINJ     OF  XFDLIB  JOINING   受   AS  PREFIX.
001240*
001250 FD  ＥＸ保険者特別負担マスタ IS EXTERNAL   BLOCK   CONTAINS   1   RECORDS.
001260     COPY HOKENTK         OF  XFDLIB  JOINING   保特 AS PREFIX.
001270*
001280*----------------------------------------------------------------*
001290******************************************************************
001300*                WORKING-STORAGE SECTION                         *
001310******************************************************************
001320 WORKING-STORAGE         SECTION.
001330 01 キー入力                           PIC X    VALUE SPACE.
001340 01 状態キー                           PIC X(2) VALUE SPACE.
001350 01 終了フラグ                         PIC X(3) VALUE SPACE.
001360 01 終了フラグ２                       PIC X(3) VALUE SPACE.
001370 01 書込フラグ                         PIC X(4) VALUE SPACE.
001380 01 書込フラグ２                       PIC X(4) VALUE SPACE.
001390 01 書込許可フラグ                     PIC X(3) VALUE SPACE.
001400 01 年月フラグ                         PIC X(3) VALUE SPACE.
001410 01 追加フラグ                         PIC X(3) VALUE SPACE.
001420 01 行桁フラグ                         PIC X(3) VALUE SPACE.
001430 01 項目フラグ                         PIC X(3) VALUE SPACE.
001440 01 保険者番号Ｗ                       PIC X(6) VALUE SPACE.
001450 01 ファイル名Ｗ                       PIC N(2) VALUE SPACE.
001460 01 エラー内容Ｗ                       PIC N(18) VALUE SPACE.
001470 01 会員名Ｗ                           PIC N(16) VALUE SPACE.
001480 01 画面名称Ｗ                         PIC N(2) VALUE SPACE.
001490 01 負傷ＣＮＴ                         PIC 9    VALUE 0.
001500 01 請求移動キー                       PIC X(4) VALUE SPACE.
001510 01 画面移動キー                       PIC X(4) VALUE SPACE.
001520 01 処理移動キー                       PIC X(4) VALUE SPACE.
001530 01 再入力キーＷ                       PIC X(4) VALUE SPACE.
001540 01 入力状態Ｗ                         PIC X(4) VALUE SPACE.
001550 01 部位名称Ｗ                         PIC N(12) VALUE SPACE.
001560 01 行Ｗ                               PIC 9(4) VALUE ZERO.
001570 01 桁Ｗ                               PIC 9(4) VALUE ZERO.
001580 01 商Ｗ                               PIC 9(4) VALUE ZERO.
001590 01 剰余Ｗ                             PIC 9(4) VALUE ZERO.
001600 01 前和暦Ｗ                           PIC 9 VALUE ZERO.
001610 01 カレント和暦Ｗ                     PIC 9 VALUE ZERO.
001620 01 入金額Ｗ                           PIC 9(6) VALUE ZERO.
001630 01 保険種別Ｗ                         PIC 9(2) VALUE ZERO.
001640 01 助成種別１Ｗ                       PIC 9(2) VALUE ZERO.
001650 01 助成種別２Ｗ                       PIC 9(2) VALUE ZERO.
001660*
001670*/負担額と請求額を６桁に修正0311
001680 01 負担額Ｗ                           PIC 9(6) VALUE ZERO.
001690 01 負担額Ｗ２                         PIC 9(6) VALUE ZERO.
001700 01 負担額Ｗ１０                       PIC 9(6)V9 VALUE ZERO.
001710 01 請求額Ｗ                           PIC 9(6) VALUE ZERO.
001720 01 負担率Ｗ                           PIC 9(3) VALUE ZERO.
001730 01 甲乙区分Ｗ                         PIC 9    VALUE ZERO.
001740 01 本人負担率Ｗ                       PIC 9(3) VALUE ZERO.
001750 01 家族負担率Ｗ                       PIC 9(3) VALUE ZERO.
001760 01 負担率区分Ｗ                       PIC 9 VALUE ZERO.
001770*
001780 01 初検料Ｗ                           PIC 9(4) VALUE ZERO.
001790 01 再検料Ｗ                           PIC 9(4) VALUE ZERO.
001800 01 往療料Ｗ                           PIC 9(4) VALUE ZERO.
001810 01 後療料Ｗ                           PIC 9(4) VALUE ZERO.
001820 01 冷罨法料Ｗ                         PIC 9(4) VALUE ZERO.
001830 01 温罨法料Ｗ                         PIC 9(4) VALUE ZERO.
001840 01 電療料Ｗ                           PIC 9(4) VALUE ZERO.
001850 01 金属副子料Ｗ                       PIC 9(4) VALUE ZERO.
001860 01 初回処置料Ｗ                       PIC 9(4) VALUE ZERO.
001870 01 施術情報提供料Ｗ                   PIC 9(4) VALUE ZERO.
001880 01 負担率中間結果Ｗ                   PIC 9V9(3) VALUE ZERO.
001890*
001900 01 後療料Ｗ１                         PIC 9(4) VALUE ZERO.
001910 01 冷罨法料Ｗ１                       PIC 9(4) VALUE ZERO.
001920 01 温罨法料Ｗ１                       PIC 9(4) VALUE ZERO.
001930 01 電療料Ｗ１                         PIC 9(4) VALUE ZERO.
001940*
001950 01 部位コードＣＷ.
001960    03 負傷種別ＣＷ                    PIC 9(2).
001970    03 部位ＣＷ                        PIC 9(2).
001980*
001990 01 患者コードＷ.
002000     03 患者番号Ｗ                     PIC 9(6).
002010     03 枝番Ｗ                         PIC X.
002020*
002030 01 施術和暦年月日ＣＷ.
002040   03 施術和暦年月ＣＷ.
002050     05 施術和暦ＣＷ                   PIC 9.
002060     05 施術年月ＣＷ.
002070        07 施術年ＣＷ                  PIC 9(2).
002080        07 施術月ＣＷ                  PIC 9(2).
002090   03 施術日ＣＷ                       PIC 9(2).
002100*/保険種別も判定する/0609
002110  01 保険種別ＣＷ                      PIC 9(2) VALUE ZERO.
002120*
002130 01 施術和暦年月Ｗ.
002140     03 施術和暦Ｗ                     PIC 9.
002150     03 施術年月Ｗ.
002160        05 施術年Ｗ                    PIC 9(2).
002170        05 施術月Ｗ                    PIC 9(2).
002180     03 施術日Ｗ                       PIC 9(2).
002190*
002200 01 負傷種別略称Ｗ.
002210    03 負傷種別略称Ｗ１                PIC N(4).
002220    03 FILLER                          PIC N(2).
002230 01 元号名称Ｗ.
002240    03 元号名称Ｗ１                    PIC N(2).
002250    03 FILLER                          PIC N(4).
002260 01 転帰区分略称Ｗ.
002270    03 転帰区分略称Ｗ１                PIC N(4).
002280    03 FILLER                          PIC N(2).
002290*
002300 01  保険者番号Ｗ２.
002310     03  保番１Ｗ                      PIC X(2).
002320     03  保番２Ｗ                      PIC X(8).
002330* 費用負担者番号（市町村番号）ＷＯＲＫ
002340 01 費用負担者番号Ｗ.
002350     03  法別番号Ｗ                      PIC X(2).
002360     03  助保険Ｗ.
002370          05  府県Ｗ                     PIC X(2).
002380          05  助番Ｗ                     PIC X     OCCURS 3.
002390          05  助ＣＤＷ                   PIC X.
002400     03  FILLER                        PIC X(2).
002410*
002420*/年月毎持つ保険者データのワーク
002430 01 保負担率区分Ｗ                     PIC 9    VALUE ZERO.
002440 01 保本人負担率Ｗ                     PIC 9(3) VALUE ZERO.
002450 01 保家族負担率Ｗ                     PIC 9(3) VALUE ZERO.
002460 01 保本人負担率乙Ｗ                   PIC 9(3) VALUE ZERO.
002470 01 保家族負担率乙Ｗ                   PIC 9(3) VALUE ZERO.
002480*
002490*/エラー表示の修正
002500 01 エラー表示Ｆ                       PIC 9(1)  VALUE ZERO.
002510*
002710* 日付ＷＯＲＫ
002720 01 和暦終了年Ｗ                       PIC 9(4) VALUE ZERO.
002730 01 計算機和暦年Ｗ                     PIC 9(2) VALUE ZERO.
002740*
002750 01 計算機西暦.
002760    03 計算機西暦年                    PIC 9(4).
002770    03 計算機西暦月日                  PIC 9(4).
002780 01 計算機西暦Ｒ REDEFINES 計算機西暦.
002790    03 計算機世紀                      PIC 9(2).
002800    03 計算機日付                      PIC 9(6).
002810    03 計算機日付Ｒ REDEFINES 計算機日付.
002820       05 計算機年月                   PIC 9(4).
002830       05 計算機年月Ｒ REDEFINES 計算機年月.
002840         07 計算機年                   PIC 9(2).
002850         07 計算機月                   PIC 9(2).
002860       05 計算機日                     PIC 9(2).
002870*
002890*
002891*
002900**--------------------**
002901 01 負担額日計窓口Ｗ                   PIC 9(6) VALUE ZERO.
002902 01 負担額日計窓口Ｗ２                 PIC 9(6) VALUE ZERO.
002903*
002910*****************************************************************
002920*                          連結項目                             *
002930*****************************************************************
002940*
002950 01 連－共通キー IS EXTERNAL.
002960    03  連－カレント和暦               PIC 9.
002970*
002980* キー項目
002990 01 連－キー IS EXTERNAL.
003000    03  連－患者番号                   PIC 9(6).
003010    03  連－枝番                       PIC X.
003020    03  連－患者氏名                   PIC N(10).
003030    03  連－施術和暦                   PIC 9.
003040    03  連－施術年                     PIC 9(2).
003050    03  連－施術月                     PIC 9(2).
003060    03  連－施術日                     PIC 9(2).
003070    03  連－受付時                     PIC 9(2).
003080    03  連－受付分                     PIC 9(2).
003090    03  連－受付秒                     PIC 9(2).
003100*
003110 01 連計－キー IS EXTERNAL.
003120    03  連計－負担金計算区分           PIC 9.
003130    03  連計－本人家族区分             PIC 9.
003140    03  連計－保険種別                 PIC 9(2).
003150    03  連計－保険者番号               PIC X(10).
003160    03  連計－公費種別                 PIC 9(2).
003170    03  連計－市町村番号老人           PIC X(10).
003180    03  連計－助成種別                 PIC 9(2).
003190    03  連計－市町村番号助成           PIC X(10).
003200    03  連計－患者コード.
003210      05  連計－患者番号               PIC 9(6).
003220      05  連計－枝番                   PIC X.
003230    03  連計－施術和暦年月日.
003240      05  連計－施術和暦年月.
003250        07  連計－施術和暦             PIC 9.
003260        07  連計－施術年月.
003270          09  連計－施術年             PIC 9(2).
003280          09  連計－施術月             PIC 9(2).
003290      05  連計－施術日                 PIC 9(2).
003300    03  連計－費用額                   PIC 9(6).
003310    03  連計－負担額                   PIC 9(6).
003320    03  連計－請求額                   PIC 9(6).
003330    03  連計－費用額老人               PIC 9(6).
003340    03  連計－負担額老人               PIC 9(6).
003350    03  連計－請求額老人               PIC 9(6).
003360    03  連計－費用額助成               PIC 9(6).
003370    03  連計－負担額助成               PIC 9(5).
003380    03  連計－請求額助成               PIC 9(5).
003390    03  連計－実日数                   PIC 9(2).
003400    03  連計－負担率                   PIC 9(3).
003410    03  連計－負担率区分               PIC 9.
003420*
003426*-------------------------------------*
003427 01 連計日計窓口－キー IS EXTERNAL.
003428    03  連計日計窓口－負担額           PIC 9(6).
003429*-------------------------------------*
003430*
003431 01 連計Ｐ－キー IS EXTERNAL.
003440    03 連計Ｐ－費用額ＰＧ                 PIC X(8).
003450    03 連計Ｐ－負担額ＰＧ                 PIC X(8).
003460    03 連計Ｐ－負担額ＰＧ老人             PIC X(8).
003470    03 連計Ｐ－負担額ＰＧ助成             PIC X(8).
003480*
003490* 全柔用ＯＣＲ負担率記述用
003500 01 連Ｏ－キー IS EXTERNAL.
003510    03 連Ｏ－負担率                    PIC 9(2).
003520*
003530* 確認メッセージ用 (２行)
003540 01 連メ７－キー IS EXTERNAL.
003550    03  連メ７－メッセージ１             PIC X(40).
003560    03  連メ７－メッセージ２             PIC X(40).
003570******************************************************************
003580*                      PROCEDURE  DIVISION                       *
003590******************************************************************
003600 PROCEDURE               DIVISION.
003610************
003620*           *
003630* 初期処理   *
003640*           *
003650************
003660     PERFORM 初期化.
003670     PERFORM 負担率取得.
003680*
003690************
003700*           *
003710* 主処理     *
003720*           *
003730************
003740*
003750     PERFORM 負担額計算
003760*
003770************
003780*           *
003790* 終了処理   *
003800*           *
003810************
003820     PERFORM 終了処理.
003830     MOVE 1 TO PROGRAM-STATUS.
003840     EXIT PROGRAM.
003850*
003860*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003870*================================================================*
003880 初期化 SECTION.
003890*
003891* EXTERNALでOPEN無し
004110*
004120*================================================================*
004130 オープンチェック SECTION.
004140*
004150     IF 状態キー  NOT =  "00"
004160         DISPLAY ファイル名Ｗ NC"Ｆオープンエラー" UPON CONS
004170         DISPLAY NC"状態キー：" 状態キー           UPON CONS
004180         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS
004190*-----------------------------------------*
004200         CALL "actcshm"  WITH C LINKAGE
004210*-----------------------------------------*
004220         ACCEPT  キー入力 FROM CONS
004230         PERFORM ファイル閉鎖
004240         MOVE ZERO TO PROGRAM-STATUS
004250         EXIT PROGRAM.
004260*================================================================*
004270 ファイル閉鎖 SECTION.
004280*
004310*================================================================*
004320 終了処理 SECTION.
004330*
004340     PERFORM ファイル閉鎖.
004350*================================================================*
004360 エラー表示 SECTION.
004370*
004380     DISPLAY ファイル名Ｗ NC"ファイル書込エラー"   UPON CONS.
004390     DISPLAY NC"状態キー：" 状態キー               UPON CONS.
004400     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
004410     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
004420*-----------------------------------------*
004430         CALL "actcshm"  WITH C LINKAGE
004440*-----------------------------------------*
004450     ACCEPT  キー入力 FROM CONS.
004460     PERFORM ファイル閉鎖.
004470     MOVE ZERO TO PROGRAM-STATUS
004480     EXIT PROGRAM.
004490*================================================================*
004500 負担率取得 SECTION.
004510*
004520     MOVE ZERO TO 負担率区分Ｗ.
004530     MOVE ZERO TO 本人負担率Ｗ.
004540     MOVE ZERO TO 家族負担率Ｗ.
004550*
004560     MOVE 連計－施術和暦 TO 受－施術和暦.
004570     MOVE 連計－施術年   TO 受－施術年.
004580     MOVE 連計－施術月   TO 受－施術月.
004590     MOVE 連計－患者番号 TO 受－患者番号.
004600     MOVE 連計－枝番     TO 受－枝番.
004610     READ ＥＸ受診者情報Ｆ
004620     INVALID KEY
004630*/エラー表示の修正
004640         PERFORM コンソールエラー確認
004650         DISPLAY "施術月の受診者情報がありません"
004660                 " 受診者№=" 連計－患者コード
004670                 " 施術年月=" 連計－施術年 連計－施術月 UPON CONS
004680*-----------------------------------------*
004690         CALL "actcshm"  WITH C LINKAGE
004700*-----------------------------------------*
004710*         ACCEPT  キー入力 FROM CONS
004720         PERFORM ファイル閉鎖
004730         MOVE ZERO TO PROGRAM-STATUS
004740         EXIT PROGRAM
004750     NOT INVALID KEY
004760         MOVE 受－甲乙区分 TO 甲乙区分Ｗ
004770     END-READ.
004780*
004790     MOVE 連計－保険種別   TO 保－保険種別.  
004800     MOVE 連計－保険者番号 TO 保－保険者番号.
004810     READ ＥＸ保険者マスタ
004820     INVALID KEY
004821*
004822*     保険分類　6:保険証忘れ（保険種別91）は、100
004824       IF 受－保険分類 = 6
004826          MOVE 100 TO 本人負担率Ｗ
004827          MOVE 100 TO 家族負担率Ｗ
004828       ELSE
004829*
004832*/エラー表示の修正
      */保険証画面からコンソールエラー表示を削除する↓↓↓/120928
004840*         PERFORM コンソールエラー確認
004850*         DISPLAY "保険者番号が登録されていません"
004860*                 " 受診者№="   連計－患者コード
004870*                 " 施術年月="   連計－施術年 連計－施術月
004880*                 " 保険者番号=" 連計－保険者番号  UPON CONS
004890**-----------------------------------------*
004900*         CALL "actcshm"  WITH C LINKAGE
004910**-----------------------------------------*
004920*         ACCEPT  キー入力 FROM CONS
               MOVE  "保険者番号が登録されていません" TO 連メ７－メッセージ１
               MOVE  SPACE                            TO 連メ７－メッセージ２
               CALL   "MSG007"
               CANCEL "MSG007"
      */保険証画面からコンソールエラー表示を削除する↑↑↑/120928
004930         PERFORM ファイル閉鎖
004940         MOVE ZERO TO PROGRAM-STATUS
004950         EXIT PROGRAM
004951       END-IF
004960     NOT INVALID KEY
004970*/年月毎持つデータの振り分け
004980* 特別負担
004990         MOVE 連計－保険種別   TO 保特－保険種別
005000         MOVE 連計－保険者番号 TO 保特－保険者番号
005010         MOVE 連計－施術和暦   TO 保特－開始和暦
005020         MOVE 連計－施術年     TO 保特－開始年
005030         MOVE 連計－施術月     TO 保特－開始月
005040         START ＥＸ保険者特別負担マスタ KEY IS <= 保特－保険種別 
005050                                              保特－保険者番号
005060                                              保特－開始和暦年月
005070                                              REVERSED
005080         END-START
005090         IF 状態キー = "00" 
005100             READ ＥＸ保険者特別負担マスタ NEXT
005110             NOT AT END
005120                 IF ( 保特－保険種別   = 連計－保険種別 ) AND
005130                    ( 保特－保険者番号 = 連計－保険者番号 )
005140                    MOVE 保特－負担率区分   TO 保負担率区分Ｗ
005150                    MOVE 保特－本人負担率   TO 保本人負担率Ｗ
005160                    MOVE 保特－家族負担率   TO 保家族負担率Ｗ
005170                    MOVE 保特－本人負担率乙 TO 保本人負担率乙Ｗ
005180                    MOVE 保特－家族負担率乙 TO 保家族負担率乙Ｗ
005190                 END-IF
005200             END-READ
005210         END-IF
005220* 船員公傷扱い
005230         IF ( 連計－保険種別     = 07 ) AND
005240            ( 連計－本人家族区分 = 1  ) AND
005250            ( 甲乙区分Ｗ         = 1  )
005260              MOVE ZERO   TO 本人負担率Ｗ
005270              MOVE 1      TO 負担率区分Ｗ
005280*19981210
005290* 自費(負担率マスタにて対応）
005300*         ELSE IF 連計－保険種別 = 90
005310*                  MOVE 100   TO 本人負担率Ｗ
005320*                  MOVE 1     TO 負担率区分Ｗ
005330*
005340         ELSE
005350*/資格証明の対応/を含む↓負担率区分=特殊負担率が設定されていても資格証明の時は全額負担
005360*             IF ( 保負担率区分Ｗ NOT = ZERO ) OR
005370*                ( 甲乙区分Ｗ     NOT = ZERO )
005380*/市町村国保か退職で資格証明区分が１の時、以外は資格証明区分の対象外にする
005390             IF (((受－保険種別 = 01)  AND (受－保険者番号(3:1) NOT = "3")) OR
005400                 ( 受－保険種別 = 08)) AND
005410                 ( 受－資格証明区分 = 1)
005420                 CONTINUE
005430             ELSE
005440                 MOVE ZERO TO 受－資格証明区分
005450             END-IF
005460             IF (( 保負担率区分Ｗ NOT = ZERO )      OR
005470                 ( 甲乙区分Ｗ     NOT = ZERO ))    AND
005480*                (((受－保険種別            = 01)   AND
005490*                 ( 受－保険者番号(3:1) NOT = "3"))  OR
005500*                 ( 受－保険種別            = 08 )) AND
005510                 ( 受－資格証明区分    NOT = 1)
005520*/1410高齢者、３才未満の対応0210
005530*/特別区分を優先するか、負担率区分を優先させるかは解らない。↓↓↓
005540                 AND (受－特別区分 = ZERO)
005550*/特別区分を優先するか、負担率区分を優先させるかは解らない。↑↑↑
005560                 IF 甲乙区分Ｗ NOT = 2
005570                     MOVE 保本人負担率Ｗ   TO 本人負担率Ｗ
005580                     MOVE 保家族負担率Ｗ   TO 家族負担率Ｗ
005590                 ELSE
005600                     MOVE 保本人負担率乙Ｗ TO 本人負担率Ｗ
005610                     MOVE 保家族負担率乙Ｗ TO 家族負担率Ｗ
005620                 END-IF
005630*          ELSE IF ( 保－負担率区分 NOT = ZERO ) OR
005640*                  ( 甲乙区分Ｗ     NOT = ZERO )
005650*                   IF 甲乙区分Ｗ NOT = 2
005660*                       MOVE 保－本人負担率   TO 本人負担率Ｗ
005670*                       MOVE 保－家族負担率   TO 家族負担率Ｗ
005680*                   ELSE
005690*                       MOVE 保－本人負担率乙 TO 本人負担率Ｗ
005700*                       MOVE 保－家族負担率乙 TO 家族負担率Ｗ
005710*                   END-IF
005720                   MOVE 1 TO 負担率区分Ｗ
005730               ELSE
005740*/資格証明の対応/市町村国保退職＆資格証明区分１の時は全額負担(91が資格証明)
005750                   IF (((受－保険種別 = 01)  AND (受－保険者番号(3:1) NOT = "3")) OR
005760                       ( 受－保険種別 = 08)) AND
005770                       ( 受－資格証明区分 = 1)
005780                       MOVE 91             TO 負率－保険種別
005790                   ELSE
005800                       MOVE 連計－保険種別 TO 負率－保険種別
005810*/1410高齢者、３才未満の対応0210↓↓↓↓↓↓
005820                       EVALUATE 受－特別区分
005830                       WHEN ZERO
005840                           CONTINUE
005850                       WHEN 1
005860                           MOVE 21 TO 負率－保険種別
005870*/前期高齢１割＋被爆の時、２割負担で計算する/080512
005880                           IF 受－助成種別 = 54
005890                               MOVE 22 TO 負率－保険種別
005900                           END-IF
005910                       WHEN 2
005920                           MOVE 22 TO 負率－保険種別
005930*/３歳未満２割を６に変更して高齢者３割を３で追加/0608
005940*                       WHEN 3
005950                       WHEN 6
005960                           MOVE 23 TO 負率－保険種別
005970*/高齢者３割の対応/0608
005980                       WHEN 3
005990                           MOVE 24 TO 負率－保険種別
006000                       WHEN OTHER
006010                           CONTINUE
006020                       END-EVALUATE
006030                   END-IF
006040*/1410高齢者、３才未満の対応0210↑↑↑↑↑↑
006050*                   MOVE 連計－保険種別 TO 負率－保険種別
006060*/保険種別も判定する/0609
006070                   MOVE 負率－保険種別 TO 保険種別ＣＷ
006080                   MOVE 連計－施術和暦 TO 負率－開始和暦 施術和暦ＣＷ
006090                   MOVE 連計－施術年   TO 負率－開始年   施術年ＣＷ
006100                   MOVE 連計－施術月   TO 負率－開始月   施術月ＣＷ
006110                   START ＥＸ負担率マスタ KEY IS <= 負率－保険種別 
006120                                                負率－開始和暦年月
006130                                                REVERSED
006140                   END-START
006150                   IF 状態キー = "00" 
006160                       READ ＥＸ負担率マスタ NEXT
006170                       AT END
006180*/エラー表示の修正
006190                           PERFORM コンソールエラー確認
006200                           DISPLAY "施術日に合った負担率がみつかりません"
006210                                   " 受診者№="   連計－患者コード
006220                                   " 施術年月="   連計－施術年 連計－施術月 UPON CONS
006230*-----------------------------------------*
006240                           CALL "actcshm"  WITH C LINKAGE
006250*-----------------------------------------*
006260*                           ACCEPT  キー入力 FROM CONS
006270                           PERFORM ファイル閉鎖
006280                           MOVE ZERO TO PROGRAM-STATUS
006290                           EXIT PROGRAM
006300                       NOT AT END
006310                           IF ( 施術和暦年月ＣＷ >= 負率－開始和暦年月 ) AND
006320                              ( 施術和暦年月ＣＷ <= 負率－終了和暦年月 )
006330*/保険種別も判定する/0609
006340                           AND (負率－保険種別 = 保険種別ＣＷ)
006350                               MOVE 負率－本人負担率 TO 本人負担率Ｗ
006360                               MOVE 負率－家族負担率 TO 家族負担率Ｗ
006370                           ELSE
006380*/不具合の修正(メッセージ追加)/0609
006390                               PERFORM コンソールエラー確認
006400                               DISPLAY "施術日に合った負担率がみつかりません"
006410                                       " 受診者№="   連計－患者コード
006420                                       " 施術年月="   連計－施術年 連計－施術月 UPON CONS
006430*-----------------------------------------*
006440                               CALL "actcshm"  WITH C LINKAGE
006450*-----------------------------------------*
006460*                               ACCEPT  キー入力 FROM CONS
006470                               PERFORM ファイル閉鎖
006480                               MOVE ZERO TO PROGRAM-STATUS
006490                               EXIT PROGRAM
006500                           END-IF
006510                       END-READ
006520                   END-IF
006530               END-IF
006540*         END-IF
006550         END-IF
006560     END-READ.
006570*
006580*/国保組合で負担なしの対応/0501
006590     IF 甲乙区分Ｗ = 9
006600         MOVE ZERO TO 本人負担率Ｗ 家族負担率Ｗ
006610     END-IF.
006620*
006630     EVALUATE 連計－本人家族区分
006640     WHEN 1
006650         MOVE 本人負担率Ｗ TO 負担率Ｗ 連Ｏ－負担率 連計－負担率
006660         MOVE 負担率区分Ｗ TO 連計－負担率区分
006670     WHEN 2
006680         MOVE 家族負担率Ｗ TO 負担率Ｗ 連Ｏ－負担率 連計－負担率
006690         MOVE 負担率区分Ｗ TO 連計－負担率区分
006700     WHEN OTHER
006710*/エラー表示の修正
      */保険証画面からコンソールエラー表示を削除する↓↓↓/120928
006720*         PERFORM コンソールエラー確認
006730*         DISPLAY "本人家族区分が設定されていません"
006740*                 " 受診者№="   連計－患者コード
006750*                 " 施術年月="   連計－施術年 連計－施術月 UPON CONS
006760**-----------------------------------------*
006770*         CALL "actcshm"  WITH C LINKAGE
006780**-----------------------------------------*
006790*         ACCEPT  キー入力 FROM CONS
               MOVE  "本人家族区分が設定されていません" TO 連メ７－メッセージ１
               MOVE  SPACE                              TO 連メ７－メッセージ２
      */コンソール画面に年月患者コードを表示する/130601
               STRING "受診者№="       DELIMITED BY SIZE
                       連計－患者コード DELIMITED BY SIZE
                     " 施術年月="       DELIMITED BY SIZE
                       連計－施術年     DELIMITED BY SIZE
                       連計－施術月     DELIMITED BY SIZE
                  INTO 連メ７－メッセージ２
               END-STRING
               CALL   "MSG007"
               CANCEL "MSG007"
      */保険証画面からコンソールエラー表示を削除する↑↑↑/120928
006800         PERFORM ファイル閉鎖
006810         MOVE ZERO TO PROGRAM-STATUS
006820         EXIT PROGRAM
006830     END-EVALUATE.
006840*
006850*/仕様変更0401↓↓↓
006860*/埼玉県医師国保組合(113019)埼玉県歯科医師国保組合(113027)関東信越税理士国保組合(113043)
006870*/埼玉県建設国保組合(113050)埼玉県土建国保組合(113068)
006880*/で埼玉県内の41老の場合、負担率を国保の標準の物にする。(３割)
006890     IF 受－保険者番号 = "113019" OR "113027" OR "113043" OR 
006900                         "113050" OR "113068"
006910         IF (受－助成種別                =  51 ) AND
006920            (受－費用負担者番号助成(3:2) = "11")
006930             MOVE 連計－保険種別 TO 負率－保険種別
006940*/1410高齢者、３才未満の対応0210↓↓↓↓↓↓
006950             EVALUATE 受－特別区分
006960             WHEN ZERO
006970                 CONTINUE
006980             WHEN 1
006990                 MOVE 21 TO 負率－保険種別
007000             WHEN 2
007010                 MOVE 22 TO 負率－保険種別
007020*/３歳未満２割を６に変更して高齢者３割を３で追加/0608
007030*             WHEN 3
007040             WHEN 6
007050                 MOVE 23 TO 負率－保険種別
007060*/高齢者３割の対応/0608
007070             WHEN 3
007080                 MOVE 24 TO 負率－保険種別
007090             WHEN OTHER
007100                 CONTINUE
007110             END-EVALUATE
007120*/1410高齢者、３才未満の対応0210↑↑↑↑↑↑
007130*                   MOVE 連計－保険種別 TO 負率－保険種別
007140             MOVE 連計－施術和暦 TO 負率－開始和暦 施術和暦ＣＷ
007150             MOVE 連計－施術年   TO 負率－開始年   施術年ＣＷ
007160             MOVE 連計－施術月   TO 負率－開始月   施術月ＣＷ
007170             START ＥＸ負担率マスタ KEY IS <= 負率－保険種別 
007180                                          負率－開始和暦年月
007190                                          REVERSED
007200             END-START
007210             IF 状態キー = "00" 
007220                 READ ＥＸ負担率マスタ NEXT
007230                 AT END
007240*/エラー表示の修正
007250                     PERFORM コンソールエラー確認
007260                     DISPLAY "施術日に合った負担率がみつかりません"
007270                             " 受診者№="   連計－患者コード
007280                             " 施術年月="   連計－施術年 連計－施術月 UPON CONS
007290*-----------------------------------------*
007300                     CALL "actcshm"  WITH C LINKAGE
007310*-----------------------------------------*
007320*                     ACCEPT  キー入力 FROM CONS
007330                     PERFORM ファイル閉鎖
007340                     MOVE ZERO TO PROGRAM-STATUS
007350                     EXIT PROGRAM
007360                 NOT AT END
007370                     IF ( 施術和暦年月ＣＷ >= 負率－開始和暦年月 ) AND
007380                        ( 施術和暦年月ＣＷ <= 負率－終了和暦年月 )
007390                         MOVE 負率－本人負担率 TO 本人負担率Ｗ
007400                         MOVE 負率－家族負担率 TO 家族負担率Ｗ
007410*
007420                         EVALUATE 連計－本人家族区分
007430                         WHEN 1
007440                             MOVE 本人負担率Ｗ TO 負担率Ｗ 連Ｏ－負担率 連計－負担率
007450                             MOVE ZERO         TO 連計－負担率区分
007460                         WHEN 2
007470                             MOVE 家族負担率Ｗ TO 負担率Ｗ 連Ｏ－負担率 連計－負担率
007480                             MOVE ZERO         TO 連計－負担率区分
007490                         WHEN OTHER
007500*                    /エラー表示の修正
007510                             PERFORM コンソールエラー確認
007520                             DISPLAY "本人家族区分が設定されていません"
007530                                     " 受診者№="   連計－患者コード
007540                                     " 施術年月="   連計－施術年 連計－施術月 UPON CONS
007550*-----------------------------------------*
007560                             CALL "actcshm"  WITH C LINKAGE
007570*-----------------------------------------*
007580*                             ACCEPT  キー入力 FROM CONS
007590                             PERFORM ファイル閉鎖
007600                             MOVE ZERO TO PROGRAM-STATUS
007610                             EXIT PROGRAM
007620                         END-EVALUATE
007630*
007640                     END-IF
007650                 END-READ
007660             END-IF
007670         END-IF
007680     END-IF.
007690*/仕様変更0401↑↑↑
007700*/自費学校共済の対応/0409
007710     IF (受－保険種別   = 90) AND
007720        (受－保険外区分 = 1 )
007730         MOVE ZERO TO 負担率Ｗ
007740     END-IF.
007750*/9:０割の対応/0609
007760     IF 受－特別区分 = 9
007770         MOVE ZERO TO 負担率Ｗ
007780     END-IF.
007781*
007790*================================================================*
007800 負担額計算 SECTION.
007810******************************************************************
007820* ☆注意☆
007830* 窓口会計・カルテは、負担金の１円の位を四捨五入する（１０円単位）
007840* レセプトは、負担金の小数点第１位を切り上げる
007850******************************************************************
007860*
007870     MOVE ZERO TO 負担額Ｗ.
007880     MOVE ZERO TO 請求額Ｗ.
007890****************************************************************
007900* 負担金計算方法の指定  負担金計算区分  費用・請求額   負担額  *
007910* 　　　　　　　　　　　　　　０　　　　　１円単位    １円単位 *
007920*              　　　　 　　　１　　　　１０円単位             *
007930*              　　　　　　　 ２　　　　　１円単位  １０円単位 *
007940****************************************************************
007950*****************************************
007960* 負担額１０円単位（１円の位を四捨五入）*
007970*****************************************
007980     EVALUATE 連計－負担金計算区分
007990     WHEN 2
008000* 負担額は、小数点以下第１位を切り上げる
008010         COMPUTE 負担率中間結果Ｗ =  負担率Ｗ / 100
008020         COMPUTE 負担額Ｗ１０ = 連計－費用額 * 負担率中間結果Ｗ
008030         COMPUTE 負担額Ｗ = 負担額Ｗ１０ * 10
008040         DIVIDE  負担額Ｗ BY 10 GIVING    商Ｗ
008050                                  REMAINDER 剰余Ｗ
008060         END-DIVIDE
008070         IF 剰余Ｗ = ZERO
008080             COMPUTE 負担率中間結果Ｗ =  負担率Ｗ / 100
008090             COMPUTE 負担額Ｗ = 連計－費用額 * 負担率中間結果Ｗ
008100*
008110* 負担額Ｗが１円単位の状態で請求額を計算
008120             COMPUTE 請求額Ｗ = 連計－費用額 - 負担額Ｗ
008130*
008140             COMPUTE 負担額Ｗ２ ROUNDED = 負担額Ｗ / 10
008150             COMPUTE 負担額Ｗ = 負担額Ｗ２ * 10
008160         ELSE
008170             COMPUTE 負担率中間結果Ｗ =  負担率Ｗ / 100
008180             COMPUTE 負担額Ｗ = 連計－費用額 * 負担率中間結果Ｗ
008190* 小数点以下第１位が発生した場合、整数部に１を加え切り上げる
008200*/日計の場合負担額を切り上げず請求額から１を引く↑は取り消し
008210*             COMPUTE 負担額Ｗ = 負担額Ｗ + 1
008220* 負担額Ｗが１円単位の状態で請求額を計算
008230             COMPUTE 請求額Ｗ = 連計－費用額 - 負担額Ｗ - 1
008240*
008250             COMPUTE 負担額Ｗ２ ROUNDED = 負担額Ｗ / 10
008260             COMPUTE 負担額Ｗ = 負担額Ｗ２ * 10
008270         END-IF
008280*
008290******************
008300* 負担額１円単位 *
008310******************
008320     WHEN ZERO
008330         COMPUTE 負担率中間結果Ｗ = ( 負担率Ｗ / 100 )
008340         COMPUTE 負担額Ｗ１０ = 連計－費用額 * 負担率中間結果Ｗ
008350         COMPUTE 負担額Ｗ = 負担額Ｗ１０ * 10
008360         DIVIDE  負担額Ｗ BY 10 GIVING    商Ｗ
008370                                REMAINDER 剰余Ｗ
008380         END-DIVIDE
008390         IF 剰余Ｗ = ZERO
008400             COMPUTE 負担率中間結果Ｗ =  負担率Ｗ / 100
008410             COMPUTE 負担額Ｗ = 連計－費用額 * 負担率中間結果Ｗ
008420             COMPUTE 請求額Ｗ = 連計－費用額 - 負担額Ｗ
008430         ELSE
008440* 一部負担金の端数切り上げ
008450             COMPUTE 負担率中間結果Ｗ =  負担率Ｗ / 100
008460             COMPUTE 負担額Ｗ = 連計－費用額 * 負担率中間結果Ｗ
008470* DEBUG
008480*     DISPLAY NC"負担額Ｗ－１" 負担額Ｗ UPON MSGBOX
008490*
008500             COMPUTE 負担額Ｗ = 負担額Ｗ + 1
008510* DEBUG
008520*     DISPLAY NC"負担額Ｗ－２" 負担額Ｗ UPON MSGBOX
008530*
008540             COMPUTE 請求額Ｗ = 連計－費用額 - 負担額Ｗ
008550         END-IF
008551*
008552*---10円単位でも計算 ------------------------------------------*
008555         MOVE ZERO TO 負担額日計窓口Ｗ 負担額日計窓口Ｗ２
008583         COMPUTE 負担率中間結果Ｗ =  負担率Ｗ / 100
008584         COMPUTE 負担額日計窓口Ｗ = 連計－費用額 * 負担率中間結果Ｗ
008585         COMPUTE 負担額日計窓口Ｗ２ ROUNDED = 負担額日計窓口Ｗ / 10
008586         COMPUTE 負担額日計窓口Ｗ = 負担額日計窓口Ｗ２ * 10
008587         MOVE 負担額日計窓口Ｗ TO 連計日計窓口－負担額
008588*--------------------------------------------------------------*
008589*
008592     WHEN OTHER
008593*/エラー表示の修正
008594         PERFORM コンソールエラー確認
008595         DISPLAY "負担金計算区分が指定されていません"
008600                 " 受診者№="   連計－患者コード
008610                 " 施術年月="   連計－施術年 連計－施術月 UPON CONS
008620*-----------------------------------------*
008630         CALL "actcshm"  WITH C LINKAGE
008640*-----------------------------------------*
008650*         ACCEPT  キー入力 FROM CONS
008660         PERFORM ファイル閉鎖
008670         MOVE ZERO TO PROGRAM-STATUS
008680         EXIT PROGRAM
008690     END-EVALUATE.
008700*
008710     MOVE 負担額Ｗ TO 連計－負担額.
008720     MOVE 請求額Ｗ TO 連計－請求額.
008730*
008740* debug
008750*     DISPLAY NC"負担額Ｗ" 負担額Ｗ UPON MSGBOX.
008760*     DISPLAY NC"請求額Ｗ" 請求額Ｗ UPON MSGBOX.
008770*
008780*
008790*================================================================*
008800 コンソールエラー確認 SECTION.
008810*
008820     IF エラー表示Ｆ = ZERO
008830         MOVE  "エラーが発生したのでコンソール画面の内容" TO 連メ７－メッセージ１
008840         MOVE  "を確認し、修正してください。"             TO 連メ７－メッセージ２
008850         CALL   "MSG007"
008860         CANCEL "MSG007"
008870     END-IF.
008880     MOVE 1 TO エラー表示Ｆ.
008890*================================================================*
008900******************************************************************
008910 END PROGRAM HUT0210.
008920******************************************************************
