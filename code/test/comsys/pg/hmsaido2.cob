000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             HMSAIDO2.
000060 AUTHOR.                 岡田 憲和
000070*
000080*----------------------------------------------------------------*
000090*   鍼灸マ   【今回同意期間＆前回同意期間算出】
000100*----------------------------------------------------------------*
000110 DATE-WRITTEN.           2018-03-09
000120 DATE-COMPILED.          2018-03-09
000130*----------------------------------------------------------------*
000143******************************************************************
000150*            ENVIRONMENT         DIVISION                        *
000160******************************************************************
000170 ENVIRONMENT             DIVISION.
000180 CONFIGURATION           SECTION.
000190 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000200 OBJECT-COMPUTER.        FMV-DESKPOWER.
000210 SPECIAL-NAMES.          CONSOLE  IS  CONS
000220                         SYSERR   IS  MSGBOX.
000230 INPUT-OUTPUT            SECTION.
000240 FILE-CONTROL.
000480     SELECT  ＨレセプトＦ    ASSIGN      TO        HRECEL
000490                             ORGANIZATION             IS  INDEXED
000500                             ACCESS MODE              IS  DYNAMIC
000510                             RECORD KEY               IS  Ｈレセ−施術区分
000520                                                          Ｈレセ−施術和暦年月
000530                                                          Ｈレセ−患者コード
000540                                                          Ｈレセ−レセ種別
000550                             ALTERNATE RECORD KEY     IS  Ｈレセ−施術区分
000560                                                          Ｈレセ−患者コード
000570                                                          Ｈレセ−施術和暦年月
000580                                                          Ｈレセ−レセ種別
000590                             ALTERNATE RECORD KEY     IS  Ｈレセ−施術和暦年月
000600                                                          Ｈレセ−患者コード
000610                                                          Ｈレセ−レセ種別
000620                                                          Ｈレセ−施術区分
000630                             ALTERNATE RECORD KEY     IS  Ｈレセ−請求対象区分
000640                                                          Ｈレセ−請求和暦年月
000650                                                          Ｈレセ−施術区分
000660                                                          Ｈレセ−施術和暦年月
000670                                                          Ｈレセ−患者コード
000680                                                          Ｈレセ−レセ種別
000690                             FILE STATUS              IS  状態キー
000700                             LOCK        MODE         IS  AUTOMATIC.
000710*
000720******************************************************************
000730*                      DATA DIVISION                             *
000740******************************************************************
000750 DATA                    DIVISION.
000760 FILE                    SECTION.
000860*
000870 FD  ＨレセプトＦ        BLOCK   CONTAINS   1   RECORDS.
000880     COPY H_RECE     OF  XFDLIB  JOINING   Ｈレセ  AS  PREFIX.
000890*
000900******************************************************************
000910*                WORKING-STORAGE SECTION                         *
000920******************************************************************
000930 WORKING-STORAGE         SECTION.
000940 01 キー入力                           PIC X    VALUE SPACE.
000950 01 状態キー                           PIC X(2) VALUE SPACE.
000960 01 終了フラグ                         PIC X(3) VALUE SPACE.
000970 01 ファイル名                         PIC N(10) VALUE SPACE.
001000*
001001 01 プログラム名Ｗ PIC X(8) VALUE SPACE.
001002*
001003*/ 連結項目退避 /*
001010 01 施術区分ＷＲ                       PIC 9(1).
001014 01 施術和暦年月ＷＲ.
001022    03 施術和暦ＷＲ                    PIC 9.
001032    03 施術年月ＷＲ.
001042       05 施術年ＷＲ                   PIC 9(2).
001052       05 施術月ＷＲ                   PIC 9(2).
001062 01 患者コードＷＲ.
001072    03 患者番号ＷＲ                    PIC 9(6).
001082    03 枝番ＷＲ                        PIC X.
001092 01 レセ種別ＷＲ                       PIC 9(2).
001102 01 負傷主キーＷＲ                     PIC 9(8).
001112*
001481 01 負傷主キーＷ                       PIC 9(8).
001482*
001483 01 今回同意和暦年月日Ｗ               PIC 9(7).
001485 01 施術内容区分Ｗ                     PIC 9(2).
001486*
001487********************************
001488* 再同意予定日算出(処理前保存) *
001489********************************
001490 01 保存連再同−キー.
001491*/ in
001492    03 保存連再同−再同意和暦年月日      PIC 9(7).
001493    03 保存連再同−負傷主キー            PIC 9(8).
001494*
001495    03 保存連再同−施術区分              PIC 9(1).
001496    03 保存連再同−施術内容区分          PIC 9(2).
001497*/ out
001498    03 保存連再同−再同意予定和暦年月日.
001499       05 保存連再同−再同意予定和暦年月.
001500          07 保存連再同−再同意予定和暦  PIC 9.
001501          07 保存連再同−再同意予定年    PIC 9(2).
001502          07 保存連再同−再同意予定月    PIC 9(2).
001503       05 保存連再同−再同意予定日       PIC 9(2).
001504    03 保存連再同−初療和暦年月日.
001505       05 保存連再同−初療和暦年月.
001506          07 保存連再同−初療和暦        PIC 9.
001507          07 保存連再同−初療年          PIC 9(2).
001508          07 保存連再同−初療月          PIC 9(2).
001509       05 保存連再同−初療日             PIC 9(2).
001510*/  ( 1:変形徒手あり )
001511    03 保存連再同−変形徒手フラグ        PIC 9(1).
001512*
001513******************************************************************
001514*                          連結項目                              *
001515******************************************************************
001516************************
001520* 今回前回同意期間算出 *
001530************************
001540 01 連再同２−キー IS EXTERNAL.
001550*/ in
001560*   入力はＨレセプトＦの主キー
001561    03 連再同２−施術区分                PIC 9.
001562    03 連再同２−施術和暦年月.
001563       05 連再同２−施術和暦             PIC 9.
001564       05 連再同２−施術年月.
001565         07 連再同２−施術年             PIC 9(2).
001566         07 連再同２−施術月             PIC 9(2).
001567    03 連再同２−患者コード.
001568       05 連再同２−患者番号             PIC 9(6).
001569       05 連再同２−枝番                 PIC X.
001571    03 連再同２−レセ種別                PIC 9(2).
001610    03 連再同２−負傷主キー              PIC 9(8).
001611*
001612*/ out
001620    03 連再同２−今回同意開始和暦年月日.
001630       05 連再同２−今回同意開始和暦年月.
001640          07 連再同２−今回同意開始和暦  PIC 9.
001650          07 連再同２−今回同意開始年    PIC 9(2).
001660          07 連再同２−今回同意開始月    PIC 9(2).
001670       05 連再同２−今回同意開始日       PIC 9(2).
001740    03 連再同２−今回同意終了和暦年月日.
001741       05 連再同２−今回同意終了和暦年月.
001742          07 連再同２−今回同意終了和暦  PIC 9.
001743          07 連再同２−今回同意終了年    PIC 9(2).
001744          07 連再同２−今回同意終了月    PIC 9(2).
001745       05 連再同２−今回同意終了日       PIC 9(2).
001746    03 連再同２−前回同意開始和暦年月日.
001747       05 連再同２−前回同意開始和暦年月.
001748          07 連再同２−前回同意開始和暦  PIC 9.
001749          07 連再同２−前回同意開始年    PIC 9(2).
001750          07 連再同２−前回同意開始月    PIC 9(2).
001751       05 連再同２−前回同意開始日       PIC 9(2).
001752    03 連再同２−前回同意終了和暦年月日.
001753       05 連再同２−前回同意終了和暦年月.
001754          07 連再同２−前回同意終了和暦  PIC 9.
001755          07 連再同２−前回同意終了年    PIC 9(2).
001756          07 連再同２−前回同意終了月    PIC 9(2).
001757       05 連再同２−前回同意終了日       PIC 9(2).
001758*/  ( 1:変形徒手あり )
001759    03 連再同２−今回変形徒手フラグ      PIC 9(1).
001760    03 連再同２−前回変形徒手フラグ      PIC 9(1).
001770*
002113********************
002114* 再同意予定日算出 *
002115********************
002116 01 連再同−キー IS EXTERNAL.
002117*/ in
002118    03 連再同−再同意和暦年月日      PIC 9(7).
002119    03 連再同−負傷主キー            PIC 9(8).
002120*
002121    03 連再同−施術区分              PIC 9(1).
002122    03 連再同−施術内容区分          PIC 9(2).
002123*/ out
002124    03 連再同−再同意予定和暦年月日.
002125       05 連再同−再同意予定和暦年月.
002126          07 連再同−再同意予定和暦  PIC 9.
002127          07 連再同−再同意予定年    PIC 9(2).
002128          07 連再同−再同意予定月    PIC 9(2).
002129       05 連再同−再同意予定日       PIC 9(2).
002130    03 連再同−初療和暦年月日.
002131       05 連再同−初療和暦年月.
002132          07 連再同−初療和暦        PIC 9.
002133          07 連再同−初療年          PIC 9(2).
002134          07 連再同−初療月          PIC 9(2).
002135       05 連再同−初療日             PIC 9(2).
002136*/  ( 1:変形徒手あり )
002137    03 連再同−変形徒手フラグ        PIC 9(1).
002138*
002139******************************************************************
002140*                      PROCEDURE  DIVISION                       *
002141******************************************************************
002142 PROCEDURE               DIVISION.
002150************
002160*           *
002170* 初期処理   *
002180*           *
002190************
002200     PERFORM ファイルオープン.
002210     PERFORM 連結項目待避.
002223************
002230*           *
002240* 主処理     *
002250*           *
002260************
002271     INITIALIZE 連再同２−キー.
002280
002290     PERFORM 今回同意期間取得.
002301
002302*    レセファイルが読まない場合、負傷主キーは画面引渡し値とする。
002303     IF Ｈレセ−負傷主キー NOT = ZERO
002304        MOVE Ｈレセ−負傷主キー     TO 負傷主キーＷ
002305     ELSE
002306        MOVE 負傷主キーＷＲ         TO 負傷主キーＷ
002307     END-IF.
002310     PERFORM 前回同意期間取得.
002762*
002770************
002780*           *
002790* 終了処理   *
002800*           *
002810************
002830     PERFORM 終了処理.
002840
002841*    HMSAIDOの値を復元(他処理で直接共通変数を読んでいてもおかしくならない様にする為)
002842     MOVE 保存連再同−キー           TO 連再同−キー.
002843
002844     MOVE ZERO TO PROGRAM-STATUS.
002851     EXIT PROGRAM.
002860*
002870*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002880*================================================================*
002890 ファイルオープン SECTION.
002900*================================================================*
003000     OPEN INPUT ＨレセプトＦ.
003010         MOVE NC"ＨレセプトＦ" TO ファイル名.
003020         PERFORM オープンチェック.
003030*
003040*================================================================*
003050 オープンチェック SECTION.
003060*
003070     IF ( 状態キー  NOT =  "00" )
003080         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
003090         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003100         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003110                                                 UPON CONS
003120*-----------------------------------------*
003130         CALL "actcshm"  WITH C LINKAGE
003140*-----------------------------------------*
003150         ACCEPT  キー入力 FROM CONS
003160         PERFORM ファイル閉鎖
003170         MOVE 99 TO PROGRAM-STATUS
003180         EXIT PROGRAM.
003190*
003200*================================================================*
003210 連結項目待避 SECTION.
003220*================================================================*
003264*    HMSAIDOの値を保存
003265     MOVE 連再同−キー             TO 保存連再同−キー. 
003266*
003267*    HMSAIDO2の値を保存
003268     MOVE 連再同２−施術区分       TO 施術区分ＷＲ.
003269     MOVE 連再同２−施術和暦年月   TO 施術和暦年月ＷＲ.
003280     MOVE 連再同２−患者コード     TO 患者コードＷＲ.
003290     MOVE 連再同２−レセ種別       TO レセ種別ＷＲ.
003291     MOVE 連再同２−負傷主キー     TO 負傷主キーＷＲ.
003292*
003300*================================================================*
003310 今回同意期間取得 SECTION.
003320*================================================================*
003330     INITIALIZE Ｈレセ−レコード.
003332     MOVE 施術区分ＷＲ             TO Ｈレセ−施術区分.
003333     MOVE 施術和暦年月ＷＲ         TO Ｈレセ−施術和暦年月.
003334     MOVE 患者コードＷＲ           TO Ｈレセ−患者コード.
003335     MOVE レセ種別ＷＲ             TO Ｈレセ−レセ種別.
003340*
003350     READ ＨレセプトＦ
003360     INVALID KEY
003370          MOVE SPACE TO Ｈレセ−レコード
003380          INITIALIZE Ｈレセ−レコード
003390     END-READ.
003402*
003410     INITIALIZE 連再同−キー.
003411     MOVE Ｈレセ−同意和暦年月日   TO 連再同−再同意和暦年月日.
003412     MOVE Ｈレセ−同意和暦年月日   TO 今回同意和暦年月日Ｗ.
003413     MOVE Ｈレセ−負傷主キー       TO 連再同−負傷主キー.
003414     MOVE Ｈレセ−施術区分         TO 連再同−施術区分.
003415     MOVE Ｈレセ−施術内容区分     TO 連再同−施術内容区分.
003416     MOVE Ｈレセ−施術内容区分     TO 施術内容区分Ｗ.
003417*
003418     MOVE "HMSAIDO" TO プログラム名Ｗ.
003419     CALL プログラム名Ｗ.
003420     CANCEL プログラム名Ｗ.
003421     MOVE Ｈレセ−同意和暦年月日       TO 連再同２−今回同意開始和暦年月日.
003422     MOVE 連再同−再同意予定和暦年月日 TO 連再同２−今回同意終了和暦年月日.
003423     MOVE 連再同−変形徒手フラグ       TO 連再同２−今回変形徒手フラグ.
003425*
003430*================================================================*
003431 前回同意期間取得 SECTION.
003432*================================================================*
003433     INITIALIZE Ｈレセ−レコード.
003434     MOVE 施術区分ＷＲ             TO Ｈレセ−施術区分.
003435     MOVE 施術和暦年月ＷＲ         TO Ｈレセ−施術和暦年月.
003436     MOVE 患者コードＷＲ           TO Ｈレセ−患者コード.
003437     MOVE 3                        TO Ｈレセ−レセ種別.
003438*
003440     START ＨレセプトＦ     KEY IS <  Ｈレセ−施術区分
003441                                      Ｈレセ−患者コード
003442                                      Ｈレセ−施術和暦年月
003443                                      Ｈレセ−レセ種別
003444                                      REVERSED
003445     END-START.
003447     IF 状態キー = "00"
003448        MOVE SPACE TO 終了フラグ
003449        PERFORM ＨレセプトＦ読込
003451*       繰り返し（同一患者番号６けたで同一負傷主キー　枝番考慮）
003458        PERFORM UNTIL ( Ｈレセ−施術区分        NOT = 施術区分ＷＲ           ) OR
003459                      ( Ｈレセ−患者番号        NOT = 患者番号ＷＲ           ) OR
003460                      ( Ｈレセ−負傷主キー      NOT = 負傷主キーＷ           ) OR
003461                      ( Ｈレセ−同意和暦年月日  NOT = 今回同意和暦年月日Ｗ   ) OR
003462                      ( Ｈレセ−施術内容区分    NOT = 施術内容区分Ｗ         ) OR
003463                      ( 終了フラグ              NOT = SPACE                  )
003464            PERFORM ＨレセプトＦ読込
003465        END-PERFORM
003467*
003468        IF ( Ｈレセ−施術区分    = 施術区分ＷＲ ) AND
003469           ( Ｈレセ−患者番号    = 患者番号ＷＲ ) AND
003470           ( Ｈレセ−負傷主キー  = 負傷主キーＷ ) AND
003471           ( 終了フラグ          = SPACE        )
003473*           前同開の有効期限を取得
003474            INITIALIZE 連再同−キー
003475            MOVE Ｈレセ−同意和暦年月日 TO 連再同−再同意和暦年月日
003476            MOVE Ｈレセ−負傷主キー     TO 連再同−負傷主キー
003477            MOVE Ｈレセ−施術区分       TO 連再同−施術区分
003478            MOVE Ｈレセ−施術内容区分   TO 連再同−施術内容区分
003479*
003480            MOVE "HMSAIDO" TO プログラム名Ｗ
003481            CALL プログラム名Ｗ
003482            CANCEL プログラム名Ｗ
003483            MOVE Ｈレセ−同意和暦年月日       TO 連再同２−前回同意開始和暦年月日
003484            MOVE 連再同−再同意予定和暦年月日 TO 連再同２−前回同意終了和暦年月日
003485            MOVE 連再同−変形徒手フラグ       TO 連再同２−前回変形徒手フラグ
003486        END-IF
003487     END-IF.
003488*
003489*================================================================*
003490 ＨレセプトＦ読込 SECTION.
003491*
003492     READ ＨレセプトＦ NEXT
003496     AT END
003497        MOVE "YES" TO 終了フラグ
003498        INITIALIZE Ｈレセ−レコード
003499     NOT AT END
003500        PERFORM UNTIL (Ｈレセ−レセ種別 NOT = 3) OR (終了フラグ NOT = SPACE)
003501           READ ＨレセプトＦ NEXT
003502           AT END
003503              MOVE "YES" TO 終了フラグ
003504              INITIALIZE Ｈレセ−レコード
003505           END-READ
003506        END-PERFORM
003507     END-READ.
003508*
005050*================================================================*
005060 終了処理 SECTION.
005070*================================================================*
005080     PERFORM ファイル閉鎖.
005090*
005100*================================================================*
005110 ファイル閉鎖 SECTION.
005120*
005130     CLOSE ＨレセプトＦ.
005140*
005150*================================================================*
005160******************************************************************
005170 END PROGRAM HMSAIDO2.
005180******************************************************************
005190
