000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             KJIHI.
000060 AUTHOR.                 佐野 誠
000070*
000080*------------------------------------------------------------------*
000090* 計算プログラム：自費用ＰＧ
000091*
000110*------------------------------------------------------------------*
000120 DATE-WRITTEN.           2009-10-05
000130 DATE-COMPILED.          2009-10-05
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000260*
000842     SELECT  ＨレセプトＦ    ASSIGN      TO        HRECEL
000843                             ORGANIZATION             IS  INDEXED
000844                             ACCESS MODE              IS  DYNAMIC
000845                             RECORD KEY               IS  Ｈレセ−施術区分
000846                                                          Ｈレセ−施術和暦年月
000847                                                          Ｈレセ−患者コード
000848                                                          Ｈレセ−レセ種別
000849                             ALTERNATE RECORD KEY     IS  Ｈレセ−施術区分
000850                                                          Ｈレセ−患者コード
000851                                                          Ｈレセ−施術和暦年月
000852                                                          Ｈレセ−レセ種別
000853                             ALTERNATE RECORD KEY     IS  Ｈレセ−施術和暦年月
000854                                                          Ｈレセ−患者コード
000855                                                          Ｈレセ−レセ種別
000856                                                          Ｈレセ−施術区分
000857                             ALTERNATE RECORD KEY     IS  Ｈレセ−請求対象区分
000858                                                          Ｈレセ−請求和暦年月
000859                                                          Ｈレセ−施術区分
000860                                                          Ｈレセ−施術和暦年月
000861                                                          Ｈレセ−患者コード
000862                                                          Ｈレセ−レセ種別
000863                             FILE STATUS              IS  状態キー
000864                             LOCK        MODE         IS  AUTOMATIC.
000865*
000890     SELECT  Ｈ日計データＦ  ASSIGN      TO        HNIKEIL
000891                             ORGANIZATION             IS  INDEXED
000892                             ACCESS MODE              IS  DYNAMIC
000893                             RECORD KEY               IS  Ｈ日−施術区分
000894                                                          Ｈ日−施術和暦年月日
000895                                                          Ｈ日−患者コード
000896                             ALTERNATE RECORD KEY     IS  Ｈ日−施術区分
000897                                                          Ｈ日−患者コード
000898                                                          Ｈ日−施術和暦年月日
000899                             ALTERNATE RECORD KEY     IS  Ｈ日−施術和暦年月日
000900                                                          Ｈ日−登録順
000901                             FILE STATUS              IS  状態キー
000902                             LOCK        MODE         IS  AUTOMATIC.
000903*
000904******************************************************************
000905*                      DATA DIVISION                             *
000906******************************************************************
000907 DATA                    DIVISION.
000908 FILE                    SECTION.
000909*
001081 FD  ＨレセプトＦ        BLOCK   CONTAINS   1   RECORDS.
001082     COPY H_RECE     OF  XFDLIB  JOINING   Ｈレセ  AS  PREFIX.
001087 FD  Ｈ日計データＦ      BLOCK   CONTAINS   1   RECORDS.
001088     COPY H_NIKEI    OF  XFDLIB  JOINING   Ｈ日   AS  PREFIX.
001089*
001090*---------------------------------------------------------------*
002090******************************************************************
002100*                WORKING-STORAGE SECTION                         *
002110******************************************************************
002120 WORKING-STORAGE         SECTION.
002130 01 キー入力                           PIC X    VALUE SPACE.
002140 01 状態キー                           PIC X(2) VALUE SPACE.
002150 01 終了フラグ                         PIC X(3) VALUE SPACE.
002170 01 ファイル名                         PIC N(10) VALUE SPACE.
002171 01 カウンタ                           PIC 9(2) VALUE ZERO.
002180*
002419*---------------------------------------------------------------------* 
002429 01 計算合計額Ｗ                       PIC 9(6) VALUE ZERO.
002446*
002453*---------------------------------------------------------------------* 
002524*
002580*
002590******************************************************************
002600*                          連結項目                              *
002610******************************************************************
002620*
002637*
002638 01 連メ−キー IS EXTERNAL.
002639    03 連メ−メッセージ                PIC N(20).
002640*
002641*--------------------------------------------------------
002642*
002660*
003532* 計算連携キー
003533 01 Ｈ連計算−キー IS EXTERNAL.
003534* / inパラメタ /
003535   03 Ｈ連計算−施術区分                   PIC 9.
003536   03 Ｈ連計算−施術和暦年月日.
003537      05 Ｈ連計算−施術和暦年月.
003538        07 Ｈ連計算−施術和暦              PIC 9.
003539        07 Ｈ連計算−施術年月.
003540          09 Ｈ連計算−施術年              PIC 9(2).
003541          09 Ｈ連計算−施術月              PIC 9(2).
003542      05 Ｈ連計算−施術日                  PIC 9(2).
003543   03 Ｈ連計算−患者コード.
003544      05 Ｈ連計算−患者番号                PIC 9(6).
003545      05 Ｈ連計算−枝番                    PIC X.
003546   03 Ｈ連計算−保険種別                   PIC 9(2).
003547* / 内部パラメタ/
003548   03 Ｈ連計算−内部パラメタ.
003550      05 Ｈ連計算−負担金計算区分          PIC 9.
003551      05 Ｈ連計算−費用額                  PIC 9(6).
003552*  初検日に"YES"(負担区分1,2,3,4)
003553      05 Ｈ連計算−初検日フラグ            PIC X(3).
003554*  当月の初回なら"YES"(負担区分5)
003555      05 Ｈ連計算−初診療フラグ            PIC X(3).
003556*  初検料の金額を転記(負担区分1)
003557      05 Ｈ連計算−初検料                  PIC 9(4).
003558*  当月の当日までの実日数(枝番も含む)(負担区分6,8,13)
003559      05 Ｈ連計算−実日数                  PIC 9(2).
003560*  日整静岡乳幼児用。当日の負担のありなし  ０：なし　１：あり (負担区分7)
003561      05 Ｈ連計算−その他計算区分１        PIC 9.
003562*  out
003563      05 Ｈ連計算−負担額                  PIC 9(6).
003564      05 Ｈ連計算−請求額                  PIC 9(6).
003565      05 Ｈ連計算−負担額助成              PIC 9(5).
003566      05 Ｈ連計算−請求額助成              PIC 9(5).
003567      05 Ｈ連計算−負担率                  PIC 9(3).
003568*
003569**
003586*
003587******************************************************************
003588*                      PROCEDURE  DIVISION                       *
003589******************************************************************
003590 PROCEDURE               DIVISION.
003591************
003592*           *
003593* 初期処理   *
003594*           *
003595************
003596     PERFORM 初期化.
003600************
003601*           *
003602* 主処理     *
003603*           *
003604************
003611     PERFORM 日計データ処理.
003612*   上記の後
003616     PERFORM レセプト処理.
003620************
003630*           *
003640* 終了処理   *
003650*           *
003660************
003670     PERFORM 終了処理.
003680     MOVE ZERO TO PROGRAM-STATUS.
003690     EXIT PROGRAM.
003700*
003710*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003720*================================================================*
003730 初期化 SECTION.
003740*
003741*   / 内部パラメタの初期化 /
003742     INITIALIZE Ｈ連計算−内部パラメタ.
003743*
003746*
003750     PERFORM ファイルオープン.
003844*
003855*
003857*================================================================*
003860 ファイルオープン SECTION.
003870*
004025*
004032     OPEN INPUT Ｈ日計データＦ.
004033         MOVE NC"Ｈ日計データＦ" TO ファイル名.
004034         PERFORM オープンチェック.
004035     OPEN I-O ＨレセプトＦ.
004036         MOVE NC"ＨレセプトＦ" TO ファイル名.
004037         PERFORM オープンチェック.
004038*
004039*================================================================*
004040 オープンチェック SECTION.
004041*
004042     IF 状態キー  NOT =  "00"
004043         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
004050         DISPLAY NC"状態キー：" 状態キー         UPON CONS
004060         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004070                                                 UPON CONS
004071*-----------------------------------------*
004072         CALL "actcshm"  WITH C LINKAGE
004073*-----------------------------------------*
004080         ACCEPT  キー入力 FROM CONS
004090         PERFORM ファイル閉鎖
004100         MOVE 99 TO PROGRAM-STATUS
004110         EXIT PROGRAM.
004111*
004120*================================================================*
004130 ファイル閉鎖 SECTION.
004140*
004173     CLOSE ＨレセプトＦ Ｈ日計データＦ.
004174*================================================================*
004180 終了処理 SECTION.
004190*
004200     PERFORM ファイル閉鎖.
004210*================================================================*
005610*================================================================*
006522 日計データ処理 SECTION.
006523*
006524** Ｈ日計データＦの自費額の累計を算出する。
006525*
006527     MOVE ZERO TO 計算合計額Ｗ.
006535*
006538     MOVE Ｈ連計算−施術区分  TO  Ｈ日−施術区分.
006539     MOVE Ｈ連計算−患者番号  TO  Ｈ日−患者番号.
006540     MOVE Ｈ連計算−枝番      TO  Ｈ日−枝番.
006543     MOVE Ｈ連計算−施術和暦  TO  Ｈ日−施術和暦.
006544     MOVE Ｈ連計算−施術年    TO  Ｈ日−施術年.
006545     MOVE Ｈ連計算−施術月    TO  Ｈ日−施術月.
006546     MOVE 1                   TO  Ｈ日−施術日.
006548*
006549     START Ｈ日計データＦ KEY IS >= Ｈ日−施術区分
006550                                    Ｈ日−患者コード
006551                                    Ｈ日−施術和暦年月日
006552     END-START.
006553*
006554     IF 状態キー = "00"
006555         MOVE SPACE TO 終了フラグ
006556         PERFORM Ｈ日計データＦ読込
006557*
006631*        繰り返し（当月）
006632         PERFORM UNTIL ( Ｈ日−施術区分   NOT = Ｈ連計算−施術区分   ) OR
006633                       ( Ｈ日−患者コード NOT = Ｈ連計算−患者コード ) OR
006634                       ( Ｈ日−施術和暦   NOT = Ｈ連計算−施術和暦   ) OR
006635                       ( Ｈ日−施術年     NOT = Ｈ連計算−施術年     ) OR
006636                       ( Ｈ日−施術月     NOT = Ｈ連計算−施術月     ) OR
006637                       ( 終了フラグ       NOT = SPACE )
006640*
006667             COMPUTE 計算合計額Ｗ = 計算合計額Ｗ + Ｈ日−自費額
006668*
006669             PERFORM Ｈ日計データＦ読込
006670         END-PERFORM
006671     END-IF.
006672*
009790*================================================================*
009800 Ｈ日計データＦ読込 SECTION.
009810*
009820     READ Ｈ日計データＦ NEXT
009830     AT END
009840         MOVE "YES"  TO 終了フラグ
009850     END-READ.
009860*
009861*================================================================*
010392*================================================================*
010393*================================================================*
010394 レセプト処理 SECTION.
010395*
010396* / レセ種別　6:自費 /
010397* Ｈ日計データＦの自費額の累計を、費用額・負担額にセット。請求額0
010398*
010399     MOVE Ｈ連計算−施術区分  TO  Ｈレセ−施術区分.
010400     MOVE Ｈ連計算−施術和暦  TO  Ｈレセ−施術和暦.
010401     MOVE Ｈ連計算−施術年    TO  Ｈレセ−施術年.
010402     MOVE Ｈ連計算−施術月    TO  Ｈレセ−施術月.
010403     MOVE Ｈ連計算−患者番号  TO  Ｈレセ−患者番号.
010404     MOVE Ｈ連計算−枝番      TO  Ｈレセ−枝番.
010405     MOVE 6                   TO  Ｈレセ−レセ種別.
010406*
010407     READ ＨレセプトＦ
010408     NOT INVALID KEY
010428*
010429**       / ファイルの金額欄初期化 /
010430         INITIALIZE Ｈレセ−金額合計欄
010431         INITIALIZE Ｈレセ−はり金額欄
010432         INITIALIZE Ｈレセ−マッサージ金額欄
010433         INITIALIZE Ｈレセ−例外レセ金額合計欄
010434         INITIALIZE Ｈレセ−例外本体枝番まとめ単独金額合計欄
010435**
010436* Ｈ日計データＦの自費額の累計をセット
010437         MOVE 計算合計額Ｗ  TO Ｈ連計算−費用額
010438         MOVE 計算合計額Ｗ  TO Ｈ連計算−負担額
010439         MOVE ZERO          TO Ｈ連計算−請求額
010440
010441* 上記後ファイルへセット
010442*（同じ値をＨレセ−例外負担額とＨレセ−例外請求額にもセット）
010443         MOVE Ｈ連計算−費用額  TO  Ｈレセ−費用額
010444         MOVE Ｈ連計算−負担額  TO  Ｈレセ−負担額 Ｈレセ−例外負担額
010445         MOVE Ｈ連計算−請求額  TO  Ｈレセ−請求額 Ｈレセ−例外請求額
010446*
010447*（負担割合10）
010448         MOVE 10 TO  Ｈレセ−負担割合
010449*
010487**
010488         REWRITE Ｈレセ−レコード
010489         IF 状態キー NOT = "00"
010490            MOVE NC"ＨレセプトＦ" TO ファイル名
010491            PERFORM エラー表示
010492         END-IF
010493**
010496     END-READ.
010497*
010498*================================================================*
010869*================================================================*
010870 エラー表示 SECTION.
010871*
010872     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
010873     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
010874     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
010875     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"  UPON CONS.
010876*-----------------------------------------*
010877     CALL "actcshm"  WITH C LINKAGE.
010878*-----------------------------------------*
010879     ACCEPT  キー入力 FROM CONS.
010880*
010881*================================================================*
010882*================================================================*
010883*================================================================*
010884******************************************************************
010885 END PROGRAM KJIHI.
010886******************************************************************
