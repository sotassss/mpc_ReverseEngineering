000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             GETUJIK.
000060 AUTHOR.                 上田  育正
000070*
000080*----------------------------------------------------------------*
000090*         月次更新　（柔）
000100*         MED =
000110*----------------------------------------------------------------*
000120 DATE-WRITTEN.           1998-02-13
000130 DATE-COMPILED.          1997-02-13
      */請求無し(年月ゼロ)に対応/150807
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000260     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000270                             ORGANIZATION             IS  INDEXED
000280                             ACCESS MODE              IS  DYNAMIC
000290                             RECORD KEY               IS  受−施術和暦年月
000300                                                          受−患者コード
000310                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000320                                                          受−患者カナ
000330                                                          受−患者コード
000340                             ALTERNATE RECORD KEY     IS  受−患者コード
000350                                                          受−施術和暦年月
000360                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000370                                                          受−保険種別
000380                                                          受−保険者番号
000390                                                          受−患者コード
000400                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000410                                                          受−公費種別
000420                                                          受−費用負担者番号
000430                                                          受−患者コード
000440                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000450                                                          受−助成種別
000460                                                          受−費用負担者番号助成
000470                                                          受−患者コード
000480                             ALTERNATE RECORD KEY     IS  受−請求和暦年月
000490                                                          受−施術和暦年月
000500                                                          受−患者コード
000510                             FILE STATUS              IS  状態キー
000520                             LOCK        MODE         IS  AUTOMATIC.
000530*
000540     SELECT  受診者情報２Ｆ  ASSIGN      TO        JUSINJ2L
000550                             ORGANIZATION             IS INDEXED
000560                             ACCESS MODE              IS DYNAMIC
000570                             RECORD KEY               IS 受２−施術和暦年月
000580                                                         受２−患者コード
000590                             ALTERNATE RECORD KEY     IS 受２−請求対象区分
000600                                                         受２−請求和暦年月
000610                                                         受２−施術和暦年月
000620                                                         受２−患者コード
000630                             ALTERNATE RECORD KEY     IS 受２−助成請求対象区分
000640                                                         受２−助成請求和暦年月
000650                                                         受２−施術和暦年月
000660                                                         受２−患者コード
000670                             FILE STATUS              IS  状態キー
000680                             LOCK        MODE         IS  AUTOMATIC.
000690     SELECT  負傷データＦ    ASSIGN      TO        HUSYOUL
000700                             ORGANIZATION             IS  INDEXED
000710                             ACCESS MODE              IS  DYNAMIC
000720                             RECORD KEY               IS  負−施術和暦年月
000730                                                          負−患者コード
000740                             ALTERNATE RECORD KEY     IS  負−患者コード
000750                                                          負−施術和暦年月
000760                             FILE STATUS              IS  状態キー
000770                             LOCK        MODE         IS  AUTOMATIC.
000780     SELECT  施術記録Ｆ      ASSIGN      TO        SEKIROKL
000790                             ORGANIZATION             IS  INDEXED
000800                             ACCESS MODE              IS  DYNAMIC
000810                             RECORD KEY           IS 施記−施術和暦年月日
000820                                                     施記−患者コード
000830                             ALTERNATE RECORD KEY IS 施記−患者コード
000840                                                     施記−施術和暦年月日
000850                             FILE STATUS              IS  状態キー
000860                             LOCK        MODE         IS  AUTOMATIC.
000870*
000880     SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
000890                             ORGANIZATION             IS  INDEXED
000900                             ACCESS MODE              IS  DYNAMIC
000910                             RECORD KEY               IS  レセ−施術和暦年月
000920                                                          レセ−患者コード
000930                                                          レセ−レセ種別
000940                             ALTERNATE RECORD KEY     IS  レセ−患者コード
000950                                                          レセ−施術和暦年月
000960                                                          レセ−レセ種別
000970                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000980                                                          レセ−施術和暦年月
000990                                                          レセ−患者コード
001000                                                          レセ−レセ種別
001010                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
001020                                                          レセ−レセ種別
001030                                                          レセ−請求保険者番号
001040                                                          レセ−患者コード
001050                                                          レセ−施術和暦年月
001060                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
001070                                                          レセ−請求保険者番号
001080                                                          レセ−患者コード
001090                                                          レセ−レセ種別
001100                                                          レセ−施術和暦年月
001110                             FILE STATUS              IS  状態キー
001120                             LOCK        MODE         IS  AUTOMATIC.
001130**
001140*
001150     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
001160                             ORGANIZATION             IS  INDEXED
001170                             ACCESS MODE              IS  DYNAMIC
001180                             RECORD KEY               IS  元−元号区分
001190                             FILE STATUS              IS  状態キー
001200                             LOCK        MODE         IS  AUTOMATIC.
001210     SELECT  制御情報マスタ  ASSIGN      TO        SEIGYOL
001220                             ORGANIZATION             IS  INDEXED
001230                             ACCESS MODE              IS  DYNAMIC
001240                             RECORD KEY               IS  制−制御区分
001250                             FILE STATUS              IS  状態キー
001260                             LOCK        MODE         IS  AUTOMATIC.
001270******************************************************************
001280*                      DATA DIVISION                             *
001290******************************************************************
001300 DATA                    DIVISION.
001310 FILE                    SECTION.
001320*                           ［ＲＬ＝  ３２０］
001330 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001340     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
001350*
001360 FD  受診者情報２Ｆ      BLOCK   CONTAINS   1   RECORDS.
001370     COPY JUSINJ2         OF  XFDLIB  JOINING   受２   AS  PREFIX.
001380*                           ［ＲＬ＝  １２８］
001390 FD  負傷データＦ        BLOCK   CONTAINS   1   RECORDS.
001400     COPY HUSYOU          OF  XFDLIB  JOINING   負   AS  PREFIX.
001410*                           ［ＲＬ＝  ２５６］
001420 FD  施術記録Ｆ          BLOCK   CONTAINS   1   RECORDS.
001430     COPY SEKIROK         OF  XFDLIB  JOINING   施記 AS  PREFIX.
001440*                           ［ＲＬ＝  １２８］
001450 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
001460     COPY GENGOU          OF  XFDLIB  JOINING   元   AS  PREFIX.
001470*                           ［ＲＬ＝  ２５６］
001480 FD  制御情報マスタ          BLOCK   CONTAINS   1   RECORDS.
001490     COPY SEIGYO          OF  XFDLIB  JOINING   制   AS  PREFIX.
001500     COPY SEIGYO01        OF  XFDLIB  JOINING   制０１ AS  PREFIX.
001510*
001520 FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS.
001530     COPY RECEPT          OF  XFDLIB  JOINING   レセ   AS  PREFIX.
001540*
001550*----------------------------------------------------------------*
001560******************************************************************
001570*                WORKING-STORAGE SECTION                         *
001580******************************************************************
001590 WORKING-STORAGE         SECTION.
001600 01 キー入力                           PIC X    VALUE SPACE.
001610 01 状態キー                           PIC X(2) VALUE SPACE.
001620 01 終了フラグ                         PIC X(3) VALUE SPACE.
001630 01 終了フラグ２                       PIC X(3) VALUE SPACE.
001640 01 終了フラグ３                       PIC X(3) VALUE SPACE.
001650 01 更新フラグ                         PIC X(3) VALUE SPACE.
001660 01 遅延フラグ                         PIC X(3) VALUE SPACE.
001670 01 書込フラグ                         PIC X(4) VALUE SPACE.
001680 01 保険区分Ｗ                         PIC 9(2) VALUE ZERO.
001690 01 保険者番号Ｗ                       PIC X(6) VALUE SPACE.
001700 01 ファイル名                         PIC N(6) VALUE SPACE.
001710 01 ファイル名Ｗ                       PIC N(6) VALUE SPACE.
001720 01 エラー内容Ｗ                       PIC N(18) VALUE SPACE.
001730 01 エラー内容２Ｗ                     PIC N(18) VALUE SPACE.
001740 01 遅延ＣＮＴ                         PIC 9(5) VALUE ZERO.
001750 01 行ＣＮＴ                           PIC 9(2) VALUE ZERO.
001760 01 負傷ＣＮＴ                         PIC 9(2) VALUE ZERO.
001770 01 日毎部位数Ｗ                       PIC 9 VALUE ZERO.
001780 01 受付患者数Ｗ                       PIC 9(3) VALUE ZERO.
001790 01 前詰終了フラグ                     PIC X(3) VALUE SPACE.
001800 01 受付カウンタ                       PIC 9(2) VALUE ZERO.
001810 01 表カウンタ                         PIC 9(2) VALUE ZERO.
001820 01 部位カウンタ                       PIC 9(2) VALUE ZERO.
001830 01 部位カウンタ２                     PIC 9(2) VALUE ZERO.
001840 01 部位カウンタＷ                     PIC 9(2) VALUE ZERO.
001850 01 部位カウンタＷ２                   PIC 9(2) VALUE ZERO.
001860 01 選択処理区分Ｗ                     PIC X(4) VALUE SPACE.
001870 01 施術和暦年月ＮＷ.
001880   03 施術和暦ＮＷ                     PIC 9.
001890   03 施術年ＮＷ                       PIC 9(2).
001900   03 施術月ＮＷ                       PIC 9(2).
001910*
001920 01 行カウンタ                         PIC 9(2) VALUE ZERO.
001930 01 遅延カウンタ                       PIC 9(4) VALUE ZERO.
001940 01 遅延回数Ｗ                         PIC 9(4) VALUE ZERO.
001950 01 選択行位置Ｗ                       PIC 9(2) VALUE ZERO.
001960 01 請求移動キー                       PIC X(4) VALUE SPACE.
001970 01 処理移動キー                       PIC X(4) VALUE SPACE.
001980 01 使用モードＷ                       PIC 9.
001990 01 退避保険区分Ｗ                     PIC 9(2)  VALUE ZERO.
002000 01 退避保険者番号Ｗ                   PIC X(10) VALUE SPACE.
002010 01 前和暦Ｗ                           PIC 9 VALUE ZERO.
002020 01 患者コードＷ.
002030    03 患者番号Ｗ                      PIC 9(6) VALUE ZERO.
002040    03 枝番Ｗ                          PIC X VALUE SPACE.
002050 01 カレント和暦Ｗ                     PIC 9 VALUE ZERO.
002060 01 保険種別Ｗ１                       PIC 9(2) VALUE ZERO.
002070 01 保険種別Ｗ２                       PIC 9(2) VALUE ZERO.
002080 01 部位名称Ｗ                         PIC N(12) VALUE SPACE.
002090 01 負傷連続登録Ｗ                     PIC 9 VALUE ZERO.
002100*
002110*
002120*
002130 01 現在時Ｗ                           PIC 9(2).
002140 01 現在分Ｗ                           PIC 9(2).
002150 01 現在秒Ｗ                           PIC 9(2).
002160 01 現在年Ｗ                           PIC 9(2).
002170 01 現在月Ｗ                           PIC 9(2).
002180 01 現在日Ｗ                           PIC 9(2).
002190*
002200 01 負傷時Ｗ                           PIC X(2).
002210 01 負傷時Ｒ REDEFINES 負傷時Ｗ.
002220    03 負傷時ＷＺ                      PIC 9(2).
002230*
002240 01 負傷分Ｗ                           PIC X(2).
002250 01 負傷分Ｒ REDEFINES 負傷分Ｗ.
002260    03 負傷分ＷＺ                      PIC 9(2).
002270*
002280 01 元号名称Ｗ.
002290    03 元号名称Ｗ１                    PIC N(2).
002300    03 FILLER                          PIC N(4).
002310*
002320 01 負傷番号Ｗ                         PIC 9.
002330 01 負傷番号Ｒ REDEFINES 負傷番号Ｗ.
002340    03 負傷番号Ｗ１                    PIC X.
002350*
002360 01 全角負傷番号Ｗ                     PIC N.
002370 01 全角負傷番号Ｒ REDEFINES 全角負傷番号Ｗ.
002380    03 全角負傷番号Ｗ１                PIC X(2).
002390*
002400 01 件数Ｗ                             PIC 9(6).
002410 01 頁数Ｗ                             PIC 9(4).
002420 01 総頁数Ｗ                           PIC 9(4).
002430 01 剰余Ｗ                             PIC 9(2).
002440*
002450*
002460** 01 計算和暦年月Ｗ.
002470**     03 計算和暦Ｗ                     PIC 9.
002480**     03 計算年月Ｗ.
002490**        05 計算年Ｗ                    PIC 9(2).
002500**        05 計算月Ｗ                    PIC 9(2).
002510**     03 計算日Ｗ                       PIC 9(2).
002520*
002530 01 絶対値Ｗ                           PIC 9(6).
002540 01 絶対値月Ｗ                         PIC 9(3).
002550 01 絶対値月Ｗ２                       PIC 9(3).
002560 01 負傷経過日Ｗ                       PIC 9(3).
002570 01 西暦年Ｗ                           PIC 9(4).
002580 01 西暦年差Ｗ                         PIC 9(2).
002590 01 西暦年差２Ｗ                       PIC 9(2).
002600 01 閏年回数Ｗ                         PIC 9(2).
002610 01 通常年回数Ｗ                       PIC 9(2).
002620 01 通常年絶対値Ｗ                     PIC 9(6).
002630 01 閏年絶対値Ｗ                       PIC 9(6).
002640 01 年絶対値Ｗ                         PIC 9(6).
002650 01 負傷絶対値Ｗ                       PIC 9(6).
002660 01 施術絶対値Ｗ                       PIC 9(6).
002670*
002680 01 冷罨日数骨折Ｗ                     PIC 9(2).
002690 01 冷罨日数脱臼Ｗ                     PIC 9(2).
002700 01 冷罨日数打撲Ｗ                     PIC 9(2).
002710 01 温罨待機骨折Ｗ                     PIC 9(2).
002720 01 温罨待機脱臼Ｗ                     PIC 9(2).
002730*
002740 01 施術和暦年月日Ｗ.
002750     03 施術和暦年月Ｗ.
002760         05 施術和暦Ｗ                 PIC 9.
002770         05 施術年月Ｗ.
002780            07 施術年Ｗ                PIC 9(2).
002790            07 施術月Ｗ                PIC 9(2).
002800     03 施術日Ｗ                       PIC 9(2).
002810*
002820 01 施術和暦年月日ＣＷ.
002830     03 施術和暦年月ＣＷ.
002840         05 施術和暦ＣＷ               PIC 9.
002850         05 施術年月ＣＷ.
002860            07 施術年ＣＷ              PIC 9(2).
002870            07 施術月ＣＷ              PIC 9(2).
002880     03 施術日ＣＷ                     PIC 9(2).
002890*
002900 01 開始和暦年月日ＣＷ.
002910     03 開始和暦年月ＣＷ.
002920         05 開始和暦ＣＷ               PIC 9.
002930         05 開始年月ＣＷ.
002940            07 開始年ＣＷ              PIC 9(2).
002950            07 開始月ＣＷ              PIC 9(2).
002960     03 開始日ＣＷ                     PIC 9(2).
002970*
002980 01 終了和暦年月日ＣＷ.
002990   03 終了和暦ＣＷ                     PIC 9.
003000   03 終了年月ＣＷ.
003010     05 終了年ＣＷ                     PIC 9(2).
003020     05 終了月ＣＷ                     PIC 9(2).
003030   03 終了日ＣＷ                       PIC 9(2).
003040*
003050*
003060* 日付ＷＯＲＫ
003070 01 和暦終了年Ｗ                       PIC 9(4) VALUE ZERO.
003080 01 計算機和暦年Ｗ                     PIC 9(2) VALUE ZERO.
003090*
003100 01 計算機西暦.
003110    03 計算機西暦年                    PIC 9(4).
003120    03 計算機西暦月日                  PIC 9(4).
003130 01 計算機西暦Ｒ REDEFINES 計算機西暦.
003140    03 計算機世紀                      PIC 9(2).
003150    03 計算機日付                      PIC 9(6).
003160    03 計算機日付Ｒ REDEFINES 計算機日付.
003170       05 計算機年月                   PIC 9(4).
003180       05 計算機年月Ｒ REDEFINES 計算機年月.
003190         07 計算機年                   PIC 9(2).
003200         07 計算機月                   PIC 9(2).
003210       05 計算機日                     PIC 9(2).
003220*
003230 01 計算機時間.
003240    03 計算機時                        PIC 9(2).
003250    03 計算機分                        PIC 9(2).
003260    03 計算機秒                        PIC 9(2).
003270    03 FILLER                          PIC 9(2).
003280*
003290****
003300*
003310 01 請求年月ＨＷ.
003320     03 請求和暦ＨＷ                   PIC 9.
003330     03 請求年ＨＷ                     PIC 9(2).
003340     03 請求月ＨＷ                     PIC 9(2).
003350*
003360   03 レコードデータＴＷ.
003370     05 初検和暦年月日ＴＷ.
003380       07 初検和暦年月ＴＷ.
003390         09 初検和暦ＴＷ               PIC 9.
003400         09 初検年月ＴＷ.
003410           11 初検年ＴＷ               PIC 9(2).
003420           11 初検月ＴＷ               PIC 9(2).
003430       07 初検日ＴＷ                   PIC 9(2).
003440     05 FILLER                         PIC X(2).
003450     05 部位数ＴＷ                     PIC 9.
003460     05 負傷データＴＷ      OCCURS   9.
003470       07 部位コードＴＷ.
003480         09 負傷種別ＴＷ               PIC 9(2).
003490         09 部位ＴＷ                   PIC 9(2).
003500         09 左右区分ＴＷ               PIC 9.
003510         09 負傷位置番号ＴＷ           PIC 9(2).
003520       07 負傷和暦年月日ＴＷ.
003530         09 負傷和暦年月ＴＷ.
003540           11 負傷和暦ＴＷ             PIC 9.
003550           11 負傷年月ＴＷ.
003560             13 負傷年ＴＷ             PIC 9(2).
003570             13 負傷月ＴＷ             PIC 9(2).
003580         09 負傷日ＴＷ                 PIC 9(2).
003590       07 負傷時間ＴＷ.
003600         09 負傷時ＴＷ                 PIC 9(2).
003610         09 負傷分ＴＷ                 PIC 9(2).
003620       07 開始和暦年月日ＴＷ.
003630         09 開始和暦年月ＴＷ.
003640           11 開始和暦ＴＷ             PIC 9.
003650           11 開始年月ＴＷ.
003660             13 開始年ＴＷ             PIC 9(2).
003670             13 開始月ＴＷ             PIC 9(2).
003680         09 開始日ＴＷ                 PIC 9(2).
003690       07 終了和暦年月日ＴＷ.
003700         09 終了和暦年月ＴＷ.
003710           11 終了和暦ＴＷ             PIC 9.
003720           11 終了年月ＴＷ.
003730             13 終了年ＴＷ             PIC 9(2).
003740             13 終了月ＴＷ             PIC 9(2).
003750         09 終了日ＴＷ                 PIC 9(2).
003760       07 転帰区分ＴＷ                 PIC 9.
003770       07 算定区分ＴＷ                 PIC 9.
003780       07 電療請求区分ＴＷ             PIC 9.
003790       07 包帯交換数ＴＷ               PIC 9(2).
003800       07 金属副子数ＴＷ.
003810         09 大ＴＷ                     PIC 9.
003820         09 中ＴＷ                     PIC 9.
003830         09 小ＴＷ                     PIC 9.
003840       07 負傷原因コードＴＷ.
003850         09 負傷患者番号ＴＷ           PIC 9(6).
003860         09 負傷連番ＴＷ               PIC 9(4).
003870       07 経過コードＴＷ               PIC 9(4).
003880     05 全体経過コードＴＷ             PIC 9(2).
003890     05 初検請求区分ＴＷ               PIC 9.
003900     05 往療距離ＴＷ                   PIC 9(3)V9.
003910* 初検加算の時刻
003920     05 初検加算時間ＴＷ.
003930       07 診療時ＴＷ                   PIC 9(2).
003940       07 診療分ＴＷ                   PIC 9(2).
003950* 部位毎の情報提供料
003960     05 情報提供区分ＴＷ               PIC 9  OCCURS 9.
003970     05 開始診療日手動区分ＴＷ         PIC 9.
003980*
003990     05 運動療法算定区分ＴＷ           PIC 9.
004000* 自賠責のその他金額
004010     05 自賠責その他金額ＴＷ           PIC 9(6).
004020*
004030     05 レセ署名区分ＴＷ               PIC 9.
004040     05 レセ負傷原因印刷区分ＴＷ       PIC 9.
004050* 部位毎の後療区分
004060     05 後療区分ＴＷ                   PIC 9  OCCURS 9.
004070     05 指導管理算定区分ＴＷ           PIC 9.
004080     05 包帯交換算定区分ＴＷ           PIC 9.
004090*
004100     05 初検時相談料区分ＴＷ           PIC 9.
004110     05 拘縮後療区分ＴＷ               PIC 9.
004120     05 レセ長期理由印刷区分ＴＷ       PIC 9.
004130*/コメントをやめる/130710
004140     05 労災自賠施術証明無区分ＴＷ     PIC X.
004150     05 労災自賠施術明細無区分ＴＷ     PIC X.
004160*
004170     05 運動更新区分ＴＷ               PIC 9(1).
004180     05 指導更新区分ＴＷ               PIC 9(1).
004190     05 包帯更新区分ＴＷ               PIC 9(1) OCCURS 7.
004200     05 FILLER                         PIC X(77).
004210**
004220*******
004230**
004240 01 レセ負傷データＷＷ.
004250     03 レセ負傷データＷ   OCCURS   7.
004260        05 部位名称１Ｗ                PIC N(10).
004270        05 部位名称２Ｗ                PIC N(10).
004280**
004290*******
004300**
004310*/前回通院月判定
004320 01 計算年月日Ｗ.
004330    03 計算和暦Ｗ                      PIC 9(1)  VALUE ZERO.
004340    03 計算年Ｗ                        PIC S9(2)  VALUE ZERO.
004350    03 計算月Ｗ                        PIC S9(2)  VALUE ZERO.
004360    03 計算日Ｗ                        PIC S9(2)  VALUE ZERO.
004370 01 開始年月日２Ｗ.
004380    03 開始和暦２Ｗ                    PIC 9(1)  VALUE ZERO.
004390    03 開始年２Ｗ                      PIC 9(2)  VALUE ZERO.
004400    03 開始月２Ｗ                      PIC 9(2)  VALUE ZERO.
004410    03 開始日２Ｗ                      PIC 9(2)  VALUE ZERO.
004420    03 開始西暦年Ｗ                    PIC S9(4) VALUE ZERO.
004430 01 終了年月日２Ｗ.
004440    03 終了和暦２Ｗ                    PIC 9(1)  VALUE ZERO.
004450    03 終了年２Ｗ                      PIC 9(2)  VALUE ZERO.
004460    03 終了月２Ｗ                      PIC 9(2)  VALUE ZERO.
004470    03 終了日２Ｗ                      PIC 9(2)  VALUE ZERO.
004480    03 終了西暦年Ｗ                    PIC S9(4) VALUE ZERO.
004490** 前月初検のみ用
004500 01 初日再検フラグ                     PIC X(3)  VALUE SPACE.
004510 01 前月フラグ                         PIC X(3)  VALUE SPACE.
004520**データチェック用
004530 01 実行キーＷ                         PIC X(3)  VALUE SPACE.
004540 01 施術記録有Ｗ                       PIC X(3) VALUE SPACE.
004550**
004560 01 運動療法初期値Ｗ                   PIC 9    VALUE ZERO.
004570 01 保険種別Ｗ                         PIC 9(2) VALUE ZERO.
004580 01 公費種別Ｗ                         PIC 9(2) VALUE ZERO.
004590 01 助成種別Ｗ                         PIC 9(2) VALUE ZERO.
004600*
004610*/労災自賠責指導包帯/0708
004620 01 指導管理初期値Ｗ                   PIC 9    VALUE ZERO.
004630 01 包帯交換初期値Ｗ                   PIC 9    VALUE ZERO.
004640*
004650 01 はりきゅう使用区分Ｗ               PIC 9 VALUE ZERO.
004660*
004670 01 レセ負傷原因印刷区分Ｗ             PIC 9 VALUE ZERO.
004680*
004690*
004700*初検時相談料初期値
004710 01 初検時相談料区分Ｗ                 PIC 9 VALUE ZERO.
004720*
004730**
004740 01 レセ存在フラグ                     PIC X(3)  VALUE SPACE.
004750 01 存在フラグ                         PIC X(3)  VALUE SPACE.
004760 01 自費フラグ                         PIC X(3)  VALUE SPACE.
004770*
004780*-----------------------------------------------------------------*
004790*負傷データＦ退避用 2014.02.03
004800 01 負傷データＦ退避Ｗ.
004810     03 負傷レセ署名区Ｗ                     PIC 9.
004820*
004830*-----------------------------------------------------------------*
004840*レセプトＦ退避用
004850 01 レセプトＦ退避Ｗ.
004860     03 レセ本体請求年月情報Ｗ.
004870        05 レセ本体請求区分Ｗ                PIC 9.
004880        05 レセ本体請求和暦年月Ｗ.
004890          07 レセ本体請求和暦Ｗ              PIC 9.
004900          07 レセ本体請求年Ｗ                PIC 9(2).
004910          07 レセ本体請求月Ｗ                PIC 9(2).
004920     03 レセ助成請求年月情報Ｗ.
004930        05 レセ助成請求区分Ｗ                PIC 9.
004940        05 レセ助成請求和暦年月Ｗ.
004950          07 レセ助成請求和暦Ｗ              PIC 9.
004960          07 レセ助成請求年Ｗ                PIC 9(2).
004970          07 レセ助成請求月Ｗ                PIC 9(2).
004980     03 レセ負傷情報Ｗ.
004990        05 レセ負傷部位名Ｗ   OCCURS   7.
005000          07 レセ部位名称１Ｗ                PIC N(10).
005010          07 レセ部位名称２Ｗ                PIC N(10).
005020        05 レセ実日数Ｗ                      PIC 9(2).
005030*-----------------------------------------------------------------*
005040*
005050**
005060*****
005070** 受診者レコード退避用
005080     COPY JUSINJ   OF  XFDLIB  JOINING   作業受   AS  PREFIX.
005090     COPY JUSINJ2  OF  XFDLIB  JOINING   作業受２ AS  PREFIX.
005100**
005110*
005120*/レセプトファイル退避
005130   01 レセプトファイルＷ.
005140       03 レセレコードキーＷ.
005150         05 レセ施術和暦年月Ｗ.
005160           07 レセ施術和暦Ｗ                     PIC 9.
005170           07 レセ施術年月Ｗ.
005180             09 レセ施術年Ｗ                     PIC 9(2).
005190             09 レセ施術月Ｗ                     PIC 9(2).
005200         05 レセ患者コードＷ.
005210           07 レセ患者番号Ｗ                     PIC 9(6).
005220           07 レセ枝番Ｗ                         PIC X.
005230*       / 1:一般,2:老人,3:助成,4:労災,5:自賠責,6:自費,7:生保単独,8保険証忘れ /
005240         05 レセレセ種別Ｗ                       PIC 9(2).
005250*
005260       03 レコードデータＷ.
005270         05 レセ請求年月情報Ｗ.
005280*           / 0：請求　1：月遅れ請求 2:返戻、9：請求しない /
005290            07 レセ請求区分Ｗ                    PIC 9.
005300            07 レセ請求和暦年月Ｗ.
005310              09 レセ請求和暦Ｗ                  PIC 9.
005320              09 レセ請求年月Ｗ.
005330                11 レセ請求年Ｗ                  PIC 9(2).
005340                11 レセ請求月Ｗ                  PIC 9(2).
005350*****************************************************************
005360*                          連結項目                             *
005370*****************************************************************
005380*
005390** 月次更新連携キー
005400 01 連月−キー IS EXTERNAL.
005410    03  連月−施術和暦年月.
005420      05  連月−施術和暦               PIC 9.
005430      05  連月−施術年月.
005440        07  連月−施術年               PIC 9(2).
005450        07  連月−施術月               PIC 9(2).
005460    03  連月−患者コード.
005470      05  連月−患者番号               PIC 9(6).
005480      05  連月−枝番                   PIC X.
005490    03  連月−負傷のみ区分             PIC X(3).
005500**
005510*
005520 01 連メ−キー IS EXTERNAL.
005530    03  連メ−メッセージ                 PIC N(20).
005540*
005550*----------------------------------------------------------*
005560* 平成20年4月からの後期高齢者用保険証データ変換
005570 01 連後期高齢ＣＮＶ−キー IS EXTERNAL.
005580    03 連後期高齢ＣＮＶ−施術和暦年月.
005590       05 連後期高齢ＣＮＶ−施術和暦            PIC 9.
005600       05 連後期高齢ＣＮＶ−施術年月.
005610          07 連後期高齢ＣＮＶ−施術年           PIC 9(2).
005620          07 連後期高齢ＣＮＶ−施術月           PIC 9(2).
005630    03 連後期高齢ＣＮＶ−患者コード.
005640       05 連後期高齢ＣＮＶ−患者番号            PIC 9(6).
005650       05 連後期高齢ＣＮＶ−枝番                PIC X.
005660    03 連後期高齢ＣＮＶ−変換データ.
005670       05 連後期高齢ＣＮＶ−変換対象フラグ      PIC X(3).
005680       05 連後期高齢ＣＮＶ−患者氏名            PIC X(50).
005690       05 連後期高齢ＣＮＶ−患者カナ            PIC X(50).
005700       05 連後期高齢ＣＮＶ−患者性別            PIC 9.
005710       05 連後期高齢ＣＮＶ−患者生年月日.
005720          07 連後期高齢ＣＮＶ−患者和暦         PIC 9.
005730          07 連後期高齢ＣＮＶ−患者年           PIC 9(2).
005740          07 連後期高齢ＣＮＶ−患者月           PIC 9(2).
005750          07 連後期高齢ＣＮＶ−患者日           PIC 9(2).
005760       05 連後期高齢ＣＮＶ−患者郵便番号.
005770          07 連後期高齢ＣＮＶ−患者郵便番号１   PIC X(3).
005780          07 連後期高齢ＣＮＶ−患者郵便番号２   PIC X(4).
005790       05 連後期高齢ＣＮＶ−患者住所１          PIC X(50).
005800       05 連後期高齢ＣＮＶ−患者住所２          PIC X(50).
005810       05 連後期高齢ＣＮＶ−患者電話番号        PIC X(30).
005820       05 連後期高齢ＣＮＶ−本人家族区分        PIC 9.
005830       05 連後期高齢ＣＮＶ−保険種別            PIC 9(2).
005840       05 連後期高齢ＣＮＶ−保険者番号          PIC X(10).
005850       05 連後期高齢ＣＮＶ−公費種別            PIC 9(2).
005860       05 連後期高齢ＣＮＶ−費用負担者番号      PIC X(10).
005870       05 連後期高齢ＣＮＶ−老人負担金免除      PIC 9.
005880       05 連後期高齢ＣＮＶ−特別区分            PIC 9.
005890*
005900*
005910*-------------------------------------------------------------------*
005920* 労災・自賠責・生保ファイルの月次更新連携キー
005930 01 連月労災等−キー IS EXTERNAL.
005940    03  連月労災等−施術和暦年月.
005950      05  連月労災等−施術和暦               PIC 9.
005960      05  連月労災等−施術年月.
005970        07  連月労災等−施術年               PIC 9(2).
005980        07  連月労災等−施術月               PIC 9(2).
005990    03  連月労災等−患者コード.
006000      05  連月労災等−患者番号               PIC 9(6).
006010      05  連月労災等−枝番                   PIC X.
006020*   / 1:健康保険、2:労災、3:自賠責、4:生保、5:自費 /
006030    03  連月労災等−保険分類                 PIC 9(2).
006040    03  連月労災等−最新施術和暦年月.
006050      05  連月労災等−最新施術和暦           PIC 9.
006060      05  連月労災等−最新施術年月.
006070        07  連月労災等−最新施術年           PIC 9(2).
006080        07  連月労災等−最新施術月           PIC 9(2).
006090*
006100*
006110*
006120* 助成レセまとめ
006130 01 連助成レセまとめ−キー IS EXTERNAL.
006140     03 連助成レセまとめ−柔整協会コード         PIC 9(2).
006150     03 連助成レセまとめ−鍼灸協会コード         PIC 9(2).
006160     03 連助成レセまとめ−保険種別               PIC 9(2).
006170     03 連助成レセまとめ−保険者番号             PIC X(10).
006180     03 連助成レセまとめ−助成種別               PIC 9(2).
006190     03 連助成レセまとめ−費用負担者番号助成     PIC X(10).
006200     03 連助成レセまとめ−公費種別               PIC 9(2).
006210     03 連助成レセまとめ−費用負担者番号         PIC X(10).
006220*  / OUT /
006230* 0:対象、1:鍼灸のみ本体まとめ、2:柔整のみ本体まとめ、3:両方本体まとめ、4:鍼灸のみレセ印刷なし、5:柔整のみレセ印刷なし、6:両方印刷なし（0以外はレセ印刷なし）
006240     03 連助成レセまとめ−助成レセ印刷対象区分           PIC 9.
006250* 0:対象、1:鍼灸のみ印刷なし、2:柔整のみ印刷なし、3:両方印刷なし（0以外は印刷なし）
006260     03 連助成レセまとめ−助成レセ会総括表印刷対象区分   PIC 9.
006270*
006280* 助成レセまとめ２
006290 01 連助成レセまとめ２−キー IS EXTERNAL.
006300     03 連助成レセまとめ２−施術和暦年月.
006310        05 連助成レセまとめ２−施術和暦          PIC 9.
006320        05 連助成レセまとめ２−施術年月.
006330          07 連助成レセまとめ２−施術年          PIC 9(2).
006340          07 連助成レセまとめ２−施術月          PIC 9(2).
006350*
006360* 助成レセまとめ３
006370 01 連助成レセまとめ３−キー IS EXTERNAL.
006380     03 連助成レセまとめ３−患者コード.
006390       05 連助成レセまとめ３−患者番号           PIC 9(6).
006400       05 連助成レセまとめ３−枝番               PIC X.
006410*
006420******************************************************************
006430*                      PROCEDURE  DIVISION                       *
006440******************************************************************
006450 PROCEDURE               DIVISION.
006460************
006470*           *
006480* 初期処理   *
006490*           *
006500************
006510     PERFORM 初期化.
006520     PERFORM 制御情報取得.
006530************
006540*           *
006550* 主処理     *
006560*           *
006570************
006580*----------------------------------------------------------------*
006590*    / YAW020,030より部位数0で呼ばれる /
006600*    / 負傷Ｆ+レセプトＦのみ月次更新して終了 /
006610     IF 連月−負傷のみ区分 = "YES"
006620*       / 部位数0の時だけやる（自費除く） /
006630        MOVE SPACE TO 自費フラグ
006640        MOVE 連月−施術和暦     TO 負−施術和暦
006650        MOVE 連月−施術年       TO 負−施術年
006660        MOVE 連月−施術月       TO 負−施術月
006670        MOVE 連月−患者番号     TO 負−患者番号
006680        MOVE 連月−枝番         TO 負−枝番
006690        READ 負傷データＦ
006700        NOT INVALID KEY
006710             MOVE 連月−施術和暦     TO 受−施術和暦
006720             MOVE 連月−施術年       TO 受−施術年
006730             MOVE 連月−施術月       TO 受−施術月
006740             MOVE 連月−患者番号     TO 受−患者番号
006750             MOVE 連月−枝番         TO 受−枝番
006760             READ 受診者情報Ｆ
006770             NOT INVALID KEY
006780                 IF ( 受−保険種別 = 90 ) AND ( 受−保険外区分 NOT = 9 )
006790                    MOVE "YES" TO 自費フラグ
006800                 END-IF
006810             END-READ
006820*
006830             IF 自費フラグ NOT = "YES"
006840                IF 負−部位数 = ZERO
006850                   PERFORM 負傷データ生成
006860                END-IF
006870             END-IF
006880        END-READ
006890*
006900        PERFORM 終了処理
006910        EXIT PROGRAM
006920     END-IF.
006930*----------------------------------------------------------------*
006940*
006950*----------------------------------------------------------------*
006960*    / 柔整で保険証入力 YAW602よりCALL → レセプトＦのみ作り直して終了 /
006970*    / 鍼灸で保険証入力 YAW602よりCALL → 当月のレセプトＦがある時のみ、レセプトＦのみ作り直して終了 /
006980     IF 連月−負傷のみ区分 = "YAW" OR "HM"
006990        PERFORM レセプトＦ生成
007000        PERFORM 終了処理
007010        EXIT PROGRAM
007020     END-IF.
007030*----------------------------------------------------------------*
007040*
007050***
007060     PERFORM 月次更新.
007070***
007080************
007090*           *
007100* 終了処理   *
007110*           *
007120************
007130     PERFORM 終了処理.
007140     EXIT PROGRAM.
007150*
007160*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
007170*================================================================*
007180 初期化 SECTION.
007190*
007200     OPEN I-O 受診者情報Ｆ.
007210         MOVE NC"受診" TO ファイル名.
007220         PERFORM オープンチェック.
007230     OPEN I-O 受診者情報２Ｆ.
007240         MOVE NC"受診２" TO ファイル名.
007250         PERFORM オープンチェック.
007260     OPEN I-O 負傷データＦ.
007270         MOVE NC"負傷" TO ファイル名.
007280         PERFORM オープンチェック.
007290     OPEN I-O レセプトＦ.
007300         MOVE NC"レセプト" TO ファイル名.
007310         PERFORM オープンチェック.
007320     OPEN INPUT   施術記録Ｆ.
007330         MOVE NC"施記Ｆ" TO ファイル名.
007340         PERFORM オープンチェック.
007350     OPEN INPUT   元号マスタ.
007360         MOVE NC"元号" TO ファイル名.
007370         PERFORM オープンチェック.
007380     OPEN INPUT 制御情報マスタ.
007390         MOVE NC"制御" TO ファイル名.
007400         PERFORM オープンチェック.
007410*
007420*================================================================*
007430 オープンチェック SECTION.
007440*
007450     IF 状態キー  NOT =  "00"
007460         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
007470         DISPLAY NC"状態キー：" 状態キー         UPON CONS
007480         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
007490                                                 UPON CONS
007500*-----------------------------------------*
007510         CALL "actcshm"  WITH C LINKAGE
007520*-----------------------------------------*
007530         ACCEPT  キー入力 FROM CONS
007540         PERFORM ファイル閉鎖
007550         EXIT PROGRAM.
007560*================================================================*
007570 ファイル閉鎖 SECTION.
007580*
007590     CLOSE 負傷データＦ 受診者情報Ｆ 受診者情報２Ｆ 施術記録Ｆ レセプトＦ 元号マスタ 制御情報マスタ.
007600*
007610*================================================================*
007620 終了処理 SECTION.
007630*
007640     PERFORM ファイル閉鎖.
007650     MOVE 1 TO PROGRAM-STATUS.
007660*================================================================*
007670 エラー表示 SECTION.
007680*
007690     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
007700     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
007710     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
007720     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
007730*-----------------------------------------*
007740     CALL "actcshm"  WITH C LINKAGE.
007750*-----------------------------------------*
007760     ACCEPT  キー入力 FROM CONS.
007770     PERFORM 終了処理.
007780     MOVE ZERO TO PROGRAM-STATUS.
007790     EXIT PROGRAM.
007800*
007810*================================================================*
007820 エラー表示Ｒ SECTION.
007830*
007840     DISPLAY NC"ファイル読込エラー" ファイル名     UPON CONS.
007850     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
007860     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
007870*-----------------------------------------*
007880     CALL "actcshm"  WITH C LINKAGE.
007890*-----------------------------------------*
007900     ACCEPT  キー入力 FROM CONS.
007910     PERFORM 終了処理.
007920     MOVE ZERO TO PROGRAM-STATUS.
007930     EXIT PROGRAM.
007940*
007950*================================================================*
007960 エラー表示Ｄ SECTION.
007970*
007980     DISPLAY NC"ファイル削除エラー：" ファイル名   UPON CONS.
007990     DISPLAY NC"状態キー：" 状態キー               UPON CONS.
008000     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
008010     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
008020*-----------------------------------------*
008030     CALL "actcshm"  WITH C LINKAGE.
008040*-----------------------------------------*
008050     ACCEPT  キー入力 FROM CONS.
008060     PERFORM 終了処理.
008070     MOVE ZERO TO PROGRAM-STATUS.
008080     EXIT PROGRAM.
008090*
008100*================================================================*
008110 制御情報取得 SECTION.
008120*
008130*  / 01 レコード /
008140     MOVE 01 TO  制０１−制御区分.
008150     READ 制御情報マスタ
008160     NOT INVALID KEY
008170*/労災自賠責指導包帯/0708
008180         MOVE 制０１−指導管理区分 TO 指導管理初期値Ｗ
008190         MOVE 制０１−包帯交換区分 TO 包帯交換初期値Ｗ
008200*        / 2008/06
008210         MOVE 制０１−初検時相談料区分 TO 初検時相談料区分Ｗ
008220     END-READ.
008230*
008240*  / 00 レコード /
008250     MOVE ZERO TO 制−制御区分.
008260     READ 制御情報マスタ
008270     NOT INVALID KEY
008280         MOVE 制−運動療法区分         TO 運動療法初期値Ｗ
008290         MOVE 制−はりきゅう使用区分   TO はりきゅう使用区分Ｗ
008300         MOVE 制−レセ負傷原因印刷区分 TO レセ負傷原因印刷区分Ｗ
008310     END-READ.
008320*
008330*================================================================*
008340 月次更新 SECTION.
008350*
008360     MOVE SPACE TO 更新フラグ.
008370*
008380     MOVE 連月−施術和暦     TO 受−施術和暦.
008390     MOVE 連月−施術年       TO 受−施術年.
008400     MOVE 連月−施術月       TO 受−施術月.
008410     MOVE 連月−患者番号     TO 受−患者番号.
008420     MOVE 連月−枝番         TO 受−枝番.
008430     READ 受診者情報Ｆ
008440     INVALID KEY
008450         MOVE "YES" TO 更新フラグ
008460     NOT INVALID KEY
008470         CONTINUE
008480     END-READ.
008490*
008500     IF 更新フラグ NOT = SPACE
008510         MOVE ZERO           TO 受−施術和暦
008520         MOVE ZERO           TO 受−施術年
008530         MOVE ZERO           TO 受−施術月
008540         MOVE 連月−患者番号 TO 受−患者番号
008550         MOVE 連月−枝番     TO 受−枝番
008560         READ 受診者情報Ｆ
008570         INVALID KEY
008580             DISPLAY NC"月次更新ができません" UPON CONS
008590             DISPLAY NC"システム管理者に連絡してください" UPON CONS
008600             MOVE NC"受診" TO ファイル名
008610             PERFORM エラー表示Ｒ
008620         NOT INVALID KEY
008630*
008640*-------------------------------------------------------------------*
008650             INITIALIZE  作業受２−レコード
008660             MOVE ZERO             TO 受２−施術和暦
008670             MOVE ZERO             TO 受２−施術年
008680             MOVE ZERO             TO 受２−施術月
008690             MOVE 連月−患者番号   TO 受２−患者番号
008700             MOVE 連月−枝番       TO 受２−枝番
008710             READ 受診者情報２Ｆ
008720             INVALID KEY
008730                DISPLAY NC"月次更新ができません" UPON CONS
008740                DISPLAY NC"システム管理者に連絡してください" UPON CONS
008750                MOVE NC"受診２" TO ファイル名
008760                PERFORM エラー表示Ｒ
008770             NOT INVALID KEY
008780                MOVE 受２−レコード   TO 作業受２−レコード
008790             END-READ
008800*-------------------------------------------------------------------*
008810*
008820**           / 最新レコード退避 /
008830             INITIALIZE  作業受−レコード
008840             MOVE 受−レコード   TO 作業受−レコード
008850**
008860             MOVE 受−施術和暦Ｎ TO 施術和暦ＮＷ
008870             MOVE 受−施術年Ｎ   TO 施術年ＮＷ
008880             MOVE 受−施術月Ｎ   TO 施術月ＮＷ
008890*
008900             MOVE 連月−施術和暦 TO 受−施術和暦 受−請求和暦
008910             MOVE 連月−施術年   TO 受−施術年   受−請求年  
008920             MOVE 連月−施術月   TO 受−施術月   受−請求月  
008930* 初期化する
008940             MOVE ZERO           TO 受−継続区分 受−請求区分
008950             MOVE ZERO           TO 受−レセ印刷区分
008960             MOVE ZERO           TO 受−レセ印刷区分助成
008970*
008980             MOVE ZERO           TO 受−老人負担回数
008990             MOVE ZERO           TO 受−老人負担額累計
009000             MOVE ZERO           TO 受−老人最終負担日
009010             MOVE ZERO           TO 受−老人負担額端数
009020             MOVE ZERO           TO 受−老人負担無し
009030             MOVE SPACE          TO 受−枝番作成時枝番
009040             MOVE ZERO           TO 受−はり区分
009050             MOVE ZERO           TO 受−あんま区分
009060             MOVE ZERO           TO 受−柔整有区分
009070             MOVE ZERO           TO 受−鍼灸有区分
009080             MOVE ZERO           TO 受−マッサージ有区分
009090             MOVE ZERO           TO 受−併用区分
009100             INITIALIZE             受−保険変更情報
009110             MOVE ZERO           TO 受−助成月途中開始日
009120*
009130*
      *令和４年１０月より特別区分２割を繰り越さない対応をやめる↓↓↓/20221031
009140**---/ 平成18年10月以降、18年4月〜9月から繰り越す場合のみ、2割を3割に変換する。それ以外は初期値に戻す/0609
009150*             IF 連月−施術和暦年月 >= 41810
009160*                 IF 受−特別区分 = 2
009170*                     IF (41804 <= 受−施術和暦年月Ｎ) AND (受−施術和暦年月Ｎ <= 41809)
009180*                         MOVE 3 TO 受−特別区分
009190*                     ELSE
009200**/前期高齢＋被爆の２割の場合はそのままにする120509
009210*                         IF (受−保険種別 NOT = 05) AND (受−助成種別 = 54)
009220*                             CONTINUE
009230*                         ELSE
009240*                             MOVE 1 TO 受−特別区分
009250*                         END-IF
009260*                     END-IF
009270*                 END-IF
009280*                 IF 受−老人負担金免除 = 2
009290*                     IF (41804 <= 受−施術和暦年月Ｎ) AND (受−施術和暦年月Ｎ <= 41809)
009300*                         MOVE 3    TO 受−老人負担金免除
009310*                     ELSE
009320*                         MOVE ZERO TO 受−老人負担金免除
009330*                     END-IF
009340*                 END-IF
009350*             END-IF
      *令和４年１０月より特別区分２割を繰り越さない対応をやめる↑↑↑/20221031
009360****            / 上記後、平成22年4月以降+後期高齢者でない時は、特別区分1→2 /
009370****            / 上記後、時期未定+後期高齢者でない時は、特別区分1→2 /
009380*            / 上記後、平成26年5月以降+後期高齢者でない+生年月日が昭和19年4月2日以降の時は、特別区分1→2 (2014.04) /
009390             IF ( 連月−施術和暦年月 >= "42605" ) AND ( 受−保険種別 NOT = 05 )
009400                 IF ( 受−特別区分 = 1 )
009410                    IF ( 受−患者生年月日 >= "3190402" )
009420                       MOVE 2 TO 受−特別区分
009430                    END-IF
009440                 END-IF
009450             END-IF
009460*---*
009470*---------------------------------------------------------------------------------------------*
009480* 最後に実施！
009490* 平成20年4月以降の後期高齢者データの変換
009500             MOVE SPACE TO 連後期高齢ＣＮＶ−キー
009510             INITIALIZE    連後期高齢ＣＮＶ−キー
009520             IF ( 連月−施術和暦年月 >= 42004 ) AND ( 受−保険種別 NOT = 05 ) AND ( 受−保険分類 = 01 )
009530                MOVE 連月−施術和暦年月 TO 連後期高齢ＣＮＶ−施術和暦年月
009540                MOVE 連月−患者コード   TO 連後期高齢ＣＮＶ−患者コード
009550                CALL   "KKCNV"
009560                CANCEL "KKCNV"
009570*
009580                IF 連後期高齢ＣＮＶ−変換対象フラグ = "YES"
009590**                 / レコード初期化 /
009600                   INITIALIZE  受−レコード
009610**
009620                   MOVE 連月−施術和暦 TO 受−施術和暦 受−請求和暦
009630                   MOVE 連月−施術年   TO 受−施術年   受−請求年  
009640                   MOVE 連月−施術月   TO 受−施術月   受−請求月  
009650                   MOVE 連月−患者番号 TO 受−患者番号
009660                   MOVE 連月−枝番     TO 受−枝番
009670*
009680                   MOVE 連後期高齢ＣＮＶ−保険種別       TO 受−保険種別
009690                   MOVE 連後期高齢ＣＮＶ−保険者番号     TO 受−保険者番号
009700                   MOVE 連後期高齢ＣＮＶ−公費種別       TO 受−公費種別
009710                   MOVE 連後期高齢ＣＮＶ−費用負担者番号 TO 受−費用負担者番号
009720                   MOVE 連後期高齢ＣＮＶ−老人負担金免除 TO 受−老人負担金免除
009730                   MOVE 連後期高齢ＣＮＶ−特別区分       TO 受−特別区分
009740*
009750                   MOVE 連後期高齢ＣＮＶ−本人家族区分   TO 受−本人家族区分
009760                   MOVE 連後期高齢ＣＮＶ−患者氏名       TO 受−患者氏名 受−被保険者氏名
009770                   MOVE 連後期高齢ＣＮＶ−患者カナ       TO 受−患者カナ 受−被保険者カナ
009780                   MOVE 連後期高齢ＣＮＶ−患者性別       TO 受−患者性別 受−被保険者性別
009790                   MOVE 連後期高齢ＣＮＶ−患者生年月日   TO 受−患者生年月日 受−被保険者生年月日
009800                   MOVE 連後期高齢ＣＮＶ−患者郵便番号   TO 受−患者郵便番号 受−郵便番号
009810                   MOVE 連後期高齢ＣＮＶ−患者住所１     TO 受−患者住所１ 受−住所１
009820                   MOVE 連後期高齢ＣＮＶ−患者住所２     TO 受−患者住所２ 受−住所２
009830                   MOVE 連後期高齢ＣＮＶ−患者電話番号   TO 受−患者電話番号 受−電話番号
009840*                 / 退避レコードより /
009850                   MOVE 作業受−助成種別                 TO 受−助成種別
009860                   MOVE 作業受−費用負担者番号助成       TO 受−費用負担者番号助成
009870                   MOVE 作業受−受益者番号助成           TO 受−受益者番号助成
009880                   MOVE 作業受−有効年月日助成           TO 受−有効年月日助成
009890                   MOVE 作業受−助成負担金免除           TO 受−助成負担金免除
009900***                   MOVE 作業受−備考                     TO 受−備考
009910                   MOVE 作業受−カルテ裏印刷行数         TO 受−カルテ裏印刷行数
009920                   MOVE 作業受−カルテ一般裏印刷行数     TO 受−カルテ一般裏印刷行数
009930                   MOVE 作業受−カルテ裏印刷列数         TO 受−カルテ裏印刷列数
009940*
009950**                  / 新システム追加項目 /
009960                   MOVE 作業受−保険分類                 TO 受−保険分類
009970                   MOVE 作業受−助成償還払い区分         TO 受−助成償還払い区分
009980                   MOVE 作業受−助成レセ印刷対象区分     TO 受−助成レセ印刷対象区分
009990                   MOVE 作業受−資格取得年月日助成       TO 受−資格取得年月日助成
010000                   MOVE 作業受−助成レセ会総括表印刷対象区分   TO 受−助成レセ会総括表印刷対象区分
010010*
010020                   MOVE 作業受−柔整郵便電話番号印刷     TO 受−柔整郵便電話番号印刷
010030                   MOVE 作業受−鍼灸郵便電話番号印刷     TO 受−鍼灸郵便電話番号印刷
010040*
010050**
010060                END-IF
010070             END-IF
010080*---------------------------------------------------------------------------------------------*
010090*/受付負傷で繰り越す場合はレセ印刷区分総括表印刷区分を初期値の設定に変更する↓↓↓/130425
010100*(保険証画面ではこの処理の後に保険証画面で選択)
010110             INITIALIZE 連助成レセまとめ−キー 連助成レセまとめ２−キー 連助成レセまとめ３−キー
010120             MOVE 制−協会コード         TO 連助成レセまとめ−柔整協会コード    
010130             MOVE 受−保険種別           TO 連助成レセまとめ−保険種別          
010140             MOVE 受−保険者番号         TO 連助成レセまとめ−保険者番号        
010150             MOVE 受−助成種別           TO 連助成レセまとめ−助成種別          
010160             MOVE 受−費用負担者番号助成 TO 連助成レセまとめ−費用負担者番号助成
010170             MOVE 受−公費種別           TO 連助成レセまとめ−公費種別          
010180             MOVE 受−費用負担者番号     TO 連助成レセまとめ−費用負担者番号    
010190             MOVE 受−施術和暦年月       TO 連助成レセまとめ２−施術和暦年月
010200             MOVE 受−患者コード         TO 連助成レセまとめ３−患者コード
010210             CALL   "JRECEOF2"
010220             CANCEL "JRECEOF2"
010230             IF (連助成レセまとめ−助成レセ印刷対象区分         NOT = 受−助成レセ印刷対象区分        ) OR
010240                (連助成レセまとめ−助成レセ会総括表印刷対象区分 NOT = 受−助成レセ会総括表印刷対象区分)
010250                 MOVE 連助成レセまとめ−助成レセ印刷対象区分         TO 受−助成レセ印刷対象区分 
010260                 MOVE 連助成レセまとめ−助成レセ会総括表印刷対象区分 TO 受−助成レセ会総括表印刷対象区分
010270             END-IF
010280*/受付負傷で繰り越す場合はレセ印刷区分総括表印刷区分を初期値の設定に変更する↑↑↑/130425
010290*
010300             WRITE 受−レコード
010310             INVALID KEY
010320                 MOVE NC"受診" TO ファイル名
010330                 PERFORM エラー表示
010340             END-WRITE
010350*
010360*---------------------------------------------------------------------------------------------*
010370* 受診者２
010380             MOVE 連月−施術和暦 TO 受２−施術和暦 受２−請求和暦  受２−助成請求和暦
010390             MOVE 連月−施術年   TO 受２−施術年   受２−請求年    受２−助成請求年
010400             MOVE 連月−施術月   TO 受２−施術月   受２−請求月    受２−助成請求月
010410
010420* 初期化する
010430             MOVE ZERO             TO 受２−請求区分      受２−助成請求区分
010440             MOVE ZERO             TO 受２−請求対象区分  受２−助成請求対象区分
010450             MOVE ZERO             TO 受２−来院動機区分
010460             MOVE ZERO             TO 受２−紹介者患者番号
010470***
010480* 受診者Ｆより編集
010490*             IF 作業受−保険証償還払い区分 = 2 OR 3
010500*                MOVE 1    TO 受２−柔整本体償還払い区分
010510*             ELSE
010520*                MOVE ZERO TO 受２−柔整本体償還払い区分
010530*             END-IF
010540*
010550*             IF 作業受−助成償還払い区分 = 2 OR 3
010560*                MOVE 1    TO 受２−柔整助成償還払い区分
010570*             ELSE
010580*                MOVE ZERO TO 受２−柔整助成償還払い区分
010590*             END-IF
010600*
010610*             IF 作業受−助成レセ印刷対象区分 = 2 OR 3 OR 5 OR 6
010620*                MOVE 1 TO 受２−柔整助成レセ印刷対象区分
010630*                IF 作業受−助成レセ印刷対象区分 = 2 OR 3
010640*                   MOVE 1    TO 受２−柔整本体まとめ区分
010650*                ELSE
010660*                   MOVE ZERO TO 受２−柔整本体まとめ区分
010670*                END-IF
010680*             ELSE
010690*                MOVE ZERO TO 受２−柔整助成レセ印刷対象区分
010700*             END-IF
010710*
010720*             IF 作業受−助成レセ会総括表印刷対象区分 = 2 OR 3
010730*                MOVE 1    TO 受２−柔整助成レセ会総括表印刷対象区分
010740*             ELSE
010750*                MOVE ZERO TO 受２−柔整助成レセ会総括表印刷対象区分
010760*             END-IF
010770*
010780***
010790* 上記の区分は使用しなくなった（receptへ移動）ので、初期化する
010800             MOVE ZERO           TO 受２−柔整本体償還払い区分
010810             MOVE ZERO           TO 受２−柔整本体まとめ区分
010820             MOVE ZERO           TO 受２−柔整助成償還払い区分
010830             MOVE ZERO           TO 受２−柔整助成レセ印刷対象区分
010840             MOVE ZERO           TO 受２−柔整助成レセ会総括表印刷対象区分
010850             MOVE ZERO           TO 受２−柔整本体枝番まとめ区分
010860***
010870*------------------------------------------------------*
010880             IF 連後期高齢ＣＮＶ−変換対象フラグ = "YES"
010890                MOVE SPACE TO 受２−事業所名称
010900                MOVE SPACE TO 受２−事業所郵便番号
010910                MOVE SPACE TO 受２−事業所住所１
010920                MOVE SPACE TO 受２−事業所住所２
010930             END-IF
010940*------------------------------------------------------*
010950*
010960             WRITE 受２−レコード
010970             INVALID KEY
010980                 MOVE NC"受診２" TO ファイル名
010990                 PERFORM エラー表示
011000             END-WRITE
011010*
011020*---------------------------------------------------------------------------------------------*
011030*---------------------------------------------------------------------------------------------*
011040* 最新レコード
011050* 処理年月が最新レコードより過去の場合は更新しない
011060**             IF 受−施術和暦年月Ｎ < 連月−施術和暦年月 
011070             IF 施術和暦年月ＮＷ < 連月−施術和暦年月 
011080                 MOVE ZERO           TO 受−施術和暦
011090                 MOVE ZERO           TO 受−施術年  
011100                 MOVE ZERO           TO 受−施術月  
011110                 MOVE 連月−施術和暦 TO 受−施術和暦Ｎ
011120                 MOVE 連月−施術年   TO 受−施術年Ｎ
011130                 MOVE 連月−施術月   TO 受−施術月Ｎ
011140                 MOVE ZERO           TO 受−請求和暦
011150                 MOVE ZERO           TO 受−請求年  
011160                 MOVE ZERO           TO 受−請求月  
011170                 MOVE ZERO           TO 受−継続区分
011180                 MOVE ZERO           TO 受−請求区分
011190                 MOVE ZERO           TO 受−レセ印刷区分
011200                 MOVE ZERO           TO 受−レセ印刷区分助成
011210                 MOVE ZERO           TO 受−老人負担回数
011220                 MOVE ZERO           TO 受−老人負担額累計
011230                 MOVE ZERO           TO 受−老人最終負担日
011240                 MOVE ZERO           TO 受−老人負担額端数
011250                 MOVE ZERO           TO 受−老人負担無し
011260                 MOVE SPACE          TO 受−枝番作成時枝番
011270                 MOVE ZERO           TO 受−はり区分
011280                 MOVE ZERO           TO 受−あんま区分
011290                 MOVE ZERO           TO 受−柔整有区分
011300                 MOVE ZERO           TO 受−鍼灸有区分
011310                 MOVE ZERO           TO 受−マッサージ有区分
011320                 MOVE ZERO           TO 受−併用区分
011330                 INITIALIZE             受−保険変更情報
011340                 MOVE ZERO           TO 受−助成月途中開始日
011350*
011360                 REWRITE 受−レコード
011370                 INVALID KEY
011380                     MOVE NC"受診" TO ファイル名
011390                     PERFORM エラー表示
011400                 END-REWRITE
011410*
011420*---------------------*
011430* 受診者２
011440                 MOVE ZERO           TO 受２−施術和暦
011450                 MOVE ZERO           TO 受２−施術年  
011460                 MOVE ZERO           TO 受２−施術月  
011470                 MOVE ZERO           TO 受２−請求和暦
011480                 MOVE ZERO           TO 受２−請求年  
011490                 MOVE ZERO           TO 受２−請求月  
011500                 MOVE ZERO           TO 受２−請求区分
011510                 MOVE ZERO           TO 受２−請求対象区分
011520                 MOVE ZERO           TO 受２−助成請求和暦
011530                 MOVE ZERO           TO 受２−助成請求年  
011540                 MOVE ZERO           TO 受２−助成請求月  
011550                 MOVE ZERO           TO 受２−助成請求区分
011560                 MOVE ZERO           TO 受２−助成請求対象区分
011570                 MOVE ZERO           TO 受２−来院動機区分
011580                 MOVE ZERO           TO 受２−紹介者患者番号
011590                 MOVE ZERO           TO 受２−柔整本体償還払い区分
011600                 MOVE ZERO           TO 受２−柔整本体まとめ区分
011610                 MOVE ZERO           TO 受２−柔整助成償還払い区分
011620                 MOVE ZERO           TO 受２−柔整助成レセ印刷対象区分
011630                 MOVE ZERO           TO 受２−柔整助成レセ会総括表印刷対象区分
011640                 MOVE ZERO           TO 受２−柔整本体枝番まとめ区分
011650*
011660                 REWRITE 受２−レコード
011670                 INVALID KEY
011680                     MOVE NC"受診２" TO ファイル名
011690                     PERFORM エラー表示
011700                 END-REWRITE
011710*---------------------*
011720             ELSE
011730                 MOVE  NC"　当月以降の負傷データ有り。確認下さい。" TO 連メ−メッセージ
011740                 CALL   "MSG001"
011750                 CANCEL "MSG001"
011760             END-IF
011770*
011780*-----------------------------------------------------------*
011790*            労災・自賠責・生保ファイルの月次更新
011800             IF 作業受−保険分類 = 2 OR 3 OR 4
011810                INITIALIZE 連月労災等−キー
011820                MOVE 連月−施術和暦年月 TO 連月労災等−施術和暦年月
011830                MOVE 連月−患者コード   TO 連月労災等−患者コード
011840                MOVE 作業受−保険分類   TO 連月労災等−保険分類
011850                MOVE 施術和暦年月ＮＷ   TO 連月労災等−最新施術和暦年月
011860                CALL   "GETUJIK2"
011870                CANCEL "GETUJIK2"
011880             END-IF
011890*-----------------------------------------------------------*
011900*
011910             PERFORM 負傷データ生成
011920         END-READ
011930     END-IF.
011940*
011950*================================================================*
011960 負傷データ生成 SECTION.
011970*
011980     PERFORM 前回通院月判定.
011990*
012000     PERFORM レコード退避処理.
012010*
012020     IF 実行キーＷ = "YES"
012030        PERFORM 負傷前詰処理
012040     END-IF.
012050*
012060*
012070     MOVE 連月−施術和暦     TO 負−施術和暦.
012080     MOVE 連月−施術年       TO 負−施術年.
012090     MOVE 連月−施術月       TO 負−施術月.
012100     MOVE 連月−患者番号     TO 負−患者番号.
012110     MOVE 連月−枝番         TO 負−枝番.
012120     READ 負傷データＦ
012130     INVALID KEY
012140        PERFORM レコードセット
012150        WRITE 負−レコード
012160        IF 状態キー NOT = "00"
012170           MOVE NC"負傷" TO ファイル名
012180           PERFORM エラー表示
012190        END-IF
012200     NOT INVALID KEY
012210        MOVE 負−レセ署名区分 TO 負傷レセ署名区Ｗ
012220        PERFORM レコードセット
012230        MOVE 負傷レセ署名区Ｗ TO 負−レセ署名区分
012240        REWRITE 負−レコード
012250        IF 状態キー NOT = "00"
012260           MOVE NC"負傷更新" TO ファイル名
012270           PERFORM エラー表示
012280        END-IF
012290     END-READ.
012300*
012310*-------------------------------*
012320* 上記後
012330     PERFORM レセプトＦ生成.
012340*-------------------------------*
012350*
012360*================================================================*
012370 レコード退避処理 SECTION.
012380*
012390     MOVE SPACE TO レコードデータＴＷ.
012400     INITIALIZE    レコードデータＴＷ.
012410*
012420     MOVE 負−レコードデータ TO レコードデータＴＷ.
012430*/以下のデータは引き継がない
012440*/初回処置料算定区分 ０：通常　    １：初回処置料請求なし
012450*/電療請求区分       ０：通常請求  １：請求しない
012460*/初検請求区分       ０：通常      
012470*                    １：初検料・再検料・初回処置なし
012480*                    ２：初検料・再検料なし 
012490*                    ３：初回処置なし
012500*                    ４：初険加算時間外
012510*                    ５：初険加算休日
012520*                    ６：初険加算深夜
012530*/包帯交換数
012540*/初検加算の時刻
012550*/部位毎の金属副子数
012560*/部位毎の情報提供料
012570*/開始診療日手動区分
012580*/運動療法算定区分
012590*/自賠責その他金額
012600*/レセ署名区分
012610*/レセ長期理由印刷区分ＴＷ
012620*/労災自賠施術証明無区分ＴＷ
012630*/労災自賠施術明細無区分ＴＷ
012640*
012650     PERFORM VARYING 部位カウンタ FROM 1 BY 1
012660             UNTIL ( 部位カウンタ > 部位数ＴＷ )
012670         MOVE ZERO TO 算定区分ＴＷ    (部位カウンタ)
012680         MOVE ZERO TO 電療請求区分ＴＷ(部位カウンタ)
012690         MOVE ZERO TO 包帯交換数ＴＷ  (部位カウンタ)
012700         MOVE ZERO TO 大ＴＷ          (部位カウンタ)
012710         MOVE ZERO TO 中ＴＷ          (部位カウンタ)
012720         MOVE ZERO TO 小ＴＷ          (部位カウンタ)
012730         MOVE ZERO TO 情報提供区分ＴＷ(部位カウンタ)
012740***         MOVE ZERO TO 後療区分ＴＷ(部位カウンタ)
012750         MOVE ZERO TO 包帯更新区分ＴＷ(部位カウンタ)
012760     END-PERFORM.
012770     MOVE ZERO TO 初検請求区分ＴＷ.
012780     MOVE ZERO TO 診療時ＴＷ.
012790     MOVE ZERO TO 診療分ＴＷ.
012800     MOVE ZERO TO 開始診療日手動区分ＴＷ.
012810     MOVE ZERO TO 自賠責その他金額ＴＷ.
012820     MOVE ZERO TO レセ署名区分ＴＷ.
012830     MOVE ZERO TO レセ長期理由印刷区分ＴＷ.
012840     MOVE ZERO TO 運動更新区分ＴＷ.
012850     MOVE ZERO TO 指導更新区分ＴＷ.
012860*/前月の状態に関わらず算定するように変更/130710
012870     MOVE SPACE TO  労災自賠施術証明無区分ＴＷ
012880     MOVE SPACE TO  労災自賠施術明細無区分ＴＷ
012890*
012900** / 保険種別取得 /
012910     MOVE 連月−施術和暦     TO 受−施術和暦.
012920     MOVE 連月−施術年       TO 受−施術年.
012930     MOVE 連月−施術月       TO 受−施術月.
012940     MOVE 連月−患者番号     TO 受−患者番号.
012950     MOVE 連月−枝番         TO 受−枝番.
012960     READ 受診者情報Ｆ
012970     NOT INVALID KEY
012980         MOVE 受−保険種別 TO 保険種別Ｗ
012990         MOVE 受−公費種別 TO 公費種別Ｗ
013000         MOVE 受−助成種別 TO 助成種別Ｗ
013010     END-READ.
013020* 労災・自賠責用(運動療法区分)
013030     IF 保険種別Ｗ = 70 OR 80
013040         MOVE 運動療法初期値Ｗ  TO  運動療法算定区分ＴＷ
013050*/労災自賠責指導包帯/0708
013060         MOVE 指導管理初期値Ｗ  TO  指導管理算定区分ＴＷ
013070         MOVE 包帯交換初期値Ｗ  TO  包帯交換算定区分ＴＷ
013080     ELSE
013090         MOVE ZERO  TO  運動療法算定区分ＴＷ
013100*/労災自賠責指導包帯/0708
013110         MOVE ZERO  TO  指導管理算定区分ＴＷ 包帯交換算定区分ＴＷ
013120     END-IF.
013130*
013140*================================================================*
013150 レコードセット SECTION.
013160*
013170     MOVE SPACE TO 負−レコード.
013180     INITIALIZE    負−レコード.
013190*
013200     MOVE 連月−施術和暦     TO 負−施術和暦.
013210     MOVE 連月−施術年       TO 負−施術年.
013220     MOVE 連月−施術月       TO 負−施術月.
013230     MOVE 連月−患者番号     TO 負−患者番号.
013240     MOVE 連月−枝番         TO 負−枝番.
013250*
013260     IF 実行キーＷ = "YES"
013270         MOVE レコードデータＴＷ TO 負−レコードデータ
013280*
013290**       / 2008/06 治癒繰越時、制01−初検時相談料区分=1(加算なし)の時は、負−初検時相談料区分=1にする。/
013300         IF 部位数ＴＷ = ZERO
013310            IF 連月−施術和暦年月 >= 42006
013320               IF 初検時相談料区分Ｗ = 1
013330                  MOVE 1    TO 負−初検時相談料区分
013340               ELSE
013350                  MOVE ZERO TO 負−初検時相談料区分
013360               END-IF
013370            END-IF
      */負傷原因を印刷しないフラグ。治癒中止の場合は繰り越さないように変更/140916
                  MOVE ZERO TO 負−レセ負傷原因印刷区分
013380         END-IF
013390**
013400     ELSE
013410**      / 繰越でない時で、制−レセ負傷原因印刷区分=2の時は、負−レセ負傷原因印刷区分=1(印刷しない)にする。/
013420         IF レセ負傷原因印刷区分Ｗ = 2
013430            MOVE 1 TO 負−レセ負傷原因印刷区分
013440         END-IF
013450**      / 2008/06 繰越でない時で、制01−初検時相談料区分=1(加算なし)の時は、負−初検時相談料区分=1にする。/
013460         IF 連月−施術和暦年月 >= 42006
013470            IF 初検時相談料区分Ｗ = 1
013480               MOVE 1 TO 負−初検時相談料区分
013490            END-IF
013500         END-IF
013510**
013520     END-IF.
013530*
013540*================================================================*
013550*================================================================*
013560 負傷前詰処理 SECTION.
013570*
013580*---------------------------------------------------------*
013590* RECEPTより部位名称を退避
013600     MOVE SPACE TO レセ負傷データＷＷ.
013610     INITIALIZE    レセ負傷データＷＷ.
013620*
013630     MOVE 負−施術和暦     TO レセ−施術和暦.
013640     MOVE 負−施術年       TO レセ−施術年.
013650     MOVE 負−施術月       TO レセ−施術月.
013660     MOVE 負−患者番号     TO レセ−患者番号.
013670     MOVE 負−枝番         TO レセ−枝番.
013680     MOVE ZERO             TO レセ−レセ種別.
013690*
013700     START レセプトＦ KEY IS >= レセ−施術和暦年月
013710                                レセ−患者コード
013720                                レセ−レセ種別
013730     END-START.
013740*
013750     IF 状態キー = "00"
013760         MOVE SPACE TO 終了フラグ３
013770         PERFORM レセプトＦ読込
013780*
013790         IF ( レセ−施術和暦   = 負−施術和暦   ) AND
013800            ( レセ−施術年     = 負−施術年     ) AND
013810            ( レセ−施術月     = 負−施術月     ) AND
013820            ( レセ−患者コード = 負−患者コード ) AND
013830            ( 終了フラグ３     = SPACE )
013840*            / 部位名称退避 /
013850             PERFORM VARYING 行カウンタ FROM 1 BY 1  UNTIL ( 行カウンタ > 7 ) 
013860                MOVE レセ−部位名称１(行カウンタ) TO 部位名称１Ｗ(行カウンタ)
013870                MOVE レセ−部位名称２(行カウンタ) TO 部位名称２Ｗ(行カウンタ)
013880             END-PERFORM
013890         END-IF
013900     END-IF.
013910*---------------------------------------------------------*
013920*
013930**
013940     MOVE SPACE TO 前詰終了フラグ.
013950     PERFORM UNTIL 前詰終了フラグ NOT = SPACE
013960         MOVE SPACE TO 終了フラグ
013970         PERFORM VARYING 部位カウンタ FROM 1 BY 1
013980                   UNTIL ( 部位カウンタ > 部位数ＴＷ ) OR
013990                         ( 終了フラグ NOT = SPACE    )
014000*
014010             IF ( 転帰区分ＴＷ(部位カウンタ) NOT = ZERO ) AND
014020                ( 転帰区分ＴＷ(部位カウンタ) NOT = 9    )
014030                 PERFORM VARYING 部位カウンタ２ FROM 部位カウンタ BY 1
014040                         UNTIL 部位カウンタ２ >= 部位数ＴＷ
014050                     COMPUTE 部位カウンタＷ = 部位カウンタ２ + 1
014060                     MOVE 負傷データＴＷ(部位カウンタＷ) TO 
014070                                         負傷データＴＷ(部位カウンタ２)
014080                     MOVE 後療区分ＴＷ(部位カウンタＷ) TO 
014090                                         後療区分ＴＷ(部位カウンタ２)
014100*
014110                     MOVE レセ負傷データＷ(部位カウンタＷ) TO レセ負傷データＷ(部位カウンタ２)
014120*
014130                 END-PERFORM
014140*
014150* 最大部位位置を初期化
014160                 INITIALIZE 負傷データＴＷ(部位カウンタ２)
014170                 INITIALIZE 後療区分ＴＷ(部位カウンタ２)
014180                 INITIALIZE レセ負傷データＷ(部位カウンタ２)
014190*
014200* １部位前詰したため部位数を最大部位数−１とする。
014210                 COMPUTE 部位カウンタＷ２ = 部位カウンタ２ - 1
014220                 MOVE 部位カウンタＷ２ TO 部位数ＴＷ
014230                 MOVE "YES" TO 終了フラグ
014240             END-IF
014250         END-PERFORM
014260*
014270         MOVE "YES" TO 前詰終了フラグ
014280         PERFORM VARYING 部位カウンタ FROM 1 BY 1
014290                   UNTIL ( 部位カウンタ   > 部位数ＴＷ ) OR 
014300                         ( 前詰終了フラグ = SPACE      )
014310             IF ( 転帰区分ＴＷ(部位カウンタ) NOT = ZERO ) AND
014320                ( 転帰区分ＴＷ(部位カウンタ) NOT = 9    )
014330                 MOVE SPACE TO 前詰終了フラグ
014340             END-IF
014350         END-PERFORM
014360     END-PERFORM.
014370*
014380     IF 部位数ＴＷ = ZERO
014390         MOVE ZERO TO 初検和暦ＴＷ
014400         MOVE ZERO TO 初検年ＴＷ
014410         MOVE ZERO TO 初検月ＴＷ
014420         MOVE ZERO TO 初検日ＴＷ
014430     END-IF.
014440*
014450*================================================================*
014460*================================================================*
014470 レセプトＦ読込 SECTION.
014480*
014490     READ レセプトＦ NEXT
014500     AT END
014510         MOVE "YES"  TO 終了フラグ３
014520     END-READ.
014530*
014540*================================================================*
014550 レセプトＦ生成 SECTION.
014560*
014570**
014580     INITIALIZE レセプトＦ退避Ｗ.
014590**
014600* 現行レコード退避
014610     MOVE SPACE TO レセ存在フラグ.
014620     MOVE 連月−施術和暦     TO レセ−施術和暦.
014630     MOVE 連月−施術年       TO レセ−施術年.
014640     MOVE 連月−施術月       TO レセ−施術月.
014650     MOVE 連月−患者番号     TO レセ−患者番号.
014660     MOVE 連月−枝番         TO レセ−枝番.
014670     MOVE ZERO               TO レセ−レセ種別.
014680*
014690     START レセプトＦ KEY IS >= レセ−施術和暦年月
014700                                レセ−患者コード
014710                                レセ−レセ種別
014720     END-START.
014730*
014740     IF 状態キー = "00"
014750           MOVE SPACE TO 終了フラグ３
014760           PERFORM レセプトＦ読込
014770*
014780           IF ( レセ−施術和暦   = 連月−施術和暦   ) AND
014790              ( レセ−施術年     = 連月−施術年     ) AND
014800              ( レセ−施術月     = 連月−施術月     ) AND
014810              ( レセ−患者コード = 連月−患者コード ) AND
014820              ( 終了フラグ３     = SPACE )
014830*             / 退避 /
014840               MOVE レセ−請求区分      TO レセ本体請求区分Ｗ
014850               MOVE レセ−請求和暦年月  TO レセ本体請求和暦年月Ｗ
014860               MOVE レセ−レセ実日数    TO レセ実日数Ｗ
014870*
014880               PERFORM VARYING 行カウンタ FROM 1 BY 1  UNTIL ( 行カウンタ > 7 ) 
014890                  MOVE レセ−部位名称１(行カウンタ) TO レセ部位名称１Ｗ(行カウンタ)
014900                  MOVE レセ−部位名称２(行カウンタ) TO レセ部位名称２Ｗ(行カウンタ)
014910               END-PERFORM
014920*
014930               MOVE "YES" TO レセ存在フラグ
014940           END-IF
014950     END-IF.
014960*
014970*
014980**** 助成レセレコードがあれば、助成レコード固有の情報を退避する。
014990     MOVE 連月−施術和暦     TO レセ−施術和暦.
015000     MOVE 連月−施術年       TO レセ−施術年.
015010     MOVE 連月−施術月       TO レセ−施術月.
015020     MOVE 連月−患者番号     TO レセ−患者番号.
015030     MOVE 連月−枝番         TO レセ−枝番.
015040     MOVE 3                  TO レセ−レセ種別.
015050     READ レセプトＦ
015060     NOT INVALID KEY
015070          MOVE レセ−請求区分      TO レセ助成請求区分Ｗ
015080          MOVE レセ−請求和暦年月  TO レセ助成請求和暦年月Ｗ
015090     END-READ.
015100*****
015110*
015120* 現行レコード削除
015130     IF レセ存在フラグ = "YES"
015140        MOVE 連月−施術和暦     TO レセ−施術和暦
015150        MOVE 連月−施術年       TO レセ−施術年
015160        MOVE 連月−施術月       TO レセ−施術月
015170        MOVE 連月−患者番号     TO レセ−患者番号
015180        MOVE 連月−枝番         TO レセ−枝番
015190        MOVE ZERO               TO レセ−レセ種別
015200*
015210        START レセプトＦ KEY IS >= レセ−施術和暦年月
015220                                   レセ−患者コード
015230                                   レセ−レセ種別
015240        END-START
015250*
015260        IF 状態キー = "00"
015270              MOVE SPACE TO 終了フラグ３
015280              PERFORM レセプトＦ読込
015290*
015300              PERFORM UNTIL ( レセ−施術和暦   NOT = 連月−施術和暦   ) OR
015310                            ( レセ−施術年     NOT = 連月−施術年     ) OR
015320                            ( レセ−施術月     NOT = 連月−施術月     ) OR
015330                            ( レセ−患者コード NOT = 連月−患者コード ) OR
015340                            ( 終了フラグ３     NOT = SPACE )
015350**
015360                   DELETE  レセプトＦ
015370                   IF 状態キー NOT = "00"
015380                      MOVE NC"レセプトＦ" TO ファイル名
015390                      PERFORM エラー表示Ｄ
015400                   END-IF
015410**
015420                   PERFORM レセプトＦ読込
015430              END-PERFORM
015440        END-IF
015450     END-IF.
015460*
015470*----------------------------------------------------------------------------------*
015480*
015490* 新規WRITE
015500*
015510* 鍼灸で保険証入力 YAW602よりCALL → 鍼灸で保険証入力時、当月の柔整レセプトＦがある時のみ、
015520* レセプトＦのみ作り直す
015530     IF (( 連月−負傷のみ区分 = "HM" ) AND ( レセ存在フラグ = "YES" )) OR
015540        ( 連月−負傷のみ区分 NOT = "HM" )
015550*
015560*
015570** / 保険種別取得 /
015580        MOVE ZERO TO 保険種別Ｗ
015590        MOVE ZERO TO 公費種別Ｗ
015600        MOVE ZERO TO 助成種別Ｗ
015610        MOVE 連月−施術和暦     TO 受−施術和暦
015620        MOVE 連月−施術年       TO 受−施術年
015630        MOVE 連月−施術月       TO 受−施術月
015640        MOVE 連月−患者番号     TO 受−患者番号
015650        MOVE 連月−枝番         TO 受−枝番
015660        READ 受診者情報Ｆ
015670        NOT INVALID KEY
015680             MOVE 受−保険種別 TO 保険種別Ｗ
015690             MOVE 受−公費種別 TO 公費種別Ｗ
015700             MOVE 受−助成種別 TO 助成種別Ｗ
015710        END-READ
015720**
015730*
015740        MOVE SPACE TO レセ−レコード
015750        INITIALIZE    レセ−レコード
015760*
015770* キー + 請求和暦年月 セット
015780        MOVE 連月−施術和暦     TO レセ−施術和暦 レセ−請求和暦
015790        MOVE 連月−施術年       TO レセ−施術年 レセ−請求年
015800        MOVE 連月−施術月       TO レセ−施術月 レセ−請求月
015810        MOVE 連月−患者番号     TO レセ−患者番号
015820        MOVE 連月−枝番         TO レセ−枝番
015830*
015840*   / 1:一般,2:老人,3:助成,4:労災,5:自賠責,6:自費,7:生保単独,8保険証忘れ /
015850        IF 公費種別Ｗ NOT = ZERO
015860           MOVE  2   TO レセ−レセ種別
015870        ELSE
015880           EVALUATE 保険種別Ｗ
015890           WHEN 70
015900               MOVE  4   TO レセ−レセ種別
015910           WHEN 80
015920               MOVE  5   TO レセ−レセ種別
015930           WHEN 90
015940               MOVE  6   TO レセ−レセ種別
015950           WHEN 85
015960               MOVE  7   TO レセ−レセ種別
015970           WHEN 91
015980               MOVE  8   TO レセ−レセ種別
015990           WHEN OTHER
016000               MOVE  1   TO レセ−レセ種別
016010           END-EVALUATE
016020        END-IF
016030*
016040**
016050* 受診者情報ＦREAD中
016060        IF ( 受−施術和暦年月 = 連月−施術和暦年月 ) AND ( 受−患者コード = 連月−患者コード) 
016070*
016080* 受診者Ｆより編集
016090                IF 受−保険証償還払い区分 = 2 OR 3
016100                   MOVE 1    TO レセ−償還払い区分
016110                END-IF
016120*
016130                IF 受−助成レセ印刷対象区分 = 2 OR 3
016140                   MOVE 1    TO レセ−本体まとめ区分
016150                END-IF
016160**
016170                EVALUATE レセ−レセ種別
016180                WHEN 1
016190                   MOVE 受−保険者番号         TO レセ−請求保険者番号
016200                WHEN 2
016210                   MOVE 受−費用負担者番号     TO レセ−請求保険者番号
016220                WHEN 3
016230                   MOVE 受−費用負担者番号助成 TO レセ−請求保険者番号
016240                WHEN OTHER
016250                   MOVE SPACE                  TO レセ−請求保険者番号
016260                END-EVALUATE
016270**
016280        END-IF
016290**
016300*---*
016310*    繰越の時、部位名を繰り越す
016320        IF 実行キーＷ = "YES"
016330           PERFORM VARYING 行カウンタ FROM 1 BY 1  UNTIL ( 行カウンタ > 7 ) 
016340              MOVE 部位名称１Ｗ(行カウンタ) TO レセ−部位名称１(行カウンタ)
016350              MOVE 部位名称２Ｗ(行カウンタ) TO レセ−部位名称２(行カウンタ)
016360           END-PERFORM
016370        END-IF
016380*---*
016390*
016400*---*
016410*       / 退避データを戻す /
016420        IF レセ存在フラグ = "YES"
016430*
016440           MOVE レセ実日数Ｗ TO レセ−レセ実日数
016450*
016460*          / 請求年月情報ＷZEROは転帰なし /
      */請求無し(年月ゼロ)に対応↓↓↓/150807
016470*           IF レセ本体請求和暦年月Ｗ NOT = ZERO
016470           IF (レセ本体請求和暦年月Ｗ NOT = ZERO) OR
                    (レセ本体請求区分Ｗ     NOT = ZERO)
      */請求無し(年月ゼロ)に対応↑↑↑/150807
016480              MOVE レセ本体請求区分Ｗ     TO レセ−請求区分
016490              MOVE レセ本体請求和暦年月Ｗ TO レセ−請求和暦年月
016500           END-IF
016510*
016520*          / 保険証入力 YAW602よりCALL の時、部位名を戻す /
016530           IF 連月−負傷のみ区分 = "YAW" OR "HM"
016540              PERFORM VARYING 行カウンタ FROM 1 BY 1  UNTIL ( 行カウンタ > 7 ) 
016550                 MOVE レセ部位名称１Ｗ(行カウンタ) TO レセ−部位名称１(行カウンタ)
016560                 MOVE レセ部位名称２Ｗ(行カウンタ) TO レセ−部位名称２(行カウンタ)
016570              END-PERFORM
016580           END-IF
016590        END-IF
016600*---*
016610*
016620***
016630**/データ不備の対応コメント/130910
016640*        WRITE レセ−レコード
016650*        IF 状態キー NOT = "00"
016660*            DISPLAY "施術年月・患者コード" レセ−施術年月 " " レセ−患者コード
016670*            MOVE NC"レセプト" TO ファイル名
016680*            PERFORM エラー表示
016690*        END-IF
016700*
016710        WRITE レセ−レコード
016720        IF 状態キー NOT = "00"
016730*/データ不備の対応/130910
016740            IF 状態キー = "22"          *>キーの重複
016750                MOVE レセ−レコードキー TO レセレコードキーＷ
016760                MOVE レセ−請求年月情報 TO レセ請求年月情報Ｗ
016770                MOVE SPACE TO レセ−レコード
016780                INITIALIZE    レセ−レコード
016790                MOVE レセレコードキーＷ TO レセ−レコードキー
016800                MOVE レセ請求年月情報Ｗ TO レセ−請求年月情報
016810*                REWRITE レセ−レコード
016820                WRITE レセ−レコード
016830                IF 状態キー NOT = "00"
016840                    DISPLAY "施術年月・患者コード " レセ−施術年月 " " レセ−患者コード
016850                    MOVE NC"レセプト" TO ファイル名
016860                    PERFORM エラー表示
016870                END-IF
016880            ELSE
016890                DISPLAY "施術年月・患者コード " レセ−施術年月 " " レセ−患者コード
016900                MOVE NC"レセプト" TO ファイル名
016910                PERFORM エラー表示
016920            END-IF
016930        END-IF
016940*
016950*
016960****
016970*    / 助成時、助成レコード作成 /
016980        IF ( 助成種別Ｗ NOT = ZERO ) AND ( 保険種別Ｗ < 10 )
016990           MOVE  3   TO レセ−レセ種別
017000*
017010           IF ( 受−施術和暦年月 = 連月−施術和暦年月 ) AND ( 受−患者コード = 連月−患者コード) 
017020*       受診者Ｆより編集
017030*            / 初期化 /
017040                MOVE ZERO TO レセ−償還払い区分
017050                MOVE ZERO TO レセ−レセ印刷対象区分
017060                MOVE ZERO TO レセ−会総括表印刷対象区分
017070                MOVE ZERO TO レセ−本体まとめ区分
017080*
017090                IF 受−助成償還払い区分 = 2 OR 3
017100                   MOVE 1    TO レセ−償還払い区分
017110                END-IF
017120*
017130                IF 受−助成レセ印刷対象区分 = 2 OR 3 OR 5 OR 6
017140                   MOVE 1 TO レセ−レセ印刷対象区分
017150                   IF 受−助成レセ印刷対象区分 = 2 OR 3
017160                      MOVE 1    TO レセ−本体まとめ区分
017170                   END-IF
017180                END-IF
017190*
017200                IF 受−助成レセ会総括表印刷対象区分 = 2 OR 3
017210                   MOVE 1    TO レセ−会総括表印刷対象区分
017220                END-IF
017230**
017240                MOVE 受−費用負担者番号助成 TO レセ−請求保険者番号
017250**
017260           END-IF
017270**
017280*          / 初期値 /
017290           MOVE ZERO                  TO レセ−請求区分
017300           MOVE レセ−施術和暦年月    TO レセ−請求和暦年月
017310           IF レセ存在フラグ = "YES"
017320*              / 助成請求年月情報ＷZEROは転帰なし /
      */請求無し(年月ゼロ)に対応↓↓↓/150807
017330*               IF レセ助成請求和暦年月Ｗ NOT = ZERO
017330               IF (レセ助成請求和暦年月Ｗ NOT = ZERO) OR
                        (レセ助成請求区分Ｗ     NOT = ZERO)
      */請求無し(年月ゼロ)に対応↑↑↑/150807
017340                  MOVE レセ助成請求区分Ｗ     TO レセ−請求区分
017350                  MOVE レセ助成請求和暦年月Ｗ TO レセ−請求和暦年月
017360               END-IF
017370           END-IF
017380**
017390           WRITE レセ−レコード
017400           IF 状態キー NOT = "00"
017410              MOVE NC"レセプト助成" TO ファイル名
017420              PERFORM エラー表示
017430           END-IF
017440        END-IF
017450     END-IF.
017460*
017470*================================================================*
017480*================================================================*
017490 前回通院月判定 SECTION.
017500*
017510*** 前回の負傷データが前月か判定 
017520     MOVE  SPACE            TO 前月フラグ.
017530     MOVE 連月−患者コード  TO 負−患者コード.
017540     MOVE 連月−施術和暦    TO 負−施術和暦.
017550     MOVE 連月−施術年      TO 負−施術年.
017560     MOVE 連月−施術月      TO 負−施術月.
017570     START 負傷データＦ KEY IS <  負−患者コード
017580                                  負−施術和暦年月
017590                                  REVERSED
017600     END-START.
017610     IF 状態キー = "00"
017620         MOVE SPACE  TO 終了フラグ２
017630         PERFORM 負傷データＦ読込
017640         IF ( 終了フラグ２    = SPACE            ) AND
017650            ( 負−患者コード  = 連月−患者コード )
017660            PERFORM 前月判定
017670         END-IF
017680     END-IF.
017690*
017700*================================================================*
017710 前月判定 SECTION.
017720* 
017730*** 読み込んだ負傷データの年月が、前月かどうか判定 (年月の差が 1 か?)
017740      MOVE  SPACE  TO  前月フラグ.
017750      INITIALIZE  計算年月日Ｗ 開始年月日２Ｗ 終了年月日２Ｗ.
017760**
017770      MOVE 連月−施術和暦  TO 終了和暦２Ｗ.
017780      MOVE 連月−施術年    TO 終了年２Ｗ.
017790      MOVE 連月−施術月    TO 終了月２Ｗ.
017800      MOVE 負−施術和暦    TO 開始和暦２Ｗ.
017810      MOVE 負−施術年      TO 開始年２Ｗ.
017820      MOVE 負−施術月      TO 開始月２Ｗ.
017830*
017840      EVALUATE TRUE
017850       WHEN (開始和暦２Ｗ = 終了和暦２Ｗ) AND (開始年２Ｗ = 終了年２Ｗ)
017860            PERFORM  前月比較月
017870       WHEN (開始和暦２Ｗ = 終了和暦２Ｗ) AND (開始年２Ｗ NOT = 終了年２Ｗ)
017880            PERFORM  前月比較年
017890       WHEN  開始和暦２Ｗ NOT = 終了和暦２Ｗ 
017900            PERFORM  前月比較元号
017910      END-EVALUATE.
017920*
017930*--- 元号日付チェック ---*
017940*****      IF 計算月Ｗ = 1
017950*****         MOVE  "YES"  TO  前月フラグ
017960*****         PERFORM データチェック
017970******      END-IF.
017980      IF ( 計算月Ｗ = 1 )
017990         MOVE  "YES"  TO  前月フラグ
018000         IF ( 開始和暦２Ｗ NOT = 終了和暦２Ｗ )
018010            MOVE "YES"  TO  実行キーＷ
018020         ELSE
018030            PERFORM データチェック
018040         END-IF
018050      END-IF.
018060*
018070*================================================================*
018080 前月比較月  SECTION.
018090*
018100     IF  終了月２Ｗ >  開始月２Ｗ
018110         COMPUTE 計算月Ｗ = 終了月２Ｗ - 開始月２Ｗ
018120     ELSE
018130        MOVE ZERO TO 計算月Ｗ
018140     END-IF.
018150*
018160*================================================================*
018170 前月比較年  SECTION.
018180*
018190     IF  終了年２Ｗ >  開始年２Ｗ
018200         COMPUTE 計算年Ｗ = 終了年２Ｗ - 開始年２Ｗ
018210         COMPUTE 計算月Ｗ = (計算年Ｗ * 12 + 終了月２Ｗ) - 開始月２Ｗ
018220     ELSE
018230        MOVE ZERO TO 計算月Ｗ
018240     END-IF.
018250*
018260*================================================================*
018270 前月比較元号  SECTION.
018280*
018290     MOVE 開始和暦２Ｗ TO 元−元号区分.
018300     READ 元号マスタ
018310     NOT INVALID KEY
018320         MOVE 元−開始西暦年 TO 開始西暦年Ｗ
018330     END-READ.
018340     MOVE 終了和暦２Ｗ TO 元−元号区分.
018350     READ 元号マスタ
018360     NOT INVALID KEY
018370         MOVE 元−開始西暦年 TO 終了西暦年Ｗ
018380     END-READ.
018390**
018400     IF (開始西暦年Ｗ NOT = ZERO) AND (終了西暦年Ｗ NOT = ZERO)
018410        COMPUTE 開始西暦年Ｗ = 開始西暦年Ｗ + 開始年２Ｗ - 1
018420        COMPUTE 終了西暦年Ｗ = 終了西暦年Ｗ + 終了年２Ｗ - 1
018430*
018440        IF 終了西暦年Ｗ =  開始西暦年Ｗ
018450           PERFORM  前月比較月
018460*--- 元号日付チェック ---*
018470           IF ( 計算月Ｗ = ZERO )
018480              MOVE 1 TO 計算月Ｗ
018490           END-IF
018500*
018510        ELSE
018520           IF  終了西暦年Ｗ >  開始西暦年Ｗ
018530               COMPUTE 計算年Ｗ = 終了西暦年Ｗ - 開始西暦年Ｗ
018540               COMPUTE 計算月Ｗ = (計算年Ｗ * 12 + 終了月２Ｗ) - 開始月２Ｗ
018550           ELSE
018560               MOVE ZERO TO 計算月Ｗ
018570           END-IF
018580        END-IF
018590     ELSE
018600        MOVE ZERO TO 計算月Ｗ
018610     END-IF.
018620*
018630*================================================================*
018640 負傷データＦ読込 SECTION.
018650*
018660     READ 負傷データＦ NEXT
018670     AT END
018680         MOVE "YES" TO 終了フラグ２
018690     END-READ.
018700*================================================================*
018710 データチェック SECTION.
018720*
018730     MOVE SPACE  TO 実行キーＷ.
018740* *****************************************************************
018750* * 負傷部位有無チェック：部位数 = 0 の場合データ作成対象としない *
018760* *****************************************************************
018770     IF 負−部位数 NOT = ZERO
018780*    *************************************************************
018790*    * 施術記録チェック：通院数 = 0 の場合データ作成対象としない *
018800*    *************************************************************
018810         MOVE 負−患者番号  TO 施記−患者番号
018820         MOVE 負−枝番      TO 施記−枝番
018830         MOVE 負−施術和暦  TO 施記−施術和暦
018840         MOVE 負−施術年    TO 施記−施術年
018850         MOVE 負−施術月    TO 施記−施術月
018860         MOVE ZERO          TO 施記−施術日
018870         START 施術記録Ｆ   KEY IS >= 施記−患者コード
018880                                      施記−施術和暦年月日
018890         END-START
018900         IF 状態キー = "00"
018910             MOVE SPACE TO 終了フラグ２
018920             MOVE SPACE TO 施術記録有Ｗ
018930             PERFORM 施術記録Ｆ読込
018940             PERFORM UNTIL ( 終了フラグ２         = "YES"          ) OR
018950                           ( 施記−患者コード NOT = 負−患者コード ) OR
018960                           ( 施記−施術和暦   NOT = 負−施術和暦   ) OR
018970                           ( 施記−施術年     NOT = 負−施術年     ) OR
018980                           ( 施記−施術月     NOT = 負−施術月     ) OR
018990                           ( 施術記録有Ｗ         = "YES"          )
019000                 MOVE "YES"  TO 施術記録有Ｗ
019010                 MOVE "YES"  TO 実行キーＷ
019020             END-PERFORM
019030         ELSE
019040             MOVE SPACE  TO 実行キーＷ
019050         END-IF
019060     ELSE
019070         MOVE SPACE  TO 実行キーＷ
019080     END-IF.
019090*
019100*================================================================*
019110 施術記録Ｆ読込 SECTION.
019120*
019130     READ 施術記録Ｆ NEXT
019140     AT END
019150         MOVE "YES" TO 終了フラグ２
019160     END-READ.
019170*================================================================*
019180*================================================================*
019190******************************************************************
019200 END PROGRAM GETUJIK.
019210******************************************************************
