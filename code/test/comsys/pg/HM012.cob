000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             HM012.
000060 AUTHOR.                 佐野 誠
000070*
000080*------------------------------------------------------------------*
000090* 枝番作成（ＨレセプトＦとＨ日計データＦとＨ往療実績・Ｈ往療予定Ｆ）
000100*
000110*------------------------------------------------------------------*
000120 DATE-WRITTEN.           2004-09-27
000130 DATE-COMPILED.          2004-09-27
000140*----------------------------------------------------------------*
000150* 2018/10/29 岡田 憲和 Ｈ報告書ファイルを追加
000151*----------------------------------------------------------------*
000152******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000260     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000270                             ORGANIZATION             IS  INDEXED
000280                             ACCESS MODE              IS  DYNAMIC
000290                             RECORD KEY               IS  受－施術和暦年月
000300                                                          受－患者コード
000310                             ALTERNATE RECORD KEY     IS  受－施術和暦年月
000320                                                          受－患者カナ
000330                                                          受－患者コード
000340                             ALTERNATE RECORD KEY     IS  受－患者コード
000350                                                          受－施術和暦年月
000360                             ALTERNATE RECORD KEY     IS  受－施術和暦年月
000370                                                          受－保険種別
000380                                                          受－保険者番号
000390                                                          受－患者コード
000400                             ALTERNATE RECORD KEY     IS  受－施術和暦年月
000410                                                          受－公費種別
000420                                                          受－費用負担者番号
000430                                                          受－患者コード
000440                             ALTERNATE RECORD KEY     IS  受－施術和暦年月
000450                                                          受－助成種別
000460                                                          受－費用負担者番号助成
000470                                                          受－患者コード
000480                             ALTERNATE RECORD KEY     IS  受－請求和暦年月
000490                                                          受－施術和暦年月
000500                                                          受－患者コード
000510                             FILE STATUS              IS  状態キー
000520                             LOCK        MODE         IS  AUTOMATIC.
000530*
000540     SELECT  ＨレセプトＦ    ASSIGN      TO        HRECEL
000550                             ORGANIZATION             IS  INDEXED
000560                             ACCESS MODE              IS  DYNAMIC
000570                             RECORD KEY               IS  Ｈレセ－施術区分
000580                                                          Ｈレセ－施術和暦年月
000590                                                          Ｈレセ－患者コード
000600                                                          Ｈレセ－レセ種別
000610                             ALTERNATE RECORD KEY     IS  Ｈレセ－施術区分
000620                                                          Ｈレセ－患者コード
000630                                                          Ｈレセ－施術和暦年月
000640                                                          Ｈレセ－レセ種別
000650                             ALTERNATE RECORD KEY     IS  Ｈレセ－施術和暦年月
000660                                                          Ｈレセ－患者コード
000670                                                          Ｈレセ－レセ種別
000680                                                          Ｈレセ－施術区分
000690                             ALTERNATE RECORD KEY     IS  Ｈレセ－請求対象区分
000700                                                          Ｈレセ－請求和暦年月
000710                                                          Ｈレセ－施術区分
000720                                                          Ｈレセ－施術和暦年月
000730                                                          Ｈレセ－患者コード
000740                                                          Ｈレセ－レセ種別
000750                             FILE STATUS              IS  状態キー
000760                             LOCK        MODE         IS  AUTOMATIC.
000770*
000780     SELECT  Ｈ日計データＦ  ASSIGN      TO        HNIKEIL
000790                             ORGANIZATION             IS  INDEXED
000800                             ACCESS MODE              IS  DYNAMIC
000810                             RECORD KEY               IS  Ｈ日－施術区分
000820                                                          Ｈ日－施術和暦年月日
000830                                                          Ｈ日－患者コード
000840                             ALTERNATE RECORD KEY     IS  Ｈ日－施術区分
000850                                                          Ｈ日－患者コード
000860                                                          Ｈ日－施術和暦年月日
000870                             ALTERNATE RECORD KEY     IS  Ｈ日－施術和暦年月日
000880                                                          Ｈ日－登録順
000890                             FILE STATUS              IS  状態キー
000900                             LOCK        MODE         IS  AUTOMATIC.
000910*
000920     SELECT  Ｈ負傷データＦ  ASSIGN      TO        HHUSYOUL
000930                             ORGANIZATION             IS  INDEXED
000940                             ACCESS MODE              IS  DYNAMIC
000950                             RECORD KEY               IS  Ｈ負－主キー
000960                             ALTERNATE RECORD KEY     IS  Ｈ負－施術区分
000970                                                          Ｈ負－患者コード
000980                                                          Ｈ負－主キー
000990                             FILE STATUS              IS  状態キー
001000                             LOCK        MODE         IS  AUTOMATIC.
001010*
001011     SELECT  Ｈ往療実績Ｆ    ASSIGN      TO        HNOURYOL
001012                             ORGANIZATION             IS  INDEXED
001013                             ACCESS MODE              IS  DYNAMIC
001014                             RECORD KEY               IS  Ｈ往実－施術区分
001015                                                          Ｈ往実－施術和暦年月日
001016                                                          Ｈ往実－患者コード
001017                             ALTERNATE RECORD KEY     IS  Ｈ往実－施術区分
001018                                                          Ｈ往実－患者コード
001019                                                          Ｈ往実－施術和暦年月日
001020                             ALTERNATE RECORD KEY     IS  Ｈ往実－施術和暦年月日
001021                                                          Ｈ往実－施術者番号
001022                                                          Ｈ往実－登録順
001023                             ALTERNATE RECORD KEY     IS  Ｈ往実－施術和暦年月日
001024                                                          Ｈ往実－施術開始時間
001025                                                          Ｈ往実－施術者番号
001026                                                          Ｈ往実－登録順
001027                             FILE STATUS              IS  状態キー
001028                             LOCK        MODE         IS  AUTOMATIC.
001029     SELECT  Ｈ往療予定Ｆ    ASSIGN      TO        HNOYOTEL
001030                             ORGANIZATION             IS  INDEXED
001031                             ACCESS MODE              IS  DYNAMIC
001032                             RECORD KEY               IS  Ｈ往予－施術区分
001033                                                          Ｈ往予－施術和暦年月日
001034                                                          Ｈ往予－患者コード
001035                             ALTERNATE RECORD KEY     IS  Ｈ往予－施術和暦年月日
001036                                                          Ｈ往予－患者コード
001037                                                          Ｈ往予－施術区分
001038                             ALTERNATE RECORD KEY     IS  Ｈ往予－患者コード
001039                                                          Ｈ往予－施術和暦年月日
001040                                                          Ｈ往予－施術区分
001041                             ALTERNATE RECORD KEY     IS  Ｈ往予－施術和暦年月日
001042                                                          Ｈ往予－施術者番号
001043                                                          Ｈ往予－施術開始時間
001044                                                          Ｈ往予－登録順
001045                                                          Ｈ往予－患者コード
001046                             ALTERNATE RECORD KEY     IS  Ｈ往予－施術和暦年月日
001047                                                          Ｈ往予－施術開始時間
001048                                                          Ｈ往予－施術者番号
001049                                                          Ｈ往予－登録順
001050                                                          Ｈ往予－患者コード
001051                             FILE STATUS              IS  状態キー
001052                             LOCK        MODE         IS  AUTOMATIC.
001053*
001054     SELECT  Ｈ報告書Ｆ      ASSIGN      TO        HHOKOKL
001055                             ORGANIZATION             IS  INDEXED
001056                             ACCESS                   IS  DYNAMIC
001057                             RECORD      KEY          IS  Ｈ報－施術区分
001058                                                          Ｈ報－用紙区分
001059                                                          Ｈ報－施術和暦年月
001060                                                          Ｈ報－患者コード
001061                                                          Ｈ報－連番
001062                             ALTERNATE RECORD KEY     IS  Ｈ報－施術区分
001063                                                          Ｈ報－用紙区分
001064                                                          Ｈ報－患者コード
001065                                                          Ｈ報－施術和暦年月
001066                                                          Ｈ報－施術日
001067                                                          Ｈ報－連番
001068                             FILE        STATUS       IS  状態キー
001069                             LOCK        MODE         IS  AUTOMATIC.
001070*
001072******************************************************************
001073*                      DATA DIVISION                             *
001074******************************************************************
001075 DATA                    DIVISION.
001076 FILE                    SECTION.
001077*                           ［ＲＬ＝  ３２０］
001080 FD  受診者情報Ｆ    BLOCK   CONTAINS   1   RECORDS.
001090     COPY JUSINJ     OF  XFDLIB  JOINING   受   AS  PREFIX.
001100*
001110*                           ［ＲＬ＝  ７６８］
001120 FD  ＨレセプトＦ        BLOCK   CONTAINS   1   RECORDS.
001130     COPY H_RECE     OF  XFDLIB  JOINING   Ｈレセ  AS  PREFIX.
001140*                           ［ＲＬ＝  ５１２］
001150 FD  Ｈ日計データＦ      BLOCK   CONTAINS   1   RECORDS.
001160     COPY H_NIKEI    OF  XFDLIB  JOINING   Ｈ日   AS  PREFIX.
001170*                           ［ＲＬ＝  ６４０］
001180 FD  Ｈ負傷データＦ      BLOCK   CONTAINS   1   RECORDS.
001190     COPY H_HUSYOU   OF  XFDLIB  JOINING   Ｈ負 AS  PREFIX.
001200*
001201 FD  Ｈ往療実績Ｆ   BLOCK   CONTAINS   1   RECORDS.
001202     COPY H_NOURYO   OF  XFDLIB  JOINING   Ｈ往実   AS  PREFIX.
001203*
001204 FD  Ｈ往療予定Ｆ   BLOCK   CONTAINS   1   RECORDS.
001205     COPY H_NOYOTE   OF  XFDLIB  JOINING   Ｈ往予   AS  PREFIX.
001206*                           ［ＲＬ＝  2048］
001207 FD  Ｈ報告書Ｆ          BLOCK   CONTAINS   1   RECORDS GLOBAL.
001208     COPY H_HOKOK         OF  XFDLIB  JOINING   Ｈ報     AS  PREFIX.
001209*
001211*---------------------------------------------------------------*
001220******************************************************************
001230*                WORKING-STORAGE SECTION                         *
001240******************************************************************
001250 WORKING-STORAGE         SECTION.
001260 01 キー入力                           PIC X    VALUE SPACE.
001270 01 状態キー                           PIC X(2) VALUE SPACE.
001280 01 終了フラグ                         PIC X(3) VALUE SPACE.
001290 01 終了フラグ２                       PIC X(3) VALUE SPACE.
001300 01 ファイル名                         PIC N(10) VALUE SPACE.
001310*
001320*--------------------------------------------------------------*
001330* 一般ワーク
001340*
001350 01 施術区分Ｗ                         PIC 9 VALUE ZERO.
001360 01 誕生日Ｗ                           PIC 9(2) VALUE ZERO.
001370*
001380*
001390* 実日数取得用
001400 01 実日数Ｗ                           PIC 9(2).
001410 01 枝番Ｗ                             PIC X.
001411*
001412* 通院テーブル
001413 01 日計情報Ｗ.
001414     03 通院日集団Ｗ.
001415        05 通院日Ｗ                    PIC 9 OCCURS 31  VALUE ZERO.
001416*
001417 01 カウンタ                           PIC 9(2)  VALUE ZERO.
001419 01 開始日Ｗ                           PIC 9(2) VALUE ZERO.
001420 01 終了日Ｗ                           PIC 9(2) VALUE ZERO.
001421*
001430******************************************************************
001440*                          連結項目                              *
001450******************************************************************
001460*
001470* 枝番連携キー
001480 01 連患者枝番－キー IS EXTERNAL.
001490    03 連患者枝番－変更日                   PIC 9(2).
001500    03 連患者枝番－保険変更区分             PIC 9.
001510*
001520 01 Ｈ連枝番作成－キー IS EXTERNAL.
001530   03 Ｈ連枝番作成－施術和暦年月.
001540      05 Ｈ連枝番作成－施術和暦              PIC 9.
001550      05 Ｈ連枝番作成－施術年月.
001560        07 Ｈ連枝番作成－施術年              PIC 9(2).
001570        07 Ｈ連枝番作成－施術月              PIC 9(2).
001580   03 Ｈ連枝番作成－患者コード.
001590     05 Ｈ連枝番作成－患者番号               PIC 9(6).
001600     05 Ｈ連枝番作成－枝番                   PIC X.
001610   03 Ｈ連枝番作成－老人判定                 PIC 9.
001611   03 Ｈ連枝番作成－元患者コード.
001612     05 Ｈ連枝番作成－元患者番号             PIC 9(6).
001613     05 Ｈ連枝番作成－元枝番                 PIC X.
001620*
001630*----------------------------------------------------------------*
001640* 計算連携キー
001650 01 Ｈ連計算－キー IS EXTERNAL.
001660* / inパラメタ /
001670   03 Ｈ連計算－施術区分                   PIC 9.
001680   03 Ｈ連計算－施術和暦年月日.
001690      05 Ｈ連計算－施術和暦年月.
001700        07 Ｈ連計算－施術和暦              PIC 9.
001710        07 Ｈ連計算－施術年月.
001720          09 Ｈ連計算－施術年              PIC 9(2).
001730          09 Ｈ連計算－施術月              PIC 9(2).
001740      05 Ｈ連計算－施術日                  PIC 9(2).
001750   03 Ｈ連計算－患者コード.
001760      05 Ｈ連計算－患者番号                PIC 9(6).
001770      05 Ｈ連計算－枝番                    PIC X.
001780   03 Ｈ連計算－保険種別                   PIC 9(2).
001790* / 内部パラメタ/
001800   03 Ｈ連計算－内部パラメタ.
001810      05 Ｈ連計算－負担金計算区分          PIC 9.
001820      05 Ｈ連計算－費用額                  PIC 9(6).
001830*  初検日に"YES"(負担区分1,2,3,4)
001840      05 Ｈ連計算－初検日フラグ            PIC X(3).
001850*  当月の初回なら"YES"(負担区分5)
001860      05 Ｈ連計算－初診療フラグ            PIC X(3).
001870*  初検料の金額を転記(負担区分1)
001880      05 Ｈ連計算－初検料                  PIC 9(4).
001890*  当月の当日までの実日数(枝番も含む)(負担区分6,8,13)
001900      05 Ｈ連計算－実日数                  PIC 9(2).
001910*  日整静岡乳幼児用。当日の負担のありなし  ０：なし　１：あり (負担区分7)
001920      05 Ｈ連計算－その他計算区分１        PIC 9.
001930*  out
001940      05 Ｈ連計算－負担額                  PIC 9(6).
001950      05 Ｈ連計算－請求額                  PIC 9(6).
001960      05 Ｈ連計算－負担額助成              PIC 9(5).
001970      05 Ｈ連計算－請求額助成              PIC 9(5).
001980      05 Ｈ連計算－負担率                  PIC 9(3).
001990*
002000*
002010******************************************************************
002020*                      PROCEDURE  DIVISION                       *
002030******************************************************************
002040 PROCEDURE               DIVISION.
002050************
002060*           *
002070* 初期処理   *
002080*           *
002090************
002100     PERFORM 初期化.
002110************
002120*           *
002130* 主処理     *
002140*           *
002150************
002160*
002170     MOVE 1 TO 施術区分Ｗ.
002180     PERFORM ＨレセプトＦ処理.
002181     PERFORM Ｈ日計データＦ処理.
002182     PERFORM Ｈ往療実績Ｆ処理.
002200     PERFORM Ｈ報告書Ｆ処理.
002201*
002210     MOVE 2 TO 施術区分Ｗ.
002220     PERFORM ＨレセプトＦ処理.
002230     PERFORM Ｈ日計データＦ処理.
002231     PERFORM Ｈ往療実績Ｆ処理.
002232     PERFORM Ｈ報告書Ｆ処理.
002233*
002234*   1回でOK
002235     PERFORM Ｈ往療予定Ｆ処理.
002240*
002250*----------------------------------*
002260     PERFORM 計算呼び出し処理.
002270*----------------------------------*
002280*
002290************
002300*           *
002310* 終了処理   *
002320*           *
002330************
002340     PERFORM 終了処理.
002350     EXIT PROGRAM.
002360*
002370*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002380*================================================================*
002390 初期化 SECTION.
002400*
002410*
002420     PERFORM ファイルオープン.
002430*
002431* 使用なし
002440*    / 今月75歳の時、誕生日取得 /
002450*     IF Ｈ連枝番作成－老人判定 = 4
002460*        MOVE Ｈ連枝番作成－施術和暦    TO 受－施術和暦
002470*        MOVE Ｈ連枝番作成－施術年      TO 受－施術年
002480*        MOVE Ｈ連枝番作成－施術月      TO 受－施術月
002490*        MOVE Ｈ連枝番作成－患者番号    TO 受－患者番号
002500*        MOVE Ｈ連枝番作成－枝番        TO 受－枝番
002510*        READ 受診者情報Ｆ
002520*        NOT INVALID KEY
002530*            MOVE 受－患者日 TO 誕生日Ｗ
002540*        END-READ
002550*     END-IF.
002560*
002570*================================================================*
002580 ファイルオープン SECTION.
002590*
002600     OPEN INPUT 受診者情報Ｆ.
002610         MOVE NC"受診" TO ファイル名.
002620         PERFORM オープンチェック.
002630     OPEN I-O ＨレセプトＦ.
002640         MOVE NC"ＨレセプトＦ" TO ファイル名.
002650         PERFORM オープンチェック.
002660     OPEN I-O Ｈ日計データＦ.
002670         MOVE NC"Ｈ日計データＦ" TO ファイル名.
002680         PERFORM オープンチェック.
002681     OPEN I-O Ｈ往療実績Ｆ.
002682         MOVE NC"Ｈ往療実績Ｆ" TO ファイル名.
002683         PERFORM オープンチェック.
002684     OPEN I-O Ｈ往療予定Ｆ.
002685         MOVE NC"Ｈ往療予定Ｆ" TO ファイル名.
002686         PERFORM オープンチェック.
002690     OPEN INPUT Ｈ負傷データＦ.
002700         MOVE NC"Ｈ負傷データＦ" TO ファイル名.
002710         PERFORM オープンチェック.
002720     OPEN I-O Ｈ報告書Ｆ.
002721         MOVE NC"Ｈ報告書Ｆ" TO ファイル名.
002722         PERFORM オープンチェック.
002723*
002730*================================================================*
002740 オープンチェック SECTION.
002750*
002760     IF 状態キー  NOT =  "00"
002770         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
002780         DISPLAY NC"状態キー：" 状態キー         UPON CONS
002790         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
002800                                                 UPON CONS
002810*-----------------------------------------*
002820         CALL "actcshm"  WITH C LINKAGE
002830*-----------------------------------------*
002840         ACCEPT  キー入力 FROM CONS
002850         PERFORM ファイル閉鎖
002860         MOVE 99 TO PROGRAM-STATUS
002870         EXIT PROGRAM.
002880*
002890*================================================================*
002900 ファイル閉鎖 SECTION.
002910*
002920     CLOSE 受診者情報Ｆ ＨレセプトＦ Ｈ日計データＦ Ｈ往療実績Ｆ Ｈ往療予定Ｆ Ｈ負傷データＦ.
002921     CLOSE Ｈ報告書Ｆ.
002930*================================================================*
002940 終了処理 SECTION.
002950*
002960     PERFORM ファイル閉鎖.
002970*================================================================*
002980*================================================================*
002990 ＨレセプトＦ処理 SECTION.
003000*
003010     MOVE 施術区分Ｗ              TO  Ｈレセ－施術区分.
003020     MOVE Ｈ連枝番作成－施術和暦  TO  Ｈレセ－施術和暦.
003030     MOVE Ｈ連枝番作成－施術年    TO  Ｈレセ－施術年.
003040     MOVE Ｈ連枝番作成－施術月    TO  Ｈレセ－施術月.
003050     MOVE Ｈ連枝番作成－元患者番号  TO  Ｈレセ－患者番号.
003060     MOVE Ｈ連枝番作成－元枝番      TO  Ｈレセ－枝番.
003070     MOVE ZERO                      TO  Ｈレセ－レセ種別.
003080*
003090     START ＨレセプトＦ KEY IS >= Ｈレセ－施術区分
003100                                  Ｈレセ－施術和暦年月
003110                                  Ｈレセ－患者コード
003120                                  Ｈレセ－レセ種別
003130     END-START.
003140*
003150     IF 状態キー = "00"
003160            MOVE SPACE TO 終了フラグ
003170            PERFORM ＨレセプトＦ読込
003180            PERFORM UNTIL ( Ｈレセ－施術区分   NOT = 施術区分Ｗ   ) OR
003190                          ( Ｈレセ－施術和暦   NOT = Ｈ連枝番作成－施術和暦   ) OR
003200                          ( Ｈレセ－施術年     NOT = Ｈ連枝番作成－施術年     ) OR
003210                          ( Ｈレセ－施術月     NOT = Ｈ連枝番作成－施術月     ) OR
003220                          ( Ｈレセ－患者番号   NOT = Ｈ連枝番作成－元患者番号 ) OR
003230                          ( Ｈレセ－枝番       NOT = Ｈ連枝番作成－元枝番 ) OR
003240                          ( 終了フラグ         NOT = SPACE )
003250**
003260*------------    / 枝番有りレコードの作成 / -------------------------------*
003270                MOVE Ｈ連枝番作成－枝番   TO Ｈレセ－枝番
003280                WRITE Ｈレセ－レコード
003290                IF 状態キー NOT = "00"
003300                   MOVE NC"ＨレセプトＦ" TO ファイル名
003310                   PERFORM エラー表示
003320                END-IF
003330*--------------------------------------------------------------------------*
003340*
003350*-----------    / 元のレコードの下記の項目を初期化する。/-----------*
003360                MOVE Ｈ連枝番作成－元枝番 TO Ｈレセ－枝番
003370*             (初期化)
003380                MOVE ZERO TO Ｈレセ－請求対象区分
003390                MOVE ZERO TO Ｈレセ－レセ請求区分
003400                MOVE ZERO TO Ｈレセ－実日数
003410                INITIALIZE  Ｈレセ－金額合計欄
003420                INITIALIZE  Ｈレセ－はり金額欄
003430                INITIALIZE  Ｈレセ－マッサージ金額欄
003440                INITIALIZE  Ｈレセ－例外レセ金額合計欄
003450                INITIALIZE  Ｈレセ－例外本体枝番まとめ単独金額合計欄
003460                MOVE ZERO TO Ｈレセ－署名区分
003470                MOVE ZERO TO Ｈレセ－レセ印刷区分
003480*
003490                REWRITE Ｈレセ－レコード
003500                IF 状態キー NOT = "00"
003510                   MOVE NC"ＨレセプトＦ" TO ファイル名
003520                   PERFORM エラー表示
003530                END-IF
003540*--------------------------------------------------------------------------*
003550**
003560                PERFORM ＨレセプトＦ読込
003570            END-PERFORM
003580     END-IF.
003590*
003600*================================================================*
003610 ＨレセプトＦ読込 SECTION.
003620*
003630     READ ＨレセプトＦ NEXT
003640     AT END
003650         MOVE "YES"  TO 終了フラグ
003660     END-READ.
003670*
003680*================================================================*
003690 Ｈ日計データＦ処理 SECTION.
003700*
003710     MOVE 施術区分Ｗ                TO Ｈ日－施術区分.
003720     MOVE Ｈ連枝番作成－元患者番号  TO Ｈ日－患者番号.
003730     MOVE Ｈ連枝番作成－元枝番      TO Ｈ日－枝番.
003740     MOVE Ｈ連枝番作成－施術和暦    TO Ｈ日－施術和暦.
003750     MOVE Ｈ連枝番作成－施術年      TO Ｈ日－施術年.
003760     MOVE Ｈ連枝番作成－施術月      TO Ｈ日－施術月.
003770     MOVE 1                         TO Ｈ日－施術日.
003780*
003790     START Ｈ日計データＦ KEY IS >= Ｈ日－施術区分
003800                                    Ｈ日－患者コード
003810                                    Ｈ日－施術和暦年月日
003820     END-START.
003830*
003840     IF 状態キー = "00"
003850         MOVE SPACE TO 終了フラグ
003860         PERFORM Ｈ日計データＦ読込
004010*        繰り返し（当月）
004020*        / 変更日以降コンバートしない
004030         PERFORM UNTIL ( Ｈ日－施術区分   NOT = 施術区分Ｗ             ) OR
004040                       ( Ｈ日－患者番号   NOT = Ｈ連枝番作成－元患者番号 ) OR
004050                       ( Ｈ日－枝番       NOT = Ｈ連枝番作成－元枝番     ) OR
004060                       ( Ｈ日－施術和暦   NOT = Ｈ連枝番作成－施術和暦   ) OR
004070                       ( Ｈ日－施術年     NOT = Ｈ連枝番作成－施術年     ) OR
004080                       ( Ｈ日－施術月     NOT = Ｈ連枝番作成－施術月     ) OR
004090                       ( 終了フラグ       NOT = SPACE ) OR
004100                       ( Ｈ日－施術日        >= 連患者枝番－変更日 )
004110*
004120*
004130*------------    / 枝番有りレコードの作成 / -------------------------------*
004140                MOVE Ｈ連枝番作成－枝番   TO Ｈ日－枝番
004150                COMPUTE Ｈ日－登録順  = Ｈ日－登録順 + 0.1
004160                WRITE Ｈ日－レコード
004170                IF 状態キー NOT = "00"
004180                   MOVE NC"Ｈ日計データＦ" TO ファイル名
004190                   PERFORM エラー表示
004200                END-IF
004210*--------------------------------------------------------------------------*
004220*
004230*------------   / 元のレコードの削除 / -------------------------------*
004240                MOVE Ｈ連枝番作成－元枝番  TO Ｈ日－枝番
004250                DELETE Ｈ日計データＦ
004260                IF 状態キー NOT = "00"
004270                   MOVE NC"Ｈ日計データＦ" TO ファイル名
004280                   PERFORM エラー表示Ｄ
004290                END-IF
004300*--------------------------------------------------------------------------*
004310**
004320                PERFORM Ｈ日計データＦ読込
004480***
004490         END-PERFORM
004500     END-IF.
004510*
004520*================================================================*
004530 Ｈ日計データＦ読込 SECTION.
004540*
004550     READ Ｈ日計データＦ NEXT
004560     AT END
004570         MOVE "YES"  TO 終了フラグ
004580     END-READ.
004590*
004591*================================================================*
004592 Ｈ往療実績Ｆ処理 SECTION.
004593*
004594     MOVE 施術区分Ｗ                TO Ｈ往実－施術区分.
004595     MOVE Ｈ連枝番作成－元患者番号  TO Ｈ往実－患者番号.
004596     MOVE Ｈ連枝番作成－元枝番      TO Ｈ往実－枝番.
004597     MOVE Ｈ連枝番作成－施術和暦    TO Ｈ往実－施術和暦.
004598     MOVE Ｈ連枝番作成－施術年      TO Ｈ往実－施術年.
004599     MOVE Ｈ連枝番作成－施術月      TO Ｈ往実－施術月.
004600     MOVE 1                         TO Ｈ往実－施術日.
004601*
004602     START Ｈ往療実績Ｆ KEY IS >= Ｈ往実－施術区分
004603                                  Ｈ往実－患者コード
004604                                  Ｈ往実－施術和暦年月日
004605     END-START.
004606*
004607     IF 状態キー = "00"
004608         MOVE SPACE TO 終了フラグ
004609         PERFORM Ｈ往療実績Ｆ読込
004610*        繰り返し（当月）
004611*        / 変更日以降コンバートしない
004612         PERFORM UNTIL ( Ｈ往実－施術区分   NOT = 施術区分Ｗ               ) OR
004613                       ( Ｈ往実－患者番号   NOT = Ｈ連枝番作成－元患者番号 ) OR
004614                       ( Ｈ往実－枝番       NOT = Ｈ連枝番作成－元枝番     ) OR
004615                       ( Ｈ往実－施術和暦   NOT = Ｈ連枝番作成－施術和暦   ) OR
004616                       ( Ｈ往実－施術年     NOT = Ｈ連枝番作成－施術年     ) OR
004617                       ( Ｈ往実－施術月     NOT = Ｈ連枝番作成－施術月     ) OR
004618                       ( 終了フラグ         NOT = SPACE ) OR
004619                       ( Ｈ往実－施術日        >= 連患者枝番－変更日 )
004620*
004621*
004622*------------    / 枝番有りレコードの作成 / -------------------------------*
004623                MOVE Ｈ連枝番作成－枝番   TO Ｈ往実－枝番
004624                COMPUTE Ｈ往実－登録順  = Ｈ往実－登録順 + 0.1
004625                WRITE Ｈ往実－レコード
004626                IF 状態キー NOT = "00"
004627                   MOVE NC"Ｈ往療実績Ｆ" TO ファイル名
004628                   PERFORM エラー表示
004629                END-IF
004630*--------------------------------------------------------------------------*
004631*
004632*------------   / 元のレコードの削除 / -------------------------------*
004633                MOVE Ｈ連枝番作成－元枝番  TO Ｈ往実－枝番
004634                DELETE Ｈ往療実績Ｆ
004635                IF 状態キー NOT = "00"
004636                   MOVE NC"Ｈ往療実績Ｆ" TO ファイル名
004637                   PERFORM エラー表示Ｄ
004638                END-IF
004639*--------------------------------------------------------------------------*
004640**
004641                PERFORM Ｈ往療実績Ｆ読込
004642***
004643         END-PERFORM
004644     END-IF.
004645*
004646*================================================================*
004647 Ｈ往療実績Ｆ読込 SECTION.
004648*
004649     READ Ｈ往療実績Ｆ NEXT
004650     AT END
004651         MOVE "YES"  TO 終了フラグ
004652     END-READ.
004653*
004654*================================================================*
004655*================================================================*
004656*================================================================*
004657 Ｈ往療予定Ｆ処理 SECTION.
004658*
004670     MOVE Ｈ連枝番作成－元患者番号  TO Ｈ往予－患者番号.
004671     MOVE Ｈ連枝番作成－元枝番      TO Ｈ往予－枝番.
004672     MOVE Ｈ連枝番作成－施術和暦    TO Ｈ往予－施術和暦.
004673     MOVE Ｈ連枝番作成－施術年      TO Ｈ往予－施術年.
004674     MOVE Ｈ連枝番作成－施術月      TO Ｈ往予－施術月.
004675     MOVE 1                         TO Ｈ往予－施術日.
004676     MOVE ZERO                      TO Ｈ往予－施術区分.
004677*
004678     START Ｈ往療予定Ｆ KEY IS >= Ｈ往予－患者コード
004679                                  Ｈ往予－施術和暦年月日
004680                                  Ｈ往予－施術区分
004683     END-START.
004684*
004685     IF 状態キー = "00"
004686         MOVE SPACE TO 終了フラグ
004687         PERFORM Ｈ往療予定Ｆ読込
004688*        繰り返し（当月）
004689*        / 変更日以降コンバートしない
004690         PERFORM UNTIL ( Ｈ往予－患者番号   NOT = Ｈ連枝番作成－元患者番号 ) OR
004692                       ( Ｈ往予－枝番       NOT = Ｈ連枝番作成－元枝番     ) OR
004693                       ( Ｈ往予－施術和暦   NOT = Ｈ連枝番作成－施術和暦   ) OR
004694                       ( Ｈ往予－施術年     NOT = Ｈ連枝番作成－施術年     ) OR
004695                       ( Ｈ往予－施術月     NOT = Ｈ連枝番作成－施術月     ) OR
004696                       ( 終了フラグ         NOT = SPACE ) OR
004697                       ( Ｈ往予－施術日        >= 連患者枝番－変更日 )
004698*
004699*
004700*------------    / 枝番有りレコードの作成 / -------------------------------*
004701                MOVE Ｈ連枝番作成－枝番   TO Ｈ往予－枝番
004702                COMPUTE Ｈ往予－登録順  = Ｈ往予－登録順 + 0.1
004703                WRITE Ｈ往予－レコード
004704                IF 状態キー NOT = "00"
004705                   MOVE NC"Ｈ往療予定Ｆ" TO ファイル名
004706                   PERFORM エラー表示
004707                END-IF
004708*--------------------------------------------------------------------------*
004709*
004710*------------   / 元のレコードの削除 / -------------------------------*
004711                MOVE Ｈ連枝番作成－元枝番  TO Ｈ往予－枝番
004712                DELETE Ｈ往療予定Ｆ
004713                IF 状態キー NOT = "00"
004714                   MOVE NC"Ｈ往療予定Ｆ" TO ファイル名
004715                   PERFORM エラー表示Ｄ
004716                END-IF
004717*--------------------------------------------------------------------------*
004718**
004719                PERFORM Ｈ往療予定Ｆ読込
004720***
004721         END-PERFORM
004722     END-IF.
004723*
004724*================================================================*
004725 Ｈ往療予定Ｆ読込 SECTION.
004726*
004727     READ Ｈ往療予定Ｆ NEXT
004728     AT END
004729         MOVE "YES"  TO 終了フラグ
004730     END-READ.
004731*
004732*================================================================*
004733*================================================================*
004734*================================================================*
004735*================================================================*
004736*================================================================*
004737 計算呼び出し処理 SECTION.
004738*
004739* 元番号
004740     MOVE Ｈ連枝番作成－元枝番  TO 枝番Ｗ.
004741     PERFORM 計算処理.
004742* 枝番あり
004743     MOVE Ｈ連枝番作成－枝番 TO 枝番Ｗ.
004744     PERFORM 計算処理.
004745*
004746*================================================================*
004747 計算処理 SECTION.
004748*
004749     MOVE Ｈ連枝番作成－施術和暦    TO 受－施術和暦.
004750     MOVE Ｈ連枝番作成－施術年      TO 受－施術年.
004760     MOVE Ｈ連枝番作成－施術月      TO 受－施術月.
004770     MOVE Ｈ連枝番作成－患者番号    TO 受－患者番号.
004780     MOVE 枝番Ｗ                    TO 受－枝番.
004790     READ 受診者情報Ｆ
004800     NOT INVALID KEY
004810*------------------------------------------*
004820         PERFORM 計算連結項目セット
004830         MOVE 1 TO Ｈ連計算－施術区分
004840         CALL   "KEISAN"
004850         CANCEL "KEISAN"
004860*
004870         MOVE 1 TO 施術区分Ｗ
004880         PERFORM レセプトＦ更新処理
004890*------------------------------------------*
004900         PERFORM 計算連結項目セット
004910         MOVE 2 TO Ｈ連計算－施術区分
004920         CALL   "KEISAN"
004930         CANCEL "KEISAN"
004940*
004950         MOVE 2 TO 施術区分Ｗ
004960         PERFORM レセプトＦ更新処理
004970*------------------------------------------*
004980     END-READ.
004990*
005000*================================================================*
005010 計算連結項目セット SECTION.
005020*
005030     MOVE SPACE TO Ｈ連計算－キー.
005040     INITIALIZE Ｈ連計算－キー.
005050*
005060     MOVE 受－施術和暦    TO Ｈ連計算－施術和暦.
005070     MOVE 受－施術年      TO Ｈ連計算－施術年.
005080     MOVE 受－施術月      TO Ｈ連計算－施術月.
005090     MOVE 受－患者番号    TO Ｈ連計算－患者番号.
005100     MOVE 受－枝番        TO Ｈ連計算－枝番.
005110     MOVE 受－保険種別    TO Ｈ連計算－保険種別.
005120     MOVE ZERO            TO Ｈ連計算－施術日.
005130*
005140*================================================================*
005150*================================================================*
005160 レセプトＦ更新処理 SECTION.
005170*
005180*----------------------------------------------------------------------------*
005190* ①実日数取得
005200     MOVE ZERO TO 実日数Ｗ.
005201*
005202     INITIALIZE 日計情報Ｗ.
005203     MOVE ZERO TO 開始日Ｗ 終了日Ｗ.
005210*
005220     MOVE 施術区分Ｗ              TO Ｈ日－施術区分.
005230     MOVE Ｈ連枝番作成－患者番号  TO Ｈ日－患者番号.
005240     MOVE 枝番Ｗ                  TO Ｈ日－枝番.
005250     MOVE Ｈ連枝番作成－施術和暦  TO Ｈ日－施術和暦.
005260     MOVE Ｈ連枝番作成－施術年    TO Ｈ日－施術年.
005270     MOVE Ｈ連枝番作成－施術月    TO Ｈ日－施術月.
005280     MOVE ZERO                    TO Ｈ日－施術日.
005290*
005300     START Ｈ日計データＦ KEY IS >= Ｈ日－施術区分
005310                                    Ｈ日－患者コード
005320                                    Ｈ日－施術和暦年月日
005330     END-START.
005340     IF ( 状態キー = "00" )
005350        MOVE SPACE TO 終了フラグ
005360        PERFORM Ｈ日計データＦ読込
005370*
005380        PERFORM UNTIL ( Ｈ日－施術区分   NOT = 施術区分Ｗ   ) OR
005390                      ( Ｈ日－施術和暦   NOT = Ｈ連枝番作成－施術和暦   ) OR
005400                      ( Ｈ日－施術年     NOT = Ｈ連枝番作成－施術年     ) OR
005410                      ( Ｈ日－施術月     NOT = Ｈ連枝番作成－施術月     ) OR
005420                      ( Ｈ日－患者番号   NOT = Ｈ連枝番作成－患者番号 ) OR
005430                      ( Ｈ日－枝番       NOT = 枝番Ｗ ) OR
005440                      ( 終了フラグ       NOT = SPACE )
005441           MOVE 1 TO 通院日Ｗ(Ｈ日－施術日)
005450           ADD  1 TO 実日数Ｗ
005460           PERFORM Ｈ日計データＦ読込
005470        END-PERFORM
005480     END-IF.
005481**
005482
005483* 施術開始日と施術終了日
005484      IF 通院日集団Ｗ  NOT = ZERO
005485*
005493         PERFORM VARYING カウンタ FROM 1 BY 1   UNTIL ( カウンタ > 31 ) 
005494               IF 通院日Ｗ(カウンタ) NOT = ZERO
005495                  MOVE カウンタ TO 開始日Ｗ
005496                  MOVE 31       TO カウンタ 
005497               END-IF
005498         END-PERFORM
005499*
005500         PERFORM VARYING カウンタ FROM 31 BY -1   UNTIL ( カウンタ < 1 ) 
005501               IF 通院日Ｗ(カウンタ) NOT = ZERO
005502                  MOVE カウンタ TO 終了日Ｗ
005503                  MOVE 1        TO カウンタ 
005504               END-IF
005505         END-PERFORM
005506*
005512      END-IF.
005513*
005517*----------------------------------------------------------------------------*
005518*
005519*----------------------------------------------------------------------------*
005520* ②レセＦ更新
005530*
005540     MOVE 施術区分Ｗ              TO  Ｈレセ－施術区分.
005550     MOVE Ｈ連枝番作成－施術和暦  TO  Ｈレセ－施術和暦.
005560     MOVE Ｈ連枝番作成－施術年    TO  Ｈレセ－施術年.
005570     MOVE Ｈ連枝番作成－施術月    TO  Ｈレセ－施術月.
005580     MOVE Ｈ連枝番作成－患者番号  TO  Ｈレセ－患者番号.
005590     MOVE 枝番Ｗ                  TO  Ｈレセ－枝番.
005600     MOVE ZERO                    TO  Ｈレセ－レセ種別.
005610*
005620     START ＨレセプトＦ KEY IS >= Ｈレセ－施術区分
005630                                  Ｈレセ－施術和暦年月
005640                                  Ｈレセ－患者コード
005650                                  Ｈレセ－レセ種別
005660     END-START.
005670*
005680     IF 状態キー = "00"
005690            MOVE SPACE TO 終了フラグ
005700            PERFORM ＨレセプトＦ読込
005710            PERFORM UNTIL ( Ｈレセ－施術区分   NOT = 施術区分Ｗ   ) OR
005720                          ( Ｈレセ－施術和暦   NOT = Ｈ連枝番作成－施術和暦   ) OR
005730                          ( Ｈレセ－施術年     NOT = Ｈ連枝番作成－施術年     ) OR
005740                          ( Ｈレセ－施術月     NOT = Ｈ連枝番作成－施術月     ) OR
005750                          ( Ｈレセ－患者番号   NOT = Ｈ連枝番作成－患者番号 ) OR
005760                          ( Ｈレセ－枝番       NOT = 枝番Ｗ ) OR
005770                          ( 終了フラグ         NOT = SPACE )
005780*
005790*              / 実日数 /
005800                MOVE 実日数Ｗ TO Ｈレセ－実日数
005810*
005820*
005830*              / 請求対象区分 /
005840                IF Ｈレセ－請求区分 = 9
005850                   MOVE ZERO  TO Ｈレセ－請求対象区分
005860                ELSE
005870                   IF 実日数Ｗ = ZERO
005880                      MOVE ZERO  TO Ｈレセ－請求対象区分
005890                   ELSE
005900                      MOVE 1     TO Ｈレセ－請求対象区分
005910                   END-IF
005920                END-IF
005930*
005940*
005950*              / レセ請求区分 /
005951                MOVE ZERO    TO Ｈレセ－レセ請求区分
005960                MOVE Ｈレセ－負傷主キー TO Ｈ負－主キー
005970                READ Ｈ負傷データＦ
005980                NOT INVALID KEY
005990                   IF ( Ｈ連枝番作成－施術和暦 = Ｈ負－初療和暦 ) AND
006000                      ( Ｈ連枝番作成－施術年   = Ｈ負－初療年   ) AND
006010                      ( Ｈ連枝番作成－施術月   = Ｈ負－初療月   ) AND
006011                      ( 開始日Ｗ NOT = ZERO ) AND
006012                      ( 終了日Ｗ NOT = ZERO )
006013                      IF ( Ｈ負－初療日 >= 開始日Ｗ ) AND ( Ｈ負－初療日 <= 終了日Ｗ )
006020                          MOVE 1  TO Ｈレセ－レセ請求区分
006021                      END-IF
006050                   END-IF
006060                END-READ
006070*
006080*
006090                REWRITE Ｈレセ－レコード
006100                IF 状態キー NOT = "00"
006110                   MOVE NC"ＨレセプトＦ" TO ファイル名
006120                   PERFORM エラー表示
006130                END-IF
006140*
006150                PERFORM ＨレセプトＦ読込
006160            END-PERFORM
006170     END-IF.
006180*
006190*================================================================*
006200*================================================================*
006201 Ｈ報告書Ｆ処理 SECTION.
006204*
006257     MOVE 施術区分Ｗ                 TO Ｈ報－施術区分.
006258     MOVE ZERO                       TO Ｈ報－用紙区分.
006260     MOVE Ｈ連枝番作成－施術和暦     TO Ｈ報－施術和暦.
006261     MOVE Ｈ連枝番作成－施術年       TO Ｈ報－施術年.
006262     MOVE Ｈ連枝番作成－施術月       TO Ｈ報－施術月.
006263     MOVE Ｈ連枝番作成－元患者番号   TO Ｈ報－患者番号.
006264     MOVE SPACE                      TO Ｈ報－枝番.
006265     MOVE ZERO                       TO Ｈ報－連番.
006266*
006267     START Ｈ報告書Ｆ KEY IS >= Ｈ報－施術区分
006268                                Ｈ報－用紙区分
006269                                Ｈ報－施術和暦年月
006270                                Ｈ報－患者コード
006271                                Ｈ報－連番
006272     END-START.
006273*
006274     IF 状態キー = "00"
006275         MOVE SPACE            TO 終了フラグ
006276         PERFORM Ｈ報告書Ｆ読込
006277         PERFORM UNTIL (終了フラグ         = "YES") OR 
                             (Ｈ報－施術区分 NOT = 施術区分Ｗ)
006278*/変更日以降はコンバートしない
006279             IF (Ｈ報－施術区分      = 施術区分Ｗ) AND
006284                (Ｈ報－施術和暦      = Ｈ連枝番作成－施術和暦) AND
006285                (Ｈ報－施術年        = Ｈ連枝番作成－施術年) AND
006286                (Ｈ報－施術月        = Ｈ連枝番作成－施術月) AND
006287                (Ｈ報－患者番号      = Ｈ連枝番作成－元患者番号) AND
006288                (Ｈ報－枝番          = Ｈ連枝番作成－元枝番)
      *
      *                用紙によって判定日を変える
                       IF ((Ｈ報－用紙区分 = 1) AND (Ｈ報－施術日   < 連患者枝番－変更日)) OR 
                          ((Ｈ報－用紙区分 = 2) AND (Ｈ報－評価日鍼 < 連患者枝番－変更日)) OR 
                          ((Ｈ報－用紙区分 = 3) AND (Ｈ報－評価日マ < 連患者枝番－変更日)) OR 
                          ( Ｈ報－用紙区分 NOT = 1 AND 2 AND 3)
006290                     MOVE Ｈ連枝番作成－枝番 TO Ｈ報－枝番
006294                     WRITE Ｈ報－レコード
006295                     INVALID KEY
006296                         MOVE NC"Ｈ報告" TO ファイル名
006297                         PERFORM エラー表示
006298                     END-WRITE
006299                     MOVE Ｈ連枝番作成－元枝番  TO Ｈ報－枝番
006300                     DELETE Ｈ報告書Ｆ
006301                         INVALID KEY
006302                         MOVE NC"Ｈ報告" TO ファイル名
006303                         PERFORM エラー表示Ｄ
006304                     END-DELETE
                       END-IF
006305             END-IF
006306             PERFORM Ｈ報告書Ｆ読込
006307         END-PERFORM
006308     END-IF.
006309*
006310*================================================================*
006311 Ｈ報告書Ｆ読込 SECTION.
006312*
006313     READ Ｈ報告書Ｆ NEXT
006314     AT END
006315         MOVE "YES" TO 終了フラグ
006316     END-READ.
006317*================================================================*
006318*================================================================*
006319 エラー表示 SECTION.
006320*
006321     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
006322     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
006323     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
006324     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"  UPON CONS.
006325*-----------------------------------------*
006326     CALL "actcshm"  WITH C LINKAGE.
006327*-----------------------------------------*
006328     ACCEPT  キー入力 FROM CONS.
006329*
006330*================================================================*
006331 エラー表示Ｄ SECTION.
006340*
006350     DISPLAY NC"ファイル削除エラー：" ファイル名   UPON CONS.
006360     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
006370     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
006380     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"  UPON CONS.
006390*-----------------------------------------*
006400     CALL "actcshm"  WITH C LINKAGE.
006410*-----------------------------------------*
006420     ACCEPT  キー入力 FROM CONS.
006430*
006440*================================================================*
006450*================================================================*
006460*================================================================*
006470******************************************************************
006480 END PROGRAM HM012.
006490******************************************************************
