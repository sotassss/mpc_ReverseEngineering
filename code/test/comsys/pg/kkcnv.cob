000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             KKCNV.
000060 AUTHOR.                 佐野 誠
000070*
000080*------------------------------------------------------------------*
000100* 平成20年4月からの後期高齢者用保険証データ変換
000110*------------------------------------------------------------------*
000120 DATE-WRITTEN.           2008-02-15
000130 DATE-COMPILED.          2008-02-15
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000260     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000270                             ORGANIZATION             IS  INDEXED
000280                             ACCESS MODE              IS  DYNAMIC
000290                             RECORD KEY               IS  受−施術和暦年月
000300                                                          受−患者コード
000310                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000320                                                          受−患者カナ
000330                                                          受−患者コード
000340                             ALTERNATE RECORD KEY     IS  受−患者コード
000350                                                          受−施術和暦年月
000360                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000370                                                          受−保険種別
000380                                                          受−保険者番号
000390                                                          受−患者コード
000400                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000410                                                          受−公費種別
000420                                                          受−費用負担者番号
000430                                                          受−患者コード
000440                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000450                                                          受−助成種別
000460                                                          受−費用負担者番号助成
000470                                                          受−患者コード
000480                             ALTERNATE RECORD KEY     IS  受−請求和暦年月
000490                                                          受−施術和暦年月
000500                                                          受−患者コード
000510                             FILE STATUS              IS  状態キー
000520                             LOCK        MODE         IS  AUTOMATIC.
001130*
001131     SELECT  地区マスタ      ASSIGN    TO          TIKUCODL
001132                             ORGANIZATION             IS  INDEXED
001133                             ACCESS MODE              IS  DYNAMIC
001134                             RECORD KEY               IS  地区−公共団体コード
001135                             ALTERNATE RECORD KEY     IS  地区−老人市町村番号
001136                             FILE STATUS              IS  状態キー
001137                             LOCK        MODE         IS  AUTOMATIC.
001165*
001166     SELECT  郵便マスタ    ASSIGN    TO          YUUBINL
001167                           ORGANIZATION             IS  INDEXED
001168                           ACCESS MODE              IS  DYNAMIC
001169                           RECORD KEY               IS  郵−郵便番号
001170                           ALTERNATE RECORD KEY     IS  郵−市区町カナ
001171                                                        郵−郵便番号
001172                           ALTERNATE RECORD KEY     IS  郵−都道府県コード
001173                                                        郵−市区カナ
001174                                                        郵−町村カナ
001175                                                        郵−郵便番号
001176                           ALTERNATE RECORD KEY     IS  郵−市区名
001177                                                        郵−町村名
001178                                                        郵−郵便番号
001179                           FILE STATUS              IS  状態キー
001180                           LOCK        MODE         IS  AUTOMATIC.
001181*
001182******************************************************************
001183*                      DATA DIVISION                             *
001184******************************************************************
001185 DATA                    DIVISION.
001186 FILE                    SECTION.
001190*                           ［ＲＬ＝  ３２０］
001200 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001210     COPY JUSINJ         OF  XFDLIB  JOINING   受   AS  PREFIX.
001220*
001230*                           ［ＲＬ＝  ２５６］
001240 FD  地区マスタ          BLOCK   CONTAINS   1   RECORDS.
001250     COPY TIKUCODE       OF  XFDLIB  JOINING   地区   AS  PREFIX.
001260*
001270*                           ［ＲＬ＝  ２５６］
001280 FD  郵便マスタ          BLOCK   CONTAINS   1   RECORDS.
001290     COPY YUUBIN          OF  XFDLIB  JOINING   郵   AS  PREFIX.
001300*
002080*----------------------------------------------------------------*
002090******************************************************************
002100*                WORKING-STORAGE SECTION                         *
002110******************************************************************
002120 WORKING-STORAGE         SECTION.
002130 01 キー入力                           PIC X    VALUE SPACE.
002140 01 状態キー                           PIC X(2) VALUE SPACE.
002150 01 終了フラグ                         PIC X(3) VALUE SPACE.
002170 01 ファイル名                         PIC N(6) VALUE SPACE.
002180 01 後期高齢者保険者番号Ｗ             PIC X(10) VALUE SPACE.
002271*
002292*
002323**
002590******************************************************************
002600*                          連結項目                              *
002610******************************************************************
002620*
002622* 平成20年4月からの後期高齢者用保険証データ変換
002623 01 連後期高齢ＣＮＶ−キー IS EXTERNAL.
002624    03 連後期高齢ＣＮＶ−施術和暦年月.
002625       05 連後期高齢ＣＮＶ−施術和暦            PIC 9.
002626       05 連後期高齢ＣＮＶ−施術年月.
002627          07 連後期高齢ＣＮＶ−施術年           PIC 9(2).
002628          07 連後期高齢ＣＮＶ−施術月           PIC 9(2).
002629    03 連後期高齢ＣＮＶ−患者コード.
002630       05 連後期高齢ＣＮＶ−患者番号            PIC 9(6).
002631       05 連後期高齢ＣＮＶ−枝番                PIC X.
002632    03 連後期高齢ＣＮＶ−変換データ.
002642       05 連後期高齢ＣＮＶ−変換対象フラグ      PIC X(3).
002648       05 連後期高齢ＣＮＶ−患者氏名            PIC X(50).
002649       05 連後期高齢ＣＮＶ−患者カナ            PIC X(50).
002650       05 連後期高齢ＣＮＶ−患者性別            PIC 9.
002651       05 連後期高齢ＣＮＶ−患者生年月日.
002652          07 連後期高齢ＣＮＶ−患者和暦         PIC 9.
002653          07 連後期高齢ＣＮＶ−患者年           PIC 9(2).
002654          07 連後期高齢ＣＮＶ−患者月           PIC 9(2).
002655          07 連後期高齢ＣＮＶ−患者日           PIC 9(2).
002656       05 連後期高齢ＣＮＶ−患者郵便番号.
002657          07 連後期高齢ＣＮＶ−患者郵便番号１   PIC X(3).
002658          07 連後期高齢ＣＮＶ−患者郵便番号２   PIC X(4).
002659       05 連後期高齢ＣＮＶ−患者住所１          PIC X(50).
002660       05 連後期高齢ＣＮＶ−患者住所２          PIC X(50).
002661       05 連後期高齢ＣＮＶ−患者電話番号        PIC X(30).
002662       05 連後期高齢ＣＮＶ−本人家族区分        PIC 9.
002665       05 連後期高齢ＣＮＶ−保険種別            PIC 9(2).
002666       05 連後期高齢ＣＮＶ−保険者番号          PIC X(10).
002707       05 連後期高齢ＣＮＶ−公費種別            PIC 9(2).
002708       05 連後期高齢ＣＮＶ−費用負担者番号      PIC X(10).
002776       05 連後期高齢ＣＮＶ−老人負担金免除      PIC 9.
002825       05 連後期高齢ＣＮＶ−特別区分            PIC 9.
002856*
002857*----------------------------------------------------------*
002858**
002859* 年齢判定用
002860 01 連年齢２−キー IS EXTERNAL.
002861    03 連年齢２−施術和暦年月.
002862       05 連年齢２−施術和暦      PIC 9.
002863       05 連年齢２−施術年月.
002864         07 連年齢２−施術年      PIC 9(2).
002865         07 連年齢２−施術月      PIC 9(2).
002866    03 連年齢２−和暦年月日.
002867       05 連年齢２−和暦          PIC 9.
002868       05 連年齢２−年            PIC 9(2).
002869       05 連年齢２−月            PIC 9(2).
002870       05 連年齢２−日            PIC 9(2).
002871    03 連年齢２−保険年齢         PIC 9(3).
002872    03 連年齢２−乳幼児判定       PIC 9.
002873    03 連年齢２−老人判定         PIC 9.
002874*
003481******************************************************************
003482*                      PROCEDURE  DIVISION                       *
003483******************************************************************
003490 PROCEDURE               DIVISION.
003500************
003510*           *
003520* 初期処理   *
003530*           *
003540************
003550     PERFORM 初期化.
003551     INITIALIZE  連後期高齢ＣＮＶ−変換データ.
003560************
003570*           *
003580* 主処理     *
003590*           *
003600************
003601*
003642     PERFORM 変換処理
003643*
003644************
003645*           *
003646* 終了処理   *
003650*           *
003660************
003670     PERFORM 終了処理.
003690     EXIT PROGRAM.
003700*
003710*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003720*================================================================*
003730 初期化 SECTION.
003740*
003741     PERFORM ファイルオープン.
003862*
003874*================================================================*
003884 ファイルオープン SECTION.
003894*
003904     OPEN INPUT 受診者情報Ｆ.
003914         MOVE NC"受診者情報Ｆ" TO ファイル名.
003924         PERFORM オープンチェック.
003925     OPEN INPUT 地区マスタ.
003926         MOVE NC"地区マスタ" TO ファイル名.
003927         PERFORM オープンチェック.
003928     OPEN INPUT 郵便マスタ.
003929         MOVE NC"郵便マスタ" TO ファイル名.
003930         PERFORM オープンチェック.
003931*
003934*================================================================*
003944 オープンチェック SECTION.
003954*
003964     IF 状態キー  NOT =  "00"
003974         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
003984         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003994         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004004                                                 UPON CONS
004005*-----------------------------------------*
004006         CALL "actcshm"  WITH C LINKAGE
004007*-----------------------------------------*
004014         ACCEPT  キー入力 FROM CONS
004024         PERFORM ファイル閉鎖
004034         MOVE 99 TO PROGRAM-STATUS
004044         EXIT PROGRAM.
004045*================================================================*
004143*================================================================*
004144 ファイル閉鎖 SECTION.
004145*
004146     CLOSE 受診者情報Ｆ 郵便マスタ 地区マスタ.
004147*
004148*================================================================*
004149 終了処理 SECTION.
004150*
004151     PERFORM ファイル閉鎖.
004152*
004153*================================================================*
006561*================================================================*
006562 変換処理 SECTION.
006563*-------------------------------------------------------------* 
006564* 当月の受診者Ｆは、まだできていない。ZEROレコードREAD
006565*-------------------------------------------------------------* 
006567     MOVE ZERO       TO 受−施術和暦.
006568     MOVE ZERO       TO 受−施術年.
006569     MOVE ZERO       TO 受−施術月.
006570     MOVE 連後期高齢ＣＮＶ−患者コード   TO 受−患者コード.
006571     READ 受診者情報Ｆ
006572     NOT INVALID KEY
006573        IF ( 受−保険種別 NOT = 05 ) AND
006574           ( 受−保険種別 NOT = 70 ) AND
006575           ( 受−保険種別 NOT = 80 ) AND
006576           ( 受−保険種別 NOT = 90 ) AND
006577           ( 受−保険種別 NOT = ZERO ) AND
006578           ( 連後期高齢ＣＮＶ−施術和暦年月 >= 42004 )
006579*
006580           MOVE SPACE TO 連年齢２−キー
006581           INITIALIZE    連年齢２−キー
006582           MOVE 連後期高齢ＣＮＶ−施術和暦年月 TO 連年齢２−施術和暦年月
006583           MOVE 受−患者和暦     TO 連年齢２−和暦
006584           MOVE 受−患者年       TO 連年齢２−年
006585           MOVE 受−患者月       TO 連年齢２−月
006586           MOVE 受−患者日       TO 連年齢２−日
006587           CALL   "NENREI2"
006588           CANCEL "NENREI2"
006589*
006590*   *--------------------------------------------------------------------*
006591*   *  連年齢２−老人判定　 → 1:70〜74高齢者   , 2:27老人
006592*   *                          3:75〜後期高齢者 , 4:今月75歳 後期高齢者
006593*   * ⇒ 3:75〜後期高齢者の時 + 公費種別05
006594*   *--------------------------------------------------------------------*
006595           IF ( 連年齢２−老人判定 = 3 ) OR ( 受−公費種別 = 05 )
006596              PERFORM 後期高齢者変換処理
006598           END-IF
006599        END-IF
006632     END-READ.
006633*
006634*================================================================*
006635 後期高齢者変換処理 SECTION.
006636*
006637* 受診者Ｆ ZEROレコードREAD中
006638*
006639     MOVE "YES" TO 連後期高齢ＣＮＶ−変換対象フラグ.
006641*
006642*------------------------------------------------------------*
006643     MOVE SPACE TO 後期高齢者保険者番号Ｗ.
006644*
006645     IF 受−公費種別 = 05
006646*      / 27老人 /
006649        MOVE 受−費用負担者番号   TO 地区−老人市町村番号
006650        READ 地区マスタ   KEY IS  地区−老人市町村番号
006651        INVALID KEY
006653            PERFORM  郵便地区マスタ検索
006659        NOT INVALID KEY
006660            MOVE 地区−後期高齢者保険者番号 TO 後期高齢者保険者番号Ｗ
006661        END-READ
006662     ELSE
006663*      / 以外 /
006664        PERFORM  郵便地区マスタ検索
006666     END-IF.
006667****
006668     MOVE 05                     TO 連後期高齢ＣＮＶ−保険種別  連後期高齢ＣＮＶ−公費種別.
006669     MOVE 後期高齢者保険者番号Ｗ TO 連後期高齢ＣＮＶ−保険者番号  連後期高齢ＣＮＶ−費用負担者番号.
006671***
006672*------------------------------------------------------------*
006673*
006674     MOVE 1                TO 連後期高齢ＣＮＶ−本人家族区分.
006675     MOVE 受−患者氏名     TO 連後期高齢ＣＮＶ−患者氏名.
006677     MOVE 受−患者カナ     TO 連後期高齢ＣＮＶ−患者カナ.
006678     MOVE 受−患者性別     TO 連後期高齢ＣＮＶ−患者性別
006679     MOVE 受−患者生年月日 TO 連後期高齢ＣＮＶ−患者生年月日.
006680     MOVE 受−患者郵便番号 TO 連後期高齢ＣＮＶ−患者郵便番号.
006681     MOVE 受−患者住所１   TO 連後期高齢ＣＮＶ−患者住所１.
006682     MOVE 受−患者住所２   TO 連後期高齢ＣＮＶ−患者住所２.
006683     MOVE 受−患者電話番号 TO 連後期高齢ＣＮＶ−患者電話番号.
006694***
006695     IF ( 受−特別区分 = 3 ) OR ( 受−老人負担金免除 = 3 )
006696        MOVE 3 TO 連後期高齢ＣＮＶ−特別区分
006697        MOVE 3 TO 連後期高齢ＣＮＶ−老人負担金免除
006698     ELSE
006699        MOVE 1    TO 連後期高齢ＣＮＶ−特別区分
006700        MOVE ZERO TO 連後期高齢ＣＮＶ−老人負担金免除
006701     END-IF.
006707***
006708*
006717*================================================================*
006718 郵便地区マスタ検索 SECTION.
006719*
006720     IF 受−患者郵便番号１ NOT = ZERO 
006721           MOVE 受−患者郵便番号１ TO 郵−郵便番号１
006722           MOVE 受−患者郵便番号２ TO 郵−郵便番号２
006723           READ 郵便マスタ
006724           NOT INVALID KEY
006725               IF 郵−公共団体コード NOT = ZERO
006726                   MOVE 郵−公共団体コード TO 地区−公共団体コード
006727                   READ 地区マスタ
006728                   NOT INVALID KEY
006729                       MOVE 地区−後期高齢者保険者番号 TO 後期高齢者保険者番号Ｗ
006730                   END-READ
006731               END-IF
006732           END-READ
006733     END-IF.
006734*
006736*================================================================*
010106*================================================================*
010200******************************************************************
010210 END PROGRAM KKCNV.
010220******************************************************************
