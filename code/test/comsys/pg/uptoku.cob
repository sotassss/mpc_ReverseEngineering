000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             UPTOKU.
000060 AUTHOR.                 佐野 誠
000070*
000080*------------------------------------------------------------------*
000090*  受診者Ｆの特別区分の更新                                        *
000110*------------------------------------------------------------------*
000120 DATE-WRITTEN.           2002-09-27
000130 DATE-COMPILED.          2002-09-27
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000260     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000270                             ORGANIZATION             IS  INDEXED
000280                             ACCESS MODE              IS  DYNAMIC
000290                             RECORD KEY               IS  受−施術和暦年月
000300                                                          受−患者コード
000310                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000320                                                          受−患者カナ
000330                                                          受−患者コード
000340                             ALTERNATE RECORD KEY     IS  受−患者コード
000350                                                          受−施術和暦年月
000360                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000370                                                          受−保険種別
000380                                                          受−保険者番号
000390                                                          受−患者コード
000400                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000410                                                          受−公費種別
000420                                                          受−費用負担者番号
000430                                                          受−患者コード
000440                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000450                                                          受−助成種別
000460                                                          受−費用負担者番号助成
000470                                                          受−患者コード
000480                             ALTERNATE RECORD KEY     IS  受−請求和暦年月
000490                                                          受−施術和暦年月
000500                                                          受−患者コード
000510                             FILE STATUS              IS  状態キー
000520                             LOCK        MODE         IS  AUTOMATIC.
001130*
001140******************************************************************
001150*                      DATA DIVISION                             *
001160******************************************************************
001170 DATA                    DIVISION.
001180 FILE                    SECTION.
001190*                           ［ＲＬ＝  ３２０］
001200 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001210     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
001220*
002080*----------------------------------------------------------------*
002090******************************************************************
002100*                WORKING-STORAGE SECTION                         *
002110******************************************************************
002120 WORKING-STORAGE         SECTION.
002130 01 キー入力                           PIC X    VALUE SPACE.
002140 01 状態キー                           PIC X(2) VALUE SPACE.
002150 01 終了フラグ                         PIC X(3) VALUE SPACE.
002170 01 ファイル名                         PIC N(6) VALUE SPACE.
002271*
002590******************************************************************
002600*                          連結項目                              *
002610******************************************************************
002620*
002621* 受診者Ｆ特別区分更新用14/10〜
002622 01 連受診特別−キー IS EXTERNAL.
002623    03 連受診特別−施術和暦年月.
002624       05 連受診特別−施術和暦         PIC 9.
002625       05 連受診特別−施術年月.
002626          07 連受診特別−施術年        PIC 9(2).
002627          07 連受診特別−施術月        PIC 9(2).
002628    03 連受診特別−患者コード.
002629       05 連受診特別−患者番号         PIC 9(6).
002630       05 連受診特別−枝番             PIC X.
002631    03 連受診特別−特別区分            PIC 9.
002730*
003481******************************************************************
003482*                      PROCEDURE  DIVISION                       *
003483******************************************************************
003490 PROCEDURE               DIVISION.
003500************
003510*           *
003520* 初期処理   *
003530*           *
003540************
003550     PERFORM 初期化.
003560************
003570*           *
003580* 主処理     *
003590*           *
003600************
003601*
003602     PERFORM 更新処理.
003603*
003620************
003630*           *
003640* 終了処理   *
003650*           *
003660************
003670     PERFORM 終了処理.
003690     EXIT PROGRAM.
003700*
003710*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003720*================================================================*
003730 初期化 SECTION.
003740*
003741     PERFORM ファイルオープン.
003862*
003874*================================================================*
003884 ファイルオープン SECTION.
003894*
003904     OPEN I-O 受診者情報Ｆ.
003914         MOVE NC"受診者情報Ｆ" TO ファイル名.
003924         PERFORM オープンチェック.
003925*
003934*================================================================*
003944 オープンチェック SECTION.
003954*
003964     IF 状態キー  NOT =  "00"
003974         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
003984         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003994         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004004                                                 UPON CONS
004005*-----------------------------------------*
004006         CALL "actcshm"  WITH C LINKAGE
004007*-----------------------------------------*
004014         ACCEPT  キー入力 FROM CONS
004024         PERFORM ファイル閉鎖
004034         MOVE 99 TO PROGRAM-STATUS
004044         EXIT PROGRAM.
004054*================================================================*
004064 ファイル閉鎖 SECTION.
004074*
004084     CLOSE 受診者情報Ｆ.
004085*
004094*================================================================*
004104 終了処理 SECTION.
004114*
004124     PERFORM ファイル閉鎖.
004125*
004134*================================================================*
006560*================================================================*
006561 更新処理 SECTION.
006563*-------------------------------------------------------------* 
006564* 受診者Ｆの特別区分を渡された値で更新する。
006568*-------------------------------------------------------------* 
006644*
006656     MOVE 連受診特別−施術和暦       TO 受−施術和暦.
006657     MOVE 連受診特別−施術年         TO 受−施術年.
006658     MOVE 連受診特別−施術月         TO 受−施術月.
006659     MOVE 連受診特別−患者コード     TO 受−患者コード.
006660     READ 受診者情報Ｆ
006661     NOT INVALID KEY
006668         MOVE 連受診特別−特別区分   TO 受−特別区分
006669         REWRITE 受−レコード
006670         END-REWRITE
006671         IF 状態キー NOT = "00"
006672            MOVE NC"受診者" TO ファイル名
006673            PERFORM エラー表示
006674         END-IF
006675     END-READ.
006676*
006677* / 最新レコード /
006678     MOVE ZERO                    TO 受−施術和暦.
006679     MOVE ZERO                    TO 受−施術年.
006680     MOVE ZERO                    TO 受−施術月.
006681     MOVE 連受診特別−患者コード  TO 受−患者コード.
006683     READ 受診者情報Ｆ
006688     NOT INVALID KEY
006689*          / 施術年月 が最新であれば更新 /
006692          IF 受−施術和暦年月Ｎ = 連受診特別−施術和暦年月
006693             MOVE 連受診特別−特別区分   TO 受−特別区分
006694             REWRITE 受−レコード
006695             END-REWRITE
006696             IF 状態キー NOT = "00"
006697                MOVE NC"受診者最新" TO ファイル名
006698                PERFORM エラー表示
006699             END-IF
006700          END-IF
006701     END-READ.
006702*
006704*================================================================*
006705 エラー表示 SECTION.
006706     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
006707     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
006708     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
006709     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"  UPON CONS.
006710*-----------------------------------------*
006711     CALL "actcshm"  WITH C LINKAGE.
006712*-----------------------------------------*
006713     ACCEPT  キー入力 FROM CONS
006714     PERFORM ファイル閉鎖.
006715     EXIT PROGRAM.
006716*================================================================*
010106*================================================================*
010200******************************************************************
010210 END PROGRAM UPTOKU.
010220******************************************************************
