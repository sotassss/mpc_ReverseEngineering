000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             MOJI1.
000060 AUTHOR.                 佐野 誠
000070*
000080*------------------------------------------------------------------*
000090*      文字列暗号化（登録）                                        *
000110*------------------------------------------------------------------*
000120 DATE-WRITTEN.           2012-06-01
000130 DATE-COMPILED.          2012-06-01
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000266*
001020******************************************************************
001030*                      DATA DIVISION                             *
001040******************************************************************
001050 DATA                    DIVISION.
001060 FILE                    SECTION.
001190*
002080*----------------------------------------------------------------*
002090******************************************************************
002100*                WORKING-STORAGE SECTION                         *
002110******************************************************************
002120 WORKING-STORAGE         SECTION.
002130 01 キー入力                  PIC X    VALUE SPACE.
002140 01 状態キー                  PIC X(2) VALUE SPACE.
002173**
002183 01 変換データＷ              PIC X(100) VALUE SPACE.
002274 01 パスエリアＷ.
002275    03 パスＷ                 PIC X(4)  VALUE "SANO".
002276    03 患者番号文字Ｗ         PIC X(6)  VALUE SPACE.
002283    03 パス残りＷ             PIC X(10) VALUE SPACE.
002284*
002285 01 正常フラグ                PIC X(3) VALUE SPACE.
002286 01 暗号記号Ｗ                PIC X(24) VALUE SPACE.
002287 01 暗号番号Ｗ                PIC X(30) VALUE SPACE.
002293**
002580*
002590******************************************************************
002600*                          連結項目                              *
002610******************************************************************
002620*
002630* 暗号登録用
002640 01 連暗号登録−暗号情報 IS EXTERNAL.
002642    03 連暗号登録−入力情報.
002643       05 連暗号登録−患者番号           PIC 9(6).
002644       05 連暗号登録−画面の記号         PIC X(24).
002645       05 連暗号登録−画面の番号         PIC X(30).
002646    03 連暗号登録−出力情報.
002649       05 連暗号登録−記号               PIC X(24).
002650       05 連暗号登録−番号               PIC X(30).
002652       05 連暗号登録−暗号化項目.
002653         07 連暗号登録−暗号患者番号     PIC X(6).
002654         07 連暗号登録−暗号判定記号     PIC X.
002655         07 連暗号登録−暗号判定番号     PIC X.
002656         07 連暗号登録−暗号記号         PIC X(24).
002657         07 連暗号登録−暗号番号         PIC X(30).
002730*
003481******************************************************************
003482*                      PROCEDURE  DIVISION                       *
003483******************************************************************
003490 PROCEDURE               DIVISION.
003500************
003510*           *
003520* 初期処理   *
003530*           *
003540************
003541* 出力情報初期化
003550     MOVE SPACE TO 連暗号登録−出力情報.
003551*
003552* 患者番号セット
003553     MOVE ZERO                  TO 患者番号文字Ｗ.
003554     MOVE 連暗号登録−患者番号  TO 患者番号文字Ｗ.
003555     MOVE 患者番号文字Ｗ        TO 連暗号登録−暗号患者番号.
003556*
003560************
003570*           *
003580* 主処理     *
003590*           *
003600************
003610*
003611     IF ( 連暗号登録−画面の記号 = SPACE ) AND ( 連暗号登録−画面の番号 = SPACE )
003612        CONTINUE
003613     ELSE
003616        PERFORM 暗号処理
003617     END-IF.
003618*
003620************
003630*           *
003640* 終了処理   *
003650*           *
003660************
003690     EXIT PROGRAM.
003700*
003710*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003720*================================================================*
006533*================================================================*
006543 暗号処理 SECTION.
006553*
006580*-----------------------------------------------------------------------------------*
006581* 記号
006582*
006587     MOVE SPACE                   TO パス残りＷ.
006590     MOVE SPACE                   TO 正常フラグ.
006591     MOVE SPACE                   TO 変換データＷ.
006592     MOVE SPACE                   TO 暗号記号Ｗ.
006593     MOVE 連暗号登録−画面の記号  TO 変換データＷ.
006594*
006595     CALL "strmojie"   WITH C LINKAGE  USING BY REFERENCE パスエリアＷ
006596                                             BY REFERENCE 変換データＷ.
006597*    正常時のみ暗号データセット（エラー時は暗号化なし）
006598     IF PROGRAM-STATUS = ZERO
006599        MOVE 変換データＷ   TO 暗号記号Ｗ
006600*
006602*       / 複合化して文字列が一致してるかチェック/
006603        MOVE SPACE          TO パス残りＷ
006604        MOVE SPACE          TO 変換データＷ
006605        MOVE 暗号記号Ｗ     TO 変換データＷ
006606        CALL "strmojid"   WITH C LINKAGE  USING BY REFERENCE パスエリアＷ
006607                                                BY REFERENCE 変換データＷ
006608        IF PROGRAM-STATUS = ZERO
006609           IF 変換データＷ = 連暗号登録−画面の記号
006610              MOVE "1"             TO 連暗号登録−暗号判定記号
006611              MOVE 暗号記号Ｗ      TO 連暗号登録−暗号記号
006612              MOVE SPACE           TO 連暗号登録−記号
006613              MOVE "YES"           TO 正常フラグ
006618           END-IF
006619        END-IF
006620     END-IF.
006621*
006622     IF 正常フラグ NOT = "YES"
006624        MOVE SPACE TO 連暗号登録−暗号判定記号
006625        MOVE SPACE TO 連暗号登録−暗号記号
006626        MOVE 連暗号登録−画面の記号  TO 連暗号登録−記号
006627     END-IF.
006628*
006630*-----------------------------------------------------------------------------------*
006631*-----------------------------------------------------------------------------------*
006632* 番号
006633     MOVE SPACE                   TO パス残りＷ.
006634     MOVE SPACE                   TO 正常フラグ.
006635     MOVE SPACE                   TO 変換データＷ.
006636     MOVE SPACE                   TO 暗号番号Ｗ.
006637     MOVE 連暗号登録−画面の番号  TO 変換データＷ.
006638*
006639     CALL "strmojie"   WITH C LINKAGE  USING BY REFERENCE パスエリアＷ
006640                                             BY REFERENCE 変換データＷ.
006641*    正常時のみ暗号データセット（エラー時は暗号化なし）
006642     IF PROGRAM-STATUS = ZERO
006643        MOVE 変換データＷ   TO 暗号番号Ｗ
006644*
006645*       / 複合化して文字列が一致してるかチェック/
006646        MOVE SPACE          TO パス残りＷ
006647        MOVE SPACE          TO 変換データＷ
006648        MOVE 暗号番号Ｗ     TO 変換データＷ
006649        CALL "strmojid"   WITH C LINKAGE  USING BY REFERENCE パスエリアＷ
006650                                                BY REFERENCE 変換データＷ
006651        IF PROGRAM-STATUS = ZERO
006652           IF 変換データＷ = 連暗号登録−画面の番号
006653              MOVE "1"             TO 連暗号登録−暗号判定番号
006654              MOVE 暗号番号Ｗ      TO 連暗号登録−暗号番号
006655              MOVE SPACE           TO 連暗号登録−番号
006656              MOVE "YES"           TO 正常フラグ
006657           END-IF
006658        END-IF
006659     END-IF.
006660*
006661     IF 正常フラグ NOT = "YES"
006662        MOVE SPACE TO 連暗号登録−暗号判定番号
006663        MOVE SPACE TO 連暗号登録−暗号番号
006664        MOVE 連暗号登録−画面の番号  TO 連暗号登録−番号
006665     END-IF.
006666*
010001*================================================================*
010106*================================================================*
010200******************************************************************
010210 END PROGRAM MOJI1.
010220******************************************************************
