000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YAW9221.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*         患者データ出力【データ作成】
000100*         MED = YAW922
      *         PowerCOBOL用に変更/09.08.20
      */2014.07.14 受診者カナ順印刷追加
000110*----------------------------------------------------------------*
000120 DATE-WRITTEN.           2009-08-20
000130 DATE-COMPILED.          2009-08-20
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000260     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000270                             ORGANIZATION             IS  INDEXED
000280                             ACCESS MODE              IS  DYNAMIC
000290                             RECORD KEY               IS  元－元号区分
000300                             FILE STATUS              IS  状態キー
000310                             LOCK        MODE         IS  AUTOMATIC.
000320     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000330                             ORGANIZATION             IS  INDEXED
000340                             ACCESS MODE              IS  DYNAMIC
000350                             RECORD KEY               IS  受－施術和暦年月
000360                                                          受－患者コード
000370                             ALTERNATE RECORD KEY     IS  受－施術和暦年月
000380                                                          受－患者カナ
000390                                                          受－患者コード
000400                             ALTERNATE RECORD KEY     IS  受－患者コード
000410                                                          受－施術和暦年月
000420                             ALTERNATE RECORD KEY     IS  受－施術和暦年月
000430                                                          受－保険種別
000440                                                          受－保険者番号
000450                                                          受－患者コード
000460                             ALTERNATE RECORD KEY     IS  受－施術和暦年月
000470                                                          受－公費種別
000480                                                          受－費用負担者番号
000490                                                          受－患者コード
000500                             ALTERNATE RECORD KEY     IS  受－施術和暦年月
000510                                                          受－助成種別
000520                                                          受－費用負担者番号助成
000530                                                          受－患者コード
000540                             ALTERNATE RECORD KEY     IS  受－請求和暦年月
000550                                                          受－施術和暦年月
000560                                                          受－患者コード
000570                             FILE STATUS              IS  状態キー
000580                             LOCK        MODE         IS  AUTOMATIC.
000830*---------------------------------------------------------------------------------------*
           SELECT  作業ファイル    ASSIGN      TO        "C:\MAKISHISYS\HMOBJ\TEMP\H9220L.DAT"
                                   ORGANIZATION             IS  INDEXED
                                   ACCESS                   IS  DYNAMIC
                                   RECORD      KEY          IS  作－患者コード
                                   ALTERNATE RECORD KEY     IS  作－患者カナ
                                                                作－患者コード
                                   ALTERNATE RECORD KEY     IS  作－患者氏名
                                                                作－患者コード
                                   FILE        STATUS       IS  状態キー
                                   LOCK        MODE         IS  AUTOMATIC.
000840     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\HMOBJ\TEMP\H9221L.DAT"
000850                             ORGANIZATION             IS  INDEXED
000860                             ACCESS                   IS  DYNAMIC
000870                             RECORD      KEY          IS  作１－施術和暦年月Ｎ
000880                                                          作１－患者コード
000890                             ALTERNATE RECORD KEY     IS  作１－印刷区分
000900                                                          作１－施術和暦年月Ｎ
000910                                                          作１－患者コード
000920*/                                                       */並び順の変更/0404
000930                             ALTERNATE RECORD KEY     IS  作１－患者郵便番号
000940                                                          作１－患者カナ
000950                                                          作１－患者コード
000960                                                          作１－施術和暦年月Ｎ
000970                             ALTERNATE RECORD KEY     IS  作１－印刷区分
000980                                                          作１－患者郵便番号
000990                                                          作１－患者カナ
001000                                                          作１－患者コード
001010                                                          作１－施術和暦年月Ｎ
000930                             ALTERNATE RECORD KEY     IS  作１－印刷区分
000980                                                          作１－患者カナ
000950                                                          作１－患者コード
000960                                                          作１－施術和暦年月Ｎ
001020                             FILE        STATUS       IS  状態キー
001030                             LOCK        MODE         IS  AUTOMATIC.
001040******************************************************************
001050*                      DATA DIVISION                             *
001060******************************************************************
001070 DATA                    DIVISION.
001080 FILE                    SECTION.
001090*                           ［ＲＬ＝  １２８］
001100 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
001110     COPY GENGOU          OF  XFDLIB  JOINING   元   AS  PREFIX.
001120*                           ［ＲＬ＝  ３２０］
001130 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001140     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
001210*-------------------------------------------------------------------*
001220**
       FD  作業ファイル RECORD  CONTAINS 128 CHARACTERS.
       01  作－レコード.
           03  作－レコードキー.
               05  作－患者コード.
                   07 作－患者番号               PIC 9(6).
                   07 作－枝番                   PIC X(1).
           03  作－レコードデータ.
               05  作－患者氏名                  PIC X(50).
               05  作－患者カナ                  PIC X(50).
               05  作－施術和暦年月.
                   07 作－施術和暦               PIC 9(1).
                   07 作－施術年                 PIC 9(2).
                   07 作－施術月                 PIC 9(2).
               05  FILLER                        PIC X(16).
001230*
001240 FD  作業ファイル１ RECORD  CONTAINS 320 CHARACTERS.
001250 01  作１－レコード.
001260     03  作１－レコードキー.
001270       05 作１－施術和暦年月Ｎ.
001280         07 作１－施術和暦Ｎ                 PIC 9.
001290         07 作１－施術年月Ｎ.
001300           09 作１－施術年Ｎ                 PIC 9(2).
001310           09 作１－施術月Ｎ                 PIC 9(2).
001320       05 作１－患者コード.
001330         07 作１－患者番号                   PIC 9(6).
001340         07 作１－枝番                       PIC X.
001350     03  作１－レコードデータ.
001360       05 作１－患者カナ                     PIC X(50).
001370       05 作１－患者氏名                     PIC X(50).
001380       05 作１－患者郵便番号.
001390         07 作１－患者郵便番号１             PIC X(3).
001400         07 作１－患者郵便番号２             PIC X(4).
001410       05 作１－患者住所１                   PIC X(50).
001420       05 作１－患者住所２                   PIC X(50).
001430       05 作１－患者生年月日.
001440         07 作１－患者和暦                   PIC 9.
001450         07 作１－患者年                     PIC 9(2).
001460         07 作１－患者月                     PIC 9(2).
001470         07 作１－患者日                     PIC 9(2).
001480       05 作１－患者電話番号                 PIC X(30).
001490       05 作１－患者性別                     PIC 9.
001500       05 作１－印刷区分                     PIC 9.
001510       05 作１－インデックス                 PIC 9(7).
001520       05 FILLER                             PIC X(55).
001530*----------------------------------------------------------------*
001540******************************************************************
001550*                WORKING-STORAGE SECTION                         *
001560******************************************************************
001570 WORKING-STORAGE         SECTION.
001580 01 キー入力                           PIC X     VALUE SPACE.
001590 01 状態キー                           PIC X(2)  VALUE SPACE.
001600 01 ファイル名                         PIC N(2)  VALUE SPACE.
001610 01 終了フラグ                         PIC X(3)  VALUE SPACE.
001620 01 終了フラグ２                       PIC X(3)  VALUE SPACE.
001630 01 カウンタ                           PIC 9(2)  VALUE ZERO.
001640 01 カウンタ２                         PIC 9(2)  VALUE ZERO.
001650*
001660 01 データフラグ                       PIC X(1)  VALUE SPACE.
001670 01 対象件数Ｗ                         PIC 9(5)  VALUE ZERO.
001680*
001690 01 地名Ｗ.
001700    03 地名ＷＸ                        PIC X(20) VALUE SPACE.
001710 01 地名カウンタ                       PIC 9(2)  VALUE ZERO.
001720 01 地名ＷＰ                           PIC X(22) VALUE SPACE.
001730*
001740 01 患者年齢Ｗ                         PIC 9(3)  VALUE ZERO.
001750 01 患者西暦年Ｗ                       PIC 9(4)  VALUE ZERO.
001760 01 施術西暦年Ｗ                       PIC 9(4)  VALUE ZERO.
001770*
001780*/エラー表示の修正
001790 01 エラー表示Ｆ                       PIC 9(1)  VALUE ZERO.
001800*
001810 01 計算機西暦.
001820    03 計算機西暦年                    PIC 9(4).
001830    03 計算機西暦月日                  PIC 9(4).
001840 01 計算機西暦Ｒ REDEFINES 計算機西暦.
001850    03 計算機世紀                      PIC 9(2).
001860    03 計算機日付                      PIC 9(6).
001870    03 計算機日付Ｒ REDEFINES 計算機日付.
001880       05 計算機年月                   PIC 9(4).
001890       05 計算機年月Ｒ REDEFINES 計算機年月.
001900         07 計算機年                   PIC 9(2).
001910         07 計算機月                   PIC 9(2).
001920       05 計算機日                     PIC 9(2).
001930*
001940******************************************************************
001950*                          連結項目                              *
001960******************************************************************
001970****************
001980* 画面入力情報 *
001990****************
       01 連入－入力区分９２２ IS EXTERNAL GLOBAL.
          03 連入－表示区分                PIC 9(1).
      */0:受診者住所を出力、1:被保険者住所を出力。
          03 連入－住所区分                PIC 9(1).
      */0:ラベルを出力、1:リストを出力、3:ＣＳＶ。
          03 連入－印刷形式                PIC 9(1).
      */ラベルの開始位置
          03 連入－ラベル開始位置          PIC 9(2).
002280*-----------------------------------------------------------------------*
002290 01 連メ２－キー IS EXTERNAL.
002300    03  連メ２－メッセージ               PIC N(20).
002310    03  連メ２－メッセージ１             PIC X(6).
002320    03  連メ２－メッセージ２             PIC N(6).
002330*
002340* 確認メッセージ用 (２行)
002350 01 連メ７－キー IS EXTERNAL.
002360    03  連メ７－メッセージ１             PIC X(40).
002370    03  連メ７－メッセージ２             PIC X(40).
002380*
002390******************************************************************
002400*                      PROCEDURE  DIVISION                       *
002410******************************************************************
002420 PROCEDURE               DIVISION.
002430************
002440*           *
002450* 初期処理   *
002460*           *
002470************
002480     PERFORM ファイルオープン.
002500     PERFORM 初期化.
002510************
002520*           *
002530* 主処理     *
002540*           *
002550************
002560     OPEN OUTPUT 作業ファイル１.
002570         MOVE NC"作業" TO ファイル名.
002580         PERFORM オープンチェック.
002590*
           PERFORM 作業ファイル１作成.
002670*
002680     CLOSE 作業ファイル１.
002690*
002700************
002710*           *
002720* 終了処理   *
002730*           *
002740************
002750     PERFORM 終了処理.
002770     MOVE ZERO TO PROGRAM-STATUS.
002780     EXIT PROGRAM.
002790*
002800*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002810*================================================================*
002820 ファイルオープン SECTION.
002830*================================================================*
002840     OPEN INPUT 元号マスタ.
002850             MOVE NC"元号" TO ファイル名.
002860             PERFORM オープンチェック.
002870     OPEN INPUT 受診者情報Ｆ.
002880         MOVE NC"受診者情報Ｆ" TO ファイル名.
002890         PERFORM オープンチェック.
002900     OPEN INPUT 作業ファイル.
002910         MOVE NC"作業ファイル" TO ファイル名.
002920         PERFORM オープンチェック.
002930*
002940*================================================================*
002950 オープンチェック SECTION.
002960*
002970     IF ( 状態キー  NOT =  "00" )
002980         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
002990         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003000         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003010                                                 UPON CONS
000090         CALL "actcshm"  WITH C LINKAGE
003020         ACCEPT  キー入力 FROM CONS
003030         PERFORM ファイル閉鎖
003040         MOVE 99 TO PROGRAM-STATUS
003050         EXIT PROGRAM.
003060*
003110*================================================================*
003120 初期化 SECTION.
003130*================================================================*
003140*    /* 現在日付取得 */
003150     ACCEPT 計算機日付 FROM DATE.
003160*    /* 1980～2079年の間で設定 */
003170     IF ( 計算機年 > 80 )
003180         MOVE 19 TO 計算機世紀
003190     ELSE
003200         MOVE 20 TO 計算機世紀
003210     END-IF.
003220*
005560*================================================================*
005570 作業ファイル１作成 SECTION.
005580*================================================================*
           MOVE ZERO   TO 作－患者番号.
           MOVE SPACE  TO 作－枝番.
           START 作業ファイル KEY IS >= 作－患者コード
           END-START.
           IF ( 状態キー = "00" )
              MOVE SPACE TO 終了フラグ
              PERFORM 作業ファイル読込
      *
              PERFORM UNTIL ( 終了フラグ NOT = SPACE )
                  PERFORM 受診者情報取得
005590            PERFORM レコードセット
005600*
005610*            IF ( 作１－患者住所１ = SPACE ) AND
005630*               ( 作１－患者住所２ = SPACE )
005640*               PERFORM エラー処理
005650*            ELSE
005660               PERFORM ファイル書込
005670*            END-IF
                  PERFORM 作業ファイル読込
              END-PERFORM
           END-IF.
005680*
      *================================================================*
       作業ファイル読込 SECTION.
      *================================================================*
           READ 作業ファイル NEXT
           AT END
              MOVE "YES" TO 終了フラグ
           END-READ.
*
003780*================================================================*
003790 受診者情報取得 SECTION.
003800*================================================================*
003810     MOVE 作－施術和暦年月   TO 受－施術和暦年月.
003820     MOVE 作－患者コード     TO 受－患者コード.
003830*
003840     READ 受診者情報Ｆ
003850     INVALID KEY
003860         MOVE SPACE TO 受－レコード
003870         INITIALIZE    受－レコード
003880     END-READ.
003890*
005690*================================================================*
005700 レコードセット SECTION.
005710*================================================================*
005720     MOVE SPACE TO 作１－レコード.
005730     INITIALIZE 作１－レコード.
005740*
           IF 作－施術和暦年月 = ZERO
005750         MOVE 受－施術和暦Ｎ     TO  作１－施術和暦Ｎ
005760         MOVE 受－施術年Ｎ       TO  作１－施術年Ｎ
005770         MOVE 受－施術月Ｎ       TO  作１－施術月Ｎ
           ELSE
005750         MOVE 作－施術和暦       TO  作１－施術和暦Ｎ
005760         MOVE 作－施術年         TO  作１－施術年Ｎ
005770         MOVE 作－施術月         TO  作１－施術月Ｎ
           END-IF.
005780     MOVE 受－患者番号           TO  作１－患者番号.
005790     MOVE 受－枝番               TO  作１－枝番.
005800     MOVE 受－患者カナ           TO  作１－患者カナ.
005810     MOVE 受－患者氏名           TO  作１－患者氏名.
005820     IF ( 連入－住所区分 = ZERO )
005830         MOVE 受－患者郵便番号１ TO  作１－患者郵便番号１
005840         MOVE 受－患者郵便番号２ TO  作１－患者郵便番号２
005850         MOVE 受－患者住所１     TO  作１－患者住所１
005860         MOVE 受－患者住所２     TO  作１－患者住所２
005870     ELSE
005880         MOVE 受－郵便番号１     TO  作１－患者郵便番号１
005890         MOVE 受－郵便番号２     TO  作１－患者郵便番号２
005900         MOVE 受－住所１         TO  作１－患者住所１
005910         MOVE 受－住所２         TO  作１－患者住所２
005920     END-IF.
005930     MOVE 受－患者和暦           TO  作１－患者和暦.
005940     MOVE 受－患者年             TO  作１－患者年.
005950     MOVE 受－患者月             TO  作１－患者月.
005960     MOVE 受－患者日             TO  作１－患者日.
005970     MOVE 受－患者電話番号       TO  作１－患者電話番号.
005980     MOVE 受－患者性別           TO  作１－患者性別.
005990* 一括印刷の時
006000*     IF ( Ｈ連入－処理モード = "F1" )
      * 検索選択済みなので全て印刷
006010        MOVE 1                   TO 作１－印刷区分.
006020*     ELSE
006030*        MOVE ZERO                TO 作１－印刷区分
006040*     END-IF.
006050*
006060*================================================================*
006070 エラー処理 SECTION.
006080*================================================================*
006090*/仕様変更住所か郵便番号の未入力があった場合はそのデータのみを印刷しない
006100*/エラーはコンソール画面に表示
006110      PERFORM コンソールエラー確認.
006120      DISPLAY "受診者の住所が入力されていません " 
006130              " 受診者№=" 受－患者番号
006140              " 施術年月=" 受－施術年月Ｎ
006150              " "          受－患者氏名   UPON CONS.
006160*
006170*================================================================*
006180 コンソールエラー確認 SECTION.
006190*
006200     IF ( エラー表示Ｆ = ZERO )
006210         MOVE  "エラーが発生したのでコンソール画面の内容" TO 連メ７－メッセージ１
006220         MOVE  "を確認し、修正してください。"             TO 連メ７－メッセージ２
006230         CALL   "MSG007"
006240         CANCEL "MSG007"
006250     END-IF.
006260     MOVE 1 TO エラー表示Ｆ.
006270*
006280*================================================================*
006290 ファイル書込 SECTION.
006300*================================================================*
006310     WRITE 作１－レコード
006320     NOT INVALID KEY
006330         COMPUTE 対象件数Ｗ = 対象件数Ｗ + 1
006370     END-WRITE.
006380*
006390*================================================================*
006400 エラー表示 SECTION.
006410*
006420     DISPLAY NC"状態キー" 状態キー  UPON CONS.
006430     DISPLAY NC"５ファイル書込エラー：" ファイル名   UPON CONS.
006440     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
006450     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
000240     CALL "actcshm"  WITH C LINKAGE.
006460     ACCEPT  キー入力 FROM CONS.
006470     PERFORM ファイル閉鎖.
006480     MOVE 99 TO PROGRAM-STATUS.
006490     EXIT PROGRAM.
006500*
006510*================================================================*
006520 終了処理 SECTION.
006530*================================================================*
006540     PERFORM ファイル閉鎖.
006550*
006560*================================================================*
006570 ファイル閉鎖 SECTION.
006580*
006590     CLOSE 元号マスタ 受診者情報Ｆ 作業ファイル.
006600*
006610*================================================================*
006620******************************************************************
006630 END PROGRAM YAW9221.
006640******************************************************************
