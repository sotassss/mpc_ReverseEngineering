000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             NENREI2.
000060 AUTHOR.                 佐野 誠
000070*
000080*------------------------------------------------------------------*
000090*  保険年齢計算から乳幼児・老人の判定   柔ｳｨﾝﾄﾞｳｽﾞ95版             *
000110*------------------------------------------------------------------*
000120 DATE-WRITTEN.           2002-09-03
000130 DATE-COMPILED.          2002-09-03
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
001070     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
001080                             ORGANIZATION             IS  INDEXED
001090                             ACCESS MODE              IS  DYNAMIC
001100                             RECORD KEY               IS  元−元号区分
001110                             FILE STATUS              IS  状態キー
001120                             LOCK        MODE         IS  AUTOMATIC.
001130*
001140******************************************************************
001150*                      DATA DIVISION                             *
001160******************************************************************
001170 DATA                    DIVISION.
001180 FILE                    SECTION.
001190*                           ［ＲＬ＝  １２８］
001200 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
001210     COPY GENGOU          OF  XFDLIB  JOINING   元   AS  PREFIX.
001220*
002080*----------------------------------------------------------------*
002090******************************************************************
002100*                WORKING-STORAGE SECTION                         *
002110******************************************************************
002120 WORKING-STORAGE         SECTION.
002130 01 キー入力                           PIC X    VALUE SPACE.
002140 01 状態キー                           PIC X(2) VALUE SPACE.
002150 01 終了フラグ                         PIC X(3) VALUE SPACE.
002170 01 ファイル名                         PIC N(6) VALUE SPACE.
002173**
002234** 患者年齢用
002235 01 患者年齢ＷＷ.
002236    03 患者和暦Ｗ                     PIC 9    VALUE ZERO.
002237    03 患者年Ｗ                       PIC 9(2) VALUE ZERO.
002238    03 患者月Ｗ                       PIC 9(2) VALUE ZERO.
002239    03 患者日Ｗ                       PIC 9(2) VALUE ZERO.
002240    03 患者年齢Ｗ                     PIC S9(3) VALUE ZERO.
002241    03 患者西暦年Ｗ                   PIC 9(4)  VALUE ZERO.
002242** 西暦比較用
002243 01 現在西暦年月日Ｗ.
002244    03 現在西暦年月Ｗ                 PIC 9(6)  VALUE ZERO.
002245    03 現在西暦日Ｗ                   PIC 9(2)  VALUE ZERO.
002246 01 施術西暦年月Ｗ.
002247    03 施術西暦年Ｗ                   PIC 9(4)  VALUE ZERO.
002248    03 施術西暦月Ｗ                   PIC 9(2)  VALUE ZERO.
002249*
002250** 患者西暦誕生日
002251 01 患者西暦年月日Ｗ.
002252    03 患者西暦年ＷＷ                 PIC 9(4)  VALUE ZERO.
002253    03 患者西暦月ＷＷ                 PIC 9(2)  VALUE ZERO.
002254    03 患者西暦日ＷＷ                 PIC 9(2)  VALUE ZERO.
002255*
002256** 義務教育就学前期限用
002257 01 就学前西暦年月Ｗ.
002258    03 就学前西暦年Ｗ                 PIC 9(4)  VALUE ZERO.
002259    03 就学前西暦月Ｗ                 PIC 9(2)  VALUE ZERO.
002260 01 比較患者月日Ｗ.
002263    03 比較患者月Ｗ                   PIC 9(2)  VALUE ZERO.
002264    03 比較患者日Ｗ                   PIC 9(2)  VALUE ZERO.
002265*
002266* 後期高齢者用
002267 01 当月含む年齢Ｗ                    PIC S9(3) VALUE ZERO.
002268 01 当月誕生日フラグ                  PIC X(3) VALUE SPACE.
002269*
002270*
002271**********************************************************************
002272*
002273* 日付ＷＯＲＫ
002274 01 計算機西暦.
002275    03 計算機西暦年                    PIC 9(4).
002276    03 計算機西暦月日                  PIC 9(4).
002277 01 計算機西暦Ｒ REDEFINES 計算機西暦.
002278    03 計算機世紀                      PIC 9(2).
002279    03 計算機日付                      PIC 9(6).
002280    03 計算機日付Ｒ REDEFINES 計算機日付.
002281       05 計算機年月                   PIC 9(4).
002282       05 計算機年月Ｒ REDEFINES 計算機年月.
002283         07 計算機年                   PIC 9(2).
002284         07 計算機月                   PIC 9(2).
002285       05 計算機日                     PIC 9(2).
002286*
002590******************************************************************
002600*                          連結項目                              *
002610******************************************************************
002620*
002630* 年齢判定用
002640 01 連年齢２−キー IS EXTERNAL.
002641    03 連年齢２−施術和暦年月.
002642       05 連年齢２−施術和暦      PIC 9.
002643       05 連年齢２−施術年月.
002644         07 連年齢２−施術年      PIC 9(2).
002645         07 連年齢２−施術月      PIC 9(2).
002655    03 連年齢２−和暦年月日.
002665       05 連年齢２−和暦          PIC 9.
002675       05 連年齢２−年            PIC 9(2).
002685       05 連年齢２−月            PIC 9(2).
002695       05 連年齢２−日            PIC 9(2).
002710    03 連年齢２−保険年齢         PIC 9(3).
002720    03 連年齢２−乳幼児判定       PIC 9.
002721    03 連年齢２−老人判定         PIC 9.
002730*
003481******************************************************************
003482*                      PROCEDURE  DIVISION                       *
003483******************************************************************
003490 PROCEDURE               DIVISION.
003500************
003510*           *
003520* 初期処理   *
003530*           *
003540************
003550     PERFORM 初期化.
003551     PERFORM 施術年月西暦化.
003560************
003570*           *
003580* 主処理     *
003590*           *
003600************
003601*
003602     PERFORM 年齢判定.
003603*
003620************
003630*           *
003640* 終了処理   *
003650*           *
003660************
003670     PERFORM 終了処理.
003690     EXIT PROGRAM.
003700*
003710*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003720*================================================================*
003730 初期化 SECTION.
003740*
003741     PERFORM ファイルオープン.
003742* 念のため初期化
003750     MOVE ZERO TO 連年齢２−保険年齢.
003751     MOVE ZERO TO 連年齢２−乳幼児判定.
003752     MOVE ZERO TO 連年齢２−老人判定.
003760* 連結項目の待避
003790     MOVE 連年齢２−和暦    TO 患者和暦Ｗ.
003800     MOVE 連年齢２−年      TO 患者年Ｗ.
003801     MOVE 連年齢２−月      TO 患者月Ｗ.
003810     MOVE 連年齢２−日      TO 患者日Ｗ.
003844*
003845*    /* 現在日付取得 */
003846     ACCEPT 計算機日付 FROM DATE.
003847*    /* 1980〜2079年の間で設定 */
003848     IF 計算機年 > 80
003849         MOVE 19 TO 計算機世紀
003850     ELSE
003851         MOVE 20 TO 計算機世紀
003852     END-IF.
003860*
003861     MOVE 計算機西暦Ｒ  TO  現在西暦年月日Ｗ.
003862*
003874*================================================================*
003884 ファイルオープン SECTION.
003894*
003904     OPEN INPUT 元号マスタ.
003914         MOVE NC"元号マスタ" TO ファイル名.
003924         PERFORM オープンチェック.
003934*================================================================*
003944 オープンチェック SECTION.
003954*
003964     IF 状態キー  NOT =  "00"
003974         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
003984         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003994         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004004                                                 UPON CONS
004005*-----------------------------------------*
004006         CALL "actcshm"  WITH C LINKAGE
004007*-----------------------------------------*
004014         ACCEPT  キー入力 FROM CONS
004024         PERFORM ファイル閉鎖
004034         MOVE 99 TO PROGRAM-STATUS
004044         EXIT PROGRAM.
004054*================================================================*
004064 ファイル閉鎖 SECTION.
004074*
004084     CLOSE 元号マスタ.
004094*================================================================*
004104 終了処理 SECTION.
004114*
004124     PERFORM ファイル閉鎖.
004134*================================================================*
004135*================================================================*
004136 施術年月西暦化 SECTION.
004137**
004138     MOVE 連年齢２−施術和暦 TO 元−元号区分.
004139     READ 元号マスタ
004140     NOT INVALID KEY
004141         COMPUTE 施術西暦年Ｗ = 元−開始西暦年 + ( 連年齢２−施術年 - 1 )
004142     END-READ.
004143*
004144     MOVE 連年齢２−施術月 TO  施術西暦月Ｗ.
004145*
004146*================================================================*
004210*================================================================*
006533*================================================================*
006543 年齢判定 SECTION.
006553*
006554* 平成14/10以降は７０以上と３歳未満の判定
006555* 平成20/4 以降は７０以上と７５以上と義務教育就学前の判定
006556*
006557     IF 連年齢２−施術和暦年月  >= 41410
006558        IF 連年齢２−施術和暦年月  >= 42004
006559           PERFORM 年齢計算２
006560        ELSE
006564           PERFORM 年齢計算
006565        END-IF
006566     END-IF.
006567*
006573*================================================================*
006574*================================================================*
006575 年齢計算 SECTION.
006576*-------------------------------------------------------------* 
006577*  施術年月から年齢を算出（保険年齢）
006578*
006579*  連年齢２−乳幼児判定 → 1:３歳未満
006580*  連年齢２−老人判定　 → 1:70〜74で27でない人 , 2:27老人
006581*-------------------------------------------------------------* 
006582     MOVE ZERO TO 患者年齢Ｗ.
006583*
006584     IF ( 患者和暦Ｗ NOT = ZERO ) AND
006585        ( 患者年Ｗ   NOT = ZERO ) AND
006586        ( 患者月Ｗ   NOT = ZERO ) AND
006587        ( 患者日Ｗ   NOT = ZERO )
006588**
006589         MOVE 患者和暦Ｗ TO 元−元号区分
006590         READ 元号マスタ
006591         NOT INVALID KEY
006592              COMPUTE 患者西暦年Ｗ = 元−開始西暦年 + ( 患者年Ｗ - 1 )
006593              COMPUTE 患者年齢Ｗ   = 施術西暦年Ｗ - 患者西暦年Ｗ
006594*
006595*             / (1日生まれは先月生まれ扱い) /
006596              IF 施術西暦月Ｗ <= 患者月Ｗ
006597                 IF 施術西暦月Ｗ = 患者月Ｗ
006598                    IF 患者日Ｗ NOT = 1
006599                       COMPUTE 患者年齢Ｗ = 患者年齢Ｗ - 1
006600                    END-IF
006601                 ELSE
006602                    COMPUTE 患者年齢Ｗ = 患者年齢Ｗ - 1
006603                 END-IF
006604              END-IF
006605*
006606         END-READ
006607*
006608         IF 患者年齢Ｗ < ZERO
006609            MOVE ZERO TO 患者年齢Ｗ
006610         END-IF
006611*
006612*      / 年齢セット /
006613         MOVE 患者年齢Ｗ  TO 連年齢２−保険年齢
006614*
006615*      / 乳幼児判定
006616         IF 患者年齢Ｗ < 3
006617            MOVE 1 TO 連年齢２−乳幼児判定
006618         END-IF
006619*
006620*      / 老人判定
006621* 昭和７年１０月１日(1932/10/01)以降生まれは、７５才になるまで２７番号もらえない。
006622         IF 患者年齢Ｗ >= 70
006626            MOVE 患者西暦年Ｗ  TO 患者西暦年ＷＷ
006627            MOVE 患者月Ｗ      TO 患者西暦月ＷＷ
006628            MOVE 患者日Ｗ      TO 患者西暦日ＷＷ
006631            IF 患者西暦年月日Ｗ >= 19321001
006632*       / 27でないけど老人扱い /
006633               MOVE 1 TO 連年齢２−老人判定
006634               IF 患者年齢Ｗ >= 75
006635                  MOVE 2 TO 連年齢２−老人判定
006636               END-IF
006637            ELSE
006638*       / 27老人 /
006639               MOVE 2 TO 連年齢２−老人判定
006640            END-IF
006641         END-IF
006642*
006643     END-IF.
006644*
006645*================================================================*
006655*================================================================*
006665 年齢計算２ SECTION.
006675*-------------------------------------------------------------* 
006685*  施術年月から年齢を算出（保険年齢）
006695*
006705*  連年齢２−乳幼児判定 → 1:義務教育就学前
006706*  連年齢２−老人判定　 → 1:70〜74高齢者   , 2:27老人(使用なし)
006715*                          3:75〜後期高齢者 , 4:今月75歳 後期高齢者(1日生まれ除く）
006725*-------------------------------------------------------------* 
006726     MOVE ZERO  TO 患者年齢Ｗ.
006727     MOVE ZERO  TO 当月含む年齢Ｗ.
006728     MOVE SPACE TO 当月誕生日フラグ.
006730*
006731*--------------------------------------------*
006732* 就学前西暦月Ｗは３月までなので 3 セット
006733     MOVE ZERO TO 就学前西暦年Ｗ.
006735     MOVE 3    TO 就学前西暦月Ｗ.
006736*--------------------------------------------*
006739*
006745*
006755     IF ( 患者和暦Ｗ NOT = ZERO ) AND
006765        ( 患者年Ｗ   NOT = ZERO ) AND
006775        ( 患者月Ｗ   NOT = ZERO ) AND
006785        ( 患者日Ｗ   NOT = ZERO )
006795**
006805         MOVE 患者和暦Ｗ TO 元−元号区分
006815         READ 元号マスタ
006825         NOT INVALID KEY
006835              COMPUTE 患者西暦年Ｗ = 元−開始西暦年 + ( 患者年Ｗ - 1 )
006845              COMPUTE 患者年齢Ｗ   = 施術西暦年Ｗ - 患者西暦年Ｗ
006846              MOVE 患者年齢Ｗ TO 当月含む年齢Ｗ
006855*
006865*             / (1日生まれは先月生まれ扱い) /
006875              IF 施術西暦月Ｗ <= 患者月Ｗ
006885                 IF 施術西暦月Ｗ = 患者月Ｗ
006895                    IF 患者日Ｗ NOT = 1
006905                       COMPUTE 患者年齢Ｗ = 患者年齢Ｗ - 1
006906                       MOVE "YES" TO 当月誕生日フラグ
006915                    END-IF
006925                 ELSE
006935                    COMPUTE 患者年齢Ｗ = 患者年齢Ｗ - 1
006936                    MOVE 患者年齢Ｗ TO 当月含む年齢Ｗ
006945                 END-IF
006955              END-IF
006965*
006975         END-READ
006985*
006995         IF 患者年齢Ｗ < ZERO
007005            MOVE ZERO TO 患者年齢Ｗ
007015         END-IF
007025*
007035*      / 年齢セット /
007045         MOVE 患者年齢Ｗ  TO 連年齢２−保険年齢
007055*
007056*---------------------------------------------------------------------*
007076*      / 義務教育就学前の判定
007077         IF 患者年齢Ｗ < 6
007085            MOVE 1 TO 連年齢２−乳幼児判定
007086         ELSE
007088            IF 患者年齢Ｗ = 6
007090*              / 4月1日生まれは、先月生まれ扱い /
007091               MOVE 患者月Ｗ  TO 比較患者月Ｗ
007092               MOVE 患者日Ｗ  TO 比較患者日Ｗ
007094               IF 比較患者月日Ｗ <= "0401"
007095                  COMPUTE 就学前西暦年Ｗ = 患者西暦年Ｗ + 6
007096               ELSE
007097                  COMPUTE 就学前西暦年Ｗ = 患者西暦年Ｗ + 7
007098               END-IF
007099*
007100               IF 施術西暦年月Ｗ <= 就学前西暦年月Ｗ
007101                  MOVE 1 TO 連年齢２−乳幼児判定
007102               END-IF
007105            END-IF
007108         END-IF
007109*---------------------------------------------------------------------*
007112*
007113*
007115*---------------------------------------------------------------------*
007116*
007117*      / 老人判定
007125* 昭和７年１０月１日(1932/10/01)以降生まれは、７５才になるまで後期高齢者にならない。
007126*
007127* 連年齢２−老人判定　 → 1:70〜74高齢者
007128*                         3:75〜後期高齢者 , 4:今月75歳 後期高齢者
007129*
007135         IF 患者年齢Ｗ >= 70
007145            MOVE 患者西暦年Ｗ  TO 患者西暦年ＷＷ
007155            MOVE 患者月Ｗ      TO 患者西暦月ＷＷ
007165            MOVE 患者日Ｗ      TO 患者西暦日ＷＷ
007175            IF 患者西暦年月日Ｗ >= 19321001
007185*              / 高齢者 /
007195               MOVE 1 TO 連年齢２−老人判定
007197*              / 後期高齢者 /
007198               IF 当月含む年齢Ｗ >= 75
007199                  IF ( 当月含む年齢Ｗ = 75 ) AND ( 当月誕生日フラグ = "YES" )
007200                     MOVE 4 TO 連年齢２−老人判定
007201                  ELSE
007202                     MOVE 3 TO 連年齢２−老人判定
007203                  END-IF
007204               END-IF
007235            ELSE
007245*              / 後期高齢者 /
007255               MOVE 3 TO 連年齢２−老人判定
007265            END-IF
007275         END-IF
007285*
007295     END-IF.
007296*---------------------------------------------------------------------*
007305*
007315*================================================================*
010106*================================================================*
010200******************************************************************
010210 END PROGRAM NENREI2.
010220******************************************************************
