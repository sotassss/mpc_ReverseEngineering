000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YHN583.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*       日骨用       申請書データ送付書 【印刷】
000100*       MED = YHN580 YHN583P 
000110*----------------------------------------------------------------*
000120 DATE-WRITTEN.           2014-09-04
000130 DATE-COMPILED.          2014-09-04
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000610     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000620                             ORGANIZATION             IS  INDEXED
000630                             ACCESS MODE              IS  DYNAMIC
000640                             RECORD KEY               IS  元−元号区分
000650                             FILE STATUS              IS  状態キー
000660                             LOCK        MODE         IS  AUTOMATIC.
000730     SELECT  施術所情報マスタ ASSIGN     TO        SEJOHOL
000740                             ORGANIZATION             IS  INDEXED
000750                             ACCESS MODE              IS  DYNAMIC
000760                             RECORD KEY               IS  施情−施術所番号
000760                             FILE STATUS              IS  状態キー
000770                             LOCK        MODE         IS  AUTOMATIC.
000420     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000430                             ORGANIZATION             IS  INDEXED
000440                             ACCESS MODE              IS  DYNAMIC
000450                             RECORD KEY               IS 受−施術和暦年月
000460                                                          受−患者コード
000470                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000480                                                          受−患者カナ
000490                                                          受−患者コード
000500                             ALTERNATE RECORD KEY     IS  受−患者コード
000510                                                         受−施術和暦年月
000520                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000530                                                          受−保険種別
000540                                                          受−保険者番号
000550                                                          受−患者コード
000560                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000570                                                          受−公費種別
000580                                                     受−費用負担者番号
000590                                                          受−患者コード
000600                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000610                                                          受−助成種別
000620                                                  受−費用負担者番号助成
000630                                                          受−患者コード
000640                             ALTERNATE RECORD KEY  IS 受−請求和暦年月
000650                                                      受−施術和暦年月
000660                                                      受−患者コード
000670                             FILE STATUS              IS  状態キー
000680                             LOCK        MODE         IS  AUTOMATIC.
           SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
                                   ORGANIZATION             IS  INDEXED
                                   ACCESS MODE              IS  DYNAMIC
                                   RECORD KEY               IS  レセ−施術和暦年月
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−患者コード
                                                                レセ−施術和暦年月
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−施術和暦年月
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−レセ種別
                                                                レセ−請求保険者番号
                                                                レセ−患者コード
                                                                レセ−施術和暦年月
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−請求保険者番号
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                                                レセ−施術和暦年月
                                   FILE STATUS              IS  状態キー
                                   LOCK        MODE         IS  AUTOMATIC.
000780     SELECT  印刷ファイル    ASSIGN      TO     GS-PRTF001
000790                             SYMBOLIC    DESTINATION  IS "PRT"
000800                             FORMAT                   IS  定義体名Ｐ
000810                             GROUP                    IS  項目群名Ｐ
000820                             PROCESSING  MODE         IS  処理種別Ｐ
000830                             UNIT        CONTROL      IS  拡張制御Ｐ
000840                             FILE        STATUS       IS  通知情報Ｐ.
000850******************************************************************
000860*                      DATA DIVISION                             *
000870******************************************************************
000880 DATA                    DIVISION.
000890 FILE                    SECTION.
000950*                           ［ＲＬ＝  １２８］
000960 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
000970     COPY GENGOU         OF  XFDLIB  JOINING    元   AS  PREFIX.
001010*                           ［ＲＬ＝  ６４０］
001020 FD  施術所情報マスタ    BLOCK   CONTAINS   1   RECORDS.
001030     COPY SEJOHO         OF  XFDLIB  JOINING    施情 AS  PREFIX.
001120*                           ［ＲＬ＝  ３２０］
001130 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001140     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
      *
       FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS GLOBAL.
           COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
001500*
001040 FD  印刷ファイル.
001050     COPY YHN583P        OF  XMDLIB.
001060******************************************************************
001070*                WORKING-STORAGE SECTION                         *
001080******************************************************************
001090 WORKING-STORAGE         SECTION.
001640 01 キー入力                           PIC X    VALUE SPACE.
001650 01 状態キー                           PIC X(2) VALUE SPACE.
001160 01 ファイル名Ｗ                       PIC N(2) VALUE SPACE.
001180 01 実行キーＷ                         PIC X(4) VALUE SPACE.
001660 01 終了フラグ                         PIC X(3) VALUE SPACE.
001210 01 オープンフラグ                     PIC X(3) VALUE SPACE.
001560*
001830 01 請求和暦年月ＷＲ.
001840    03 請求和暦ＷＲ                    PIC 9    VALUE ZERO.
001850    03 請求年ＷＲ                      PIC 9(2) VALUE ZERO.
001860    03 請求月ＷＲ                      PIC 9(2) VALUE ZERO.
001320 01 元号名称Ｗ                         PIC N(2) VALUE SPACE.
       01 システム日付Ｗ.
          03 西暦Ｗ                          PIC X(2) VALUE SPACE.
          03 年月日Ｗ                        PIC X(6) VALUE SPACE.
       01 時間ＷＰ.
          03 時分秒Ｗ                        PIC X(6) VALUE SPACE.
          03 秒以下Ｗ                        PIC X(2) VALUE SPACE.
001800** 合計用
       01 集計Ｗ.
          03 件数Ｗ                          PIC 9(4) VALUE ZERO.
          03 請求金額Ｗ                      PIC 9(9) VALUE ZERO.
002433* エラーメッセージ用
002434 01 エラーメッセージＷ.
002435    03 エラー患者コードＷ              PIC X(7)  VALUE SPACE.
002436    03 エラー区切りＷ                  PIC X(1)  VALUE SPACE.
002437    03 エラー保険種別Ｗ                PIC X(2)  VALUE SPACE.
002438    03 FILLER                          PIC X(10) VALUE SPACE.
001530*
001540 01 印刷制御.
001550     03 定義体名Ｐ                     PIC X(8) VALUE SPACE.
001560     03 項目群名Ｐ                     PIC X(8) VALUE SPACE.
001570     03 処理種別Ｐ                     PIC X(2) VALUE SPACE.
001580     03 拡張制御Ｐ.
001590         05 端末制御Ｐ.
001600             07 移動方向Ｐ             PIC X(1).
001610             07 移動行数Ｐ             PIC 9(3).
001620         05 詳細制御Ｐ                 PIC X(2).
001630     03 通知情報Ｐ                     PIC X(2) VALUE SPACE.
001640     03 ユニット名Ｐ                   PIC X(8) VALUE SPACE.
001650 01 計算機西暦年Ｗ                     PIC 9(2).
001660* 日付ＷＯＲＫ
001670 01 和暦終了年Ｗ                       PIC 9(4).
001680 01 計算機和暦年Ｗ                     PIC 9(2).
001690 01 計算機西暦.
001700    03 計算機西暦年                    PIC 9(4).
001710    03 計算機西暦月日                  PIC 9(4).
001720 01 計算機西暦Ｒ REDEFINES 計算機西暦.
001730    03 計算機世紀                      PIC 9(2).
001740    03 計算機日付                      PIC 9(6).
001750    03 計算機日付Ｒ REDEFINES 計算機日付.
001760       05 計算機年月                   PIC 9(4).
001770       05 計算機年月Ｒ REDEFINES 計算機年月.
001780         07 計算機年                   PIC 9(2).
001790         07 計算機月                   PIC 9(2).
001800       05 計算機日                     PIC 9(2).
001810*
003340******************************************************************
003350*                          連結項目                              *
003360******************************************************************
003370*
003370**********************
003370* メッセージ表示キー *
003370**********************
003370*
003370 01 連メ−キー IS EXTERNAL.
003370    03  連メ−メッセージ                 PIC N(20).
003370*
002634 01 連メ３−キー IS EXTERNAL.
002635    03  連メ３−メッセージ             PIC N(20).
002636    03  連メ３−メッセージ１           PIC X(20).
002636*
002230****************
002240* 画面入力情報 *
002250****************
002260 01 連入−画面情報ＹＨＮ５８０ IS EXTERNAL.
002270    03 連入−請求和暦年月.
002280       05 連入−請求和暦               PIC 9.
002290       05 連入−請求年月.
002300         07 連入−請求年               PIC 9(2).
002310         07 連入−請求月               PIC 9(2).
          03 連入−プレビュー区分             PIC 9.
002231*
000540************************************
000550* プリンタファイル作成用           *
000560************************************
000570 01 Ｈ連ＰＲＴＦ−作成データ IS EXTERNAL.
000580     03 Ｈ連ＰＲＴＦ−ファイル名           PIC X(8).
000590     03 Ｈ連ＰＲＴＦ−プレビュー区分       PIC 9.
000600     03 Ｈ連ＰＲＴＦ−帳票プログラム名     PIC X(8).
000610     03 Ｈ連ＰＲＴＦ−オーバレイ名         PIC X(8).
003890******************************************************************
003900*                      PROCEDURE  DIVISION                       *
003910******************************************************************
003920 PROCEDURE               DIVISION.
003930************
003940*           *
003950* 初期処理   *
003960*           *
003970************
002560     MOVE SPACE TO オープンフラグ.
002570     PERFORM プリンタファイル作成.
003980     PERFORM 初期化.
001790************
001800*           *
001810* 主処理     *
001820*           *
001830************
004550     PERFORM 集計処理.
004550     PERFORM 印刷処理.
004620************
004630*           *
004640* 終了処理   *
004650*           *
004660************
004670     PERFORM 終了処理.
004680     EXIT PROGRAM.
004690*
004700*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002860*================================================================*
002870 プリンタファイル作成 SECTION.
002880*================================================================*
002890*   / 初期化 /
002900     MOVE SPACE TO Ｈ連ＰＲＴＦ−作成データ.
002910     INITIALIZE Ｈ連ＰＲＴＦ−作成データ.
002920*
002930*
002940*--↓↓ 変更箇所 ↓↓--------------------------------------*
002970*   使用するプリンタファイル名セット
      *   印字調整なし
002971     MOVE "PRTF001"             TO Ｈ連ＰＲＴＦ−ファイル名.
002972*
002973*   使用する帳票プログラム名セット
002974     MOVE "YHN583"              TO Ｈ連ＰＲＴＦ−帳票プログラム名.
002975*
002976*--↑↑-----------------------------------------------------*
002980*
002990*   / プレビュー区分セット /
003000     MOVE 連入−プレビュー区分 TO Ｈ連ＰＲＴＦ−プレビュー区分.
003010*
003020     CALL   "CRTPRTF".
003030     CANCEL "CRTPRTF".
003040*
004710*================================================================*
004720 初期化 SECTION.
004730*
004770     OPEN INPUT 元号マスタ.
004780             MOVE NC"元号" TO ファイル名Ｗ.
004790             PERFORM オープンチェック.
004830     OPEN INPUT 施術所情報マスタ.
004840             MOVE NC"施情" TO ファイル名Ｗ.
004850             PERFORM オープンチェック.
003070     OPEN INPUT 受診者情報Ｆ.
003080         MOVE NC"受診者情報Ｆ" TO ファイル名Ｗ.
003090         PERFORM オープンチェック.
003250     OPEN INPUT レセプトＦ.
003260         MOVE NC"レセプトＦ" TO ファイル名Ｗ.
003270         PERFORM オープンチェック.
002960* 連結項目の待避
002970     MOVE 連入−請求和暦      TO 請求和暦ＷＲ.
002980     MOVE 連入−請求年        TO 請求年ＷＲ.
002990     MOVE 連入−請求月        TO 請求月ＷＲ.
004900*
005080*================================================================*
005090 オープンチェック SECTION.
005100*
005110     IF 状態キー  NOT =  "00"
005120         DISPLAY ファイル名Ｗ NC"Ｆオープンエラー" UPON CONS
005130         DISPLAY NC"状態キー：" 状態キー           UPON CONS
005140         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
005150                                                   UPON CONS
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
005160         ACCEPT  キー入力 FROM CONS
005170         PERFORM ファイル閉鎖
005180         EXIT PROGRAM.
006150*================================================================*
006770 ファイル閉鎖 SECTION.
006780*
003570     IF ( オープンフラグ = "YES" )
003580         CLOSE 印刷ファイル
003630     END-IF.
003640*
006790     CLOSE  元号マスタ   施術所情報マスタ.
006810*================================================================*
006820 終了処理 SECTION.
006830*
006840     PERFORM ファイル閉鎖.
003440*================================================================*
003450 集計処理 SECTION.
003460*
005170     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
005180     MOVE 請求年ＷＲ    TO レセ−請求年.  
005190     MOVE 請求月ＷＲ    TO レセ−請求月.  
013730     MOVE ZERO          TO レセ−レセ種別.
005200     MOVE ZERO          TO レセ−施術和暦.
005210     MOVE ZERO          TO レセ−施術年.  
005220     MOVE ZERO          TO レセ−施術月.  
005230     MOVE ZERO          TO レセ−患者番号.
005240     MOVE SPACE         TO レセ−枝番.    
013770     START レセプトＦ   KEY IS >= レセ−請求和暦年月
000230                                  レセ−施術和暦年月
000240                                  レセ−患者コード
000250                                  レセ−レセ種別
003620     END-START.
003630     IF 状態キー = "00"
003640         MOVE SPACE  TO 終了フラグ
003650         PERFORM レセプトＦ読込
003660         PERFORM UNTIL ( 終了フラグ = "YES" ) OR
005330                       ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
005340                       ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
005350                       ( レセ−請求月   NOT = 請求月ＷＲ   )
003736             PERFORM データチェック
003730*
003731** 労災・自賠責・自由・生保単独は対象外
003790             IF  レセ−レセ種別 = 4 OR 5 OR 6 OR 7 OR 8
003733                CONTINUE
003734             ELSE
003737                IF  実行キーＷ = "YES"
003738                    PERFORM 合計処理
003739                END-IF
003741             END-IF
003850             PERFORM レセプトＦ読込
003860         END-PERFORM
003870     END-IF.
003980*
004046*================================================================*
004050 データチェック SECTION.
004060*
004070     MOVE SPACE  TO 実行キーＷ.
005300     IF ( レセ−レセ印刷対象区分 NOT = 1 )
005310        MOVE "YES"  TO 実行キーＷ
005320     END-IF.
019520* *****************************************************************
019530* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
019540* *****************************************************************
005360     IF 実行キーＷ  = "YES"
005370*      (再度、実行キーＷ SPACE)
005380        MOVE SPACE  TO 実行キーＷ
019640        IF レセ−請求対象区分 NOT = ZERO
004090           MOVE レセ−施術和暦  TO 受−施術和暦
004100           MOVE レセ−施術年    TO 受−施術年
004110           MOVE レセ−施術月    TO 受−施術月
004120           MOVE レセ−患者番号  TO 受−患者番号
004130           MOVE レセ−枝番      TO 受−枝番
                 READ 受診者情報Ｆ
                 NOT INVALID KEY
019880              MOVE "YES"  TO 実行キーＷ
                 END-READ
019900        ELSE
019910           MOVE SPACE  TO 実行キーＷ
              END-IF
019950     END-IF.
004530*
004630*================================================================*
004640 レセプトＦ読込 SECTION.
004650*
004660     READ レセプトＦ NEXT
004670     AT END
004680         MOVE "YES" TO 終了フラグ
004690     END-READ.
004700*
004780*================================================================*
004790 合計処理 SECTION.
004800*
           IF レセ−レセ種別 = 3
               IF  レセ−助成請求金額 NOT = ZERO
                   COMPUTE 請求金額Ｗ = 請求金額Ｗ + レセ−助成請求金額
                   COMPUTE 件数Ｗ     = 件数Ｗ     + 1
               END-IF
005524     ELSE
               COMPUTE 請求金額Ｗ = 請求金額Ｗ + レセ−請求金額
               COMPUTE 件数Ｗ     = 件数Ｗ     + 1
005541*
005545     END-IF.
004854*
006840*================================================================*
010770 印刷処理 SECTION.
010780*
010780     MOVE SPACE TO YHN583P.
010780     INITIALIZE YHN583P.
      *
           IF 件数Ｗ = ZERO
               MOVE NC"　　データがありません。" TO 連メ−メッセージ
006020         CALL   "MSG001"
006030         CANCEL "MSG001"
           ELSE
               ACCEPT 年月日Ｗ FROM DATE
      *    /* 1980〜2079年の間で設定 */
               IF 年月日Ｗ(1:2) > 80
                   MOVE 19 TO 西暦Ｗ
               ELSE
                   MOVE 20 TO 西暦Ｗ
               END-IF
               ACCEPT 時間ＷＰ FROM TIME
      *
               STRING システム日付Ｗ(1:4)    DELIMITED BY SIZE
                      "/"                    DELIMITED BY SIZE
                      システム日付Ｗ(5:2)    DELIMITED BY SIZE
                      "/"                    DELIMITED BY SIZE
                      システム日付Ｗ(7:2)    DELIMITED BY SIZE
                      " "                    DELIMITED BY SIZE
                      時間ＷＰ(1:2)          DELIMITED BY SIZE
                      ":"                    DELIMITED BY SIZE
                      時間ＷＰ(3:2)          DELIMITED BY SIZE
                      ":"                    DELIMITED BY SIZE
                      時間ＷＰ(5:2)          DELIMITED BY SIZE
                 INTO 印刷日付
               END-STRING
      *
027710         MOVE ZERO  TO 施情−施術所番号
027720         READ 施術所情報マスタ
027730         INVALID KEY
027740             CONTINUE
027750         NOT INVALID KEY
022640             MOVE 施情−郵便番号１        TO 郵便番号１
022650             MOVE 施情−郵便番号２        TO 郵便番号２
022700             MOVE 施情−住所１            TO 住所１
022710             MOVE 施情−住所２            TO 住所２
022730             MOVE 施情−電話番号          TO 電話番号
027680             MOVE 施情−接骨師会会員番号  TO 会員番号
027940             MOVE 施情−代表者名          TO 代表者名
022680             MOVE 施情−接骨院名          TO 施術所名 施術所名２
                   STRING 施情−代表者名    DELIMITED BY SPACE
                          "　様"            DELIMITED BY SIZE
                     INTO 代表者名２
                   END-STRING
               END-READ
      */元号修正/↓↓↓20190514
               MOVE 連入−請求和暦     TO 元−元号区分
037380         READ 元号マスタ
037390         NOT INVALID KEY
037400             MOVE 元−元号名称   TO 提出和暦 提出和暦２ 提出和暦３
037410         END-READ
      */元号修正/↑↑↑20190514
      *         MOVE 連入−請求年       TO 提出年
      *         MOVE 連入−請求月       TO 提出月
               MOVE 件数Ｗ             TO 提出件数
               MOVE 請求金額Ｗ         TO 合計金額
      *
               PERFORM 明細印刷処理
           END-IF.
010780*
011820*================================================================*
011820 明細印刷処理 SECTION.
011790*
006050     IF ( オープンフラグ NOT = "YES" )
006060        MOVE "YES" TO オープンフラグ
006070        OPEN I-O  印刷ファイル
006080        PERFORM エラー処理Ｐ
006090     END-IF.
011800     MOVE "YHN583P"  TO  定義体名Ｐ.
011810     MOVE "GRP001"   TO  項目群名Ｐ.
011820     WRITE YHN583P.
011820     PERFORM エラー処理Ｐ.
014110*================================================================*
014110 エラー処理Ｐ SECTION.
014110*
014110     IF 通知情報Ｐ NOT = "00"
014110         DISPLAY NC"帳票エラー"              UPON CONS
014110         DISPLAY NC"項目群名Ｐ：" 項目群名Ｐ UPON CONS
014110         DISPLAY NC"通知情報Ｐ：" 通知情報Ｐ UPON CONS
014110         DISPLAY NC"拡張制御Ｐ：" 拡張制御Ｐ UPON CONS
014110         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
014110                                             UPON CONS
004380*-----------------------------------------*
004390         CALL "actcshm"  WITH C LINKAGE
004400*-----------------------------------------*
014110         ACCEPT  キー入力 FROM CONS
014110         PERFORM ファイル閉鎖
014110         MOVE 99 TO PROGRAM-STATUS
014110         EXIT PROGRAM
014110     END-IF.
001370*================================================================*
014300******************************************************************
014310 END PROGRAM YHN583.
014320******************************************************************
