000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YHP722.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*         カルテ裏【印刷】 ホープ接骨師会
000100*         MED = YHP720 YHP722P
000110*----------------------------------------------------------------*
000120 DATE-WRITTEN.           2012-08-07
000130 DATE-COMPILED.          2012-08-07
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000320     SELECT  制御情報マスタ  ASSIGN      TO        SEIGYOL
000330                             ORGANIZATION             IS  INDEXED
000340                             ACCESS MODE              IS  DYNAMIC
000350                             RECORD KEY               IS  制−制御区分
000360                             FILE STATUS              IS  状態キー
000370                             LOCK        MODE         IS  AUTOMATIC.
000590     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000600                             ORGANIZATION             IS  INDEXED
000610                             ACCESS MODE              IS  DYNAMIC
000620                             RECORD KEY               IS 受−施術和暦年月
000630                                                          受−患者コード
000640                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000650                                                          受−患者カナ
000660                                                          受−患者コード
000670                             ALTERNATE RECORD KEY     IS  受−患者コード
000680                                                         受−施術和暦年月
000690                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000700                                                          受−保険種別
000710                                                          受−保険者番号
000720                                                          受−患者コード
000730                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000740                                                          受−公費種別
000750                                                     受−費用負担者番号
000760                                                          受−患者コード
000770                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000780                                                          受−助成種別
000790                                                  受−費用負担者番号助成
000800                                                          受−患者コード
000810                             ALTERNATE RECORD KEY  IS 受−請求和暦年月
000820                                                      受−施術和暦年月
000830                                                      受−患者コード
000840                             FILE STATUS              IS  状態キー
000850                             LOCK        MODE         IS  AUTOMATIC.
000860     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W7211L.DAT"
000870                             ORGANIZATION             IS  INDEXED
000880                             ACCESS                   IS  DYNAMIC
000890                             RECORD      KEY          IS  作１−施術和暦年月日
000900                                                          作１−患者コード
000910                             ALTERNATE RECORD KEY     IS  作１−施術和暦年月日
000920                                                          作１−患者カナ
000930                                                          作１−患者コード
000940                             ALTERNATE RECORD KEY     IS  作１−患者コード
000950                                                          作１−施術和暦年月日
000960                             ALTERNATE RECORD KEY     IS  作１−患者カナ
000970                                                          作１−患者コード
000980                                                          作１−施術和暦年月日
000990                             FILE        STATUS       IS  状態キー
001000                             LOCK        MODE         IS  AUTOMATIC.
001010     SELECT  作業ファイル２  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W7212L.DAT"
001020                             ORGANIZATION             IS  INDEXED
001030                             ACCESS                   IS  DYNAMIC
001040                             RECORD      KEY          IS  作２−施術和暦年月
001050                                                          作２−患者コード
001060                             FILE        STATUS       IS  状態キー
001070                             LOCK        MODE         IS  AUTOMATIC.
001080     SELECT  印刷ファイル    ASSIGN      TO     GS-PRTF002
001090                             SYMBOLIC    DESTINATION  IS "PRT"
001100                             FORMAT                   IS  定義体名Ｐ
001110                             GROUP                    IS  項目群名Ｐ
001120                             PROCESSING  MODE         IS  処理種別Ｐ
001130                             UNIT        CONTROL      IS  拡張制御Ｐ
001140                             FILE        STATUS       IS  通知情報Ｐ.
001150******************************************************************
001160*                      DATA DIVISION                             *
001170******************************************************************
001180 DATA                    DIVISION.
001190 FILE                    SECTION.
001230*                           ［ＲＬ＝  ２５６］
001240 FD  制御情報マスタ          BLOCK   CONTAINS   1   RECORDS.
001250     COPY SEIGYO          OF  XFDLIB  JOINING   制   AS  PREFIX.
001320*                           ［ＲＬ＝  ３２０］
001330 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001340     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
001350*                         ［ＲＬ＝  ３２０］
001360 FD  作業ファイル１ RECORD  CONTAINS 320 CHARACTERS.
001370 01 作１−レコード.
001380    03 作１−レコードキー.
001390       05 作１−施術和暦年月日.
                07 作１−施術和暦年月.
001400             09 作１−施術和暦             PIC 9.
001410             09 作１−施術年月.
001420                11 作１−施術年            PIC 9(2).
001430                11 作１−施術月            PIC 9(2).
001440          07 作１−施術日                  PIC 9(2).
001450       05 作１−患者コード.
001460          07 作１−患者番号                PIC 9(6).
001470          07 作１−枝番                    PIC X(1).
001480    03 作１−レコードデータ.
001490       05 作１−保険種別                   PIC 9(2).
001500       05 作１−公費種別                   PIC 9(2).
001510       05 作１−助成種別                   PIC 9(2).
001520       05 作１−保険者番号                 PIC X(10).
001530       05 作１−本人家族区分               PIC 9(1).
001540       05 作１−被保険者カナ               PIC X(50).
001550       05 作１−患者カナ                   PIC X(50).
001560       05 作１−患者氏名                   PIC X(50).
001570       05 作１−負担割合                   PIC 9(2).
001580       05 作１−料金.
001590          07 作１−初検金額                PIC 9(5).
001680          07 作１−整復金額                PIC 9(5).
001690          07 作１−後療金額                PIC 9(5).
001700          07 作１−罨法金額                PIC 9(5).
001710          07 作１−電療金額                PIC 9(5).
001720          07 作１−一部負担金              PIC 9(5).
001730          07 作１−コメント                PIC X(100).
001740*/明細行に費用額を印字0304
001750          07 作１−費用額                  PIC 9(6).
001760       05 FILLER                           PIC X(1).
001770*                         ［ＲＬ＝  １２８］
001780 FD  作業ファイル２ RECORD  CONTAINS 128 CHARACTERS.
001790 01 作２−レコード.
001800    03 作２−レコードキー.
001810       05 作２−施術和暦年月.
001820          07 作２−施術和暦                PIC 9.
001830          07 作２−施術年月.
001840             09 作２−施術年               PIC 9(2).
001850             09 作２−施術月               PIC 9(2).
001860       05 作２−患者コード.
001870          07 作２−患者番号                PIC 9(6).
001880          07 作２−枝番                    PIC X(1).
001890    03 作２−レコードデータ.
001900       05 作２−請求開始和暦年月.
001910          07 作２−開始和暦                PIC 9.
001920          07 作２−開始年月.
001930             09 作２−開始年               PIC 9(2).
001940             09 作２−開始月               PIC 9(2).
001950             09 作２−開始日               PIC 9(2).
001960       05 作２−請求終了和暦年月.
001970          07 作２−終了和暦                PIC 9.
001980          07 作２−終了年月.
001990             09 作２−終了年               PIC 9(2).
002000             09 作２−終了月               PIC 9(2).
002010             09 作２−終了日               PIC 9(2).
002020       05 作２−請求期間                   PIC 9(2).
002030       05 作２−施術回数                   PIC 9(2).
002040       05 作２−費用額                     PIC 9(6).
002050       05 作２−請求額                     PIC 9(6).
002060       05 作２−負担金                     PIC 9(5).
002070       05 作２−転帰                       OCCURS 4.
002080          07 作２−転帰区分                PIC N(1).
002090       05 作２−初検計                     PIC 9(5).
002100       05 作２−整復計                     PIC 9(5).
002110       05 作２−後療計                     PIC 9(5).
002120       05 作２−罨法計                     PIC 9(5).
002130       05 作２−電療計                     PIC 9(5).
002140       05 作２−負担計                     PIC 9(5).
             05 作２−相談料計                   PIC 9(4).
002150       05 FILLER                           PIC X(39).
002160*
002170 FD  印刷ファイル.
002180     COPY YHP722P        OF  XMDLIB.
002190*----------------------------------------------------------------*
002200******************************************************************
002210*                WORKING-STORAGE SECTION                         *
002220******************************************************************
002230 WORKING-STORAGE         SECTION.
002240 01 キー入力                           PIC X     VALUE SPACE.
002250 01 状態キー                           PIC X(2)  VALUE SPACE.
002260 01 終了フラグ                         PIC X(3)  VALUE SPACE.
002270 01 終了フラグ２                       PIC X(3)  VALUE SPACE.
002280 01 印刷フラグ                         PIC 9     VALUE ZERO.
002290 01 初検フラグ                         PIC X(3)  VALUE SPACE.
002300 01 ファイル名                         PIC N(6)  VALUE SPACE.
002310 01 レセプトＰＧＷ                     PIC X(8)  VALUE SPACE.
002320 01 前和暦Ｗ                           PIC 9     VALUE ZERO.
002330 01 カレント元号Ｗ                     PIC 9(1)  VALUE ZERO.
001210 01 オープンフラグ                     PIC X(3)   VALUE SPACE.
002340 01 部位ＣＮＴ                         PIC 9     VALUE ZERO.
002350 01 カウンタ                           PIC 9(2)  VALUE ZERO.
002360 01 カウンタ１                         PIC 9(2)  VALUE ZERO.
002370 01 患者番号Ｗ                         PIC 9(6)  VALUE ZERO.
002380 01 行カウンタ                         PIC 9(2) VALUE ZERO.
002390 01 最大行数                           PIC 9(2) VALUE ZERO.
002400 01 ヘッダ行数                         PIC 9(2) VALUE ZERO.
002410 01 移動行数Ｗ                         PIC 9(2) VALUE ZERO.
002420*/明細行に費用額を印字0304
002430 01 文字ＣＮＴ                         PIC 9(2) VALUE ZERO.
001363 01 全角空白                           PIC X(2)  VALUE X"8140".
001364 01 半角空白                           PIC X(2)  VALUE X"2020".
002440*
002450*/改ページ不具合対応011130
002460*/次のレコードを読んだ後に前のレコードのヘッダを印刷する為のワーク。
002470*/１ページちょうどで改ページして２ページ目が合計行のみの印刷の場合に必要
002480 01 ヘッダ項目退避用ワーク.
002490   03 患者コードＨＷ.
002500     05 患者番号ＨＷ                   PIC 9(6)  VALUE ZERO.
002510     05 枝番ＨＷ                       PIC X(1)  VALUE SPACE.
002520   03 患者氏名ＨＷ                     PIC X(50) VALUE SPACE.
002530   03 保険種別ＨＷ                     PIC 9(2)  VALUE ZERO.
002540   03 公費種別ＨＷ                     PIC 9(2)  VALUE ZERO.
002550   03 助成種別ＨＷ                     PIC 9(2)  VALUE ZERO.
002560   03 本人家族区分ＨＷ                 PIC 9(1)  VALUE ZERO.
002570   03 負担割合ＨＷ                     PIC 9(2)  VALUE ZERO.
002580**
002590 01 遅延フラグ                         PIC X(3) VALUE SPACE.
002600 01 遅延回数Ｗ                         PIC 9(4) VALUE ZERO.
002610 01 遅延ＣＮＴ                         PIC 9(5) VALUE ZERO.
002620**
002630 01 合計Ｗ.
002640    03 初検料計Ｗ                      PIC 9(5) VALUE ZERO.
002650    03 整復料計Ｗ                      PIC 9(5) VALUE ZERO.
002660    03 後療料計Ｗ                      PIC 9(5) VALUE ZERO.
002670    03 罨法料計Ｗ                      PIC 9(5) VALUE ZERO.
002680    03 電療料計Ｗ                      PIC 9(5) VALUE ZERO.
002690    03 負担金計Ｗ                      PIC 9(5) VALUE ZERO.
002700*
002710 01 料金Ｗ.
002720    03 初検料Ｗ                        PIC 9(5) VALUE ZERO.
002730    03 整復料Ｗ                        PIC 9(5) VALUE ZERO.
002740    03 後療料Ｗ                        PIC 9(5) VALUE ZERO.
002750    03 罨法料Ｗ                        PIC 9(5) VALUE ZERO.
002760    03 電療料Ｗ                        PIC 9(5) VALUE ZERO.
002770    03 負担金Ｗ                        PIC 9(5) VALUE ZERO.
002780****************
002790* 連結項目待避 *
002800****************
002810*    ************
002820*    * 印刷キー *
002830*    ************
002840 01 対象データＷＲ.
002850    03 施術和暦年月ＷＲ.
002860       05 施術和暦ＷＲ                  PIC 9(1)  VALUE ZERO.
002870       05 施術年ＷＲ                    PIC 9(2)  VALUE ZERO.
002880       05 施術月ＷＲ                    PIC 9(2)  VALUE ZERO.
002890    03 開始日ＷＲ                       PIC 9(2)  VALUE ZERO.
002900    03 終了日ＷＲ                       PIC 9(2)  VALUE ZERO.
002910    03 患者カナＷＲ                     PIC X(20) VALUE SPACE.
002920    03 患者コードＷ.
002930       05 患者番号Ｗ                    PIC 9(6)  VALUE ZERO.
002940       05 枝番Ｗ                        PIC X(1)  VALUE SPACE.
002950    03 印刷モードＦＷＲ                 PIC 9(1)  VALUE ZERO.
002960    03 印字位置ＣＮＴ                   PIC 9(2)  VALUE ZERO.
002970**************
002980* 受診者情報 *
002990**************
003000 01 受診者情報Ｗ.
003010    03 施術年月Ｗ.
003020       05 施術和暦Ｗ                   PIC 9(1)   VALUE ZERO.
003030       05 施術年Ｗ                     PIC 9(2)   VALUE ZERO.
003040       05 施術月Ｗ                     PIC 9(2)   VALUE ZERO.
003050    03 保険者番号Ｗ.
003060       05 印刷保険者番号Ｗ             PIC X(6)   VALUE SPACE.
003070       05 FILLER                       PIC X(4)   VALUE SPACE.
003080    03 退職保険者番号Ｗ.
003090       05 FILLER                       PIC X(2)   VALUE SPACE.
003100       05 退職印刷保険者番号Ｗ         PIC X(6)   VALUE SPACE.
003110       05 FILLER                       PIC X(2)   VALUE SPACE.
003120    03 市町村番号Ｗ.
003130       05 印刷市町村番号Ｗ             PIC X(8)   VALUE SPACE.
003140       05 FILLER                       PIC X(2)   VALUE SPACE.
003150    03 保険種別Ｗ                      PIC 9(2)   VALUE ZERO.
003160    03 公費種別Ｗ                      PIC 9(2)   VALUE ZERO.
003170    03 助成種別Ｗ                      PIC 9(2)   VALUE ZERO.
003180    03 患者情報Ｗ.
003190       05 患者カナＷ                   PIC X(50)  VALUE SPACE.
003200       05 患者氏名Ｗ                   PIC X(50)  VALUE SPACE.
003210 01 患者氏名ＷＰ.
003220    03 患者名ＷＰ                      OCCURS 10. 
003230       05 患者名Ｗ                     PIC N(1)   VALUE SPACE.
003240 01 コメントＷ.
003250    03 コメント１Ｗ                    PIC X(50) VALUE SPACE.
003260    03 コメント２Ｗ                    PIC X(50) VALUE SPACE.
002400 01 バーコード使用区分Ｗ               PIC 9 VALUE ZERO.
003270*******************************************************************
003280 01 印刷制御.
003290     03 定義体名Ｐ                     PIC X(8) VALUE SPACE.
003300     03 項目群名Ｐ                     PIC X(8) VALUE SPACE.
003310     03 処理種別Ｐ                     PIC X(2) VALUE SPACE.
003320     03 拡張制御Ｐ.
003330         05 端末制御Ｐ.
003340             07 移動方向Ｐ             PIC X(1) VALUE SPACE.
003350             07 移動行数Ｐ             PIC 9(3) VALUE ZERO.
003360         05 詳細制御Ｐ                 PIC X(2) VALUE SPACE.
003370     03 通知情報Ｐ                     PIC X(2) VALUE SPACE.
003380     03 ユニット名Ｐ                   PIC X(8) VALUE SPACE.
003550*
003560******************************************************************
003570*                          連結項目                              *
003580******************************************************************
003590**********************
003600* メッセージ表示キー *
003610**********************
003620 01 連メ−キー IS EXTERNAL.
003630    03  連メ−メッセージ                 PIC N(20).
003640*
003650************
003660* 印刷キー *
003670************
003680 01 連印−対象データＹＨＰ７２０ IS EXTERNAL.
003690    03 連印−施術和暦年月日.
003700       05 連印−施術和暦                  PIC 9(1).
003710       05 連印−施術年                    PIC 9(2).
003720       05 連印−施術月                    PIC 9(2).
003730    03 連印−患者コード.
003740       05 連印−患者番号                  PIC 9(6).
003750       05 連印−枝番                      PIC X(1).
003760    03 連印−氏名モード                   PIC 9(1).
003770    03 連印−バーコードモード             PIC 9(1).
003780    03 連印−合計モード                   PIC 9(1).
003790    03 連印−明細モード                   PIC 9(1).
003800    03 連印−コメントモード               PIC 9(1).
003810    03 連印−初検時相談支援               PIC 9(1).
003820    03 連印−読み順                       PIC X(4).
003840    03 連印−処理キー                     PIC X(4).
003860****************
003870* 画面入力情報 *
003880****************
003890 01 連入−入力データＹＨＰ７２０ IS EXTERNAL.
003900    03 連入−開始和暦年月日.
003910       05 連入−開始和暦                  PIC 9(1).
003920       05 連入−開始年                    PIC 9(2).
003930       05 連入−開始月                    PIC 9(2).
003940       05 連入−開始日                    PIC 9(2).
003950    03 連入−終了和暦年月日.
003960       05 連入−終了和暦                  PIC 9(1).
003970       05 連入−終了年                    PIC 9(2).
003980       05 連入−終了月                    PIC 9(2).
003990       05 連入−終了日                    PIC 9(2).
004010    03 連入−保険種別                     PIC 9(2).
004020    03 連入−本人家族区分                 PIC 9(1).
004030    03 連入−患者コード.
004040       05 連入−患者番号                  PIC 9(6).
004050       05 連入−枝番                      PIC X(1).
004060*
004070    03 連入−印刷条件                     PIC 9(2).
004080    03 連入−印刷モード                   PIC X(4).
      *
       01 連入−表示フラグＹＨＰ７２０ IS EXTERNAL.
          03 連入−プレビュー区分               PIC 9(1).
004580*
000540************************************
000550* プリンタファイル作成用           *
000560************************************
000570 01 Ｈ連ＰＲＴＦ−作成データ IS EXTERNAL.
000580     03 Ｈ連ＰＲＴＦ−ファイル名           PIC X(8).
000590     03 Ｈ連ＰＲＴＦ−プレビュー区分       PIC 9.
000600     03 Ｈ連ＰＲＴＦ−帳票プログラム名     PIC X(8).
000610     03 Ｈ連ＰＲＴＦ−オーバレイ名         PIC X(8).
000993************************************
000994* プリンタファイル作成特殊用       *
000995************************************
000996 01 Ｈ連特殊ＰＲＴＦ−作成データ IS EXTERNAL.
000997     03 Ｈ連特殊ＰＲＴＦ−用紙種類         PIC X(8).
005768*
004590******************************************************************
004600*                      PROCEDURE  DIVISION                       *
004610******************************************************************
004620 PROCEDURE               DIVISION.
004630************
004640*           *
004650* 初期処理   *
004660*           *
004670************
002570     PERFORM プリンタファイル作成.
004680     PERFORM 初期化.
004690     PERFORM 制御情報取得.
004700************
004710*           *
004720* 主処理     *
004730*           *
004740************
004750* 印刷
004760     MOVE "X" TO EDIT-MODE OF バーコード.
004810     IF ( 連印−明細モード = 1 ) OR ( 連印−コメントモード = 1 )
004820         PERFORM 印刷セット１
004830     ELSE
004840         IF 連印−合計モード = 1
004850             PERFORM 印刷セット２
004860         ELSE
                   IF (連印−氏名モード           = 1   ) OR
                      (連印−バーコードモード     = 1   ) OR
                      (連印−初検時相談支援   NOT = ZERO)
004880                 MOVE 連印−患者コード TO 作１−患者コード
004890                 MOVE 連印−施術和暦   TO 作１−施術和暦
004900                 MOVE 連印−施術年     TO 作１−施術年
004910                 MOVE 連印−施術月     TO 作１−施術月
004920                 MOVE ZERO             TO 作１−施術日
004930                 START 作業ファイル１ KEY IS >= 作１−患者コード
004940                                                作１−施術和暦年月日
004950                 END-START
004980                 PERFORM 作業ファイル１読込
                       MOVE 作１−施術和暦年月 TO 施術年月Ｗ
                       MOVE 作１−患者コード   TO 患者コードＷ
                       IF 連印−氏名モード = 1
                           PERFORM ヘッダセット
                       END-IF
                       IF 連印−バーコードモード = 1
                           MOVE " " TO EDIT-MODE OF バーコード
                           MOVE 作１−患者コード TO バーコード
                       END-IF
005060                 PERFORM 明細印刷
                   END-IF
      *
005180         END-IF
005190     END-IF.
005200************
005210*           *
005220* 終了処理   *
005230*           *
005240************
005250     PERFORM 終了処理.
005260     PERFORM 遅延処理.
005270     EXIT PROGRAM.
005280*
005290*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002860*================================================================*
002870 プリンタファイル作成 SECTION.
002880*================================================================*
002890*   / 初期化 /
002900     MOVE SPACE TO Ｈ連ＰＲＴＦ−作成データ.
002910     INITIALIZE Ｈ連ＰＲＴＦ−作成データ.
002225     MOVE SPACE TO Ｈ連特殊ＰＲＴＦ−作成データ.
002226     INITIALIZE Ｈ連特殊ＰＲＴＦ−作成データ.
002920*
002930*
002940*--↓↓ 変更箇所 ↓↓--------------------------------------*
002230*   使用する用紙種別セット
           MOVE "KARUTE"              TO Ｈ連特殊ＰＲＴＦ−用紙種類.
002970*   使用するプリンタファイル名セット
002231     MOVE "PRTF002"             TO Ｈ連ＰＲＴＦ−ファイル名.
002972*
002973*   使用する帳票プログラム名セット
002974     MOVE "YHP722 "             TO Ｈ連ＰＲＴＦ−帳票プログラム名.
002975*
002976*--↑↑-----------------------------------------------------*
002980*
002990*   / プレビュー区分セット /
003000     MOVE 連入−プレビュー区分 TO Ｈ連ＰＲＴＦ−プレビュー区分.
003000*     MOVE 1 TO Ｈ連ＰＲＴＦ−プレビュー区分.
003010*
003020     CALL   "CRTPRTF".
003030     CANCEL "CRTPRTF".
003040*
005300*================================================================*
005310 初期化 SECTION.
005320*
005890     OPEN INPUT   制御情報マスタ
005900         MOVE NC"制御情報" TO ファイル名.
005910         PERFORM オープンチェック.
005980     OPEN INPUT   受診者情報Ｆ.
005990         MOVE NC"受情" TO ファイル名.
006000         PERFORM オープンチェック.
006010     OPEN INPUT 作業ファイル１
006020         MOVE NC"作業１" TO ファイル名.
006030         PERFORM オープンチェック.
006040     OPEN INPUT 作業ファイル２
006050         MOVE NC"作業２" TO ファイル名.
006060         PERFORM オープンチェック.
006070*     OPEN I-O   印刷ファイル
006080*         PERFORM エラー処理Ｐ.
006090*================================================================*
006100 オープンチェック SECTION.
006110*
006120     IF 状態キー  NOT =  "00"
006130         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
006140         DISPLAY NC"状態キー：" 状態キー         UPON CONS
006150         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
006160                                                 UPON CONS
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
006170         ACCEPT  キー入力 FROM CONS
006180         PERFORM ファイル閉鎖
006190         EXIT PROGRAM.
006200*================================================================*
006210 制御情報取得 SECTION.
006220*
006230     MOVE ZERO TO 制−制御区分
006240     READ 制御情報マスタ
006250     NOT INVALID KEY
006260         MOVE 制−遅延回数                   TO 遅延回数Ｗ
007460         MOVE 制−バーコード２３２Ｃ使用区分 TO バーコード使用区分Ｗ
006270     END-READ.
      */バーコードを使用していない場合は印刷しない
           IF バーコード使用区分Ｗ = ZERO
               MOVE ZERO TO 連印−バーコードモード
           END-IF.
006280*
006290*================================================================*
006300 遅延処理 SECTION.
006310*
006320     PERFORM VARYING 遅延ＣＮＴ FROM 1 BY 1
006330                                UNTIL 遅延ＣＮＴ > 遅延回数Ｗ
006340         MOVE SPACE TO 遅延フラグ
006350     END-PERFORM.
006360*
006370*================================================================*
006380 印刷セット１ SECTION.
006390*
006400     MOVE SPACE TO 終了フラグ.
006410     MOVE ZERO  TO 行カウンタ.
006420     PERFORM 合計値初期化.
006430     MOVE 連印−患者コード  TO 作１−患者コード.
006440     MOVE ZERO              TO 作１−施術和暦.
006450     MOVE ZERO              TO 作１−施術年.
006460     MOVE ZERO              TO 作１−施術月.
006470     START 作業ファイル１ KEY IS > 作１−患者コード
006480                                   作１−施術和暦年月日
006490     END-START.
006500     IF 状態キー  =  "00"
006510         MOVE SPACE  TO 終了フラグ
006520         PERFORM 作業ファイル１読込
006530         MOVE 作１−患者コード    TO 患者コードＷ
006540         MOVE 作１−施術和暦      TO 施術和暦Ｗ
006550         MOVE 作１−施術年        TO 施術年Ｗ
006560         MOVE 作１−施術月        TO 施術月Ｗ
006570*/改ページ不具合対応011130
006580*/次のレコードを読んでから前のヘッダを印刷する必要がある場合の為に退避しておく。(改ページ後の合計行)
006590         MOVE 作１−患者番号      TO 患者番号ＨＷ    
006600         MOVE 作１−枝番          TO 枝番ＨＷ        
006610         MOVE 作１−患者氏名      TO 患者氏名ＨＷ    
006620         MOVE 作１−保険種別      TO 保険種別ＨＷ    
006630         MOVE 作１−公費種別      TO 公費種別ＨＷ    
006640         MOVE 作１−助成種別      TO 助成種別ＨＷ    
006650         MOVE 作１−本人家族区分  TO 本人家族区分ＨＷ
006660         MOVE 作１−負担割合      TO 負担割合ＨＷ    
011230         MOVE 作１−施術和暦      TO 施術和暦ＷＲ
011231         MOVE 作１−施術年        TO 施術年ＷＲ
011232         MOVE 作１−施術月        TO 施術月ＷＲ
006750*
006760         PERFORM UNTIL ( 終了フラグ = "YES" ) OR
006770                       ( 作１−患者コード NOT = 患者コードＷ )
006780             INITIALIZE YHP722P
006790             MOVE SPACE TO YHP722P
006800             MOVE "X" TO EDIT-MODE OF バーコード
006810             IF 連印−氏名モード = 1
006840                 PERFORM ヘッダセット
006860             END-IF
006870             IF 連印−バーコードモード = 1
006880                 MOVE " " TO EDIT-MODE OF バーコード
006890                 MOVE 作１−患者コード TO バーコード
006920             END-IF
      */日に対応した印刷場所/090624
                   PERFORM UNTIL ( 作１−施術和暦年月 NOT = 施術和暦年月ＷＲ) OR
006950                           ( 作１−患者コード   NOT = 患者コードＷ    ) OR
006960                           ( 終了フラグ = "YES" )
      */日に対応した印刷場所/090624
                       MOVE 作１−施術日 TO カウンタ
007390*/月が変わった時の処理
007400                 IF ( 作１−施術年 NOT = 施術年Ｗ ) OR
007410                    ( 作１−施術月 NOT = 施術月Ｗ )
007490                     IF 連印−合計モード = 1
007500*                       */合計行の転記
007510                         PERFORM 合計印刷処理
007940                     END-IF
007950                     PERFORM 合計値初期化
007960                 END-IF
007970                 IF 連印−明細モード = 1
007980*/                  */明細行の転記処理
007990                     PERFORM 明細印刷処理
008000                 END-IF
008010                 IF 連印−コメントモード = 1
008020*/                  */コメント行のセット
008030                     PERFORM コメント印刷処理
008040                 END-IF
008050                 MOVE 作１−患者コード   TO 患者コードＷ
008060                 MOVE 作１−施術和暦     TO 施術和暦Ｗ
008070                 MOVE 作１−施術年       TO 施術年Ｗ
008080                 MOVE 作１−施術月       TO 施術月Ｗ
008090*/改ページ不具合対応011130
008100*/次のレコードを読んでから前のヘッダを印刷する必要がある場合の為に退避しておく。(改ページ後の合計行)
008110                 MOVE 作１−患者番号      TO 患者番号ＨＷ    
008120                 MOVE 作１−枝番          TO 枝番ＨＷ        
008130                 MOVE 作１−患者氏名      TO 患者氏名ＨＷ    
008140                 MOVE 作１−保険種別      TO 保険種別ＨＷ    
008150                 MOVE 作１−公費種別      TO 公費種別ＨＷ    
008160                 MOVE 作１−助成種別      TO 助成種別ＨＷ    
008170                 MOVE 作１−本人家族区分  TO 本人家族区分ＨＷ
008180                 MOVE 作１−負担割合      TO 負担割合ＨＷ    
008190                 PERFORM 作業ファイル１読込
008200             END-PERFORM
009850             IF 連印−合計モード = 1
009860                 PERFORM 合計印刷処理
009870             END-IF
009900             PERFORM 合計値初期化
009910             PERFORM 明細印刷
009920             PERFORM 改頁処理
009960             MOVE カウンタ   TO 印字位置ＣＮＴ
010080         END-PERFORM
010100     END-IF.
010110*================================================================*
010120 印刷セット２ SECTION.
010130*
010140     MOVE SPACE TO 終了フラグ.
010150     MOVE ZERO  TO 行カウンタ.
010160     PERFORM 合計値初期化.
010170     MOVE 連印−患者コード  TO 作１−患者コード.
010180     MOVE ZERO              TO 作１−施術和暦.
010190     MOVE ZERO              TO 作１−施術年.
010200     MOVE ZERO              TO 作１−施術月.
010210     START 作業ファイル１ KEY IS > 作１−患者コード
010220                                   作１−施術和暦年月日
010230     END-START.
010240     IF 状態キー  =  "00"
010250         MOVE SPACE  TO 終了フラグ
010260         PERFORM 作業ファイル１読込
010270         MOVE 作１−患者コード   TO 患者コードＷ
010280         MOVE 作１−施術和暦     TO 施術和暦Ｗ
010290         MOVE 作１−施術年       TO 施術年Ｗ
010300         MOVE 作１−施術月       TO 施術月Ｗ
010320         MOVE "X" TO EDIT-MODE OF バーコード
010330         IF 連印−氏名モード = 1
010340             PERFORM ヘッダセット
010350         END-IF
010360         IF 連印−バーコードモード = 1
010370             MOVE " " TO EDIT-MODE OF バーコード
010380             MOVE 作１−患者コード TO バーコード
010410         END-IF
010420         PERFORM UNTIL ( 作１−患者コード NOT = 患者コードＷ ) OR
010430                       ( 終了フラグ = "YES" )
010820             IF ( 作１−施術和暦 NOT = 施術和暦Ｗ ) OR
010830                ( 作１−施術年 NOT = 施術年Ｗ ) OR
010840                ( 作１−施術月 NOT = 施術月Ｗ )
010850                 PERFORM 合計印刷処理
010860                 PERFORM 合計値初期化
010870             END-IF
010880             MOVE 作１−患者コード   TO 患者コードＷ
010890             MOVE 作１−施術和暦     TO 施術和暦Ｗ
010900             MOVE 作１−施術年       TO 施術年Ｗ
010910             MOVE 作１−施術月       TO 施術月Ｗ
010920             PERFORM 作業ファイル１読込
010930         END-PERFORM
010940         PERFORM 合計印刷処理
010950         PERFORM 明細印刷
010960         PERFORM 改頁処理
010970         PERFORM 合計値初期化
011010         MOVE カウンタ   TO  印字位置ＣＮＴ
011040     END-IF.
011050*================================================================*
011060 ヘッダセット SECTION.
011070*
011080     MOVE 作１−患者番号      TO 患者番号.
011090     MOVE 作１−枝番          TO 枝番.
011100     MOVE 作１−患者氏名      TO 患者氏名 患者氏名ＷＰ.
           MOVE 作１−施術月        TO 施術月.
011110     PERFORM VARYING カウンタ１ FROM 1 BY 1
011120              UNTIL カウンタ１ > 10
011130         MOVE 患者名Ｗ(カウンタ１) TO 患者名(カウンタ１)
011140     END-PERFORM.
011220*
011230     MOVE 作１−施術和暦      TO 施術和暦ＷＲ.
011231     MOVE 作１−施術年        TO 施術年ＷＲ.
011232     MOVE 作１−施術月        TO 施術月ＷＲ.
011440     PERFORM ヘッダ印刷.
011450*================================================================*
011460 明細印刷処理 SECTION.
011470*
011510********************
011520* 料金データセット *
011530********************
011540     MOVE 作１−初検金額     TO  初検料等(カウンタ).
011620     MOVE 作１−整復金額     TO  整復料(カウンタ).
011670     MOVE 作１−後療金額     TO  後療料(カウンタ).
011720     MOVE 作１−罨法金額     TO  罨法料(カウンタ).
011770     MOVE 作１−電療金額     TO  電療料(カウンタ).
011820     MOVE 作１−一部負担金   TO  一部負担金(カウンタ).
012000*================================================================*
012010 コメント印刷処理 SECTION.
012020*
012030     MOVE 作１−コメント           TO  コメントＷ.
012040     MOVE コメント１Ｗ             TO  コメント１(カウンタ).
012050     MOVE コメント２Ｗ             TO  コメント２(カウンタ).
012060*
012070*================================================================*
012080 合計値初期化 SECTION.
012090*
012100     MOVE ZERO  TO 合計Ｗ.
013380*================================================================*
013390 作業ファイル１読込 SECTION.
013400*
013410     READ 作業ファイル１ NEXT
013420     AT END
013430         MOVE "YES" TO 終了フラグ
013440     END-READ.
013450*
013460*================================================================*
013470 ヘッダ印刷 SECTION.
013480*
004310     IF ( オープンフラグ NOT = "YES" )
004320        MOVE "YES" TO オープンフラグ
004330        OPEN I-O  印刷ファイル
004340        PERFORM エラー処理Ｐ
004350     END-IF.
011300*
013490     MOVE "YHP722P"  TO  定義体名Ｐ.
013500     MOVE "HEDDA"   TO  項目群名Ｐ.
013510     MOVE SPACE      TO  処理種別Ｐ.
013520     WRITE YHP722P.
013530     PERFORM エラー処理Ｐ.
013540*================================================================*
013550 明細印刷 SECTION.
013560*
           PERFORM 相談支援文章セット.
      *
013570     MOVE "YHP722P"  TO  定義体名Ｐ.
013580     MOVE "MEISAI"   TO  項目群名Ｐ.
013590     MOVE SPACE      TO  処理種別Ｐ.
013600     WRITE YHP722P.
013610     PERFORM エラー処理Ｐ.
013620*================================================================*
013630 印刷処理 SECTION.
013640*
004310     IF ( オープンフラグ NOT = "YES" )
004320        MOVE "YES" TO オープンフラグ
004330        OPEN I-O  印刷ファイル
004340        PERFORM エラー処理Ｐ
004350     END-IF.
011300*
013650     MOVE "YHP722P"  TO  定義体名Ｐ.
013660     MOVE "HEDDA"   TO  項目群名Ｐ.
013670     MOVE SPACE     TO  処理種別Ｐ.
013680     WRITE YHP722P.
013690     PERFORM エラー処理Ｐ.
013700*
013710     MOVE "YHP722P"  TO  定義体名Ｐ.
013720     MOVE "MEISAI"   TO  項目群名Ｐ.
013730     MOVE SPACE      TO  処理種別Ｐ.
013740     WRITE YHP722P.
013750     PERFORM エラー処理Ｐ.
013760*================================================================*
013770 改頁処理  SECTION.
013780*
013790     MOVE "YHP722P" TO  定義体名Ｐ.
013800     MOVE "CT"      TO  処理種別Ｐ.
013810     MOVE "PAGE"    TO  拡張制御Ｐ.
013820     MOVE SPACE     TO  項目群名Ｐ.
013830     WRITE YHP722P.
013840     PERFORM エラー処理Ｐ.
013850     MOVE SPACE     TO  拡張制御Ｐ.
013860*
002990     IF ( オープンフラグ = "YES" )
002991         CLOSE 印刷ファイル
003041     END-IF.
004320     MOVE SPACE TO オープンフラグ.
013900*
013910*================================================================*
013920 エラー表示 SECTION.
013930*
013940     DISPLAY ファイル名   NC"ファイル書込エラー"   UPON CONS.
013950     DISPLAY NC"状態キー：" 状態キー               UPON CONS.
013960     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
013970     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
000080*-----------------------------------------*
000090     CALL "actcshm"  WITH C LINKAGE.
000100*-----------------------------------------*
013980     ACCEPT  キー入力 FROM CONS.
013990     PERFORM ファイル閉鎖.
014000     EXIT PROGRAM.
014010*================================================================*
014020 エラー処理Ｐ SECTION.
014030*
014040     IF 通知情報Ｐ NOT = "00"
014050         DISPLAY NC"帳票エラー"              UPON CONS
014060         DISPLAY NC"項目群名Ｐ：" 項目群名Ｐ UPON CONS
014070         DISPLAY NC"通知情報Ｐ：" 通知情報Ｐ UPON CONS
014080         DISPLAY NC"拡張制御Ｐ：" 拡張制御Ｐ UPON CONS
014090         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
014100                                             UPON CONS
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
014110         ACCEPT  キー入力 FROM CONS
014120         PERFORM ファイル閉鎖
014130         MOVE 99 TO PROGRAM-STATUS
014140         EXIT PROGRAM
014150     END-IF.
014160*================================================================*
014170 ファイル閉鎖 SECTION.
014180*
002990     IF ( オープンフラグ = "YES" )
002991         CLOSE 印刷ファイル
003041     END-IF.
014200     CLOSE 制御情報マスタ 受診者情報Ｆ 作業ファイル１ 作業ファイル２.
014220*================================================================*
014230 終了処理 SECTION.
014240*
014250     PERFORM ファイル閉鎖.
015130*================================================================*
       合計印刷処理 SECTION.
      *
012140     MOVE 施術和暦Ｗ   TO 作２−施術和暦.
012150     MOVE 施術年Ｗ     TO 作２−施術年.
012160     MOVE 施術月Ｗ     TO 作２−施術月.
012170     MOVE 患者コードＷ TO 作２−患者コード.
012180     READ 作業ファイル２
012190     NOT INVALID KEY
               MOVE 作２−施術月   TO 合計施術月
002030         MOVE 作２−施術回数 TO 合計回数
002040         MOVE 作２−費用額   TO 合計費用額
002050         MOVE 作２−請求額   TO 合計請求額
002060         MOVE 作２−負担金   TO 合計負担額
001930         MOVE 作２−開始年   TO 合計開始年
001940         MOVE 作２−開始月   TO 合計開始月
001950         MOVE 作２−開始日   TO 合計開始日
001990         MOVE 作２−終了年   TO 合計終了年
002000         MOVE 作２−終了月   TO 合計終了月
002010         MOVE 作２−終了日   TO 合計終了日
               MOVE 作２−請求期間 TO 請求期間
           END-READ.
015130*================================================================*
       相談支援文章セット SECTION.
      *
      */相談支援文章
012140     MOVE 施術和暦Ｗ   TO 作２−施術和暦.
012150     MOVE 施術年Ｗ     TO 作２−施術年.
012160     MOVE 施術月Ｗ     TO 作２−施術月.
012170     MOVE 患者コードＷ TO 作２−患者コード.
012180     READ 作業ファイル２
012190     NOT INVALID KEY
               IF 作２−相談料計 = ZERO
                   MOVE SPACE TO 相談文章
               ELSE
                   EVALUATE 連印−初検時相談支援
                   WHEN 1
                       MOVE "生活面において指導する。"                     TO 相談文章
                   WHEN 2
                       MOVE "施術の方向性、予後においての判定指導をする。" TO 相談文章
                   WHEN OTHER
                       MOVE SPACE TO 相談文章
                   END-EVALUATE
               END-IF
           END-READ.
015130*================================================================*
015140******************************************************************
015150 END PROGRAM YHP722.
015160******************************************************************
