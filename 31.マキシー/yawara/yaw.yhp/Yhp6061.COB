000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YHP6061.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*      レセプト印刷一覧【ﾃﾞｰﾀ作成】柔+ｳｨﾝﾄﾞｳｽﾞ版
000100* 請求年月Ver.
000110* 
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2012-08-06
000140 DATE-COMPILED.          2012-08-06
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000390     SELECT  制御情報マスタ  ASSIGN      TO        SEIGYOL
000400                             ORGANIZATION             IS  INDEXED
000410                             ACCESS MODE              IS  DYNAMIC
000420                             RECORD KEY               IS  制−制御区分
000430                             FILE STATUS              IS  状態キー
000440                             LOCK        MODE         IS  AUTOMATIC.
000360     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000370                             ORGANIZATION             IS  INDEXED
000380                             ACCESS MODE              IS  DYNAMIC
000390                             RECORD KEY               IS  受−施術和暦年月
000400                                                          受−患者コード
000410                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000420                                                          受−患者カナ
000430                                                          受−患者コード
000440                             ALTERNATE RECORD KEY     IS  受−患者コード
000450                                                          受−施術和暦年月
000460                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000470                                                          受−保険種別
000480                                                          受−保険者番号
000490                                                          受−患者コード
000500                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000510                                                          受−公費種別
000520                                                          受−費用負担者番号
000530                                                          受−患者コード
000540                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000550                                                          受−助成種別
000560                                                          受−費用負担者番号助成
000570                                                          受−患者コード
000580                             ALTERNATE RECORD KEY     IS  受−請求和暦年月
000590                                                          受−施術和暦年月
000600                                                          受−患者コード
000610                             FILE STATUS              IS  状態キー
000620                             LOCK        MODE         IS  AUTOMATIC.
000241     SELECT  生保情報Ｆ      ASSIGN      TO        SEIHOJL
000242                             ORGANIZATION             IS INDEXED
000243                             ACCESS MODE              IS DYNAMIC
000244                             RECORD KEY               IS 生保−施術和暦年月
000245                                                         生保−患者コード
000255                             ALTERNATE RECORD KEY     IS 生保−患者コード
000265                                                         生保−施術和暦年月
000277                             FILE STATUS              IS 状態キー
000278                             LOCK        MODE         IS AUTOMATIC.
           SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
                                   ORGANIZATION             IS  INDEXED
                                   ACCESS MODE              IS  DYNAMIC
                                   RECORD KEY               IS  レセ−施術和暦年月
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−患者コード
                                                                レセ−施術和暦年月
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−施術和暦年月
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−レセ種別
                                                                レセ−請求保険者番号
                                                                レセ−患者コード
                                                                レセ−施術和暦年月
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−請求保険者番号
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                                                レセ−施術和暦年月
                                   FILE STATUS              IS  状態キー
                                   LOCK        MODE         IS  AUTOMATIC.
000720     SELECT  作業ファイル１  ASSIGN      TO   "C:\MAKISHISYS\YAWOBJ\TEMP\W60610L.DAT"
000730                             ORGANIZATION             IS  INDEXED
000740                             ACCESS                   IS  DYNAMIC
000750                             RECORD      KEY          IS  作１−請求和暦年月
000770                                                          作１−保険区分
000780                                                          作１−保険番号
000780                                                          作１−保種
000790                                                          作１−本人家族キー
000800                                                          作１−患者カナ
000810                                                          作１−患者コード
000760                                                          作１−施術和暦年月
000820                             FILE        STATUS       IS  状態キー
000830                             LOCK        MODE         IS  AUTOMATIC.
000840*
000850* レセ並び順用
000920     SELECT  作業ファイル２  ASSIGN      TO     "C:\MAKISHISYS\YAWOBJ\TEMP\W6102L.DAT"
000930                             ORGANIZATION             IS  INDEXED
000940                             ACCESS                   IS  DYNAMIC
001230                             RECORD      KEY          IS  作２−保険区分
                                                                作２−順番
                                   ALTERNATE RECORD KEY     IS  作２−施術和暦年月
                                                                作２−患者コード
                                                                作２−保険区分
                                                                作２−順番
000960                             FILE        STATUS       IS  状態キー
000970                             LOCK        MODE         IS  AUTOMATIC.
001070*
001100     SELECT  作業ファイル３  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5912L.DAT"
001101                             ORGANIZATION             IS  INDEXED
001110                             ACCESS                   IS  DYNAMIC
001120                             RECORD      KEY          IS  作３−施術和暦年月
001130                                                          作３−患者コード
001140                                                          作３−保険種別
001150                             FILE        STATUS       IS  状態キー
001160                             LOCK        MODE         IS  AUTOMATIC.
000920******************************************************************
000930*                      DATA DIVISION                             *
000940******************************************************************
000950 DATA                    DIVISION.
000960 FILE                    SECTION.
000820*                           ［ＲＬ＝  ２５６］
000830 FD  制御情報マスタ          BLOCK   CONTAINS   1   RECORDS.
000840     COPY SEIGYO          OF  XFDLIB  JOINING   制   AS  PREFIX.
001000*                           ［ＲＬ＝  ３２０］
001010 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001020     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
001080* 
000280 FD  生保情報Ｆ          BLOCK   CONTAINS   1   RECORDS.
000281     COPY SEIHOJ          OF  XFDLIB  JOINING   生保   AS  PREFIX.
      *
       FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS GLOBAL.
           COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
001060*
001070 FD  作業ファイル１ RECORD  CONTAINS 256 CHARACTERS.
001080 01  作１−レコード.
001090     03  作１−レコードキー.
001100         05  作１−請求和暦年月.
001110             07  作１−請求和暦            PIC 9.
001120             07  作１−請求年              PIC 9(2).
001130             07  作１−請求月              PIC 9(2).
001180         05  作１−保険区分                PIC 9(2).
001190         05  作１−保険番号                PIC X(10).
001200         05  作１−保種                    PIC 9.
001200         05  作１−本人家族キー            PIC 9.
001210         05  作１−患者カナ                PIC X(50).
001220         05  作１−患者コード.
001230             07 作１−患者番号             PIC 9(6).
001240             07 作１−枝番                 PIC X(1).
001140         05  作１−施術和暦年月.
001150             07  作１−施術和暦            PIC 9.
001160             07  作１−施術年              PIC 9(2).
001170             07  作１−施術月              PIC 9(2).
001250     03  作１−レコードデータ.
001260         05  作１−患者氏名                PIC X(50).
001270         05  作１−本人家族区分            PIC 9.
001280         05  作１−保険種別                PIC 9(2).
001290         05  作１−親保険種別              PIC 9(2).
001300         05  作１−老人レコード区分        PIC 9.
001310         05  作１−助成レコード区分        PIC 9.
001221         05  作１−レセ種別                PIC 9(2).
001740         05  作１−被保険者氏名            PIC X(50).
001366         05  作１−費用額                  PIC 9(6).
001367         05  作１−負担額                  PIC 9(6).
001368         05  作１−請求額                  PIC 9(6).
001369         05  作１−レセ印刷区分            PIC 9.
001190         05  作１−保険者番号              PIC X(10).
001320         05  FILLER                        PIC X(37).
001330*
001790 FD  作業ファイル２ RECORD  CONTAINS 155 CHARACTERS.
001800 01  作２−レコード.
001810     03  作２−レコードキー.
001820         05  作２−保険区分                PIC 9(1).
001820         05  作２−順番                    PIC 9(4).
001830     03  作２−レコードデータ.
               05  作２−レセ種別                PIC 9(2).
001840         05  作２−患者コード.
001850             07 作２−患者番号             PIC 9(6).
001860             07 作２−枝番                 PIC X(1).
001870         05  作２−患者氏名                PIC X(50).
001740         05  作２−被保険者氏名            PIC X(50).
001880         05  作２−保険種別                PIC 9(2).
               05  作２−請求保険者番号          PIC X(10).
001890         05  作２−本人家族                PIC X(4).
001900         05  作２−施術和暦年月.
001910             07  作２−施術和暦            PIC 9.
001920             07  作２−施術年              PIC 9(2).
001930             07  作２−施術月              PIC 9(2).
001940         05  作２−印刷指定                PIC 9(1).
               05  作２−費用額                  PIC 9(6).
               05  作２−負担額                  PIC 9(6).
               05  作２−請求額                  PIC 9(6).
               05  作２−レセ印刷区分            PIC 9.
001730*
001740 FD  作業ファイル３ RECORD  CONTAINS 32 CHARACTERS.
001750 01  作３−レコード.
001760     03  作３−レコードキー.
001770         05  作３−施術和暦年月.
001780             07  作３−施術和暦            PIC 9.
001790             07  作３−施術年              PIC 9(2).
001800             07  作３−施術月              PIC 9(2).
001810         05  作３−患者コード.
001820             07 作３−患者番号             PIC 9(6).
001830             07 作３−枝番                 PIC X(1).
001840         05  作３−保険種別                PIC 9(2).
001850     03  作３−レコードデータ.
001860         05  作３−順番                    PIC 9(4).
001870         05  FILLER                        PIC X(14).
001510*
001520*----------------------------------------------------------------*
001530******************************************************************
001540*                WORKING-STORAGE SECTION                         *
001550******************************************************************
001560 WORKING-STORAGE         SECTION.
001570 01 キー入力                           PIC X    VALUE SPACE.
001580 01 状態キー                           PIC X(2) VALUE SPACE.
001590 01 終了フラグ                         PIC X(3) VALUE SPACE.
001600 01 終了フラグ１                       PIC X(3) VALUE SPACE.
001610 01 実行キーＷ                         PIC X(3)  VALUE SPACE.
001620 01 施術記録有Ｗ                       PIC X(3) VALUE SPACE.
001630 01 カウンタ                           PIC 9(2)  VALUE ZERO.
001640**
001650 01 保険種別ＷＲ                       PIC 9(2) VALUE ZERO.
001660 01 印刷形式ＷＲ                       PIC 9    VALUE ZERO.
001670 01 保険者番号ＷＲ                     PIC X(10) VALUE SPACE.
001680 01 レセプト種類ＷＲ                   PIC X(4) VALUE SPACE.
001690 01 本人家族区分ＷＲ                   PIC 9    VALUE ZERO.
001700 01 施術和暦年月ＷＲ.
001710    03 施術和暦ＷＲ                    PIC 9    VALUE ZERO.
001720    03 施術年ＷＲ                      PIC 9(2) VALUE ZERO.
001730    03 施術月ＷＲ                      PIC 9(2) VALUE ZERO.
001740 01 請求和暦年月ＷＲ.
001750    03 請求和暦ＷＲ                    PIC 9    VALUE ZERO.
001760    03 請求年ＷＲ                      PIC 9(2) VALUE ZERO.
001770    03 請求月ＷＲ                      PIC 9(2) VALUE ZERO.
001780**
001790 01 順番Ｗ                             PIC 9(4) VALUE ZERO.
002090 01 生保順番Ｗ                         PIC 9(4)  VALUE ZERO.
001800**
001810 01 ファイル名                         PIC N(8) VALUE SPACE.
001820 01 保険種別Ｗ                         PIC 9(2) VALUE ZERO.
001780 01 レセ並び順昇降Ｗ                   PIC 9(1) VALUE ZERO.
001894*
       01 金額Ｗ.
          03 費用額Ｗ                        PIC 9(6)  VALUE ZERO.
          03 負担額Ｗ                        PIC 9(6)  VALUE ZERO.
          03 請求額Ｗ                        PIC 9(6)  VALUE ZERO.
001830*
001840** エラーメッセージ用
001850 01 エラーメッセージＷ.
001860    03 エラー患者コードＷ              PIC X(7) VALUE SPACE.
001870    03 エラー区切りＷ                  PIC X(1) VALUE SPACE.
001880    03 エラー保険種別Ｗ                PIC X(2) VALUE SPACE.
001890    03 FILLER                          PIC X(10) VALUE SPACE.
001900*
001910** 保険者番号右詰め用
001920 01 保険者番号ＷＴ.
001930    03 保険者番号左詰めＷ.
001940      05 保険者番号左詰めＷ１          PIC X OCCURS 8 VALUE SPACE.
001950    03 保険者番号右詰めＷ.
001960      05 保険者番号右詰めＷ１          PIC X OCCURS 8 VALUE ZERO.
001970    03 保険者番号数字Ｗ                PIC 9(8)  VALUE ZERO.
001980    03 保険者番号Ｗ                    PIC X(8)  VALUE SPACE.
001990**
002000 01 計算機西暦年Ｗ                     PIC 9(2).
002010* 日付ＷＯＲＫ
002020 01 計算機西暦.
002030    03 計算機西暦年                    PIC 9(4).
002040    03 計算機西暦月日                  PIC 9(4).
002050 01 計算機西暦Ｒ REDEFINES 計算機西暦.
002060    03 計算機世紀                      PIC 9(2).
002070    03 計算機日付                      PIC 9(6).
002080    03 計算機日付Ｒ REDEFINES 計算機日付.
002090       05 計算機年月                   PIC 9(4).
002100       05 計算機年月Ｒ REDEFINES 計算機年月.
002110         07 計算機年                   PIC 9(2).
002120         07 計算機月                   PIC 9(2).
002130       05 計算機日                     PIC 9(2).
002140*
002392** 助成レセまとめ用
002393 01 助成レセまとめフラグ               PIC X(3)  VALUE SPACE.
002394*
002150******************************************************************
002160*                          連結項目                              *
002170******************************************************************
002180*
002190********************
002200* メッセージ表示キー *
002210********************
002211 01 連メ−キー IS EXTERNAL.
002212    03  連メ−メッセージ               PIC N(20).
002213*
002220 01 連メ３−キー IS EXTERNAL.
002230    03  連メ３−メッセージ             PIC N(20).
002240    03  連メ３−メッセージ１           PIC X(20).
002250****************
002260* 画面入力情報 *
002270****************
002480 01 連入−入力データ６１０ IS EXTERNAL.
002490    03 連入−請求年月.
002500       05 連入−請求和暦                  PIC 9.
002510       05 連入−請求年                    PIC 9(2).
002520       05 連入−請求月                    PIC 9(2).
002540    03 連入−保険種別                     PIC 9(2).
002400*
002610*****
002611************************
002612* 助成レセまとめ
002613************************
002614 01 連レセまとめ−キー IS EXTERNAL.
002615    03 連レセまとめ−施術和暦年月.
002616       05 連レセまとめ−施術和暦               PIC 9.
002617       05 連レセまとめ−施術年月.
002618          07 連レセまとめ−施術年              PIC 9(2).
002619          07 連レセまとめ−施術月              PIC 9(2).
002620    03 連レセまとめ−患者コード.
002621       05 連レセまとめ−患者番号               PIC 9(6).
002622       05 連レセまとめ−枝番                   PIC X(1).
002623**-------------------------------------------------------**
002624*   1:助成レセプトなしの本体まとめの判定
002625*   2:横浜・川崎用の社保助成レセかの判定
002626    03 連レセまとめ−判定区分                  PIC 9.
002627**-------------------------------------------------------**
002628*  / OUT /　 0:対象外、1:対象
002629    03 連レセまとめ−判定結果                  PIC 9.
002630**
002410******************************************************************
002420*                      PROCEDURE  DIVISION                       *
002430******************************************************************
002440 PROCEDURE               DIVISION.
002450************
002460*           *
002470* 初期処理   *
002480*           *
002490************
002500     PERFORM 初期化.
002510************
002520*           *
002530* 主処理     *
002540*           *
002550************
002560     PERFORM 作業ファイル作成.
002570************
002580*           *
002590* 終了処理   *
002600*           *
002610************
002620     PERFORM 終了処理.
002630     MOVE ZERO TO PROGRAM-STATUS.
002640     EXIT PROGRAM.
002650*
002660*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002670*================================================================*
002680 初期化 SECTION.
002690*
002700     PERFORM ファイルオープン.
002710* 連結項目の待避
002720     MOVE 連入−請求和暦      TO 請求和暦ＷＲ.
002730     MOVE 連入−請求年        TO 請求年ＷＲ.
002740     MOVE 連入−請求月        TO 請求月ＷＲ.
      * レセ並び順昇順・降順セット
002740     MOVE ZEROS TO 制−制御区分.
002750     READ 制御情報マスタ
002760     NOT INVALID KEY
002770         MOVE 制−レセ並び順昇降 TO レセ並び順昇降Ｗ
002780     END-READ.
002750*
002760*================================================================*
002770 ファイルオープン SECTION.
002780*
003170     OPEN INPUT 制御情報マスタ
003180         MOVE NC"制御情報" TO ファイル名.
003190         PERFORM オープンチェック.
002820     OPEN INPUT 受診者情報Ｆ.
002830         MOVE NC"受診者情報Ｆ" TO ファイル名.
002840         PERFORM オープンチェック.
006630     OPEN INPUT 生保情報Ｆ.
006640         MOVE NC"生保" TO ファイル名.
006650         PERFORM オープンチェック.
003250     OPEN INPUT レセプトＦ.
003260         MOVE NC"レセプトＦ" TO ファイル名.
003270         PERFORM オープンチェック.
002880*================================================================*
002890 オープンチェック SECTION.
002900*
002910     IF 状態キー  NOT =  "00"
002920         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
002930         DISPLAY NC"状態キー：" 状態キー         UPON CONS
002940         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS
003131*-----------------------------------------*
003132         CALL "actcshm"  WITH C LINKAGE
003133*-----------------------------------------*
002960         ACCEPT  キー入力 FROM CONS
002970         PERFORM ファイル閉鎖
002980         MOVE 99 TO PROGRAM-STATUS
002990         EXIT PROGRAM.
003000*================================================================*
003010 終了処理 SECTION.
003020*
003030     PERFORM ファイル閉鎖.
003040*================================================================*
003050 ファイル閉鎖 SECTION.
003060*
003070     CLOSE 制御情報マスタ 受診者情報Ｆ
                 生保情報Ｆ     レセプトＦ.
003080*================================================================*
003090 作業ファイル作成 SECTION.
003100*
003110     OPEN OUTPUT 作業ファイル１.
003120         MOVE NC"作１" TO ファイル名.
003130         PERFORM オープンチェック.
003140*
003150     PERFORM 作業ファイル１作成.
003160*
003170     CLOSE 作業ファイル１.
003180*
003190     OPEN OUTPUT 作業ファイル２.
003200         MOVE NC"作２" TO ファイル名.
003210         PERFORM オープンチェック.
003220     OPEN INPUT  作業ファイル１.
003230         MOVE NC"作１" TO ファイル名.
003240         PERFORM オープンチェック.
003250*
003260* 並び順用ファイル作成
           IF レセ並び順昇降Ｗ = 1
003380         PERFORM レセ並び順降順ファイル作成
           ELSE
003270         PERFORM 作業ファイル２作成
           END-IF.
004310*
003290     CLOSE 作業ファイル１ 作業ファイル２.
004210*
003220     OPEN INPUT  作業ファイル２.
003230         MOVE NC"作２" TO ファイル名.
003240         PERFORM オープンチェック.
004220     OPEN OUTPUT 作業ファイル３.
004230         MOVE NC"作３" TO ファイル名.
004240         PERFORM オープンチェック.
003280*
004300     PERFORM 作業ファイル３作成.
004310*
003290     CLOSE 作業ファイル２ 作業ファイル３.
003300*
003310*================================================================*
003320 作業ファイル１作成 SECTION.
003330**********************************************************************
003340**   受診者情報Ｆから、該当請求年月のデータを抽出し、
003350**   作業ファイル１に書き出す.
003360**********************************************************************
003370*
005170     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
005180     MOVE 請求年ＷＲ    TO レセ−請求年.  
005190     MOVE 請求月ＷＲ    TO レセ−請求月.  
013730     MOVE ZERO          TO レセ−レセ種別.
005200     MOVE ZERO          TO レセ−施術和暦.
005210     MOVE ZERO          TO レセ−施術年.  
005220     MOVE ZERO          TO レセ−施術月.  
005230     MOVE ZERO          TO レセ−患者番号.
005240     MOVE SPACE         TO レセ−枝番.    
013770     START レセプトＦ   KEY IS >= レセ−請求和暦年月
000230                                  レセ−施術和暦年月
000240                                  レセ−患者コード
000250                                  レセ−レセ種別
003880     END-START.
003890*     PERFORM データ確認.
003900     IF 状態キー = "00"
003910        MOVE SPACE  TO 終了フラグ
003920        PERFORM レセプトＦ読込
003930        PERFORM UNTIL ( 終了フラグ = "YES" ) OR
005330                      ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
005340                      ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
005350                      ( レセ−請求月   NOT = 請求月ＷＲ   )
003580*
003590           PERFORM データチェック
003600*
003610** 労災・自賠責・自由は対象外
003620           IF  受−保険種別 = 70 OR 80 OR 90
003630              MOVE SPACE  TO 実行キーＷ
003640           END-IF
003840*
003842*↓* 特別処理（助成レセまとめ）/101108
003843           IF 受−助成種別 NOT = ZERO
003844              PERFORM 助成レセまとめ判定
003845           ELSE
003846              MOVE SPACE TO 助成レセまとめフラグ
003847           END-IF
003848*↑*
003650*
003660           IF  実行キーＷ = "YES"
003670* 健保 *
005610                EVALUATE TRUE
013940                WHEN レセ−レセ種別   = 1
                          IF (保険種別ＷＲ = ZERO) OR
                             ((受−保険種別 = 保険種別ＷＲ) AND ( 受−公費種別 = ZERO ))
003730                       MOVE SPACE TO 作１−レコード
003740                       INITIALIZE 作１−レコード
003750                       MOVE 受−保険種別       TO 作１−保険種別
003760                       MOVE 受−保険者番号     TO 作１−保険者番号 作１−保険番号
                             IF 受−保険種別 = 08
003760                           MOVE 受−保番       TO 作１−保険番号
003760                           MOVE 8              TO 作１−保種
                             END-IF
                             EVALUATE 受−保険種別
                             WHEN 01
003760                           MOVE 1                  TO 作１−保種
                                 EVALUATE 受−特別区分
                                 WHEN 1
                                     MOVE 1              TO 作１−本人家族キー
                                 WHEN 2
                                     MOVE 2              TO 作１−本人家族キー
                                 WHEN 6
                                     MOVE 4              TO 作１−本人家族キー
                                 WHEN OTHER
                                     MOVE 3              TO 作１−本人家族キー
                                 END-EVALUATE
                             WHEN OTHER
003770                           MOVE 受−本人家族区分   TO 作１−本人家族キー
003780                       END-EVALUATE
                             PERFORM 保険区分取得
004010                       PERFORM 料金セット
003790                       PERFORM 作１レコードセット
003800                       PERFORM 作１ファイル書込
003940                    END-IF
003960* 老人 *
                      WHEN レセ−レセ種別   = 2 
                          IF (保険種別ＷＲ = ZERO) OR
                             (受−公費種別 = 保険種別ＷＲ)
004000                       MOVE SPACE TO 作１−レコード
004010                       INITIALIZE 作１−レコード
004020                       MOVE 受−公費種別        TO 作１−保険種別
004030                       MOVE 受−費用負担者番号  TO 作１−保険者番号 作１−保険番号
                             MOVE 受−特別区分        TO 作１−本人家族キー
004050                       MOVE  1                  TO 作１−老人レコード区分
004060                       PERFORM 保険区分取得
004010                       PERFORM 料金セット
004070                       PERFORM 作１レコードセット
004080                       PERFORM 作１ファイル書込
004210                    END-IF
003960* 助成 *
      */ 特別処理（助成レセまとめ）**/101108
                      WHEN レセ−レセ種別   = 3
                          IF ( 助成レセまとめフラグ = SPACE ) AND
                             ((保険種別ＷＲ = ZERO) OR
                              (受−助成種別 = 保険種別ＷＲ))
005930*         / 助成の請求額０は対象外にする /170621
005930*         / 大阪の助成の請求額０は対象にする /170621
006880                       IF (レセ−助成請求金額 NOT = ZERO) OR
                                (受−費用負担者番号助成(3:2) = "27")
004110                          MOVE SPACE TO 作１−レコード
004120                          INITIALIZE 作１−レコード
004130                          MOVE 受−助成種別        TO 作１−保険種別
004140                          MOVE 受−公費種別        TO 作１−親保険種別
004150                          MOVE 受−費用負担者番号助成  TO 作１−保険者番号 作１−保険番号
004160                          MOVE  1                      TO 作１−本人家族キー
004170                          MOVE  1                  TO 作１−助成レコード区分
004180                          PERFORM 助成保険区分取得
004010                          PERFORM 助成料金セット
004190                          PERFORM 作１レコードセット
004200                          PERFORM 作１ファイル書込
                             END-IF
004210                    END-IF
003960* 生保 *
                      WHEN レセ−レセ種別   = 7
                          IF (保険種別ＷＲ = ZERO) OR
                             (受−保険種別 = 保険種別ＷＲ)
003920                       MOVE SPACE TO 作１−レコード
003930                       INITIALIZE 作１−レコード
003960                       MOVE 受−保険種別     TO 作１−保険種別
                             MOVE 受−施術和暦年月 TO 生保−施術和暦年月
                             MOVE 受−患者コード   TO 生保−患者コード
                             READ 生保情報Ｆ
                             NOT INVALID KEY
003950                          MOVE 生保−負担者番号   TO 作１−保険者番号 作１−保険番号
                             END-READ
004040                       MOVE  1                 TO 作１−本人家族キー
004060                       PERFORM 保険区分取得
004010                       PERFORM 料金セット
004370                       PERFORM 作１レコードセット
004380                       PERFORM 作１ファイル書込
004391                    END-IF
005080                END-EVALUATE
004230           END-IF
004240**
004250           PERFORM レセプトＦ読込
004260        END-PERFORM
004270     END-IF.
004280**
004290*================================================================*
004300 レセプトＦ読込 SECTION.
004310*
004320     READ レセプトＦ NEXT
004330     AT END
004340         MOVE "YES" TO 終了フラグ
004350     END-READ.
004360*
004370*================================================================*
004371 データ確認 SECTION.
004372*
004373     IF 状態キー  NOT =  "00"
004374         MOVE  NC"　　　該当年月のデータがありません。" TO 連メ−メッセージ
004375         CALL   "MSG001"
004376         CANCEL "MSG001"
004377         PERFORM ファイル閉鎖
004378         MOVE 99 TO PROGRAM-STATUS
004379         EXIT PROGRAM
004380     END-IF.
004381*
004470*================================================================*
004480 データチェック SECTION.
004490*
004500     MOVE SPACE  TO 実行キーＷ.
005300     IF ( レセ−レセ印刷対象区分 NOT = 1 )
005310        MOVE "YES"  TO 実行キーＷ
005320     END-IF.
019520* *****************************************************************
019530* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
019540* *****************************************************************
005360     IF 実行キーＷ  = "YES"
005370*      (再度、実行キーＷ SPACE)
005380        MOVE SPACE  TO 実行キーＷ
019640        IF レセ−請求対象区分 NOT = ZERO
004090           MOVE レセ−施術和暦  TO 受−施術和暦
004100           MOVE レセ−施術年    TO 受−施術年
004110           MOVE レセ−施術月    TO 受−施術月
004120           MOVE レセ−患者番号  TO 受−患者番号
004130           MOVE レセ−枝番      TO 受−枝番
                 READ 受診者情報Ｆ
                 NOT INVALID KEY
019880              MOVE "YES"  TO 実行キーＷ
                 END-READ
019900        ELSE
019910           MOVE SPACE  TO 実行キーＷ
              END-IF
019950     END-IF.
005040*================================================================*
005050 作１レコードセット SECTION.
005060*
           MOVE レセ−レセ種別     TO 作１−レセ種別.
005070     MOVE レセ−請求和暦     TO 作１−請求和暦.
005080     MOVE レセ−請求年       TO 作１−請求年.
005090     MOVE レセ−請求月       TO 作１−請求月.
005100     MOVE レセ−施術和暦     TO 作１−施術和暦.
005110     MOVE レセ−施術年       TO 作１−施術年.
005120     MOVE レセ−施術月       TO 作１−施術月.
005130     MOVE 受−患者カナ       TO 作１−患者カナ.
005140     MOVE 受−患者コード     TO 作１−患者コード.
005150     MOVE 受−患者氏名       TO 作１−患者氏名.
005160     MOVE 受−本人家族区分   TO 作１−本人家族区分.
005210     MOVE 受−患者氏名       TO 作１−患者氏名.
005210     MOVE 受−被保険者氏名   TO 作１−被保険者氏名.
           MOVE 費用額Ｗ           TO 作１−費用額.
           MOVE 請求額Ｗ           TO 作１−請求額.
           MOVE 負担額Ｗ           TO 作１−負担額.
005170*
005390*================================================================*
       料金セット SECTION.
           MOVE レセ−合計            TO 費用額Ｗ.
           MOVE レセ−請求金額        TO 請求額Ｗ.
           MOVE レセ−一部負担金      TO 負担額Ｗ.
005210     MOVE 受−レセ印刷区分      TO 作１−レセ印刷区分.
005380*
005390*================================================================*
       助成料金セット SECTION.
           MOVE レセ−合計            TO 費用額Ｗ.
           MOVE レセ−助成請求金額    TO 請求額Ｗ.
           MOVE レセ−受給者負担額    TO 負担額Ｗ.
005210     MOVE 受−レセ印刷区分助成  TO 作１−レセ印刷区分.
005380*
005180*================================================================*
005190 保険区分取得 SECTION.
005200*******************************************************************
005210* 出力順：1.社保(02) --> 2.国保(01) --> 3.船員(07) --> 4.日雇(06) *
005220*        --> 5.組合(03) --> 6.老人(05) --> 7.共済(04)・自衛官(09) *
005230*        --> 8.退職(08) --> 9.福祉(50.51.52.53.54.55.60)          *
      */出力順変更/111027
      *1協会、2船員、3日雇、4組合、5自衛官・共済、6国保、7退職、8後期、
      *9助成、10生保
      */出力順変更/180307
      *1国保、1退職、2後期、3助成、4協会、4船員、4日雇、5組合、6自衛官・共済、
      *10生保
005240*******************************************************************
005250**-- 老人 --
005260      IF 受−公費種別 = 05
               MOVE 02    TO 作１−保険区分
005280      ELSE
005290**-- 健保 --
005300         EVALUATE 受−保険種別
005310* 国保
005320         WHEN 01
                   MOVE 01 TO 作１−保険区分
005340* 社保
005350         WHEN 02
005360             MOVE 04 TO 作１−保険区分
005370* 組合
005380         WHEN 03
                   MOVE 05 TO 作１−保険区分
005400* 共済・自衛官
005410         WHEN 04
005420         WHEN 09
　                 MOVE 06 TO 作１−保険区分
005440* 日雇
005450         WHEN 06
                   MOVE 04 TO 作１−保険区分
005470* 船員
005480         WHEN 07
                   MOVE 04 TO 作１−保険区分
005500* 退職
005510         WHEN 08
                   MOVE 01 TO 作１−保険区分
005500* 生保
005510         WHEN 85
                   MOVE 10 TO 作１−保険区分
005530         WHEN OTHER
005540             MOVE SPACE                TO エラーメッセージＷ
005550             MOVE 受−患者コード       TO エラー患者コードＷ
005560             MOVE 受−保険種別         TO エラー保険種別Ｗ
005570             MOVE "-"                  TO エラー区切りＷ
005580             MOVE  NC"保険種別が異常です。患者−保険種別" TO 連メ３−メッセージ
005590             MOVE  エラーメッセージＷ  TO 連メ３−メッセージ１
005600             CALL   "MSG003"
005610             CANCEL "MSG003"
005620         END-EVALUATE
005630      END-IF.
005640*
005650*================================================================*
005660 助成保険区分取得 SECTION.
005670*
005680         EVALUATE 受−助成種別
005690* 生保
005700         WHEN 50
005710* ４１老人
005720         WHEN 51
005730* ひとり親
005740         WHEN 52
005750* 身障
005760         WHEN 53
005770* 被爆
005780         WHEN 54
005790* 乳幼児
005800         WHEN 55
005810* その他
005820         WHEN 60
                   MOVE 03     TO 作１−保険区分
005840         WHEN OTHER
005850             MOVE SPACE                TO エラーメッセージＷ
005860             MOVE 受−患者コード       TO エラー患者コードＷ
005870             MOVE 受−助成種別         TO エラー保険種別Ｗ
005880             MOVE "-"                  TO エラー区切りＷ
005890             MOVE  NC"助成種別が異常です。患者−助成種別" TO 連メ３−メッセージ
005900             MOVE  エラーメッセージＷ  TO 連メ３−メッセージ１
005910             CALL   "MSG003"
005920             CANCEL "MSG003"
005930         END-EVALUATE.
005940*
005950*================================================================*
005960 作１ファイル書込 SECTION.
005970*
005980     WRITE 作１−レコード.
005990     IF 状態キー  NOT =  "00"
006000         MOVE NC"作１"  TO ファイル名
006010         PERFORM エラー表示
006020     END-IF.
006030*
006040*================================================================*
006050 レセ並び順降順ファイル作成 SECTION.
006060*
006070* 降順作成
006080     MOVE SPACE TO 終了フラグ.
006090     MOVE ZERO  TO 順番Ｗ.      
005410     MOVE 9          TO 作１−請求和暦.
005420     MOVE 99         TO 作１−請求年.
005430     MOVE 99         TO 作１−請求月.
005440     MOVE 99         TO 作１−保険区分.
005470     MOVE HIGH-VALUE TO 作１−保険番号.
005470     MOVE 9          TO 作１−保種.
005480     MOVE 9          TO 作１−本人家族キー.
005490     MOVE HIGH-VALUE TO 作１−患者カナ.
005500     MOVE 999999     TO 作１−患者番号.
005510     MOVE HIGH-VALUE TO 作１−枝番.
005520     MOVE 9          TO 作１−施術和暦.
005530     MOVE 99         TO 作１−施術年.
005540     MOVE 99         TO 作１−施術月.
005550     START 作業ファイル１ KEY IS <=  作１−請求和暦年月
005560                                     作１−保険区分
005590                                     作１−保険番号
005590                                     作１−保種
005600                                     作１−本人家族キー
005630                                     作１−患者カナ
005620                                     作１−患者コード
005630                                     作１−施術和暦年月
012370                                     REVERSED
005640     END-START.
005650     IF 状態キー = "00"
005660*
005670         MOVE SPACE TO 終了フラグ
006100         PERFORM 作業ファイル１読込
006110         PERFORM UNTIL 終了フラグ = "YES"
006120                 PERFORM 作２レコードセット
006130                 PERFORM 作２ファイル書込
006140                 PERFORM 作業ファイル１読込
006150         END-PERFORM
           END-IF.
006160
006040*================================================================*
006050 作業ファイル２作成 SECTION.
006060*
006070* 並び順作成
006080     MOVE SPACE TO 終了フラグ.
006090     MOVE ZERO  TO 順番Ｗ.      
006100     PERFORM 作業ファイル１読込.
006110     PERFORM UNTIL 終了フラグ = "YES"
006120             PERFORM 作２レコードセット
006130             PERFORM 作２ファイル書込
006140             PERFORM 作業ファイル１読込
006150     END-PERFORM.
006160
006170*================================================================*
006180 作２レコードセット SECTION.
006190*
           MOVE 作１−レセ種別       TO 作２−レセ種別.
006200     MOVE 作１−施術和暦       TO 作２−施術和暦.
006210     MOVE 作１−施術年         TO 作２−施術年.
006220     MOVE 作１−施術月         TO 作２−施術月.
006230     MOVE 作１−患者コード     TO 作２−患者コード.
006240     MOVE 作１−保険種別       TO 作２−保険種別
006250     MOVE 作１−患者氏名       TO 作２−患者氏名.
005210     MOVE 作１−被保険者氏名   TO 作２−被保険者氏名.
006260     IF 作１−本人家族区分 = 1
006270         MOVE "本人"         TO 作２−本人家族
006280     ELSE
006290         MOVE "家族"         TO 作２−本人家族
006300     END-IF.
007592     MOVE 作１−保険者番号     TO 保険者番号ＷＲ.
           IF 保険者番号ＷＲ(1:2) = "00"
              MOVE 保険者番号ＷＲ(3:8)  TO 作２−請求保険者番号
           ELSE
              MOVE 保険者番号ＷＲ    TO 作２−請求保険者番号
           END-IF.
           MOVE 作１−費用額         TO 作２−費用額.
           MOVE 作１−請求額         TO 作２−請求額.
           MOVE 作１−負担額         TO 作２−負担額.
005210     MOVE 作１−レセ印刷区分   TO 作２−レセ印刷区分.
           IF 作１−レセ種別 = 7
              MOVE 1                 TO 作２−保険区分
006670        COMPUTE 生保順番Ｗ  = 生保順番Ｗ + 1
006680        MOVE 生保順番Ｗ            TO 作２−順番
           ELSE
              MOVE ZERO              TO 作２−保険区分
006670        COMPUTE 順番Ｗ  = 順番Ｗ + 1
006680        MOVE 順番Ｗ            TO 作２−順番
           END-IF.
006330*
006340*================================================================*
006350 作２ファイル書込 SECTION.
006360*
006370     WRITE 作２−レコード
006380     INVALID KEY
006390         MOVE NC"作２"  TO ファイル名
006400         PERFORM エラー表示
006410     END-WRITE.
007900*================================================================*
007910 作業ファイル３作成 SECTION.
007920*
007930* 並び順作成
007940     MOVE SPACE TO 終了フラグ１.
007950     MOVE ZERO  TO 順番Ｗ.
007960     PERFORM 作業ファイル２読込.
007970     PERFORM UNTIL 終了フラグ１ = "YES"
007980         PERFORM 作３レコードセット
007990         PERFORM 作３ファイル書込
008000         PERFORM 作業ファイル２読込
008010     END-PERFORM.
008020
008030*================================================================*
008040 作３レコードセット SECTION.
008050*
008060     MOVE 作２−施術和暦       TO 作３−施術和暦.
008070     MOVE 作２−施術年         TO 作３−施術年.
008080     MOVE 作２−施術月         TO 作３−施術月.
008090     MOVE 作２−患者コード     TO 作３−患者コード.
008100     MOVE 作２−保険種別       TO 作３−保険種別.
008110     COMPUTE 順番Ｗ  = 順番Ｗ + 1.
008120     MOVE 順番Ｗ               TO 作３−順番.
008130*
008140*================================================================*
008150 作３ファイル書込 SECTION.
008160*
008170     WRITE 作３−レコード
008180     INVALID KEY
008190         MOVE NC"作３"  TO ファイル名
008200         PERFORM エラー表示
008210     END-WRITE.
006420*================================================================*
006430 作業ファイル１読込 SECTION.
006440*
006450     READ 作業ファイル１ NEXT
006460     AT END
006470         MOVE "YES" TO 終了フラグ
006480     END-READ.
006490*
006420*================================================================*
006430 作業ファイル２読込 SECTION.
006440*
006450     READ 作業ファイル２ NEXT
006460     AT END
006470         MOVE "YES" TO 終了フラグ１
006480     END-READ.
006490*
006500*================================================================*
006510 エラー表示 SECTION.
006520*
006530     DISPLAY NC"状態キー" 状態キー  UPON CONS.
006540     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
006550     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
006560     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
003321*-----------------------------------------*
003322     CALL "actcshm"  WITH C LINKAGE.
003323*-----------------------------------------*
006570     ACCEPT  キー入力 FROM CONS.
006580     PERFORM ファイル閉鎖.
006590     MOVE 99 TO PROGRAM-STATUS.
006600     EXIT PROGRAM.
006610*================================================================*
006620 エラー表示Ｒ SECTION.
006630*
006640     DISPLAY NC"ファイル読込エラー" ファイル名     UPON CONS.
006650     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
006660     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
003321*-----------------------------------------*
003322     CALL "actcshm"  WITH C LINKAGE.
003323*-----------------------------------------*
006670     ACCEPT  キー入力 FROM CONS.
006680     PERFORM ファイル閉鎖.
006690     MOVE 99 TO PROGRAM-STATUS.
006700     EXIT PROGRAM.
006710*================================================================*
007632 助成レセまとめ判定 SECTION.
007633*---------------------------------------------------------------------------*
009194* 本体まとめ区分＝１
007635* の時は、フラグYES (金額を助成込みで印字）
007636*（例：横浜市の障害は、本体保険（国保系）のレセプト１枚で請求、助成レセはなし）
007637*---------------------------------------------------------------------------*
007638*
007639     MOVE SPACE TO 助成レセまとめフラグ.
007640*
009201     IF レセ−本体まとめ区分 = 1 
009202        MOVE "YES" TO 助成レセまとめフラグ
009203     END-IF.
007697*
007699*================================================================*
006720******************************************************************
006730 END PROGRAM YHP6061.
006740******************************************************************
