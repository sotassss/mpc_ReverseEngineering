000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YHP581.
000060 AUTHOR.                 岡田 憲和
000070*
000080*----------------------------------------------------------------*
000090*      総括表【ﾃﾞｰﾀ作成】柔ｳｨﾝﾄﾞｳｽﾞ95版
000100* 請求年月Ver.
000110*
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2012-09-13
000140 DATE-COMPILED.          2012-09-13
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000490     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000500                             ORGANIZATION             IS  INDEXED
000510                             ACCESS MODE              IS  DYNAMIC
000520                             RECORD KEY               IS  受−施術和暦年月
000530                                                          受−患者コード
000540                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000550                                                          受−患者カナ
000560                                                          受−患者コード
000570                             ALTERNATE RECORD KEY     IS  受−患者コード
000580                                                          受−施術和暦年月
000590                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000600                                                          受−保険種別
000610                                                          受−保険者番号
000620                                                          受−患者コード
000630                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000640                                                          受−公費種別
000650                                                          受−費用負担者番号
000660                                                          受−患者コード
000670                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000680                                                          受−助成種別
000690                                                          受−費用負担者番号助成
000700                                                          受−患者コード
000710                             ALTERNATE RECORD KEY     IS  受−請求和暦年月
000720                                                          受−施術和暦年月
000730                                                          受−患者コード
000740                             FILE STATUS              IS  状態キー
000750                             LOCK        MODE         IS  AUTOMATIC.
000950*
000951     SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
000952                             ORGANIZATION             IS  INDEXED
000953                             ACCESS MODE              IS  DYNAMIC
000954                             RECORD KEY               IS  レセ−施術和暦年月
000955                                                          レセ−患者コード
000956                                                          レセ−レセ種別
000957                             ALTERNATE RECORD KEY     IS  レセ−患者コード
000958                                                          レセ−施術和暦年月
000959                                                          レセ−レセ種別
000960                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000961                                                          レセ−施術和暦年月
000962                                                          レセ−患者コード
000963                                                          レセ−レセ種別
000964                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000965                                                          レセ−レセ種別
000966                                                          レセ−請求保険者番号
000967                                                          レセ−患者コード
000968                                                          レセ−施術和暦年月
000969                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000970                                                          レセ−請求保険者番号
000971                                                          レセ−患者コード
000972                                                          レセ−レセ種別
000973                                                          レセ−施術和暦年月
000974                             FILE STATUS              IS  状態キー
000975                             LOCK        MODE         IS  AUTOMATIC.
000976     SELECT  作業ファイル１  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5801L.DAT"
000977                             ORGANIZATION             IS  INDEXED
000978                             ACCESS                   IS  DYNAMIC
000980                             RECORD      KEY          IS  作１−請求和暦年月
000990                                                          作１−施術和暦年月
001000                                                          作１−保険区分
001010                                                          作１−保険者番号
001020                                                          作１−本人家族キー
001030                                                          作１−患者カナ
001040                                                          作１−患者コード
001050                             FILE        STATUS       IS  状態キー
001060                             LOCK        MODE         IS  AUTOMATIC.
001070*
001080* レセ並び順用
001100     SELECT  作業ファイル２  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5912L.DAT"
001101                             ORGANIZATION             IS  INDEXED
001110                             ACCESS                   IS  DYNAMIC
001120                             RECORD      KEY          IS  作２−施術和暦年月
001130                                                          作２−患者コード
001140                                                          作２−保険種別
001150                             FILE        STATUS       IS  状態キー
001160                             LOCK        MODE         IS  AUTOMATIC.
001170******************************************************************
001180*                      DATA DIVISION                             *
001190******************************************************************
001200 DATA                    DIVISION.
001210 FILE                    SECTION.
001310*                           ［ＲＬ＝  ３２０］
001320 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001330     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
001435*
001436*                          ［ＲＬ＝  １５３６］
001437 FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS.
001438     COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
001439*
001440 FD  作業ファイル１ RECORD  CONTAINS 256 CHARACTERS.
001441 01  作１−レコード.
001442     03  作１−レコードキー.
001450         05  作１−請求和暦年月.
001460             07  作１−請求和暦            PIC 9.
001470             07  作１−請求年              PIC 9(2).
001480             07  作１−請求月              PIC 9(2).
001490         05  作１−施術和暦年月.
001500             07  作１−施術和暦            PIC 9.
001510             07  作１−施術年              PIC 9(2).
001520             07  作１−施術月              PIC 9(2).
001530         05  作１−保険区分                PIC 9(2).
001540         05  作１−保険者番号              PIC X(10).
001550         05  作１−本人家族キー            PIC 9.
001560         05  作１−患者カナ                PIC X(50).
001570         05  作１−患者コード.
001580             07 作１−患者番号             PIC 9(6).
001590             07 作１−枝番                 PIC X(1).
001600     03  作１−レコードデータ.
001610         05  作１−記号                    PIC X(24).
001620         05  作１−番号                    PIC X(30).
001630         05  作１−被保険者カナ            PIC X(50).
001640         05  作１−本人家族区分            PIC 9.
001650         05  作１−費用額                  PIC 9(7).
001660         05  作１−負担額                  PIC 9(7).
001670         05  作１−請求額                  PIC 9(7).
001680         05  作１−保険種別                PIC 9(2).
001690         05  作１−親保険種別              PIC 9(2).
001700         05  作１−老人レコード区分        PIC 9.
001710         05  作１−助成レコード区分        PIC 9.
001720         05  FILLER                        PIC X(44).
001730*
001740 FD  作業ファイル２ RECORD  CONTAINS 32 CHARACTERS.
001750 01  作２−レコード.
001760     03  作２−レコードキー.
001770         05  作２−施術和暦年月.
001780             07  作２−施術和暦            PIC 9.
001790             07  作２−施術年              PIC 9(2).
001800             07  作２−施術月              PIC 9(2).
001810         05  作２−患者コード.
001820             07 作２−患者番号             PIC 9(6).
001830             07 作２−枝番                 PIC X(1).
001840         05  作２−保険種別                PIC 9(2).
001850     03  作２−レコードデータ.
001860         05  作２−順番                    PIC 9(4).
001870         05  FILLER                        PIC X(14).
001880*
001890*----------------------------------------------------------------*
001900******************************************************************
001910*                WORKING-STORAGE SECTION                         *
001920******************************************************************
001930 WORKING-STORAGE         SECTION.
001940 01 キー入力                           PIC X    VALUE SPACE.
001950 01 状態キー                           PIC X(2) VALUE SPACE.
001960 01 終了フラグ                         PIC X(3) VALUE SPACE.
001970 01 終了フラグ２                       PIC X(3) VALUE SPACE.
001980 01 実行キーＷ                         PIC X(3)  VALUE SPACE.
001990 01 施術記録有Ｗ                       PIC X(3) VALUE SPACE.
002000 01 カウンタ                           PIC 9(2)  VALUE ZERO.
002010**
002020 01 保険種別ＷＲ                       PIC 9(2) VALUE ZERO.
002030 01 印刷形式ＷＲ                       PIC 9    VALUE ZERO.
002040 01 保険者番号ＷＲ                     PIC X(10) VALUE SPACE.
002050 01 レセプト種類ＷＲ                   PIC X(4) VALUE SPACE.
002060 01 本人家族区分ＷＲ                   PIC 9    VALUE ZERO.
002070 01 施術和暦年月ＷＲ.
002080    03 施術和暦ＷＲ                    PIC 9    VALUE ZERO.
002090    03 施術年ＷＲ                      PIC 9(2) VALUE ZERO.
002100    03 施術月ＷＲ                      PIC 9(2) VALUE ZERO.
002110 01 請求和暦年月ＷＲ.
002120    03 請求和暦ＷＲ                    PIC 9    VALUE ZERO.
002130    03 請求年ＷＲ                      PIC 9(2) VALUE ZERO.
002140    03 請求月ＷＲ                      PIC 9(2) VALUE ZERO.
002150**
002160 01 順番Ｗ                             PIC 9(4) VALUE ZERO.
002170**
002180 01 ファイル名                         PIC N(8) VALUE SPACE.
002190 01 保険種別Ｗ                         PIC 9(2) VALUE ZERO.
002200*
002210** 合計集計用
002220 01 合計Ｗ.
002230    03 件数合計Ｗ                      PIC 9(4) VALUE ZERO.
002240    03 費用額合計Ｗ                    PIC 9(8) VALUE ZERO.
002250    03 請求額合計Ｗ                    PIC 9(8) VALUE ZERO.
002260** エラーメッセージ用
002270 01 エラーメッセージＷ.
002280    03 エラー患者コードＷ              PIC X(7) VALUE SPACE.
002290    03 エラー区切りＷ                  PIC X(1) VALUE SPACE.
002300    03 エラー保険種別Ｗ                PIC X(2) VALUE SPACE.
002310    03 FILLER                          PIC X(10) VALUE SPACE.
002320*
002330**
002331 01 複合プログラム名Ｗ                 PIC X(8) VALUE "MOJI2".
002332*
002333** 保険者番号右詰め用
002340 01 保険者番号ＷＴ.
002350    03 保険者番号左詰めＷ.
002360      05 保険者番号左詰めＷ１          PIC X OCCURS 8 VALUE SPACE.
002370    03 保険者番号右詰めＷ.
002380      05 保険者番号右詰めＷ１          PIC X OCCURS 8 VALUE ZERO.
002390    03 保険者番号数字Ｗ                PIC 9(8)  VALUE ZERO.
002400    03 保険者番号Ｗ                    PIC X(8)  VALUE SPACE.
002410**
002420 01 計算機西暦年Ｗ                     PIC 9(2).
002430* 日付ＷＯＲＫ
002440 01 計算機西暦.
002450    03 計算機西暦年                    PIC 9(4).
002460    03 計算機西暦月日                  PIC 9(4).
002470 01 計算機西暦Ｒ REDEFINES 計算機西暦.
002480    03 計算機世紀                      PIC 9(2).
002490    03 計算機日付                      PIC 9(6).
002500    03 計算機日付Ｒ REDEFINES 計算機日付.
002510       05 計算機年月                   PIC 9(4).
002520       05 計算機年月Ｒ REDEFINES 計算機年月.
002530         07 計算機年                   PIC 9(2).
002540         07 計算機月                   PIC 9(2).
002550       05 計算機日                     PIC 9(2).
002560*
002570** 助成レセまとめ用
002580 01 助成レセまとめフラグ               PIC X(3)  VALUE SPACE.
002590*
002600******************************************************************
002610*                          連結項目                              *
002620******************************************************************
002630*
002640********************
002650* メッセージ表示キー *
002660********************
002670 01 連メ３−キー IS EXTERNAL.
002680    03  連メ３−メッセージ             PIC N(20).
002690    03  連メ３−メッセージ１           PIC X(20).
002700***
002710 01 連入−画面情報ＹＨＰ５８０ IS EXTERNAL.
002720    03 連入−請求和暦年月.
002730       05 連入−請求和暦               PIC 9.
002740       05 連入−請求年月.
002750         07 連入−請求年               PIC 9(2).
002760         07 連入−請求月               PIC 9(2).
003090*
003100************************
003110* 助成レセまとめ
003120************************
003130 01 連レセまとめ−キー IS EXTERNAL.
003140    03 連レセまとめ−施術和暦年月.
003150       05 連レセまとめ−施術和暦               PIC 9.
003160       05 連レセまとめ−施術年月.
003170          07 連レセまとめ−施術年              PIC 9(2).
003180          07 連レセまとめ−施術月              PIC 9(2).
003190    03 連レセまとめ−患者コード.
003200       05 連レセまとめ−患者番号               PIC 9(6).
003210       05 連レセまとめ−枝番                   PIC X(1).
003220**-------------------------------------------------------**
003230*   1:助成レセプトなしの本体まとめの判定
003240*   2:横浜・川崎用の社保助成レセかの判定
003250    03 連レセまとめ−判定区分                  PIC 9.
003260**-------------------------------------------------------**
003270*  / OUT /　 0:対象外、1:対象
003280    03 連レセまとめ−判定結果                  PIC 9.
003290**
003300**
003301* 暗号複合用
003302 01 連暗号複合−暗号情報 IS EXTERNAL.
003303    03 連暗号複合−入力情報.
003304       05 連暗号複合−記号               PIC X(24).
003305       05 連暗号複合−番号               PIC X(30).
003306       05 連暗号複合−暗号化項目.
003307          07 連暗号複合−暗号患者番号    PIC X(6).
003308          07 連暗号複合−暗号判定記号    PIC X.
003309          07 連暗号複合−暗号判定番号    PIC X.
003310          07 連暗号複合−暗号記号        PIC X(24).
003311          07 連暗号複合−暗号番号        PIC X(30).
003312    03 連暗号複合−出力情報.
003313       05 連暗号複合−複合した記号       PIC X(24).
003314       05 連暗号複合−複合した番号       PIC X(30).
003315*
003316******************************************************************
003320*                      PROCEDURE  DIVISION                       *
003330******************************************************************
003340 PROCEDURE               DIVISION.
003350************
003360*           *
003370* 初期処理   *
003380*           *
003390************
003400     PERFORM 初期化.
003410************
003420*           *
003430* 主処理     *
003440*           *
003450************
003460     PERFORM 作業ファイル作成.
003470************
003480*           *
003490* 終了処理   *
003500*           *
003510************
003520     PERFORM 終了処理.
003530     MOVE ZERO TO PROGRAM-STATUS.
003540     EXIT PROGRAM.
003550*
003560*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003570*================================================================*
003580 初期化 SECTION.
003590*================================================================*
003600*
003610     PERFORM ファイルオープン.
003620* 連結項目の待避
003630     MOVE 連入−請求和暦      TO 請求和暦ＷＲ.
003640     MOVE 連入−請求年        TO 請求年ＷＲ.
003650     MOVE 連入−請求月        TO 請求月ＷＲ.
003660*
003670*================================================================*
003680 ファイルオープン SECTION.
003690*
003790     OPEN INPUT 受診者情報Ｆ.
003800         MOVE NC"受診者情報Ｆ" TO ファイル名.
003810         PERFORM オープンチェック.
003880     OPEN INPUT レセプトＦ.
003881         MOVE NC"市町村" TO ファイル名.
003882         PERFORM オープンチェック.
003883*================================================================*
003890 オープンチェック SECTION.
003900*
003910     IF 状態キー  NOT =  "00"
003920         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
003930         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003940         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003950                                                 UPON CONS
003960         ACCEPT  キー入力 FROM CONS
003970         PERFORM ファイル閉鎖
003980         MOVE 99 TO PROGRAM-STATUS
003990         EXIT PROGRAM.
004000*================================================================*
004010 終了処理 SECTION.
004020*================================================================*
004030*
004040     PERFORM ファイル閉鎖.
004050*================================================================*
004060 ファイル閉鎖 SECTION.
004070*
004080     CLOSE 受診者情報Ｆ レセプトＦ.
004100*================================================================*
004110 作業ファイル作成 SECTION.
004120*================================================================*
004130*
004140     OPEN OUTPUT 作業ファイル１.
004150         MOVE NC"作１" TO ファイル名.
004160         PERFORM オープンチェック.
004170*
004180     PERFORM 作業ファイル１作成.
004190*
004200     CLOSE 作業ファイル１.
004210*
004220     OPEN OUTPUT 作業ファイル２.
004230         MOVE NC"作２" TO ファイル名.
004240         PERFORM オープンチェック.
004250     OPEN INPUT  作業ファイル１.
004260         MOVE NC"作１" TO ファイル名.
004270         PERFORM オープンチェック.
004280*
004290* 並び順用ファイル作成
004300     PERFORM 作業ファイル２作成.
004310*
004320     CLOSE 作業ファイル１ 作業ファイル２.
004330*
004340*================================================================*
004350*================================================================*
004360 作業ファイル１作成 SECTION.
004370**********************************************************************
004380**   受診者情報Ｆから、該当請求年月のデータを抽出し、
004390**   作業ファイル１に書き出す.
004400**********************************************************************
004410*
004630     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
004631     MOVE 請求年ＷＲ    TO レセ−請求年.
004632     MOVE 請求月ＷＲ    TO レセ−請求月.
004633     MOVE 1             TO レセ−施術和暦.
004634     MOVE ZERO          TO レセ−施術年.
004635     MOVE ZERO          TO レセ−施術月.
004636     MOVE ZERO          TO レセ−患者番号.
004637     MOVE LOW-VALUE     TO レセ−枝番.
004638     MOVE ZERO          TO レセ−レセ種別.
004639     START レセプトＦ   KEY IS >= レセ−請求和暦年月
004640                                  レセ−施術和暦年月
004641                                  レセ−患者コード
004642                                  レセ−レセ種別
004643     END-START.
004644     IF 状態キー = "00"
004645         MOVE SPACE  TO 終了フラグ
004646         PERFORM レセプトＦ読込
004647         PERFORM UNTIL ( 終了フラグ = "YES" ) OR
004648                       ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
004649                       ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
004650                       ( レセ−請求月   NOT = 請求月ＷＲ   )
004651*          データチェックで受診者Ｆも読む
004652           PERFORM データチェック
004655*
004656** 労災・自賠責・自由は対象外
004657           IF  受−保険種別 = 70 OR 80 OR 90
004660               MOVE SPACE  TO 実行キーＷ
004670           END-IF
004680*
004690*↓* 特別処理（助成レセまとめ）/101109
004700           IF 受−助成種別 NOT = ZERO
004710               PERFORM 助成レセまとめ判定
004720           ELSE
004730               MOVE SPACE TO 助成レセまとめフラグ
004740           END-IF
004751*↑*
004760
004761           IF  実行キーＷ = "YES"
004771*           ********
004772*           * 健保 *
004773*           ********
004774              IF レセ−レセ種別   = 1
004775*             **********************
004776*             * 作業ファイル１作成 *
004777*             **********************
004780*   / 助成なし・助成あり 親ﾚｺｰﾄﾞ /
004781                 MOVE SPACE TO 作１−レコード
004782                 INITIALIZE 作１−レコード
004783                 MOVE 受−保険種別       TO 作１−保険種別
004784                 MOVE 受−保険者番号     TO 作１−保険者番号
004785                 MOVE 受−本人家族区分   TO 作１−本人家族キー
004786                 MOVE レセ−合計         TO 作１−費用額
004787                 MOVE レセ−一部負担金   TO 作１−負担額
004788                 MOVE レセ−請求金額     TO 作１−請求額
004797                 PERFORM 保険区分取得
004798                 PERFORM 作１レコードセット
004799                 PERFORM 作１ファイル書込
004811              END-IF
004812*           ********
004813*           * 老人 *
004814*           ********
004815              IF レセ−レセ種別   = 2
004816*             **********************
004817*             * 作業ファイル１作成 *
004818*             **********************
004830                 MOVE SPACE TO 作１−レコード
004831                 INITIALIZE 作１−レコード
004832                 MOVE 受−公費種別        TO 作１−保険種別
004833                 MOVE 受−費用負担者番号  TO 作１−保険者番号
004834                 MOVE  1                  TO 作１−本人家族キー
004835                 MOVE  1                  TO 作１−老人レコード区分
004839                 MOVE レセ−合計          TO 作１−費用額
004845                 MOVE レセ−一部負担金    TO 作１−負担額
004846                 MOVE レセ−請求金額      TO 作１−請求額
004847                 PERFORM 保険区分取得
004848                 PERFORM 作１レコードセット
004849                 PERFORM 作１ファイル書込
004850              END-IF
004851*           ********
004852*           * 助成 *
004853*           ********
004854              IF レセ−レセ種別   = 3
004857                 IF ( 助成レセまとめフラグ = SPACE )
004864                     MOVE SPACE TO 作１−レコード
004865                     INITIALIZE 作１−レコード
004866                     MOVE 受−助成種別            TO 作１−保険種別
004867                     MOVE 受−公費種別            TO 作１−親保険種別
004868                     MOVE 受−費用負担者番号助成  TO 作１−保険者番号
004869                     MOVE  1                      TO 作１−本人家族キー
004870                     MOVE  1                      TO 作１−助成レコード区分
004871*                    後期高齢の場合費用額を強制的にゼロセット。
004872*                    本来はちゃんと金額をセットすべきだが会提出データに合わせるための処置
004873                     MOVE ZERO                    TO 作１−費用額
004874                     MOVE レセ−受給者負担額      TO 作１−負担額
004876                     MOVE レセ−助成請求金額      TO 作１−請求額
004879                     PERFORM 助成保険区分取得
004880                     PERFORM 作１レコードセット
004881                     PERFORM 作１ファイル書込
004894                 END-IF
004895              END-IF
005730**
005740           END-IF
005741           PERFORM レセプトＦ読込
005750        END-PERFORM
005760     END-IF.
005770**
005870*================================================================*
005871 データチェック SECTION.
005872*
005873     MOVE SPACE          TO 実行キーＷ.
005874* *****************************************************************
005875* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
005876* *****************************************************************
005877     IF ( レセ−請求対象区分 NOT = ZERO ) AND
005878        ( レセ−償還払い区分 NOT = 2 OR 3 )
005879        IF (レセ−レセ種別 = 3) AND ( レセ−会総括表印刷対象区分 = 1 )
005880            CONTINUE
005881        ELSE
005882           MOVE レセ−施術和暦  TO 受−施術和暦
005883           MOVE レセ−施術年    TO 受−施術年
005884           MOVE レセ−施術月    TO 受−施術月
005885           MOVE レセ−患者番号  TO 受−患者番号
005886           MOVE レセ−枝番      TO 受−枝番
005887           READ 受診者情報Ｆ
005888           NOT INVALID KEY
005889**      健保のみ
005890              IF 受−保険分類 = 1
005891                 MOVE "YES"  TO 実行キーＷ
005892              END-IF
005893           END-READ
005894        END-IF
005895     END-IF.
005896*
005897*================================================================*
005898 レセプトＦ読込 SECTION.
005899*
005900     READ レセプトＦ NEXT
005901     AT END
005902         MOVE "YES" TO 終了フラグ
005903     END-READ.
005904*
006720*================================================================*
006730 作１レコードセット SECTION.
006740*================================================================*
006750*
006762*-----------------------------------------------------------------*
006763     MOVE SPACE TO 連暗号複合−暗号情報.
006764*
006765*    / 連暗号複合−入力情報セット /
006766     MOVE 受−記号       TO 連暗号複合−記号.
006767     MOVE 受−番号       TO 連暗号複合−番号.
006768     MOVE 受−暗号化項目 TO 連暗号複合−暗号化項目.
006769*
006770     CALL   複合プログラム名Ｗ.
006771     CANCEL 複合プログラム名Ｗ.
006772*
006773*-----------------------------------------------------------------*
006774
006775     MOVE レセ−請求和暦     TO 作１−請求和暦.
006776     MOVE レセ−請求年       TO 作１−請求年.
006780     MOVE レセ−請求月       TO 作１−請求月.
006790     MOVE 受−施術和暦       TO 作１−施術和暦.
006800     MOVE 受−施術年         TO 作１−施術年.
006810     MOVE 受−施術月         TO 作１−施術月.
006820     MOVE 受−患者カナ       TO 作１−患者カナ.
006830     MOVE 受−患者コード     TO 作１−患者コード.
006840     MOVE 受−被保険者カナ   TO 作１−被保険者カナ.
006850     MOVE 受−本人家族区分   TO 作１−本人家族区分.
006860* 老人・助成の時は、受益者番号を番号に、記号は空白
006870     IF  作１−老人レコード区分 = 1
006880         MOVE SPACE               TO 作１−記号
006890         MOVE 受−受益者番号老人  TO 作１−番号
006900     ELSE
006910         IF 作１−助成レコード区分 = 1
006920            MOVE SPACE               TO 作１−記号
006930            MOVE 受−受益者番号助成  TO 作１−番号
006940         ELSE
006950            MOVE 連暗号複合−複合した記号 TO 作１−記号
006960            MOVE 連暗号複合−複合した番号 TO 作１−番号
006970         END-IF
006980     END-IF.
006990*
007000*================================================================*
007010 保険区分取得 SECTION.
007020*================================================================*
007030*******************************************************************
007040* 出力順：1.社保(02) --> 2.国保(01) --> 3.船員(07) --> 4.日雇(06) *
007050*        --> 5.組合(03) --> 6.老人(05) --> 7.共済(04)・自衛官(09) *
007060*        --> 8.退職(08) --> 9.福祉(50.51.52.53.54.55.60)          *
007070*******************************************************************
007080**-- 老人 --
007090      IF 受−公費種別 = 05
007100         MOVE 06    TO 作１−保険区分
007110      ELSE
007120**-- 健保 --
007130         EVALUATE 受−保険種別
007140* 国保
007150         WHEN 01
007160             MOVE 02 TO 作１−保険区分
007170* 社保
007180         WHEN 02
007190             MOVE 01 TO 作１−保険区分
007200* 組合
007210         WHEN 03
007220             MOVE 05 TO 作１−保険区分
007230* 共済・自衛官
007240         WHEN 04
007250         WHEN 09
007260             MOVE 07 TO 作１−保険区分
007270* 日雇
007280         WHEN 06
007290             MOVE 04 TO 作１−保険区分
007300* 船員
007310         WHEN 07
007320             MOVE 03 TO 作１−保険区分
007330* 退職
007340         WHEN 08
007350             MOVE 08 TO 作１−保険区分
007360         WHEN OTHER
007370             MOVE SPACE                TO エラーメッセージＷ
007380             MOVE 受−患者コード       TO エラー患者コードＷ
007390             MOVE 受−保険種別         TO エラー保険種別Ｗ
007400             MOVE "-"                  TO エラー区切りＷ
007410             MOVE  NC"保険種別が異常です。患者−保険種別" TO 連メ３−メッセージ
007420             MOVE  エラーメッセージＷ  TO 連メ３−メッセージ１
007430             CALL   "MSG003"
007440             CANCEL "MSG003"
007450         END-EVALUATE
007460      END-IF.
007470*
007480*================================================================*
007490 助成保険区分取得 SECTION.
007500*================================================================*
007510*
007520         EVALUATE 受−助成種別
007530* 生保
007540         WHEN 50
007550* ４１老人
007560         WHEN 51
007570* ひとり親
007580         WHEN 52
007590* 身障
007600         WHEN 53
007610* 被爆
007620         WHEN 54
007630* 乳幼児
007640         WHEN 55
007650* その他
007660         WHEN 60
007670             MOVE 09     TO 作１−保険区分
007680         WHEN OTHER
007690             MOVE SPACE                TO エラーメッセージＷ
007700             MOVE 受−患者コード       TO エラー患者コードＷ
007710             MOVE 受−助成種別         TO エラー保険種別Ｗ
007720             MOVE "-"                  TO エラー区切りＷ
007730             MOVE  NC"助成種別が異常です。患者−助成種別" TO 連メ３−メッセージ
007740             MOVE  エラーメッセージＷ  TO 連メ３−メッセージ１
007750             CALL   "MSG003"
007760             CANCEL "MSG003"
007770         END-EVALUATE.
007780*
007790*================================================================*
007800 作１ファイル書込 SECTION.
007810*================================================================*
007820*
007830     WRITE 作１−レコード.
007840     IF 状態キー  NOT =  "00"
007850         MOVE NC"作１"  TO ファイル名
007860         PERFORM エラー表示
007870     END-IF.
007880*
007890*================================================================*
007900*================================================================*
007910 作業ファイル２作成 SECTION.
007920*
007930* 並び順作成
007940     MOVE SPACE TO 終了フラグ.
007950     MOVE ZERO  TO 順番Ｗ.
007960     PERFORM 作業ファイル１読込.
007970     PERFORM UNTIL 終了フラグ = "YES"
007980             PERFORM 作２レコードセット
007990             PERFORM 作２ファイル書込
008000             PERFORM 作業ファイル１読込
008010     END-PERFORM.
008020
008030*================================================================*
008040 作２レコードセット SECTION.
008050*
008060     MOVE 作１−施術和暦       TO 作２−施術和暦.
008070     MOVE 作１−施術年         TO 作２−施術年.
008080     MOVE 作１−施術月         TO 作２−施術月.
008090     MOVE 作１−患者コード     TO 作２−患者コード.
008100     MOVE 作１−保険種別       TO 作２−保険種別.
008110     COMPUTE 順番Ｗ  = 順番Ｗ + 1.
008120     MOVE 順番Ｗ               TO 作２−順番.
008130*
008140*================================================================*
008150 作２ファイル書込 SECTION.
008160*
008170     WRITE 作２−レコード
008180     INVALID KEY
008190         MOVE NC"作２"  TO ファイル名
008200         PERFORM エラー表示
008210     END-WRITE.
008220*================================================================*
008230 作業ファイル１読込 SECTION.
008240*
008250     READ 作業ファイル１ NEXT
008260     AT END
008270         MOVE "YES" TO 終了フラグ
008280     END-READ.
008290*
008300*================================================================*
008310*================================================================*
008320 エラー表示 SECTION.
008330*
008340     DISPLAY NC"状態キー" 状態キー  UPON CONS.
008350     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
008360     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
008370     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
008380     ACCEPT  キー入力 FROM CONS.
008390     PERFORM ファイル閉鎖.
008400     MOVE 99 TO PROGRAM-STATUS.
008410     EXIT PROGRAM.
008420*================================================================*
008430 エラー表示Ｒ SECTION.
008440*
008450     DISPLAY NC"ファイル読込エラー" ファイル名     UPON CONS.
008460     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
008470     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
008480     ACCEPT  キー入力 FROM CONS.
008490     PERFORM ファイル閉鎖.
008500     MOVE 99 TO PROGRAM-STATUS.
008510     EXIT PROGRAM.
009190*================================================================*
009191 助成レセまとめ判定 SECTION.
009192*---------------------------------------------------------------------------*
009193* 市町村マスタを読み、レセまとめ区分＝１でかつ、本体保険が国保・退職
009194* の時は、フラグYES (金額を助成込みで印字）
009195*（例：横浜市の障害は、本体保険（国保系）のレセプト１枚で請求、助成レセはなし）
009196*---------------------------------------------------------------------------*
009197*
009198     MOVE SPACE TO 助成レセまとめフラグ.
009199*
009200     IF レセ−本体まとめ区分 = 1
009201        MOVE "YES" TO 助成レセまとめフラグ
009202     END-IF.
009203*
009205*================================================================*
009206******************************************************************
009207 END PROGRAM YHP581.
009210******************************************************************
