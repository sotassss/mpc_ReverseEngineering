000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YIW722.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*         施術明細（柔+ｳｨﾝﾄﾞｳｽﾞ版）
000100*         MED = YIW720 YIW722P
000110*----------------------------------------------------------------*
000120 DATE-WRITTEN.           2015-09-16
000130 DATE-COMPILED.          2015-09-16
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000260     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000270                             ORGANIZATION             IS  INDEXED
000280                             ACCESS MODE              IS  DYNAMIC
000290                             RECORD KEY               IS  元−元号区分
000300                             FILE STATUS              IS  状態キー
000310                             LOCK        MODE         IS  AUTOMATIC.
000320     SELECT  制御情報マスタ  ASSIGN      TO        SEIGYOL
000330                             ORGANIZATION             IS  INDEXED
000340                             ACCESS MODE              IS  DYNAMIC
000350                             RECORD KEY               IS  制−制御区分
000360                             FILE STATUS              IS  状態キー
000370                             LOCK        MODE         IS  AUTOMATIC.
000380     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000390                             ORGANIZATION             IS  INDEXED
000400                             ACCESS MODE              IS  DYNAMIC
000410                             RECORD KEY               IS 受−施術和暦年月
000420                                                          受−患者コード
000430                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000440                                                          受−患者カナ
000450                                                          受−患者コード
000460                             ALTERNATE RECORD KEY     IS  受−患者コード
000470                                                         受−施術和暦年月
000480                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000490                                                          受−保険種別
000500                                                          受−保険者番号
000510                                                          受−患者コード
000520                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000530                                                          受−公費種別
000540                                                     受−費用負担者番号
000550                                                          受−患者コード
000560                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000570                                                          受−助成種別
000580                                                  受−費用負担者番号助成
000590                                                          受−患者コード
000600                             ALTERNATE RECORD KEY  IS 受−請求和暦年月
000610                                                      受−施術和暦年月
000620                                                      受−患者コード
000630                             FILE STATUS              IS  状態キー
000640                             LOCK        MODE         IS  AUTOMATIC.
000530     SELECT  作業ファイル１  ASSIGN      TO "C:\MAKISHISYS\YAWOBJ\TEMP\W7211L.DAT"
000540                             ORGANIZATION             IS  INDEXED
000550                             ACCESS                   IS  DYNAMIC
                                   RECORD KEY               IS  作１−施術和暦年月日
000620                             FILE        STATUS       IS  状態キー
000630                             LOCK        MODE         IS  AUTOMATIC.
000720     SELECT  印刷ファイル    ASSIGN      TO     GS-PRTF002
000730                             SYMBOLIC    DESTINATION  IS "PRT"
000740                             FORMAT                   IS  定義体名Ｐ
000750                             GROUP                    IS  項目群名Ｐ
000760                             PROCESSING  MODE         IS  処理種別Ｐ
000770                             UNIT        CONTROL      IS  拡張制御Ｐ
000780                             FILE        STATUS       IS  通知情報Ｐ.
000790******************************************************************
000800*                      DATA DIVISION                             *
000810******************************************************************
000820 DATA                    DIVISION.
000830 FILE                    SECTION.
000840*                           ［ＲＬ＝  １２８］
000850 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
000860     COPY GENGOU          OF  XFDLIB  JOINING   元   AS  PREFIX.
000870*                           ［ＲＬ＝  ２５６］
000880 FD  制御情報マスタ          BLOCK   CONTAINS   1   RECORDS.
000890     COPY SEIGYO          OF  XFDLIB  JOINING   制   AS  PREFIX.
000910*                           ［ＲＬ＝  ３２０］
000920 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
000930     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
001310*****************
001320* 作業ファイル１ *
001330*****************
001340*                         ［ＲＬ＝  １６０］
001350 FD  作業ファイル１ RECORD  CONTAINS 160 CHARACTERS.
001360 01 作１−レコード.
001370    03 作１−レコードキー.
001535       05 作１−施術和暦年月日.
001536          07 作１−施術和暦                PIC 9.
001537          07 作１−施術年月.
001538             09 作１−施術年               PIC 9(2).
001539             09 作１−施術月               PIC 9(2).
001540          07 作１−施術日                  PIC 9(2).
001490    03 作１−レコードデータ.
             05 作１−料金.
001550          07 作１−初検時間.
                   09 作１−初検時               PIC 9(2).
                   09 作１−初検分               PIC 9(2).
001550          07 作１−初検料                  PIC 9(5).
001550          07 作１−施療料                  PIC 9(5).
001550          07 作１−再検料                  PIC 9(5).
001550          07 作１−後療料                  PIC 9(5).
001550          07 作１−往療料                  PIC 9(5).
001550          07 作１−罨法料                  PIC 9(5).
001550          07 作１−電療料                  PIC 9(5).
001551          07 作１−費用額                  PIC 9(5).
001551          07 作１−一部負担金              PIC 9(5).
001551       05 作１−コメント                   PIC X(100).
001470       05 作１−初検                       PIC 9(1).
001500       05 FILLER                           PIC X(3).
000920*
001160 FD  印刷ファイル.
001170     COPY YIW722P        OF  XMDLIB.
001180*----------------------------------------------------------------*
001190******************************************************************
001200*                WORKING-STORAGE SECTION                         *
001210******************************************************************
001220 WORKING-STORAGE         SECTION.
001230 01 キー入力                           PIC X     VALUE SPACE.
001240 01 状態キー                           PIC X(2)  VALUE SPACE.
001250 01 終了フラグ                         PIC X(3)  VALUE SPACE.
001260 01 終了フラグ２                       PIC X(3)  VALUE SPACE.
001270 01 初検フラグ                         PIC X(3)  VALUE SPACE.
001280 01 ファイル名                         PIC N(6)  VALUE SPACE.
001290 01 レセプトＰＧＷ                     PIC X(8)  VALUE SPACE.
001300 01 前和暦Ｗ                           PIC 9     VALUE ZERO.
001310 01 カレント元号Ｗ                     PIC 9(1)  VALUE ZERO.
001460 01 印刷位置ＣＮＴ                     PIC 9(2)  VALUE ZERO.
001330 01 カウンタ                           PIC 9(2)  VALUE ZERO.
001330 01 カウンタ１                         PIC 9(2)  VALUE ZERO.
001340 01 患者番号Ｗ                         PIC 9(6)  VALUE ZERO.
001240 01 行カウンタ                         PIC 9(2) VALUE 0.
001580 01 オープンフラグ                     PIC X(3) VALUE SPACE.
001350**
001360 01 遅延フラグ                         PIC X(3) VALUE SPACE.
001370 01 遅延回数Ｗ                         PIC 9(4) VALUE ZERO.
001380 01 遅延ＣＮＴ                         PIC 9(5) VALUE ZERO.
001390**
001360 01 合計Ｗ.
001380    03 初検料計Ｗ                      PIC 9(5) VALUE ZERO.
001390    03 整復料計Ｗ                      PIC 9(5) VALUE ZERO.
001400    03 後療料計Ｗ                      PIC 9(5) VALUE ZERO.
001400    03 罨法料計Ｗ                      PIC 9(5) VALUE ZERO.
001400    03 電療料計Ｗ                      PIC 9(5) VALUE ZERO.
001400    03 負担金計Ｗ                      PIC 9(5) VALUE ZERO.
001400*
001360 01 料金Ｗ.
001380    03 初検料Ｗ                        PIC 9(5) VALUE ZERO.
001390    03 整復料Ｗ                        PIC 9(5) VALUE ZERO.
001400    03 後療料Ｗ                        PIC 9(5) VALUE ZERO.
001400    03 罨法料Ｗ                        PIC 9(5) VALUE ZERO.
001400    03 電療料Ｗ                        PIC 9(5) VALUE ZERO.
001400    03 負担金Ｗ                        PIC 9(5) VALUE ZERO.
      *
       01 その他Ｗ.
001380    03 相談料Ｗ                        PIC ZZZZ.
001390    03 明細料Ｗ                        PIC ZZZ.
001410****************
001420* 連結項目待避 *
001430****************
001440*    ************
001450*    * 印刷キー *
001460*    ************
001470 01 対象データＷＲ.
001480    03 施術和暦年月ＷＲ.
001490       05 施術和暦ＷＲ                  PIC 9(1)  VALUE ZERO.
001500       05 施術年ＷＲ                    PIC 9(2)  VALUE ZERO.
001510       05 施術月ＷＲ                    PIC 9(2)  VALUE ZERO.
001520    03 開始日ＷＲ                       PIC 9(2)  VALUE ZERO.
001530    03 終了日ＷＲ                       PIC 9(2)  VALUE ZERO.
001550    03 患者コードＷ.
001560       05 患者番号Ｗ                    PIC 9(6)  VALUE ZERO.
001570       05 枝番Ｗ                        PIC X(1)  VALUE SPACE.
001580    03 印刷モードＦＷＲ                 PIC 9(1)  VALUE ZERO.
       01 コメントＷ.
          03 コメント１Ｗ                       PIC X(50) VALUE SPACE.
          03 コメント２Ｗ                       PIC X(50) VALUE SPACE.
001831* POWER COBOL用
001832 01 dll-name  PIC X(260)  VALUE SPACE.
001833 01 form-name PIC X(14)   VALUE SPACE.
002210*******************************************************************
002220 01 印刷制御.
002230     03 定義体名Ｐ                     PIC X(8) VALUE SPACE.
002240     03 項目群名Ｐ                     PIC X(8) VALUE SPACE.
002250     03 処理種別Ｐ                     PIC X(2) VALUE SPACE.
002260     03 拡張制御Ｐ.
002270         05 端末制御Ｐ.
002280             07 移動方向Ｐ             PIC X(1) VALUE SPACE.
002290             07 移動行数Ｐ             PIC 9(3) VALUE ZERO.
002300         05 詳細制御Ｐ                 PIC X(2) VALUE SPACE.
002310     03 通知情報Ｐ                     PIC X(2) VALUE SPACE.
002320     03 ユニット名Ｐ                   PIC X(8) VALUE SPACE.
002330*
002500******************************************************************
002510*                          連結項目                              *
002520******************************************************************
002510**********************
002520* メッセージ表示キー *
002530**********************
002540 01 連メ−キー IS EXTERNAL.
002550    03  連メ−メッセージ                 PIC N(20).
002560*
002870************
002880* 印刷キー *
002890************
003110 01 連入−入力データＹＩＷ７２０ IS EXTERNAL.
          03 連入−施術和暦年月.
             05 連入−施術和暦                  PIC 9(1).
             05 連入−施術年                    PIC 9(2).
             05 連入−施術月                    PIC 9(2).
          03 連入−開始日付.
             05 連入−開始日                    PIC 9(2).
          03 連入−終了日付.
             05 連入−終了日                    PIC 9(2).
          03 連入−患者コード.
             05 連入−患者番号                  PIC 9(6).
             05 連入−枝番                      PIC X(1).
          03 連入−印刷モードＦ                 PIC 9(1).
          03 連入−氏名モード                   PIC 9(1).
          03 連入−年月モード                   PIC 9(1).
          03 連入−日ごとモード                 PIC 9(1).
          03 連入−コメントモード               PIC 9(1).
          03 連入−合計モード                   PIC 9(1).
          03 連入−印刷段数                     PIC 9(2).
      */連続時の氏名印刷の有無0302
          03 連入−処理モード                   PIC X(4).
      *
002830 01 連印−合計データＹＩＷ７２０ IS EXTERNAL.
          03 連印−患者氏名                     PIC X(50).
          03 連印−初検料                       PIC 9(6).
          03 連印−後療料                       PIC 9(6).
          03 連印−往療料                       PIC 9(6).
          03 連印−罨法料                       PIC 9(6).
          03 連印−電療料                       PIC 9(6).
          03 連印−費用額                       PIC 9(7).
          03 連印−負担額                       PIC 9(7).
          03 連印−情報提供料                   PIC 9(6).
          03 連印−大                           PIC N(1).
          03 連印−中                           PIC N(1).
          03 連印−小                           PIC N(1).
          03 連印−金属副子料                   PIC 9(6).
          03 連印−その他                       PIC 9(6).
          03 連印−相談料                       PIC 9(4).
          03 連印−明細料                       PIC 9(3).
          03 連印−コメント.
             05 連印−コメント１                PIC X(100).
             05 連印−コメント２                PIC X(100).
003020*
       01 連入−表示フラグＹＩＷ７２０ IS EXTERNAL GLOBAL.
          03 連入−プレビュー区分               PIC 9(1).
000540************************************
000550* プリンタファイル作成用           *
000560************************************
000570 01 Ｈ連ＰＲＴＦ−作成データ IS EXTERNAL.
000580     03 Ｈ連ＰＲＴＦ−ファイル名           PIC X(8).
000590     03 Ｈ連ＰＲＴＦ−プレビュー区分       PIC 9.
000600     03 Ｈ連ＰＲＴＦ−帳票プログラム名     PIC X(8).
000610     03 Ｈ連ＰＲＴＦ−オーバレイ名         PIC X(8).
000993************************************
000994* プリンタファイル作成特殊用       *
000995************************************
000996 01 Ｈ連特殊ＰＲＴＦ−作成データ IS EXTERNAL.
000997     03 Ｈ連特殊ＰＲＴＦ−用紙種類         PIC X(8).
007308*
       01 連メッセージＰ情報００５２ IS EXTERNAL.
          03 連メＰ−メッセージ番号                PIC 9(2).
          03 連メＰ−メッセージ.
             05 連メＰ−メッセージ内容             PIC X(40) OCCURS 6.
          03 連メＰ−メッセージ１                  PIC X(20).
          03 連メＰ−メッセージ２                  PIC X(12).
          03 連メＰ−返り値                        PIC X.
003090******************************************************************
003100*                      PROCEDURE  DIVISION                       *
003110******************************************************************
003120 PROCEDURE               DIVISION.
003130************
003140*           *
003150* 初期処理   *
003160*           *
003170************
002570     PERFORM プリンタファイル作成.
003180     PERFORM 初期化.
003190     PERFORM 制御情報取得.
003200************
003210*           *
003220* 主処理     *
003230*           *
003240************
003250* 印刷
003290     PERFORM 印刷セット１.
003350************
003360*           *
003370* 終了処理   *
003380*           *
003390************
003400     PERFORM 終了処理.
003410     PERFORM 遅延処理.
003420     EXIT PROGRAM.
003430*
003440*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002860*================================================================*
002870 プリンタファイル作成 SECTION.
002880*================================================================*
002890*   / 初期化 /
002900     MOVE SPACE TO Ｈ連ＰＲＴＦ−作成データ.
002910     INITIALIZE Ｈ連ＰＲＴＦ−作成データ.
002225     MOVE SPACE TO Ｈ連特殊ＰＲＴＦ−作成データ.
002226     INITIALIZE Ｈ連特殊ＰＲＴＦ−作成データ.
002920*
002930*
002940*--↓↓ 変更箇所 ↓↓--------------------------------------*
002230*   使用する用紙種別セット
           MOVE "KARUTE"              TO Ｈ連特殊ＰＲＴＦ−用紙種類.
002970*   使用するプリンタファイル名セット
002231     MOVE "PRTF002"             TO Ｈ連ＰＲＴＦ−ファイル名.
002972*
002973*   使用する帳票プログラム名セット
002974     MOVE "YIW722"             TO Ｈ連ＰＲＴＦ−帳票プログラム名.
002975*
002976*--↑↑-----------------------------------------------------*
002980*
002990*   / プレビュー区分セット /
003000     MOVE 連入−プレビュー区分 TO Ｈ連ＰＲＴＦ−プレビュー区分.
003000*     MOVE 1 TO Ｈ連ＰＲＴＦ−プレビュー区分.
003010*
003020     CALL   "CRTPRTF".
003030     CANCEL "CRTPRTF".
003040*
003450*================================================================*
003460 初期化 SECTION.
003470*
003480     PERFORM ファイルオープン.
003970*
003980*================================================================*
003990 ファイルオープン SECTION.
004000*
004010     OPEN INPUT   元号マスタ
004020         MOVE NC"元号" TO ファイル名.
004030         PERFORM オープンチェック.
004040     OPEN INPUT   制御情報マスタ
004050         MOVE NC"制御情報" TO ファイル名.
004060         PERFORM オープンチェック.
003940     OPEN INPUT   受診者情報Ｆ.
003950         MOVE NC"受Ｆ" TO ファイル名.
003960         PERFORM オープンチェック.
004100     OPEN INPUT 作業ファイル１
004110         MOVE NC"作業１" TO ファイル名.
004120         PERFORM オープンチェック.
004130*     OPEN I-O   印刷ファイル
004140*         PERFORM エラー処理Ｐ.
004150*================================================================*
004160 オープンチェック SECTION.
004170*
004180     IF 状態キー  NOT =  "00"
004190         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
004200         DISPLAY NC"状態キー：" 状態キー         UPON CONS
004210         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004220                                                 UPON CONS
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
004230         ACCEPT  キー入力 FROM CONS
004240         PERFORM ファイル閉鎖
004250         EXIT PROGRAM.
004260*================================================================*
004270 制御情報取得 SECTION.
004280*
004290     MOVE ZERO TO 制−制御区分
004300     READ 制御情報マスタ
004310     NOT INVALID KEY
004320         MOVE 制−遅延回数       TO 遅延回数Ｗ
004330     END-READ.
004340*
004350*================================================================*
004360 遅延処理 SECTION.
004370*
004380     PERFORM VARYING 遅延ＣＮＴ FROM 1 BY 1
004390                                UNTIL 遅延ＣＮＴ > 遅延回数Ｗ
004400         MOVE SPACE TO 遅延フラグ
004410     END-PERFORM.
004420*
004590*================================================================*
004600 印刷セット１ SECTION.
004610*
005220     IF 連入−氏名モード = 1
005230         MOVE 連印−患者氏名    TO 患者氏名
               MOVE 連入−患者番号    TO 患者番号
               MOVE 連入−枝番        TO 枝番
           END-IF.
           MOVE 1                  TO 初検フラグ.
002940     MOVE SPACE TO 終了フラグ.
005630     MOVE 連入−印刷段数 TO 印刷位置ＣＮＴ.
           MOVE 連入−施術和暦    TO 作１−施術和暦.
           MOVE 連入−施術年      TO 作１−施術年.
           MOVE 連入−施術月      TO 作１−施術月.
           MOVE 連入−開始日      TO 作１−施術日.
           START 作業ファイル１ KEY IS >= 作１−施術和暦年月日
           END-START.
013500     IF 状態キー  =  "00"
013520         PERFORM 作業ファイル１読込
      *
003070         PERFORM UNTIL ( 終了フラグ = "YES" ) OR
003090                       ( 作１−施術日 > 連入−終了日 ) OR
                             ( 印刷位置ＣＮＴ > 31 )
                   IF ( 作１−初検 = 1 )
                       MOVE NC"初検月です。新しいカルテに変更しますか？" TO 連メＰ−メッセージ
                       MOVE 1                  TO 連メＰ−メッセージ番号
                       MOVE "PMSG0052.DLL"     TO dll-name
                       MOVE "PMSG0052"         TO form-name
                       CALL "POWEROPENSHEET" USING dll-name form-name
005870                 EVALUATE  連メＰ−返り値
005871                 WHEN "Y"
                           IF ( 初検フラグ = 0 )
005810                        PERFORM 明細印刷
005820                        PERFORM 改頁処理
                              MOVE SPACE TO YIW722P
004640                        IF ( 連入−氏名モード = 1 )
      */             */ヘッダをセットして印刷する。
005230                           MOVE 連印−患者氏名    TO 患者氏名
                                 MOVE 連入−患者番号    TO 患者番号
                                 MOVE 連入−枝番        TO 枝番
004660                        END-IF
                           END-IF
005850                     MOVE 1             TO 印刷位置ＣＮＴ
005860                     MOVE 作１−施術日  TO 連入−開始日
005871                 WHEN "N"
                           CONTINUE
005931                 WHEN OTHER
                           IF ( 初検フラグ = 0 )
005810                        PERFORM 明細印刷
005820                        PERFORM 改頁処理
                           END-IF
                           PERFORM 印刷行数退避
005932                     PERFORM 終了処理
005933                     PERFORM 遅延処理
005934                     EXIT PROGRAM
005935                 END-EVALUATE
                   END-IF
004640             IF (連入−日ごとモード = 1) OR (連入−コメントモード = 1) OR
                      (連入−年月モード = 1)
                       PERFORM 明細印刷処理
                       COMPUTE 印刷位置ＣＮＴ = 印刷位置ＣＮＴ + 1
                   END-IF
      *
                   MOVE 0             TO 初検フラグ
003600             PERFORM 作業ファイル１読込
003610         END-PERFORM
           END-IF.
           IF 連入−合計モード = 1
               PERFORM 月合計印刷処理
           END-IF.
      *
      *****     PERFORM テスト印刷.
      *
           IF (印刷位置ＣＮＴ > 31) AND ( 終了フラグ NOT = "YES" )
              MOVE NC"印刷がページを超えます。印刷しますか？" TO 連メＰ−メッセージ
              MOVE 1                  TO 連メＰ−メッセージ番号
              MOVE "PMSG0052.DLL"     TO dll-name
              MOVE "PMSG0052"         TO form-name
              CALL "POWEROPENSHEET" USING dll-name form-name
005870        EVALUATE  連メＰ−返り値
005871        WHEN "Y"
                 PERFORM 明細印刷
                 PERFORM 印刷行数退避
              WHEN "N"
                 PERFORM 終了処理
                 PERFORM 遅延処理
                 EXIT PROGRAM
              END-EVALUATE
           ELSE
              PERFORM 明細印刷
              PERFORM 印刷行数退避
           END-IF.
004900*================================================================*
004910 明細印刷処理 SECTION.
004920*
           IF 連入−年月モード = 1
005510        MOVE 作１−施術月     TO 月(印刷位置ＣＮＴ)
005520        MOVE 作１−施術日     TO 日(印刷位置ＣＮＴ)
           END-IF.
004940********************
004950* 料金データセット *
004960********************
           IF 連入−日ごとモード = 1
              IF 印刷位置ＣＮＴ = 1
005010           MOVE 作１−初検時  TO  初検時
005010           MOVE 作１−初検分  TO  初検分
005010           MOVE 作１−初検料  TO  初検料
              END-IF
              IF 印刷位置ＣＮＴ = 2
005010           MOVE 作１−再検料  TO  再検料
              END-IF
005010        MOVE 作１−施療料     TO  施療料(印刷位置ＣＮＴ)
005010        MOVE 作１−後療料     TO  後療料(印刷位置ＣＮＴ)
005010        MOVE 作１−往療料     TO  往療料(印刷位置ＣＮＴ)
005010        MOVE 作１−罨法料     TO  罨法料(印刷位置ＣＮＴ)
005010        MOVE 作１−電療料     TO  電療料(印刷位置ＣＮＴ)
005010        MOVE 作１−費用額     TO  費用額(印刷位置ＣＮＴ)
005010        MOVE 作１−一部負担金 TO  一部負担金(印刷位置ＣＮＴ)
           END-IF.
005550*
           IF 連入−コメントモード = 1
              MOVE 作１−コメント   TO  コメントＷ
              MOVE コメント１Ｗ     TO  コメント１(印刷位置ＣＮＴ)
              MOVE コメント２Ｗ     TO  コメント２(印刷位置ＣＮＴ)
              MOVE 連印−コメント１ TO  コメントＷ
              MOVE コメント１Ｗ     TO  総括コメント１
              MOVE コメント２Ｗ     TO  総括コメント２
              MOVE 連印−コメント２ TO  コメントＷ
              MOVE コメント１Ｗ     TO  総括コメント３
              MOVE コメント２Ｗ     TO  総括コメント４
           END-IF.
005300*
004900*================================================================*
004910 月合計印刷処理 SECTION.
004920*
           MOVE 連入−施術月        TO 合計月.
           MOVE 連印−初検料        TO 初検料合計.
           MOVE 連印−後療料        TO 後療料合計.
           MOVE 連印−往療料        TO 往療料合計.
           MOVE 連印−罨法料        TO 罨法料合計.
           MOVE 連印−電療料        TO 電療料合計.
           MOVE 連印−費用額        TO 合計金額.
           MOVE 連印−負担額        TO 負担合計.
           MOVE 連印−情報提供料    TO 情報提供料.
           MOVE 連印−大            TO 大チェック.
           MOVE 連印−中            TO 中チェック.
           MOVE 連印−小            TO 小チェック.
           MOVE 連印−金属副子料    TO 金属副子料.
      *     MOVE 連印−その他        TO その他.
           MOVE 連印−相談料        TO 相談料Ｗ.
           MOVE 連印−明細料        TO 明細料Ｗ.
           IF 連印−相談料 NOT = ZERO
              IF 連印−明細料 NOT = ZERO
                 STRING "相談 "        DELIMITED BY SIZE
                        相談料Ｗ       DELIMITED BY SIZE
                        "円　、"       DELIMITED BY SIZE
                        "明細 "        DELIMITED BY SIZE
                        明細料Ｗ       DELIMITED BY SIZE
                   INTO その他明細
                 END-STRING
              ELSE
                 STRING "相談 "        DELIMITED BY SIZE
                        相談料Ｗ       DELIMITED BY SIZE
                   INTO その他明細
                 END-STRING
              END-IF
           ELSE
              IF 連印−明細料 NOT = ZERO
                 MOVE 連印−明細料     TO 明細料Ｗ
                 STRING "明細 "        DELIMITED BY SIZE
                        明細料Ｗ       DELIMITED BY SIZE
                   INTO その他明細
                 END-STRING
              END-IF
           END-IF.
007070*================================================================*
007080 作業ファイル１読込 SECTION.
007090*
007100     READ 作業ファイル１ NEXT
007110     AT END
007120         MOVE "YES" TO 終了フラグ
007130     END-READ.
007140*
007150*================================================================*
007160 ヘッダ印刷 SECTION.
007170*
004310     IF ( オープンフラグ NOT = "YES" )
004320        MOVE "YES" TO オープンフラグ
004330        OPEN I-O  印刷ファイル
004340        PERFORM エラー処理Ｐ
004350     END-IF.
013440*
007180     MOVE "YIW722P"  TO  定義体名Ｐ.
007190     MOVE "HEDDA"   TO  項目群名Ｐ.
003770     MOVE SPACE      TO  処理種別Ｐ.
007200     WRITE YIW722P.
007210     PERFORM エラー処理Ｐ.
007150*================================================================*
007160 明細印刷 SECTION.
007170*
004310     IF ( オープンフラグ NOT = "YES" )
004320        MOVE "YES" TO オープンフラグ
004330        OPEN I-O  印刷ファイル
004340        PERFORM エラー処理Ｐ
004350     END-IF.
013440*
007180     MOVE "YIW722P"  TO  定義体名Ｐ.
007190     MOVE "HEDDA"   TO  項目群名Ｐ.
003770     MOVE SPACE      TO  処理種別Ｐ.
007200     WRITE YIW722P.
007210     PERFORM エラー処理Ｐ.
013440*
007180     MOVE "YIW722P"  TO  定義体名Ｐ.
007190     MOVE "MEISAI"   TO  項目群名Ｐ.
003770     MOVE SPACE      TO  処理種別Ｐ.
007200     WRITE YIW722P.
007210     PERFORM エラー処理Ｐ.
003730*================================================================*
003740 改頁処理  SECTION.
003750*
003760     MOVE "YIW722P" TO  定義体名Ｐ.
003770     MOVE "CT"      TO  処理種別Ｐ.
003780     MOVE "PAGE"    TO  拡張制御Ｐ.
003790     MOVE SPACE     TO  項目群名Ｐ.
003800     WRITE YIW722P.
003810     PERFORM エラー処理Ｐ.
003820     MOVE SPACE     TO  拡張制御Ｐ.
003821*
003822     CLOSE  印刷ファイル.
004320     MOVE SPACE TO オープンフラグ.
003823*     OPEN I-O   印刷ファイル.
003824*     PERFORM エラー処理Ｐ.
003825*
007610*================================================================*
007620 エラー表示 SECTION.
007630*
007640     DISPLAY ファイル名   NC"ファイル書込エラー"   UPON CONS.
007650     DISPLAY NC"状態キー：" 状態キー               UPON CONS.
007660     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
007670     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
000080*-----------------------------------------*
000090     CALL "actcshm"  WITH C LINKAGE.
000100*-----------------------------------------*
007680     ACCEPT  キー入力 FROM CONS.
007690     PERFORM ファイル閉鎖.
007700     EXIT PROGRAM.
007290*================================================================*
007300 エラー処理Ｐ SECTION.
007310*
007320     IF 通知情報Ｐ NOT = "00"
007330         DISPLAY NC"帳票エラー"              UPON CONS
007340         DISPLAY NC"項目群名Ｐ：" 項目群名Ｐ UPON CONS
007350         DISPLAY NC"通知情報Ｐ：" 通知情報Ｐ UPON CONS
007360         DISPLAY NC"拡張制御Ｐ：" 拡張制御Ｐ UPON CONS
007370         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
007380                                             UPON CONS
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
007390         ACCEPT  キー入力 FROM CONS
007400         PERFORM ファイル閉鎖
007410         MOVE 99 TO PROGRAM-STATUS
007420         EXIT PROGRAM
007430     END-IF.
007440*================================================================*
007450 ファイル閉鎖 SECTION.
007460*
002990     IF ( オープンフラグ = "YES" )
002991         CLOSE 印刷ファイル
003041     END-IF.
007480     CLOSE 元号マスタ   制御情報マスタ
                 受診者情報Ｆ 作業ファイル１.
007490*================================================================*
007500 終了処理 SECTION.
007510*
007520     PERFORM ファイル閉鎖.
006850*================================================================*
006860 印刷行数退避 SECTION.
      *
006870     CLOSE 受診者情報Ｆ.
006880     PERFORM 遅延処理.
006890     OPEN I-O 受診者情報Ｆ.
006900         MOVE NC"受診" TO ファイル名.
006910         PERFORM オープンチェック.
006920     PERFORM 遅延処理.
006930*
006940     MOVE ZERO                TO 受−施術和暦.
006950     MOVE ZERO                TO 受−施術年.
006960     MOVE ZERO                TO 受−施術月.
006970     MOVE 連入−患者コード    TO 受−患者コード.
006990*
007000     READ 受診者情報Ｆ
007010     NOT INVALID KEY
007020         IF 印刷位置ＣＮＴ > 31
                  COMPUTE 印刷位置ＣＮＴ = 印刷位置ＣＮＴ - 31
               END-IF
007020         MOVE 印刷位置ＣＮＴ TO 受−カルテ裏印刷行数
007030         REWRITE 受−レコード
007040         INVALID KEY
007050             MOVE NC"受Ｆ" TO ファイル名
007060             PERFORM エラー表示
007070         END-REWRITE
007080     END-READ.
007090     PERFORM 遅延処理.
007100     CLOSE 受診者情報Ｆ.
007110     PERFORM 遅延処理.
007120     OPEN INPUT 受診者情報Ｆ.
007130         MOVE NC"受診" TO ファイル名.
007140         PERFORM オープンチェック.
006850*================================================================*
006860 テスト印刷 SECTION.
      *
           MOVE ALL "静"  TO 患者氏名.
           MOVE 99999     TO 患者番号.
           MOVE "X"       TO 枝番.
           PERFORM VARYING カウンタ FROM 1 BY 1 UNTIL カウンタ > 31
              MOVE 99     TO 月(カウンタ) 日(カウンタ)
              MOVE 99999  TO 施療料(カウンタ) 後療料(カウンタ) 往療料(カウンタ)
                             罨法料(カウンタ) 電療料(カウンタ) 費用額(カウンタ) 一部負担金(カウンタ)
              MOVE ALL "静"  TO コメント１(カウンタ) コメント２(カウンタ)
           END-PERFORM.
           MOVE 99        TO 合計月 初検時 初検分
           MOVE 99999     TO 初検料 再検料 初検料合計 後療料合計 往療料合計 罨法料合計
                             電療料合計 合計金額 負担合計 情報提供料 金属副子料 その他
           MOVE ALL "静"  TO 患者氏名 総括コメント１ 総括コメント２ 総括コメント３ 総括コメント４.
           MOVE NC"○"    TO 大チェック 中チェック 小チェック.
      *
004900*================================================================*
007540******************************************************************
007550 END PROGRAM YIW722.
007560******************************************************************
