000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YCH7121.
000060 AUTHOR.                 山田 浩之　池田 幸子
000070*
000080*----------------------------------------------------------------*
000090*         中央 カルテ裏印刷（柔ｳｨﾝﾄﾞｳｽﾞ版）通常用紙（静岡用)
000100*         MED = YCH710 YCH712P
000110*----------------------------------------------------------------*
000120 DATE-WRITTEN.           2011-09-02
000130 DATE-COMPILED.          2011-09-02
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000320     SELECT  制御情報マスタ  ASSIGN      TO        SEIGYOL
000330                             ORGANIZATION             IS  INDEXED
000340                             ACCESS MODE              IS  DYNAMIC
000350                             RECORD KEY               IS  制－制御区分
000360                             FILE STATUS              IS  状態キー
000370                             LOCK        MODE         IS  AUTOMATIC.
000380     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000390                             ORGANIZATION             IS  INDEXED
000400                             ACCESS MODE              IS  DYNAMIC
000410                             RECORD KEY               IS 受－施術和暦年月
000420                                                          受－患者コード
000430                             ALTERNATE RECORD KEY     IS 受－施術和暦年月
000440                                                          受－患者カナ
000450                                                          受－患者コード
000460                             ALTERNATE RECORD KEY     IS  受－患者コード
000470                                                         受－施術和暦年月
000480                             ALTERNATE RECORD KEY     IS 受－施術和暦年月
000490                                                          受－保険種別
000500                                                          受－保険者番号
000510                                                          受－患者コード
000520                             ALTERNATE RECORD KEY     IS 受－施術和暦年月
000530                                                          受－公費種別
000540                                                     受－費用負担者番号
000550                                                          受－患者コード
000560                             ALTERNATE RECORD KEY     IS 受－施術和暦年月
000570                                                          受－助成種別
000580                                                  受－費用負担者番号助成
000590                                                          受－患者コード
000600                             ALTERNATE RECORD KEY  IS 受－請求和暦年月
000610                                                      受－施術和暦年月
000620                                                      受－患者コード
000630                             FILE STATUS              IS  状態キー
000640                             LOCK        MODE         IS  AUTOMATIC.
000293     SELECT  長期継続者Ｆ    ASSIGN      TO        CHOKEIL
000294                             ORGANIZATION             IS INDEXED
000295                             ACCESS MODE              IS DYNAMIC
000296                             RECORD KEY               IS 長継－施術和暦年月
000297                                                         長継－患者コード
000298                             ALTERNATE RECORD KEY     IS 長継－患者コード
000299                                                         長継－施術和暦年月
000300                             FILE STATUS              IS 状態キー
000301                             LOCK      MODE           IS AUTOMATIC.
000650     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W7111L.DAT"
000660                             ORGANIZATION             IS  INDEXED
000670                             ACCESS                   IS  DYNAMIC
000680                             RECORD      KEY    IS  作１－施術和暦年月日
000690                             FILE        STATUS       IS  状態キー
000700                             LOCK        MODE         IS  AUTOMATIC.
000710*
000720     SELECT  印刷ファイル    ASSIGN      TO     GS-PRTF002
000730                             SYMBOLIC    DESTINATION  IS "PRT"
000740                             FORMAT                   IS  定義体名Ｐ
000750                             GROUP                    IS  項目群名Ｐ
000760                             PROCESSING  MODE         IS  処理種別Ｐ
000770                             UNIT        CONTROL      IS  拡張制御Ｐ
000780                             FILE        STATUS       IS  通知情報Ｐ.
000790******************************************************************
000800*                      DATA DIVISION                             *
000810******************************************************************
000820 DATA                    DIVISION.
000830 FILE                    SECTION.
000870*                           ［ＲＬ＝  ２５６］
000880 FD  制御情報マスタ          BLOCK   CONTAINS   1   RECORDS.
000890     COPY SEIGYO          OF  XFDLIB  JOINING   制   AS  PREFIX.
000900*                           ［ＲＬ＝  ３２０］
000910 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
000920     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
000686*                           ［ＲＬ＝  １２８］
000687 FD  長期継続者Ｆ          BLOCK   CONTAINS   1   RECORDS.
000688     COPY CHOKEI          OF  XFDLIB  JOINING   長継   AS  PREFIX.
000930*                           ［ＲＬ＝  １２８］
000940 FD  作業ファイル１ RECORD  CONTAINS 128 CHARACTERS.
000950 01 作１－レコード.
000960    03 作１－レコードキー.
000970       05  作１－施術和暦年月日.
000980          07 作１－施術和暦               PIC 9.
000990          07 作１－施術年月.
001000             09 作１－施術年              PIC 9(2).
001010             09 作１－施術月              PIC 9(2).
001020          07 作１－施術日                 PIC 9(2).
001030    03 作１－レコードデータ.
001040       05 作１－患者コード.
001050          07 作１－患者番号               PIC 9(6).
001060          07 作１－枝番                   PIC X.
001070* ＊＊＊　日毎＋部位毎データ　＊＊＊
001080         05  作１－日チェック              PIC 9.
001090         05  作１－部位データ              OCCURS 4.
001100             07  作１－部位計              PIC 9(5).
001110         05  作１－電罨料.
001120             07  作１－罨法金額            PIC 9(4).
001130         05  作１－一部負担金              PIC 9(5).
001200         05  作１－コメント                PIC X(48).
001210         05  FILLER                        PIC X(36).
001150*
001160 FD  印刷ファイル.
001170     COPY YCH712P        OF  XMDLIB.
001180*----------------------------------------------------------------*
001190******************************************************************
001200*                WORKING-STORAGE SECTION                         *
001210******************************************************************
001220 WORKING-STORAGE         SECTION.
001230 01 キー入力                           PIC X     VALUE SPACE.
001240 01 状態キー                           PIC X(2)  VALUE SPACE.
001250 01 終了フラグ                         PIC X(3)  VALUE SPACE.
001260 01 終了フラグ２                       PIC X(3)  VALUE SPACE.
001270 01 初検フラグ                         PIC X(3)  VALUE SPACE.
001280 01 ファイル名                         PIC N(6)  VALUE SPACE.
001290 01 レセプトＰＧＷ                     PIC X(8)  VALUE SPACE.
001300 01 前和暦Ｗ                           PIC 9     VALUE ZERO.
001310 01 カレント元号Ｗ                     PIC 9(1)  VALUE ZERO.
001320 01 日位置ＣＮＴ                       PIC 9(2)  VALUE ZERO.
001330 01 部位ＣＮＴ                         PIC 9     VALUE ZERO.
001340 01 患者番号Ｗ                         PIC 9(6)  VALUE ZERO.
       01 行カウンタ                         PIC 9(2)  VALUE ZERO.
       01 文字ＣＮＴ                         PIC 9(2)  VALUE ZERO.
001350**
001360 01 遅延フラグ                         PIC X(3) VALUE SPACE.
001370 01 遅延回数Ｗ                         PIC 9(4) VALUE ZERO.
001380 01 遅延ＣＮＴ                         PIC 9(5) VALUE ZERO.
001390**
001210 01 オープンフラグ                     PIC X(3)   VALUE SPACE.
001400*
      *
      */長期理由文用ワーク↓↓↓0401
       01 長期理由文２Ｗ.
          03 長期理由文Ｗ PIC N(35) OCCURS 16 VALUE SPACE.
001201 01 番号カウンタ                       PIC 9(2) VALUE ZERO.
001329****
001330 01 長期負傷種別Ｗ                     PIC 9(2) VALUE ZERO.
001331 01 長期部位Ｗ                         PIC 9(2) VALUE ZERO.
001332 01 理由コードＷ                       PIC 9(3) VALUE ZERO.
001340 01 症状コードＷ                       PIC 9(2) VALUE ZERO.
001350 01 症状名Ｗ                           PIC N(4) VALUE SPACE.
001361 01 合成理由文Ｗ.
001369    03 長期番号Ｗ                      PIC X(2)   VALUE SPACE.
001360    03 理由文Ｗ                        PIC X(320) VALUE SPACE.
001362* 01 カウンタ                           PIC 9(2)  VALUE ZERO.
001363 01 全角空白                           PIC X(2)  VALUE X"8140".
001364 01 半角空白                           PIC X(2)  VALUE X"2020".
001365***
001366*** 症状用
001367 01 症状Ｗ.
001368    03 症状内容Ｗ                      PIC N(29) VALUE SPACE.
001401** 適用４０文字レセプト用
001402 01 理由４０桁Ｗ.
001403    03 理由４０桁１Ｗ                  PIC N(40) VALUE SPACE.
001404    03 理由４０桁２Ｗ                  PIC N(40) VALUE SPACE.
001405    03 理由４０桁３Ｗ                  PIC N(40) VALUE SPACE.
001405    03 理由４０桁４Ｗ                  PIC N(40) VALUE SPACE.
001405    03 理由４０桁５Ｗ                  PIC N(40) VALUE SPACE.
001401** 適用３５文字用
001402 01 理由３５桁Ｗ.
001403    03 理由３５桁１Ｗ                  PIC N(35) VALUE SPACE.
001404    03 理由３５桁２Ｗ                  PIC N(35) VALUE SPACE.
001405    03 理由３５桁３Ｗ                  PIC N(35) VALUE SPACE.
001405    03 理由３５桁４Ｗ                  PIC N(35) VALUE SPACE.
001405    03 理由３５桁５Ｗ                  PIC N(35) VALUE SPACE.
001405    03 理由３５桁６Ｗ                  PIC N(35) VALUE SPACE.
      *
002290 01 カウンタ                           PIC 9(2)  VALUE ZERO.
      */長期理由文用ワーク↑↑↑0401
001410****************
001420* 連結項目待避 *
001430****************
001440*    ************
001450*    * 印刷キー *
001460*    ************
001470 01 対象データＷＲ.
001480    03 施術和暦年月ＷＲ.
001490       05 施術和暦ＷＲ                  PIC 9(1)  VALUE ZERO.
001500       05 施術年ＷＲ                    PIC 9(2)  VALUE ZERO.
001510       05 施術月ＷＲ                    PIC 9(2)  VALUE ZERO.
001520    03 開始日ＷＲ                       PIC 9(2)  VALUE ZERO.
001530    03 終了日ＷＲ                       PIC 9(2)  VALUE ZERO.
001540    03 患者カナＷＲ                     PIC X(20) VALUE SPACE.
001550    03 患者コードＷＲ.
001560       05 患者番号ＷＲ                  PIC 9(6)  VALUE ZERO.
001570       05 枝番ＷＲ                      PIC X(1)  VALUE SPACE.
001580    03 印刷モードＦＷＲ                 PIC 9(1)  VALUE ZERO.
001590    03 日ごとモードＷＲ                 PIC 9(1)  VALUE ZERO.
001600    03 氏名モードＷＲ                   PIC 9(1)  VALUE ZERO.
001610    03 年月モードＷＲ                   PIC 9(1)  VALUE ZERO.
001620    03 日計モードＷＲ                   PIC 9(1)  VALUE ZERO.
001630    03 下部モードＷＲ                   PIC 9(1)  VALUE ZERO.
001480    03 コメントモードＷＲ               PIC 9(1)  VALUE ZERO.
001640    03 印字位置ＣＮＴ                   PIC 9     VALUE ZERO.
          03 開始段数ＷＲ                     PIC 9(2)  VALUE ZERO.
      */長期理由印刷0401
          03 長期モードＷＲ                   PIC 9(1)  VALUE ZERO.
001650**************
001660* 受診者情報 *
001670**************
001680 01 受診者情報Ｗ.
001690    03 施術年月Ｗ.
001700       05 施術年Ｗ                     PIC 9(2)   VALUE ZERO.
001710       05 施術月Ｗ                     PIC 9(2)   VALUE ZERO.
001720    03 保険者番号Ｗ.
001730       05 印刷保険者番号Ｗ             PIC X(6)   VALUE SPACE.
001740       05 FILLER                       PIC X(4)   VALUE SPACE.
001750    03 退職保険者番号Ｗ.
001760       05 FILLER                       PIC X(2)   VALUE SPACE.
001770       05 退職印刷保険者番号Ｗ         PIC X(6)   VALUE SPACE.
001780       05 FILLER                       PIC X(2)   VALUE SPACE.
001790    03 市町村番号Ｗ.
001800       05 印刷市町村番号Ｗ             PIC X(8)   VALUE SPACE.
001810       05 FILLER                       PIC X(2)   VALUE SPACE.
001820    03 保険種別Ｗ                      PIC 9(2)   VALUE ZERO.
001830    03 患者情報Ｗ.
001840       05 患者カナＷ                   PIC X(50)  VALUE SPACE.
001850       05 患者氏名Ｗ                   PIC X(50)  VALUE SPACE.
001850       05 患者氏名ＷＰ.
                07 患者氏名２Ｗ              PIC N(1)  VALUE SPACE OCCURS 10.
001860************
001870* 料金情報 *
001880************
001890 01 料金情報Ｗ.
001900    03 初検加算Ｗ.
001910       05 時間外チェックＷ                PIC N(1) VALUE SPACE.
001920       05 休日チェックＷ                  PIC N(1) VALUE SPACE.
001930       05 深夜チェックＷ                  PIC N(1) VALUE SPACE.
001940    03 往療加算Ｗ.
001950       05 夜間チェックＷ                  PIC N(1) VALUE SPACE.
001960       05 難路チェックＷ                  PIC N(1) VALUE SPACE.
001970       05 暴風チェックＷ                  PIC N(1) VALUE SPACE.
001980    03 往療回数ＷＲ                       PIC 9(2)  VALUE ZERO.
001990    03 往療距離ＷＲ                       PIC 9(3)V9 VALUE ZERO.
002000    03 往療料ＷＲ                         PIC 9(6)  VALUE ZERO.
002010    03 往療加算料ＷＲ                     PIC 9(5)  VALUE ZERO.
002020    03 金属副子チェックＷ                 OCCURS 4.
002030       05 大チェックＷ                    PIC N(1) VALUE SPACE.
002040       05 中チェックＷ                    PIC N(1) VALUE SPACE.
002050       05 小チェックＷ                    PIC N(1) VALUE SPACE.
002060    03 初検料ＷＲ                         PIC 9(4) VALUE ZERO.
          03 相談料ＷＲ                         PIC 9(4) VALUE ZERO.
002070    03 再検料ＷＲ                         PIC 9(4) VALUE ZERO.
002080    03 初検加算料ＷＲ                     PIC 9(4) VALUE ZERO.
002090    03 初回処置料Ｗ                       PIC 9(5) OCCURS 4 VALUE ZERO.
002100    03 冷罨料Ｗ                           PIC 9(5) OCCURS 4 VALUE ZERO.
002110    03 初回処置料チェックＷ.
002120       05 整復料チェックＷ                PIC N(1) VALUE SPACE.
002130       05 固定料チェックＷ                PIC N(1) VALUE SPACE.
002140       05 施療料チェックＷ                PIC N(1) VALUE SPACE.
002150    03 情報提供料ＷＲ                     PIC 9(6) VALUE ZERO.
002160 01 明細.
002170    03 電罨料Ｗ                           PIC 9(4) VALUE ZERO.
002180    03 一部負担金Ｗ                       PIC 9(5) VALUE ZERO.
002190    03 部位Ｗ                             OCCURS 4.
002200       05 部位計Ｗ                        PIC 9(4) VALUE ZERO.
002210       05 日数Ｗ                          PIC 9(2) VALUE ZERO.
002520    03 コメントＷ.
002530       05 コメント１Ｗ                    PIC X(24) VALUE SPACE.
002540       05 コメント２Ｗ                    PIC X(24) VALUE SPACE.
002220*******************************************************************
002230 01 印刷制御.
002240     03 定義体名Ｐ                     PIC X(8) VALUE SPACE.
002250     03 項目群名Ｐ                     PIC X(8) VALUE SPACE.
002260     03 処理種別Ｐ                     PIC X(2) VALUE SPACE.
002270     03 拡張制御Ｐ.
002280         05 端末制御Ｐ.
002290             07 移動方向Ｐ             PIC X(1) VALUE SPACE.
002300             07 移動行数Ｐ             PIC 9(3) VALUE ZERO.
002310         05 詳細制御Ｐ                 PIC X(2) VALUE SPACE.
002320     03 通知情報Ｐ                     PIC X(2) VALUE SPACE.
002330     03 ユニット名Ｐ                   PIC X(8) VALUE SPACE.
002340*
002350 01 計算機西暦年Ｗ                     PIC 9(2) VALUE ZERO.
002360* 日付ＷＯＲＫ
002370 01 和暦終了年Ｗ                       PIC 9(4) VALUE ZERO.
002380 01 計算機西暦.
002390    03 計算機西暦年                    PIC 9(4) VALUE ZERO.
002400    03 計算機西暦月日                  PIC 9(4) VALUE ZERO.
002410 01 計算機西暦Ｒ REDEFINES 計算機西暦.
002420    03 計算機世紀                      PIC 9(2).
002430    03 計算機日付                      PIC 9(6).
002440    03 計算機日付Ｒ REDEFINES 計算機日付.
002450       05 計算機年月                   PIC 9(4).
002460       05 計算機年月Ｒ REDEFINES 計算機年月.
002470         07 計算機年                   PIC 9(2).
002480         07 計算機月                   PIC 9(2).
002490       05 計算機日                     PIC 9(2).
002500*
002510******************************************************************
002520*                          連結項目                              *
002530******************************************************************
002540************
002550* 印刷キー *
002560************
002570 01 連印－対象データＹＣＨ７１０ IS EXTERNAL.
002580    03 連印－初診料.
002590       05 連印－初検料                    PIC 9(4).
002600       05 連印－初検加算料                PIC 9(4).
002610       05 連印－休日                      PIC 9.
002620       05 連印－深夜                      PIC 9.
002630       05 連印－時間外                    PIC 9.
002640       05 連印－診療時                    PIC 9(2).
002650       05 連印－診療分                    PIC 9(2).
          03 連印－相談料                       PIC 9(4).
002660    03 連印－再検料                       PIC 9(4).
002670    03 連印－往療料.
002680       05 連印－夜間                      PIC 9.
002690       05 連印－難路                      PIC 9.
002700       05 連印－暴風                      PIC 9.
002710       05 連印－回数                      PIC 9(2).
002720       05 連印－距離                      PIC 9(3)V9.
002730       05 連印－往療料金額                PIC 9(6).
002740       05 連印－往療加算料                PIC 9(5).
002750    03 連印－部位ごと金額                 OCCURS 4.
002760       05 連印－金属                      PIC 9.
002770       05 連印－初回処置料                PIC 9(6).
002780       05 連印－冷罨療法.
002790          07 連印－冷罨料                 PIC 9(6).
002800       05 連印－金額                      PIC 9(6).
002810    03 連印－情報提供料                   PIC 9(6).
002820****************
002830* 画面入力情報 *
002840****************
002850 01 連入－入力データＹＣＨ７１０ IS EXTERNAL.
002860    03 連入－施術和暦年月.
002870       05 連入－施術和暦                  PIC 9(1).
002880       05 連入－施術年                    PIC 9(2).
002890       05 連入－施術月                    PIC 9(2).
002900    03 連入－開始日付.
002910       05 連入－開始日                    PIC 9(2).
002920    03 連入－終了日付.
002930       05 連入－終了日                    PIC 9(2).
002940    03 連入－患者コード.
002950       05 連入－患者番号                  PIC 9(6).
002960       05 連入－枝番                      PIC X(1).
002970    03 連入－印刷モードＦ                 PIC 9(1).
002980    03 連入－日ごとモード                 PIC 9(1).
002990    03 連入－氏名モード                   PIC 9(1).
003000    03 連入－年月モード                   PIC 9(1).
003010    03 連入－日計モード                   PIC 9(1).
003020    03 連入－下部モード                   PIC 9(1).
          03 連入－コメントモード               PIC 9(1).
003030    03 連入－印刷位置                     PIC 9(1).
          03 連入－印刷段数                     PIC 9(2).
      */連続時の氏名印刷の有無0302
          03 連入－処理モード                   PIC X(4).
      */長期理由印刷0401
003280    03 連入－長期モード                   PIC 9(1).
      *
       01 連入－表示フラグＹＣＨ７１０ IS EXTERNAL.
          03 連入－プレビュー区分               PIC 9(1).
003040*
003050 01 連－共通キー IS EXTERNAL.
003060    03  連－カレント和暦               PIC 9.
003070*
003080 01 連日数－キーＹＣＨ７１０ IS EXTERNAL.
003090    03 連日数－日数                    PIC 9(2) OCCURS 5.
      *
       01 連金運－キーＹＣＨ７１０ IS EXTERNAL.
          03 連金運－金属運動ＣＭ            PIC X(60).
003020*
000540************************************
000550* プリンタファイル作成用           *
000560************************************
000570 01 Ｈ連ＰＲＴＦ－作成データ IS EXTERNAL.
000580     03 Ｈ連ＰＲＴＦ－ファイル名           PIC X(8).
000590     03 Ｈ連ＰＲＴＦ－プレビュー区分       PIC 9.
000600     03 Ｈ連ＰＲＴＦ－帳票プログラム名     PIC X(8).
000610     03 Ｈ連ＰＲＴＦ－オーバレイ名         PIC X(8).
000993************************************
000994* プリンタファイル作成特殊用       *
000995************************************
000996 01 Ｈ連特殊ＰＲＴＦ－作成データ IS EXTERNAL.
000997     03 Ｈ連特殊ＰＲＴＦ－用紙種類         PIC X(8).
003100*
003110******************************************************************
003120*                      PROCEDURE  DIVISION                       *
003130******************************************************************
003140 PROCEDURE               DIVISION.
003150************
003160*           *
003170* 初期処理   *
003180*           *
003190************
002570     PERFORM プリンタファイル作成.
003200     PERFORM 初期化.
003210     PERFORM 制御情報取得.
003220************
003230*           *
003240* 主処理     *
003250*           *
003260************
003270* 印刷
003280     PERFORM 連結項目待避.
003290     PERFORM 印刷セット１.
003300     PERFORM 印刷処理１.
           PERFORM 印刷行数退避.
003310************
003320*           *
003330* 終了処理   *
003340*           *
003350************
003360     PERFORM 終了処理.
003370     PERFORM 遅延処理.
003380     EXIT PROGRAM.
003390*
003400*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002860*================================================================*
002870 プリンタファイル作成 SECTION.
002880*================================================================*
002890*   / 初期化 /
002900     MOVE SPACE TO Ｈ連ＰＲＴＦ－作成データ.
002910     INITIALIZE Ｈ連ＰＲＴＦ－作成データ.
002225     MOVE SPACE TO Ｈ連特殊ＰＲＴＦ－作成データ.
002226     INITIALIZE Ｈ連特殊ＰＲＴＦ－作成データ.
002920*
002930*
002940*--↓↓ 変更箇所 ↓↓--------------------------------------*
002230*   使用する用紙種別セット
           MOVE "KARUTE"              TO Ｈ連特殊ＰＲＴＦ－用紙種類.
002970*   使用するプリンタファイル名セット
002231     MOVE "PRTF002"             TO Ｈ連ＰＲＴＦ－ファイル名.
002972*
002973*   使用する帳票プログラム名セット
002974     MOVE "YCH712"             TO Ｈ連ＰＲＴＦ－帳票プログラム名.
002975*
002976*--↑↑-----------------------------------------------------*
002980*
002990*   / プレビュー区分セット /
003000     MOVE 連入－プレビュー区分 TO Ｈ連ＰＲＴＦ－プレビュー区分.
003010*
003020     CALL   "CRTPRTF".
003030     CANCEL "CRTPRTF".
003040*
003410*============================================ ====================*
003420 初期化 SECTION.
003430*
003440     PERFORM ファイルオープン.
003450*    /* 現在日付取得 */
003460     ACCEPT 計算機日付 FROM DATE.
003470*    /* 1980～2079年の間で設定 */
003480     IF 計算機年 > 80
003490         MOVE 19 TO 計算機世紀
003500     ELSE
003510         MOVE 20 TO 計算機世紀
003520     END-IF.
003530     PERFORM カレント元号取得.
003550     COMPUTE 計算機西暦年Ｗ = 計算機西暦年 - 1988.
003560*================================================================*
003570 カレント元号取得 SECTION.
003580*
003590     MOVE ZEROS TO 制－制御区分.
003600     READ 制御情報マスタ
003610     NOT INVALID KEY
003620         MOVE 制－カレント元号 TO カレント元号Ｗ
003630     END-READ.
003640*
003940*================================================================*
003950 ファイルオープン SECTION.
003960*
004000     OPEN INPUT   制御情報マスタ
004010         MOVE NC"制御情報" TO ファイル名.
004020         PERFORM オープンチェック.
004030     OPEN INPUT   受診者情報Ｆ.
004040         MOVE NC"受情" TO ファイル名.
004050         PERFORM オープンチェック.
004060     OPEN INPUT 作業ファイル１
004070         MOVE NC"作業１" TO ファイル名.
004080         PERFORM オープンチェック.
004090*     OPEN I-O   印刷ファイル
004100*         PERFORM エラー処理Ｐ.
002650     OPEN INPUT   長期継続者Ｆ.
002651         MOVE NC"長期継続者Ｆ" TO ファイル名.
002652         PERFORM オープンチェック.
004110*================================================================*
004120 オープンチェック SECTION.
004130*
004140     IF 状態キー  NOT =  "00"
004150         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
004160         DISPLAY NC"状態キー：" 状態キー         UPON CONS
004170         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004180                                                 UPON CONS
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
004190         ACCEPT  キー入力 FROM CONS
004200         PERFORM ファイル閉鎖
004210         EXIT PROGRAM.
004220*================================================================*
004230 制御情報取得 SECTION.
004240*
004250     MOVE ZERO TO 制－制御区分
004260     READ 制御情報マスタ
004270     NOT INVALID KEY
004280         MOVE 制－遅延回数       TO 遅延回数Ｗ
004290     END-READ.
004300*
004310*================================================================*
004320 遅延処理 SECTION.
004330*
004340     PERFORM VARYING 遅延ＣＮＴ FROM 1 BY 1
004350                                UNTIL 遅延ＣＮＴ > 遅延回数Ｗ
004360         MOVE SPACE TO 遅延フラグ
004370     END-PERFORM.
004380*
004390*================================================================*
004400 連結項目待避 SECTION.
004410*
004420     MOVE 連入－施術和暦           TO 施術和暦ＷＲ.
004430     MOVE 連入－施術年             TO 施術年ＷＲ.
004440     MOVE 連入－施術月             TO 施術月ＷＲ.
004450     MOVE 連入－開始日             TO 開始日ＷＲ.
004460     MOVE 連入－終了日             TO 終了日ＷＲ.
004470     MOVE 連入－患者番号           TO 患者番号ＷＲ.
004480     MOVE 連入－枝番               TO 枝番ＷＲ.
004490     MOVE 連入－印刷モードＦ       TO 印刷モードＦＷＲ.
004500     MOVE 連入－日ごとモード       TO 日ごとモードＷＲ.
004510     MOVE 連入－氏名モード         TO 氏名モードＷＲ.
004520     MOVE 連入－年月モード         TO 年月モードＷＲ.
004530     MOVE 連入－日計モード         TO 日計モードＷＲ.
004540     MOVE 連入－下部モード         TO 下部モードＷＲ.
007150     MOVE 連入－コメントモード     TO コメントモードＷＲ.
004550     MOVE 連入－印刷位置           TO 印字位置ＣＮＴ.
           MOVE 連入－印刷段数           TO 開始段数ＷＲ.
      */0401
           MOVE 連入－長期モード         TO 長期モードＷＲ.
004560*================================================================*
004570 印刷セット１ SECTION.
004580*
004590     PERFORM 項目初期化.
004600     PERFORM 受診者情報取得.
004610     IF 下部モードＷＲ = 1
004620         PERFORM 印刷セット２
004630     END-IF.
004640     IF 氏名モードＷＲ = 1
      */連続時の氏名印刷の有無0302
      */連続モード時で印刷位置が１以外の場合は氏名を印刷しない
               IF (連入－処理モード    = "RENZ" ) AND
                  (連入－印刷位置  NOT = 1)
                   MOVE SPACE TO 患者氏名
                   MOVE ZERO  TO 患者番号
                   MOVE SPACE TO 枝番
               ELSE
004650             MOVE 患者氏名Ｗ          TO 患者氏名
                   MOVE 連入－患者番号      TO 患者番号
                   MOVE 連入－枝番          TO 枝番
                   PERFORM VARYING 文字ＣＮＴ FROM 1 BY 1
                             UNTIL 文字ＣＮＴ > 10
                      MOVE 患者氏名２Ｗ(文字ＣＮＴ) TO 患者氏名２(文字ＣＮＴ)
                   END-PERFORM
               END-IF
004660     END-IF.
004670     IF 年月モードＷＲ = 1
004680         EVALUATE 印字位置ＣＮＴ
004690         WHEN 1
004700             MOVE 施術年Ｗ            TO 施術年左
004710             MOVE 施術月Ｗ            TO 施術月左
004720         WHEN 2
004730             MOVE 施術年Ｗ            TO 施術年中
004740             MOVE 施術月Ｗ            TO 施術月中
004750         WHEN 3
004760             MOVE 施術年Ｗ            TO 施術年右
004770             MOVE 施術月Ｗ            TO 施術月右
004780         END-EVALUATE
004790     END-IF.
004800     IF (日計モードＷＲ = 1) OR (日ごとモードＷＲ = 1) OR (コメントモードＷＲ = 1)
004810         EVALUATE 印字位置ＣＮＴ
004820         WHEN 1
004830             PERFORM 左側印刷
004840         WHEN 2
004850             PERFORM 中側印刷
004860         WHEN 3
004870             PERFORM 右側印刷
004880         END-EVALUATE
004890     END-IF.
      */長期理由を印刷0401
           IF 長期モードＷＲ = 1
               PERFORM 長期理由文取得
004810         EVALUATE 印字位置ＣＮＴ
004820         WHEN 1
004830             PERFORM 長期セット左
004840         WHEN 2
004850             PERFORM 長期セット中
004860         WHEN 3
004870             PERFORM 長期セット右
004880         END-EVALUATE
           END-IF.
004900*================================================================*
004910 印刷セット２ SECTION.
004920*
004930********************
004940* 料金データセット *
004950********************
004960     PERFORM 連結項目退避
004970*    ****************************************************************
004980*    * 料金（月毎）（負傷毎）（逓減毎）については連結項目よりセット *
004990*    ****************************************************************
005000     MOVE 初検料ＷＲ                   TO  初検料.
           MOVE 相談料ＷＲ                   TO  相談料.
           IF 相談料ＷＲ NOT = ZERO
               MOVE "初検時相談支援料"       TO  相談料Ｔ
               MOVE "円"                     TO  相談料円
           END-IF.
005010     MOVE 時間外チェックＷ             TO  時間外チェック.
005020     MOVE 休日チェックＷ               TO  休日チェック.
005030     MOVE 深夜チェックＷ               TO  深夜チェック.
005040*     MOVE 午前チェックＷ               TO  午前チェック.
005050*     MOVE 午後チェックＷ               TO  午後チェック.
005060*     MOVE 時Ｗ                         TO  時.
005070*     MOVE 分Ｗ                         TO  分.
005080     IF 休日チェックＷ NOT = SPACE
005090         MOVE 初検加算料ＷＲ           TO  休日加算
005100     ELSE
005110         MOVE 初検加算料ＷＲ           TO  時間外加算
005120     END-IF.
005130     MOVE 再検料ＷＲ                   TO  再検料.
005140     MOVE 往療距離ＷＲ                 TO  往療距離.
005150     MOVE 往療回数ＷＲ                 TO  往療回数.
005160     MOVE 往療料ＷＲ                   TO  往療料.
005170     MOVE 夜間チェックＷ               TO  夜間チェック.
005180     MOVE 難路チェックＷ               TO  難路チェック.
005190     MOVE 暴風チェックＷ               TO  暴風チェック.
005200     MOVE 往療加算料ＷＲ               TO  加算料合計.
005210     MOVE 情報提供料ＷＲ               TO  情報提供料.
005220     PERFORM VARYING 部位ＣＮＴ FROM 1 BY 1 UNTIL 部位ＣＮＴ > 4
005230         MOVE 冷罨料Ｗ(部位ＣＮＴ)     TO  冷罨料(部位ＣＮＴ)
005240         MOVE 初回処置料Ｗ(部位ＣＮＴ) TO  処置料(部位ＣＮＴ)
005250         MOVE 大チェックＷ(部位ＣＮＴ) TO  大チェック(部位ＣＮＴ)
005260         MOVE 中チェックＷ(部位ＣＮＴ) TO  中チェック(部位ＣＮＴ)
005270         MOVE 小チェックＷ(部位ＣＮＴ) TO  小チェック(部位ＣＮＴ)
005280     END-PERFORM.
005290*
005300*================================================================*
005310 項目初期化 SECTION.
005320*
005330     INITIALIZE 受診者情報Ｗ.
005340     INITIALIZE 料金情報Ｗ.
005350     INITIALIZE 明細.
005360     MOVE SPACE TO YCH712P.
005370     INITIALIZE YCH712P.
005380*================================================================*
005390 左側印刷 SECTION.
005400     IF 日計モードＷＲ = 1
005410         MOVE 連日数－日数(1)    TO 左日数１
005420         MOVE 連日数－日数(2)    TO 左日数２
005430         MOVE 連日数－日数(3)    TO 左日数３
005440         MOVE 連日数－日数(4)    TO 左日数４
005450         MOVE 連日数－日数(5)    TO 左電罨日数
               IF 施術和暦年月ＷＲ >= 43006
                  MOVE 連金運－金属運動ＣＭ TO 左金属運動ＣＭ
               END-IF
005460     END-IF.
005470     IF 日ごとモードＷＲ = 1
005480         MOVE 施術和暦ＷＲ       TO 作１－施術和暦
005490         MOVE 施術年ＷＲ         TO 作１－施術年
005500         MOVE 施術月ＷＲ         TO 作１－施術月
005510         MOVE 開始日ＷＲ         TO 作１－施術日
005520         START 作業ファイル１ KEY IS >= 作１－施術和暦年月日
005530         END-START
005540         IF 状態キー = "00"
005550             PERFORM 作業ファイル１読込
                   IF 開始段数ＷＲ = ZERO
                       MOVE 1 TO 行カウンタ
                   ELSE
                       MOVE 開始段数ＷＲ TO 行カウンタ
                   END-IF
005560             PERFORM VARYING 日位置ＣＮＴ FROM 開始日ＷＲ BY 1
005570                     UNTIL (日位置ＣＮＴ > 終了日ＷＲ) OR
005580                           (終了フラグ = "YES")
005590                 IF 作１－日チェック = 1
005600                     MOVE 作１－部位計(1)    TO 左１(行カウンタ)
005610                     MOVE 作１－部位計(2)    TO 左２(行カウンタ)
005620                     MOVE 作１－部位計(3)    TO 左３(行カウンタ)
005630                     MOVE 作１－部位計(4)    TO 左４(行カウンタ)
005640                     MOVE 作１－罨法金額     TO 左電罨(行カウンタ)
      *                   */日付の数字を転記する。
                           MOVE 作１－施術日       TO 施術日左(行カウンタ)
005650                     IF 印刷モードＦＷＲ = ZERO
005670                         MOVE 作１－一部負担金   TO 左一部負担金(行カウンタ)
005680                     ELSE
                               IF コメントモードＷＲ = 1
                                  MOVE 作１－コメント  TO コメントＷ
                                  MOVE コメント１Ｗ    TO 左コメント１(行カウンタ)
                                  MOVE コメント２Ｗ    TO 左コメント２(行カウンタ)
                               END-IF
005680                     END-IF
                           COMPUTE 行カウンタ = 行カウンタ + 1
005690                 END-IF
005700                 PERFORM 作業ファイル１読込
005710             END-PERFORM
005720         END-IF
005730     END-IF.
005740*================================================================*
005750 中側印刷 SECTION.
005760     IF 日計モードＷＲ = 1
005770         MOVE 連日数－日数(1)    TO 中日数１
005780         MOVE 連日数－日数(2)    TO 中日数２
005790         MOVE 連日数－日数(3)    TO 中日数３
005800         MOVE 連日数－日数(4)    TO 中日数４
005810         MOVE 連日数－日数(5)    TO 中電罨日数
               IF 施術和暦年月ＷＲ >= 43006
                  MOVE 連金運－金属運動ＣＭ TO 中金属運動ＣＭ
               END-IF
005820     END-IF.
005830     IF 日ごとモードＷＲ = 1
005840         MOVE 施術和暦ＷＲ       TO 作１－施術和暦
005850         MOVE 施術年ＷＲ         TO 作１－施術年
005860         MOVE 施術月ＷＲ         TO 作１－施術月
005870         MOVE 開始日ＷＲ         TO 作１－施術日
005880         START 作業ファイル１ KEY IS >= 作１－施術和暦年月日
005890         END-START
005900         IF 状態キー = "00"
005910             PERFORM 作業ファイル１読込
                   IF 開始段数ＷＲ = ZERO
                       MOVE 1 TO 行カウンタ
                   ELSE
                       MOVE 開始段数ＷＲ TO 行カウンタ
                   END-IF
005920             PERFORM VARYING 日位置ＣＮＴ FROM 開始日ＷＲ BY 1
005930                     UNTIL (日位置ＣＮＴ > 終了日ＷＲ) OR
005940                           (終了フラグ = "YES")
005950                 IF 作１－日チェック = 1
005960                     MOVE 作１－部位計(1)    TO 中１(行カウンタ)
005970                     MOVE 作１－部位計(2)    TO 中２(行カウンタ)
005980                     MOVE 作１－部位計(3)    TO 中３(行カウンタ)
005990                     MOVE 作１－部位計(4)    TO 中４(行カウンタ)
006000                     MOVE 作１－罨法金額     TO 中電罨(行カウンタ)
      *                   */日付の数字を転記する。
                           MOVE 作１－施術日       TO 施術日中(行カウンタ)
006010                     IF 印刷モードＦＷＲ = ZERO
006030                         MOVE 作１－一部負担金   TO 中一部負担金(行カウンタ)
005680                     ELSE
                               IF コメントモードＷＲ = 1
                                  MOVE 作１－コメント  TO コメントＷ
                                  MOVE コメント１Ｗ    TO 中コメント１(行カウンタ)
                                  MOVE コメント２Ｗ    TO 中コメント２(行カウンタ)
                               END-IF
006040                     END-IF
                           COMPUTE 行カウンタ = 行カウンタ + 1
006050                 END-IF
006060                 PERFORM 作業ファイル１読込
006070             END-PERFORM
006080         END-IF
006090     END-IF.
006100*================================================================*
006110 右側印刷 SECTION.
006120     IF 日計モードＷＲ = 1
006130         MOVE 連日数－日数(1)    TO 右日数１
006140         MOVE 連日数－日数(2)    TO 右日数２
006150         MOVE 連日数－日数(3)    TO 右日数３
006160         MOVE 連日数－日数(4)    TO 右日数４
006170         MOVE 連日数－日数(5)    TO 右電罨日数
               IF 施術和暦年月ＷＲ >= 43006
                  MOVE 連金運－金属運動ＣＭ TO 右金属運動ＣＭ
               END-IF
006180     END-IF.
006190     IF 日ごとモードＷＲ = 1
006200         MOVE 施術和暦ＷＲ       TO 作１－施術和暦
006210         MOVE 施術年ＷＲ         TO 作１－施術年
006220         MOVE 施術月ＷＲ         TO 作１－施術月
006230         MOVE 開始日ＷＲ         TO 作１－施術日
006240         START 作業ファイル１ KEY IS >= 作１－施術和暦年月日
006250         END-START
006260         IF 状態キー = "00"
006270             PERFORM 作業ファイル１読込
                   IF 開始段数ＷＲ = ZERO
                       MOVE 1 TO 行カウンタ
                   ELSE
                       MOVE 開始段数ＷＲ TO 行カウンタ
                   END-IF
006280             PERFORM VARYING 日位置ＣＮＴ FROM 開始日ＷＲ BY 1
006290                     UNTIL (日位置ＣＮＴ > 終了日ＷＲ) OR
006300                           (終了フラグ = "YES")
006310                 IF 作１－日チェック = 1
006320                     MOVE 作１－部位計(1)    TO 右１(行カウンタ)
006330                     MOVE 作１－部位計(2)    TO 右２(行カウンタ)
006340                     MOVE 作１－部位計(3)    TO 右３(行カウンタ)
006350                     MOVE 作１－部位計(4)    TO 右４(行カウンタ)
006360                     MOVE 作１－罨法金額     TO 右電罨(行カウンタ)
      *                   */日付の数字を転記する。
                           MOVE 作１－施術日       TO 施術日右(行カウンタ)
006370                     IF 印刷モードＦＷＲ = ZERO
006390                         MOVE 作１－一部負担金   TO 右一部負担金(行カウンタ)
005680                     ELSE
                               IF コメントモードＷＲ = 1
                                  MOVE 作１－コメント  TO コメントＷ
                                  MOVE コメント１Ｗ    TO 右コメント１(行カウンタ)
                                  MOVE コメント２Ｗ    TO 右コメント２(行カウンタ)
                               END-IF
006400                     END-IF
                           COMPUTE 行カウンタ = 行カウンタ + 1
006410                 END-IF
006420                 PERFORM 作業ファイル１読込
006430             END-PERFORM
006440         END-IF
006450     END-IF.
006460*================================================================*
006470 連結項目退避 SECTION.
006480*
006490     MOVE 連印－初検料     TO 初検料ＷＲ.
           MOVE 連印－相談料     TO 相談料ＷＲ.
006500     MOVE 連印－初検加算料 TO 初検加算料ＷＲ.
006510     IF 連印－休日 = 1
006520         MOVE NC"○"       TO 休日チェックＷ
006530     END-IF.
006540     IF 連印－深夜 = 1
006550         MOVE NC"○"       TO 深夜チェックＷ
006560     END-IF.
006570     IF 連印－時間外 = 1
006580         MOVE NC"○"       TO 時間外チェックＷ
006590     END-IF.
006600     MOVE 連印－再検料     TO 再検料ＷＲ.
006610     IF 連印－夜間 = 1
006620         MOVE NC"○"       TO 夜間チェックＷ
006630     END-IF.
006640     IF 連印－難路 = 1
006650         MOVE NC"○"       TO 難路チェックＷ
006660     END-IF.
006670     IF 連印－暴風 = 1
006680         MOVE NC"○"       TO 暴風チェックＷ
006690     END-IF.
006700     MOVE 連印－回数       TO 往療回数ＷＲ.
006710     MOVE 連印－距離       TO 往療距離ＷＲ.
006720     MOVE 連印－往療料金額 TO 往療料ＷＲ.
006730     MOVE 連印－往療加算料 TO 往療加算料ＷＲ.
006740     MOVE 連印－情報提供料 TO 情報提供料ＷＲ.
006750     PERFORM VARYING 部位ＣＮＴ FROM 1 BY 1 UNTIL 部位ＣＮＴ > 4
               IF 施術和暦年月ＷＲ < 43006
006760            EVALUATE 連印－金属(部位ＣＮＴ)
006770            WHEN 1
006780                MOVE NC"○"   TO 大チェックＷ(部位ＣＮＴ)
006790            WHEN 2
006800                MOVE NC"○"   TO 中チェックＷ(部位ＣＮＴ)
006810            WHEN 3
006820                MOVE NC"○"   TO 小チェックＷ(部位ＣＮＴ)
006830            END-EVALUATE
               END-IF
006840         MOVE 連印－初回処置料(部位ＣＮＴ) TO 初回処置料Ｗ(部位ＣＮＴ)
006850         MOVE 連印－冷罨料(部位ＣＮＴ)     TO 冷罨料Ｗ(部位ＣＮＴ)
006860*         MOVE 連印－金額(部位ＣＮＴ)       TO 金額Ｗ(部位ＣＮＴ)
006870     END-PERFORM.
006880*================================================================*
006890 受診者情報取得 SECTION.
006900*
006910**************************************************
006920* 連結データから受診者情報Ｆより以下の情報を取得 *
006930* ● 施術年 ..... 施術年Ｗに格納                 *
006940* ● 施術月 ..... 施術月Ｗに格納                 *
006950* ● 患者番号.... 患者番号Ｗに格納※ＦＤ連番用   *
006960* ● 患者氏名 ....患者氏名Ｗに格納               *
006970**************************************************
006980     MOVE 施術和暦ＷＲ       TO 受－施術和暦.
006990     MOVE 施術年ＷＲ         TO 受－施術年.
007000     MOVE 施術月ＷＲ         TO 受－施術月.
007010     MOVE 患者コードＷＲ     TO 受－患者コード.
007020     READ 受診者情報Ｆ
007030     INVALID KEY
007040         CONTINUE
007050*            /* ありえない */
007060     NOT INVALID KEY
007070         MOVE 受－施術年       TO 施術年Ｗ
007080         MOVE 受－施術月       TO 施術月Ｗ
007090         MOVE 受－患者番号     TO 患者番号Ｗ
007100         MOVE 受－患者氏名     TO 患者氏名Ｗ
007110     END-READ.
007120*================================================================*
007130 作業ファイル１読込 SECTION.
007140*
007150     READ 作業ファイル１ NEXT
007160     AT END
007170         MOVE "YES" TO 終了フラグ
007180     END-READ.
007190*
007200*================================================================*
007210 印刷処理１ SECTION.
007220*
004310     IF ( オープンフラグ NOT = "YES" )
004320        MOVE "YES" TO オープンフラグ
004330        OPEN I-O  印刷ファイル
004340        PERFORM エラー処理Ｐ
004350     END-IF.
007220*
007230     MOVE "YCH712P"  TO  定義体名Ｐ.
007240     MOVE "GRP001"   TO  項目群名Ｐ.
007250     WRITE YCH712P.
007260     PERFORM エラー処理Ｐ.
007270*================================================================*
007280 エラー処理Ｐ SECTION.
007290*
007300     IF 通知情報Ｐ NOT = "00"
007310         DISPLAY NC"帳票エラー"              UPON CONS
007320         DISPLAY NC"項目群名Ｐ：" 項目群名Ｐ UPON CONS
007330         DISPLAY NC"通知情報Ｐ：" 通知情報Ｐ UPON CONS
007340         DISPLAY NC"拡張制御Ｐ：" 拡張制御Ｐ UPON CONS
007350         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
007360                                             UPON CONS
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
007370         ACCEPT  キー入力 FROM CONS
007380         PERFORM ファイル閉鎖
007390         MOVE 99 TO PROGRAM-STATUS
007400         EXIT PROGRAM
007410     END-IF.
007420*================================================================*
007430 ファイル閉鎖 SECTION.
007440*
002990     IF ( オープンフラグ = "YES" )
002991         CLOSE 印刷ファイル
003041     END-IF.
007460     CLOSE 制御情報マスタ   受診者情報Ｆ  長期継続者Ｆ.
007470*================================================================*
007480 終了処理 SECTION.
007490*
007500     PERFORM ファイル閉鎖.
007510*================================================================*
006860 印刷行数退避 SECTION.
006870     CLOSE 受診者情報Ｆ.
006880     PERFORM 遅延処理.
006890     OPEN I-O 受診者情報Ｆ.
006900         MOVE NC"受診" TO ファイル名.
006910         PERFORM オープンチェック.
006920     PERFORM 遅延処理.
006930*
006940     MOVE ZERO            TO 受－施術和暦.
006950     MOVE ZERO            TO 受－施術年.
006960     MOVE ZERO            TO 受－施術月.
006970     MOVE 患者コードＷＲ  TO 受－患者コード.
006990*
007000     READ 受診者情報Ｆ
007010     NOT INVALID KEY
               IF 行カウンタ > 31
                   MOVE 1 TO 行カウンタ
               END-IF
007020         MOVE 行カウンタ TO 受－カルテ裏印刷行数
007030         REWRITE 受－レコード
007040         INVALID KEY
007050             MOVE NC"受Ｆ" TO ファイル名
007060             PERFORM エラー表示
007070         END-REWRITE
007080     END-READ.
007090     PERFORM 遅延処理.
007100     CLOSE 受診者情報Ｆ.
007110     PERFORM 遅延処理.
007120     OPEN INPUT 受診者情報Ｆ.
007130         MOVE NC"受診" TO ファイル名.
007140         PERFORM オープンチェック.
007530*================================================================*
007620 エラー表示 SECTION.
007630*
007640     DISPLAY ファイル名   NC"ファイル書込エラー"   UPON CONS.
007650     DISPLAY NC"状態キー：" 状態キー               UPON CONS.
007660     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
007670     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
                                                         UPON CONS.
000080*-----------------------------------------*
000090     CALL "actcshm"  WITH C LINKAGE.
000100*-----------------------------------------*
007680     ACCEPT  キー入力 FROM CONS.
007690     PERFORM ファイル閉鎖.
007700     EXIT PROGRAM.
007290*================================================================*
035270 長期理由文取得 SECTION.
003034     MOVE  ZERO   TO  カウンタ.
035280*
004502     MOVE 施術和暦ＷＲ   TO 長継－施術和暦
004503     MOVE 施術年ＷＲ     TO 長継－施術年.
004504     MOVE 施術月ＷＲ     TO 長継－施術月.
004505     MOVE 患者番号ＷＲ   TO 長継－患者番号.
004506     MOVE 枝番ＷＲ       TO 長継－枝番.
004507*
004508     READ 長期継続者Ｆ 
004509     INVALID KEY
                CONTINUE
004522     NOT INVALID KEY
004523         IF  (長継－理由文(1)  = ZERO )  AND
004524             (長継－理由文(2)  = ZERO )  AND
004525             (長継－理由文(3)  = ZERO )  AND
004526             (長継－理由文(4)  = ZERO )  AND
004527             (長継－理由文(5)  = ZERO )
                   CONTINUE
004535         ELSE
                   PERFORM 理由文セット
004537         END-IF
004538     END-READ.
035430*================================================================*
004541 理由文セット SECTION.
004542*
004543     PERFORM VARYING 番号カウンタ FROM 1 BY 1
004544             UNTIL   番号カウンタ  > 5 
004559*
004560         IF  長継－理由文(番号カウンタ) NOT = SPACE 
004561             PERFORM 番号転記
004595* 理由文
004598             MOVE 長継－理由文(番号カウンタ)  TO  理由文Ｗ
                   PERFORM 理由文分解３５桁
004631         END-IF
004632**
004637     END-PERFORM.
004647*
005590*================================================================*
005600 番号転記 SECTION.
005601*
005605      EVALUATE 番号カウンタ
005606      WHEN  1
005607          MOVE "①"  TO 長期番号Ｗ
005608      WHEN  2
005609          MOVE "②"  TO 長期番号Ｗ
005610      WHEN  3
005611          MOVE "③"  TO 長期番号Ｗ
005612      WHEN  4
005613          MOVE "④"  TO 長期番号Ｗ
005614      WHEN  5
005615          MOVE "⑤"  TO 長期番号Ｗ
005616      WHEN  OTHER
005617          CONTINUE
005618      END-EVALUATE.
005619*
005898*================================================================*
005706 理由文分解３５桁 SECTION.
005707**********
005708*** ３５桁
005709**********
005716* 合成した理由文を分割する。
005717     INITIALIZE 理由３５桁Ｗ.
005718     MOVE  合成理由文Ｗ  TO  理由３５桁Ｗ.
005945*半角スペースを全角にかえる(189ﾊﾞｲﾄより長いｴﾘｱに半角空白が入っているので)
005946     INSPECT 理由３５桁Ｗ REPLACING ALL 半角空白 BY 全角空白.
005719*
005720     IF  理由３５桁１Ｗ  NOT = SPACE
005721         COMPUTE カウンタ = カウンタ  +  1
005722         MOVE 理由３５桁１Ｗ  TO 長期理由文Ｗ(カウンタ)
005724     END-IF.
005725     IF  理由３５桁２Ｗ  NOT = SPACE
005726         COMPUTE カウンタ = カウンタ  +  1
005727         MOVE 理由３５桁２Ｗ  TO 長期理由文Ｗ(カウンタ)
005729     END-IF.
005730     IF  理由３５桁３Ｗ  NOT = SPACE
005731         COMPUTE カウンタ = カウンタ  +  1
005732         MOVE 理由３５桁３Ｗ  TO 長期理由文Ｗ(カウンタ)
005733     END-IF.
           IF  理由３５桁４Ｗ  NOT = SPACE
               COMPUTE カウンタ = カウンタ  +  1
               MOVE 理由３５桁４Ｗ  TO 長期理由文Ｗ(カウンタ)
           END-IF.
           IF  理由３５桁５Ｗ  NOT = SPACE
               COMPUTE カウンタ = カウンタ  +  1
               MOVE 理由３５桁５Ｗ  TO 長期理由文Ｗ(カウンタ)
           END-IF.
           IF  理由３５桁６Ｗ  NOT = SPACE
               COMPUTE カウンタ = カウンタ  +  1
               MOVE 理由３５桁６Ｗ  TO 長期理由文Ｗ(カウンタ)
           END-IF.
005734*
005898*================================================================*
004830 長期セット左 SECTION.
      *
      ************
      * 長期理由 *
      ************
      */長期理由を印刷0311
           PERFORM VARYING カウンタ FROM 1 BY 1
                   UNTIL   カウンタ > 7
               MOVE 長期理由文Ｗ(カウンタ) TO 長期理由文(カウンタ)
           END-PERFORM.
      *
007510*================================================================*
004850 長期セット中 SECTION.
      *
      ************
      * 長期理由 *
      ************
      */長期理由を印刷0311
           PERFORM VARYING カウンタ FROM 1 BY 1
                   UNTIL   カウンタ > 7
               MOVE 長期理由文Ｗ(カウンタ) TO 長期理由文２(カウンタ)
           END-PERFORM.
      *
007510*================================================================*
004870 長期セット右 SECTION.
      *
      ************
      * 長期理由 *
      ************
      */長期理由を印刷0311
           PERFORM VARYING カウンタ FROM 1 BY 1
                   UNTIL   カウンタ > 7
               MOVE 長期理由文Ｗ(カウンタ) TO 長期理由文３(カウンタ)
           END-PERFORM.
      *
007510*================================================================*
007520******************************************************************
007530 END PROGRAM YCH7121.
007540******************************************************************
