000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YCH6061.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*      レセプト印刷表示【ﾃﾞｰﾀ作成】柔ｳｨﾝﾄﾞｳｽﾞ版
000100* 請求年月Ver.
000110*      MED = YAW610
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2011-09-06
000140 DATE-COMPILED.          2011-09-06
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000261     SELECT  市町村マスタ    ASSIGN      TO        SITYOSNL
000262                             ORGANIZATION             IS  INDEXED
000263                             ACCESS MODE              IS  DYNAMIC
000264                             RECORD KEY               IS  市−公費種別
000265                                                          市−市町村番号
000266                             ALTERNATE RECORD KEY     IS  市−公費種別
000267                                                          市−市町村名称
000268                                                          市−市町村番号
000269                             FILE STATUS              IS  状態キー
000270                             LOCK        MODE         IS  AUTOMATIC.
000241     SELECT  生保情報Ｆ      ASSIGN      TO        SEIHOJL
000242                             ORGANIZATION             IS INDEXED
000243                             ACCESS MODE              IS DYNAMIC
000244                             RECORD KEY               IS 生保−施術和暦年月
000245                                                         生保−患者コード
000255                             ALTERNATE RECORD KEY     IS 生保−患者コード
000265                                                         生保−施術和暦年月
000277                             FILE STATUS              IS 状態キー
000278                             LOCK        MODE         IS AUTOMATIC.
000271     SELECT  施術記録Ｆ      ASSIGN      TO        SEKIROKL
000280                             ORGANIZATION             IS  INDEXED
000290                             ACCESS MODE              IS  DYNAMIC
000300                             RECORD KEY           IS 施記−施術和暦年月日
000310                                                     施記−患者コード
000320                             ALTERNATE RECORD KEY IS 施記−患者コード
000330                                                     施記−施術和暦年月日
000340                             FILE STATUS              IS  状態キー
000350                             LOCK        MODE         IS  AUTOMATIC.
000360     SELECT  負傷データＦ    ASSIGN      TO        HUSYOUL
000370                             ORGANIZATION             IS  INDEXED
000380                             ACCESS MODE              IS  DYNAMIC
000390                             RECORD KEY               IS 負−施術和暦年月
000400                                                         負−患者コード
000410                             ALTERNATE RECORD KEY     IS 負−患者コード
000420                                                         負−施術和暦年月
000430                             FILE STATUS              IS  状態キー
000440                             LOCK        MODE         IS  AUTOMATIC.
000450     SELECT  制御情報マスタ  ASSIGN      TO        SEIGYOL
000460                             ORGANIZATION             IS  INDEXED
000470                             ACCESS MODE              IS  DYNAMIC
000480                             RECORD KEY               IS  制−制御区分
000490                             FILE STATUS              IS  状態キー
000500                             LOCK        MODE         IS  AUTOMATIC.
000510     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000520                             ORGANIZATION             IS  INDEXED
000530                             ACCESS MODE              IS  DYNAMIC
000540                             RECORD KEY               IS  受−施術和暦年月
000550                                                          受−患者コード
000560                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000570                                                          受−患者カナ
000580                                                          受−患者コード
000590                             ALTERNATE RECORD KEY     IS  受−患者コード
000600                                                          受−施術和暦年月
000610                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000620                                                          受−保険種別
000630                                                          受−保険者番号
000640                                                          受−患者コード
000650                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000660                                                          受−公費種別
000670                                                          受−費用負担者番号
000680                                                          受−患者コード
000690                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000700                                                          受−助成種別
000710                                                          受−費用負担者番号助成
000720                                                          受−患者コード
000730                             ALTERNATE RECORD KEY     IS  受−請求和暦年月
000740                                                          受−施術和暦年月
000750                                                          受−患者コード
000760                             FILE STATUS              IS  状態キー
000770                             LOCK        MODE         IS  AUTOMATIC.
           SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
                                   ORGANIZATION             IS  INDEXED
                                   ACCESS MODE              IS  DYNAMIC
                                   RECORD KEY               IS  レセ−施術和暦年月
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−患者コード
                                                                レセ−施術和暦年月
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−施術和暦年月
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−レセ種別
                                                                レセ−請求保険者番号
                                                                レセ−患者コード
                                                                レセ−施術和暦年月
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−請求保険者番号
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                                                レセ−施術和暦年月
                                   FILE STATUS              IS  状態キー
                                   LOCK        MODE         IS  AUTOMATIC.
000780     SELECT  作業ファイル１  ASSIGN      TO    "C:\MAKISHISYS\YAWOBJ\TEMP\W60610L.DAT"
000790                             ORGANIZATION             IS  INDEXED
000800                             ACCESS                   IS  DYNAMIC
000810                             RECORD      KEY          IS  作１−請求和暦年月
000820                                                          作１−助成区分
000830                                                          作１−保険者番号
000840                                                          作１−本人家族区分
000850                                                          作１−施術和暦年月
000860                                                          作１−被保険者カナ
000870                                                          作１−患者コード
000880                                                          作１−保険者番号助成
000890                             FILE        STATUS       IS  状態キー
000900                             LOCK        MODE         IS  AUTOMATIC.
000910* レセ並び順用
000920     SELECT  作業ファイル２  ASSIGN      TO   "C:\MAKISHISYS\YAWOBJ\TEMP\W6102L.DAT"
000930                             ORGANIZATION             IS  INDEXED
000940                             ACCESS                   IS  DYNAMIC
001230                             RECORD      KEY          IS  作２−保険区分
                                                                作２−順番
                                   ALTERNATE RECORD KEY     IS  作２−施術和暦年月
                                                                作２−患者コード
                                                                作２−保険区分
                                                                作２−順番
000960                             FILE        STATUS       IS  状態キー
000970                             LOCK        MODE         IS  AUTOMATIC.
000980******************************************************************
000990*                      DATA DIVISION                             *
001000******************************************************************
001010 DATA                    DIVISION.
001020 FILE                    SECTION.
001021*                           ［ＲＬ＝  ２５６］
001022 FD  市町村マスタ          BLOCK   CONTAINS   1   RECORDS.
001023     COPY SITYOSN        OF  XFDLIB  JOINING   市   AS  PREFIX.
001080* 
000280 FD  生保情報Ｆ          BLOCK   CONTAINS   1   RECORDS.
000281     COPY SEIHOJ          OF  XFDLIB  JOINING   生保   AS  PREFIX.
001030*                           ［ＲＬ＝  ２５６］
001040 FD  施術記録Ｆ          BLOCK   CONTAINS   1   RECORDS.
001050     COPY SEKIROK         OF  XFDLIB  JOINING   施記 AS  PREFIX.
001060*                           ［ＲＬ＝  １２８］
001070 FD  負傷データＦ        BLOCK   CONTAINS   1   RECORDS.
001080     COPY HUSYOU          OF  XFDLIB  JOINING   負   AS  PREFIX.
001090*                           ［ＲＬ＝  ２５６］
001100 FD  制御情報マスタ          BLOCK   CONTAINS   1   RECORDS.
001110     COPY SEIGYO          OF  XFDLIB  JOINING   制   AS  PREFIX.
      *
       FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS GLOBAL.
           COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
001120*                           ［ＲＬ＝  ３２０］
001130 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001140     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
001150* 保険者番号順ファイル
001160 FD  作業ファイル１ RECORD  CONTAINS 256 CHARACTERS.
001170 01  作１−レコード.
001180     03  作１−レコードキー.
001190         05  作１−請求和暦年月.
001200             07  作１−請求和暦            PIC 9.
001210             07  作１−請求年              PIC 9(2).
001220             07  作１−請求月              PIC 9(2).
001230         05  作１−助成区分                PIC 9.
001240         05  作１−保険者番号              PIC 9(8).
001250         05  作１−本人家族区分            PIC 9.
001260         05  作１−施術和暦年月.
001270             07  作１−施術和暦            PIC 9.
001280             07  作１−施術年              PIC 9(2).
001290             07  作１−施術月              PIC 9(2).
001300         05  作１−被保険者カナ            PIC X(50).
001310         05  作１−患者コード.
001320             07 作１−患者番号             PIC 9(6).
001330             07 作１−枝番                 PIC X(1).
001340         05  作１−保険者番号助成          PIC 9(8).
001350     03  作１−レコードデータ.
001360         05  作１−保険種別                PIC 9(2).
001221         05  作１−レセ種別                PIC 9(2).
001740         05  作１−患者氏名                PIC X(50).
001740         05  作１−被保険者氏名            PIC X(50).
001366         05  作１−費用額                  PIC 9(6).
001367         05  作１−負担額                  PIC 9(6).
001368         05  作１−請求額                  PIC 9(6).
001369         05  作１−レセ印刷区分            PIC 9.
001370         05  FILLER                        PIC X(48).
001380*
001790 FD  作業ファイル２ RECORD  CONTAINS 155 CHARACTERS.
001800 01  作２−レコード.
001810     03  作２−レコードキー.
001820         05  作２−保険区分                PIC 9(1).
001820         05  作２−順番                    PIC 9(4).
001830     03  作２−レコードデータ.
               05  作２−レセ種別                PIC 9(2).
001840         05  作２−患者コード.
001850             07 作２−患者番号             PIC 9(6).
001860             07 作２−枝番                 PIC X(1).
001870         05  作２−患者氏名                PIC X(50).
001740         05  作２−被保険者氏名            PIC X(50).
001880         05  作２−保険種別                PIC 9(2).
               05  作２−請求保険者番号          PIC X(10).
001890         05  作２−本人家族                PIC X(4).
001900         05  作２−施術和暦年月.
001910             07  作２−施術和暦            PIC 9.
001920             07  作２−施術年              PIC 9(2).
001930             07  作２−施術月              PIC 9(2).
001940         05  作２−印刷指定                PIC 9(1).
               05  作２−費用額                  PIC 9(6).
               05  作２−負担額                  PIC 9(6).
               05  作２−請求額                  PIC 9(6).
               05  作２−レセ印刷区分            PIC 9.
001560*
001570*----------------------------------------------------------------*
001580******************************************************************
001590*                WORKING-STORAGE SECTION                         *
001600******************************************************************
001610 WORKING-STORAGE         SECTION.
001620 01 キー入力                           PIC X    VALUE SPACE.
001630 01 状態キー                           PIC X(2) VALUE SPACE.
001640 01 終了フラグ                         PIC X(3) VALUE SPACE.
001650 01 終了フラグ２                       PIC X(3) VALUE SPACE.
001660 01 実行キーＷ                         PIC X(3)  VALUE SPACE.
001670 01 施術記録有Ｗ                       PIC X(3) VALUE SPACE.
001680 01 カウンタ                           PIC 9(2)  VALUE ZERO.
001690**
001700 01 助成区分Ｗ                         PIC 9    VALUE ZERO.
001710 01 保険種別ＷＲ                       PIC 9(2) VALUE ZERO.
001720 01 印刷形式ＷＲ                       PIC 9    VALUE ZERO.
001730 01 保険者番号ＷＲ                     PIC X(10) VALUE SPACE.
001740 01 レセプト種類ＷＲ                   PIC X(4) VALUE SPACE.
001750 01 本人家族区分ＷＲ                   PIC 9    VALUE ZERO.
001760 01 施術和暦年月ＷＲ.
001770    03 施術和暦ＷＲ                    PIC 9    VALUE ZERO.
001780    03 施術年ＷＲ                      PIC 9(2) VALUE ZERO.
001790    03 施術月ＷＲ                      PIC 9(2) VALUE ZERO.
001800 01 請求和暦年月ＷＲ.
001810    03 請求和暦ＷＲ                    PIC 9    VALUE ZERO.
001820    03 請求年ＷＲ                      PIC 9(2) VALUE ZERO.
001830    03 請求月ＷＲ                      PIC 9(2) VALUE ZERO.
001840**
001850 01 ファイル名                         PIC N(8) VALUE SPACE.
001860 01 保険種別Ｗ                         PIC 9(2) VALUE ZERO.
001870 01 順番Ｗ                             PIC 9(4) VALUE ZERO.
002090 01 生保順番Ｗ                         PIC 9(4)  VALUE ZERO.
001880 01 レセ並び順昇降Ｗ                   PIC 9(1) VALUE ZERO.
001890*
001891*
001892** 助成レセまとめ用
001893 01 助成レセまとめフラグ               PIC X(3)  VALUE SPACE.
001894*
001900** 合計集計用
001910 01 合計Ｗ.
001920    03 件数合計Ｗ                      PIC 9(4) VALUE ZERO.
001930    03 費用額合計Ｗ                    PIC 9(8) VALUE ZERO.
001940    03 請求額合計Ｗ                    PIC 9(8) VALUE ZERO.
001950** エラーメッセージ用
001960 01 エラーメッセージＷ.
001970    03 エラー患者コードＷ              PIC X(7) VALUE SPACE.
001980    03 エラー区切りＷ                  PIC X(1) VALUE SPACE.
001990    03 エラー保険種別Ｗ                PIC X(2) VALUE SPACE.
002000    03 FILLER                          PIC X(10) VALUE SPACE.
002010*
002020** 保険者番号右詰め用
002030 01 保険者番号ＷＴ.
002040    03 保険者番号左詰めＷ.
002050      05 保険者番号左詰めＷ１          PIC X OCCURS 8 VALUE SPACE.
002060    03 保険者番号右詰めＷ.
002070      05 保険者番号右詰めＷ１          PIC X OCCURS 8 VALUE ZERO.
002080    03 保険者番号数字Ｗ                PIC 9(8)  VALUE ZERO.
002090    03 保険者番号Ｗ                    PIC X(8)  VALUE SPACE.
       01 金額Ｗ.
          03 費用額Ｗ                        PIC 9(6)  VALUE ZERO.
          03 負担額Ｗ                        PIC 9(6)  VALUE ZERO.
          03 請求額Ｗ                        PIC 9(6)  VALUE ZERO.
002100**
002110 01 計算機西暦年Ｗ                     PIC 9(2).
002120* 日付ＷＯＲＫ
002130 01 計算機西暦.
002140    03 計算機西暦年                    PIC 9(4).
002150    03 計算機西暦月日                  PIC 9(4).
002160 01 計算機西暦Ｒ REDEFINES 計算機西暦.
002170    03 計算機世紀                      PIC 9(2).
002180    03 計算機日付                      PIC 9(6).
002190    03 計算機日付Ｒ REDEFINES 計算機日付.
002200       05 計算機年月                   PIC 9(4).
002210       05 計算機年月Ｒ REDEFINES 計算機年月.
002220         07 計算機年                   PIC 9(2).
002230         07 計算機月                   PIC 9(2).
002240       05 計算機日                     PIC 9(2).
002250*
002260******************************************************************
002270*                          連結項目                              *
002280******************************************************************
002290*
002300********************
002310* メッセージ表示キー *
002320********************
002330 01 連メ−キー IS EXTERNAL.
002340    03  連メ−メッセージ               PIC N(20).
002350*
002360 01 連メ３−キー IS EXTERNAL.
002370    03  連メ３−メッセージ             PIC N(20).
002380    03  連メ３−メッセージ１           PIC X(20).
002390***
002480 01 連入−入力データ６１０ IS EXTERNAL.
002490    03 連入−請求年月.
002500       05 連入−請求和暦                  PIC 9.
002510       05 連入−請求年                    PIC 9(2).
002520       05 連入−請求月                    PIC 9(2).
002540    03 連入−保険種別                     PIC 9(2).
002520*
002521************************
002522* 助成レセまとめ
002523************************
002524 01 連レセまとめ−キー IS EXTERNAL.
002525    03 連レセまとめ−施術和暦年月.
002526       05 連レセまとめ−施術和暦               PIC 9.
002527       05 連レセまとめ−施術年月.
002528          07 連レセまとめ−施術年              PIC 9(2).
002529          07 連レセまとめ−施術月              PIC 9(2).
002530    03 連レセまとめ−患者コード.
002531       05 連レセまとめ−患者番号               PIC 9(6).
002532       05 連レセまとめ−枝番                   PIC X(1).
002533**-------------------------------------------------------**
002534*   1:助成レセプトなしの本体まとめの判定
002535*   2:横浜・川崎用の社保助成レセかの判定
002536    03 連レセまとめ−判定区分                  PIC 9.
002537**-------------------------------------------------------**
002538*  / OUT /　 0:対象外、1:対象
002539    03 連レセまとめ−判定結果                  PIC 9.
002540**
002541******************************************************************
002542*                      PROCEDURE  DIVISION                       *
002550******************************************************************
002560 PROCEDURE               DIVISION.
002570************
002580*           *
002590* 初期処理   *
002600*           *
002610************
002620     PERFORM 初期化.
002630************
002640*           *
002650* 主処理     *
002660*           *
002670************
002680     PERFORM 作業ファイル作成.
002690************
002700*           *
002710* 終了処理   *
002720*           *
002730************
002740     PERFORM 終了処理.
002750     MOVE ZERO TO PROGRAM-STATUS.
002760     EXIT PROGRAM.
002770*
002780*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002790*================================================================*
002800 初期化 SECTION.
002810*
002820     PERFORM ファイルオープン.
002830* 連結項目の待避
002840     MOVE 連入−請求和暦      TO 請求和暦ＷＲ.
002850     MOVE 連入−請求年        TO 請求年ＷＲ.
002860     MOVE 連入−請求月        TO 請求月ＷＲ.
002960     MOVE 連入−保険種別      TO 保険種別ＷＲ.
002870* レセ並び順昇順・降順セット
002880     MOVE ZEROS TO 制−制御区分.
002890     READ 制御情報マスタ
002900     NOT INVALID KEY
002910         MOVE 制−レセ並び順昇降 TO レセ並び順昇降Ｗ
002920     END-READ.
002930*
002940*================================================================*
002950 ファイルオープン SECTION.
002960*
002970     OPEN INPUT 施術記録Ｆ.
002980         MOVE NC"施術記録Ｆ" TO ファイル名.
002990         PERFORM オープンチェック.
003000     OPEN INPUT 負傷データＦ.
003010         MOVE NC"負傷データＦ" TO ファイル名.
003020         PERFORM オープンチェック.
003030     OPEN INPUT 制御情報マスタ
003040         MOVE NC"制御情報" TO ファイル名.
003050         PERFORM オープンチェック.
003060     OPEN INPUT 受診者情報Ｆ.
003070         MOVE NC"受診者情報Ｆ" TO ファイル名.
003080         PERFORM オープンチェック.
003081     OPEN INPUT 市町村マスタ.
003082         MOVE NC"市町村" TO ファイル名.
003083         PERFORM オープンチェック.
006630     OPEN INPUT 生保情報Ｆ.
006640         MOVE NC"生保" TO ファイル名.
006650         PERFORM オープンチェック.
003250     OPEN INPUT レセプトＦ.
003260         MOVE NC"レセプトＦ" TO ファイル名.
003270         PERFORM オープンチェック.
003090*================================================================*
003100 オープンチェック SECTION.
003110*
003120     IF 状態キー  NOT =  "00"
003130         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
003140         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003150         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003160                                                 UPON CONS
003131*-----------------------------------------*
003132         CALL "actcshm"  WITH C LINKAGE
003133*-----------------------------------------*
003170         ACCEPT  キー入力 FROM CONS
003180         PERFORM ファイル閉鎖
003190         MOVE 99 TO PROGRAM-STATUS
003200         EXIT PROGRAM.
003210*================================================================*
003220 ファイル閉鎖 SECTION.
003230*
003240     CLOSE 施術記録Ｆ 負傷データＦ 制御情報マスタ
                 生保情報Ｆ 受診者情報Ｆ 市町村マスタ   レセプトＦ.
003250*================================================================*
003260 終了処理 SECTION.
003270*
003280     PERFORM ファイル閉鎖.
003290*================================================================*
003300 エラー表示 SECTION.
003310*
003320     DISPLAY NC"状態キー" 状態キー  UPON CONS.
003330     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
003340     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
003350     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
003321*-----------------------------------------*
003322     CALL "actcshm"  WITH C LINKAGE.
003323*-----------------------------------------*
003360     ACCEPT  キー入力 FROM CONS.
003370     PERFORM ファイル閉鎖.
003380     MOVE 99 TO PROGRAM-STATUS.
003390     EXIT PROGRAM.
003400*================================================================*
003410 エラー表示Ｒ SECTION.
003420*
003430     DISPLAY NC"ファイル読込エラー" ファイル名     UPON CONS.
003440     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
003450     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
003321*-----------------------------------------*
003322     CALL "actcshm"  WITH C LINKAGE.
003323*-----------------------------------------*
003460     ACCEPT  キー入力 FROM CONS.
003470     PERFORM ファイル閉鎖.
003480     MOVE 99 TO PROGRAM-STATUS.
003490     EXIT PROGRAM.
003500*================================================================*
003510 作業ファイル作成 SECTION.
003520*
003530     PERFORM 保険者番号順ファイル作成.
003540*
003550* レセ並び順ファイル作成
003560     OPEN OUTPUT 作業ファイル２.
003570         MOVE NC"作２" TO ファイル名.
003580         PERFORM オープンチェック.
003590*
003600     IF レセ並び順昇降Ｗ = 1
003610         PERFORM レセ並び順降順ファイル作成
003620     ELSE
003630         PERFORM レセ並び順ファイル作成
003640     END-IF.
003650*
003660     CLOSE 作業ファイル２.
003670*
003680*================================================================*
003690 保険者番号順ファイル作成 SECTION.
003700**********************************************************************
003710**   受診者情報Ｆから、該当請求年月のデータを抽出し、
003720**   作業ファイル１(保険者番号順)に書き出す.
003730**********************************************************************
003740*
003750     OPEN OUTPUT 作業ファイル１.
003760         MOVE NC"作１" TO ファイル名.
003770         PERFORM オープンチェック.
003780*
003790     MOVE "YES"         TO 実行キーＷ.
005170     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
005180     MOVE 請求年ＷＲ    TO レセ−請求年.  
005190     MOVE 請求月ＷＲ    TO レセ−請求月.  
013730     MOVE ZERO          TO レセ−レセ種別.
005200     MOVE ZERO          TO レセ−施術和暦.
005210     MOVE ZERO          TO レセ−施術年.  
005220     MOVE ZERO          TO レセ−施術月.  
005230     MOVE ZERO          TO レセ−患者番号.
005240     MOVE SPACE         TO レセ−枝番.    
013770     START レセプトＦ   KEY IS >= レセ−請求和暦年月
000230                                  レセ−施術和暦年月
000240                                  レセ−患者コード
000250                                  レセ−レセ種別
003910     END-START.
003920*     PERFORM データ確認.
003930     IF 状態キー = "00"
003940         MOVE SPACE  TO 終了フラグ
003720         PERFORM レセプトＦ読込
003960         PERFORM UNTIL ( 終了フラグ = "YES" ) OR
005330                       ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
005340                       ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
005350                       ( レセ−請求月   NOT = 請求月ＷＲ   )
004000*
004010            PERFORM データチェック
004020** 労災・自賠責・自由は対象外
003790            IF  レセ−レセ種別 = 4 OR 5 OR 6 OR 8
004040               MOVE SPACE  TO 実行キーＷ
004050            END-IF
003830            IF 実行キーＷ = "YES"
      *
004060               IF  受−助成種別   = ZERO  OR 50
004070                  IF ( 受−保険種別   NOT = ZERO ) AND
004080                     ( 受−保険者番号 NOT = SPACE )
004090** 助成なし(生保親ありは助成なし扱い)
004100                     MOVE ZERO  TO   助成区分Ｗ
004110                  ELSE
004120** 生保単独
004130                     MOVE 2     TO   助成区分Ｗ
004140                  END-IF
004150               ELSE
004160** 助成あり
004170                  MOVE 1         TO   助成区分Ｗ
005153*             （助成まとめは、助成なし扱いにする）
005154                  PERFORM 助成レセまとめ判定
005155                  IF ( 助成レセまとめフラグ = "YES" ) 
005156                     MOVE ZERO  TO  助成区分Ｗ
005157                  END-IF
004177*
004189               END-IF
005610               EVALUATE TRUE
004217*            ********
004220*            * 健保 *
004230*            ********
013940                WHEN レセ−レセ種別   = 1
                          IF (保険種別ＷＲ = ZERO) OR
                             ((受−保険種別 = 保険種別ＷＲ) AND ( 受−公費種別 = ZERO ))
004260*                **********************
004270*                * 作業ファイル作成 *
004280*                **********************
004330                       MOVE SPACE TO 作１−レコード
004340                       INITIALIZE 作１−レコード
004350                       MOVE 助成区分Ｗ         TO 作１−助成区分
004360                       MOVE 受−保険種別       TO 作１−保険種別
004370                       MOVE 受−保険者番号     TO 保険者番号Ｗ
004380                       PERFORM 保険者番号右詰め
004390                       MOVE 保険者番号数字Ｗ   TO 作１−保険者番号
004010                       PERFORM 料金セット
004400                       PERFORM 作１レコードセット
004410                       PERFORM 作１ファイル書込
004640*
004660                    END-IF
004680*            ********
004690*            * 老人 *
004700*            ********
                      WHEN レセ−レセ種別   = 2 
                          IF (保険種別ＷＲ = ZERO) OR
                             (受−公費種別 = 保険種別ＷＲ)
004730*                    **********************
004740*                    * 作業ファイル作成 *
004750*                    **********************
004780                       MOVE SPACE TO 作１−レコード
004790                       INITIALIZE 作１−レコード
004800                       MOVE 助成区分Ｗ         TO 作１−助成区分
004810                       MOVE 受−公費種別       TO 作１−保険種別
004820                       MOVE 受−費用負担者番号  TO 保険者番号Ｗ
004830                       PERFORM 保険者番号右詰め
004840                       MOVE 保険者番号数字Ｗ   TO 作１−保険者番号
004010                       PERFORM 料金セット
004850                       PERFORM 作１レコードセット
004860                       PERFORM 作１ファイル書込
005110                    END-IF
004220*            ********
004230*            * 助成 *
004240*            ********
004241*↓* 特別処理（助成レセまとめ）**
                      WHEN レセ−レセ種別   = 3
                          IF (保険種別ＷＲ = ZERO) OR
                             (受−助成種別 = 保険種別ＷＲ)
                             IF 助成区分Ｗ = 1
005930*         / 横浜・川崎の小児医療の請求額０円は対象外にする /170412
006880                          IF (レセ−助成請求金額 = ZERO) AND
                                   (受−助成種別 = "55") AND
                                   (受−費用負担者番号助成(3:3) = "144" OR "145")
                                   CONTINUE
                                ELSE
004540                             MOVE SPACE TO 作１−レコード
004550                             INITIALIZE 作１−レコード
004560                             MOVE 助成区分Ｗ         TO 作１−助成区分
004570                             MOVE 受−助成種別       TO 作１−保険種別
004580                             MOVE 受−保険者番号     TO 保険者番号Ｗ
004590                             PERFORM 保険者番号右詰め
004600                             MOVE 保険者番号数字Ｗ   TO 作１−保険者番号
004610                             MOVE 受−費用負担者番号助成  TO  作１−保険者番号助成
004010                             PERFORM 助成料金セット
004620                             PERFORM 作１レコードセット
004630                             PERFORM 作１ファイル書込
                                END-IF
                             END-IF
                          END-IF
004220*                ********
004230*                * 生保 *
004240*                ********
                      WHEN レセ−レセ種別   = 7
                          IF (保険種別ＷＲ = ZERO) OR
                             (受−保険種別 = 保険種別ＷＲ)
003920                        MOVE SPACE TO 作１−レコード
003930                        INITIALIZE 作１−レコード
004560                        MOVE 助成区分Ｗ       TO 作１−助成区分
003960                        MOVE 受−保険種別     TO 作１−保険種別
                              MOVE 受−施術和暦年月 TO 生保−施術和暦年月
                              MOVE 受−患者コード   TO 生保−患者コード
                              READ 生保情報Ｆ
                              NOT INVALID KEY
003950                           MOVE 生保−負担者番号   TO 保険者番号Ｗ
                              END-READ
004380                        PERFORM 保険者番号右詰め
004390                        MOVE 保険者番号数字Ｗ   TO 作１−保険者番号
004010                        PERFORM 料金セット
004370                        PERFORM 作１レコードセット
004380                        PERFORM 作１ファイル書込
004391                    END-IF
                      END-EVALUATE
                   END-IF
005120*
004410             PERFORM レセプトＦ読込
005140         END-PERFORM
005150     END-IF.
005160*
005170     CLOSE 作業ファイル１.
005180*
005190*================================================================*
005200*================================================================*
005210 データ確認 SECTION.
005220*
005230     IF 状態キー  NOT =  "00"
005240         MOVE  NC"　　　該当年月のデータがありません。" TO 連メ−メッセージ
005250         CALL   "MSG001"
005260         CANCEL "MSG001"
005270         PERFORM ファイル閉鎖
005280         MOVE 99 TO PROGRAM-STATUS
005290         EXIT PROGRAM
005300     END-IF.
005310*
005320*================================================================*
005330 データチェック SECTION.
005340*
005350     MOVE SPACE  TO 実行キーＷ.
005300     IF ( レセ−レセ印刷対象区分 NOT = 1 )
005310        MOVE "YES"  TO 実行キーＷ
005320     END-IF.
019520* *****************************************************************
019530* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
019540* *****************************************************************
005360     IF 実行キーＷ  = "YES"
005370*      (再度、実行キーＷ SPACE)
005380        MOVE SPACE  TO 実行キーＷ
019640        IF レセ−請求対象区分 NOT = ZERO
004090           MOVE レセ−施術和暦  TO 受−施術和暦
004100           MOVE レセ−施術年    TO 受−施術年
004110           MOVE レセ−施術月    TO 受−施術月
004120           MOVE レセ−患者番号  TO 受−患者番号
004130           MOVE レセ−枝番      TO 受−枝番
                 READ 受診者情報Ｆ
                 NOT INVALID KEY
019880              MOVE "YES"  TO 実行キーＷ
                 END-READ
019900        ELSE
019910           MOVE SPACE  TO 実行キーＷ
              END-IF
019950     END-IF.
005010*
005900*================================================================*
005910 作１レコードセット SECTION.
005920*
005120     MOVE レセ−請求和暦    TO 作１−請求和暦.
005130     MOVE レセ−請求年      TO 作１−請求年.
005140     MOVE レセ−請求月      TO 作１−請求月.
005150     MOVE レセ−施術和暦    TO 作１−施術和暦.
005160     MOVE レセ−施術年      TO 作１−施術年.
005170     MOVE レセ−施術月      TO 作１−施術月.
005990     MOVE 受−本人家族区分  TO 作１−本人家族区分.
006000     MOVE 受−被保険者カナ  TO 作１−被保険者カナ.
006010     MOVE 受−患者コード    TO 作１−患者コード.
005210     MOVE 受−患者氏名      TO 作１−患者氏名.
005210     MOVE 受−被保険者氏名  TO 作１−被保険者氏名.
           MOVE レセ−レセ種別    TO 作１−レセ種別.
           MOVE 費用額Ｗ          TO 作１−費用額.
           MOVE 請求額Ｗ          TO 作１−請求額.
           MOVE 負担額Ｗ          TO 作１−負担額.
006020*
005390*================================================================*
       料金セット SECTION.
           MOVE レセ−合計        TO 費用額Ｗ.
           MOVE レセ−請求金額    TO 請求額Ｗ.
           MOVE レセ−一部負担金  TO 負担額Ｗ.
005210     MOVE 受−レセ印刷区分  TO 作１−レセ印刷区分.
005380*
005390*================================================================*
       助成料金セット SECTION.
           MOVE レセ−合計           TO 費用額Ｗ.
           MOVE レセ−助成請求金額   TO 請求額Ｗ.
           MOVE レセ−受給者負担額   TO 負担額Ｗ.
005210     MOVE 受−レセ印刷区分助成 TO 作１−レセ印刷区分.
005380*
006030*================================================================*
006040 作１ファイル書込 SECTION.
006050*
006060     WRITE 作１−レコード
006070     INVALID KEY
006080         MOVE NC"作１"  TO ファイル名
006090         PERFORM エラー表示
006100     END-WRITE.
006110*
006120*================================================================*
006130 保険者番号右詰め SECTION.
006140*
006150     MOVE 保険者番号Ｗ    TO  保険者番号左詰めＷ.
006160     MOVE ZERO            TO  保険者番号右詰めＷ.
006170     MOVE ZERO            TO  保険者番号数字Ｗ.
006180*
006190     MOVE  9  TO  カウンタ.
006200*
006210     IF  保険者番号左詰めＷ１(8) NOT = SPACE
006220         COMPUTE カウンタ = カウンタ  -  1
006230         MOVE 保険者番号左詰めＷ１(8)  TO  保険者番号右詰めＷ１(カウンタ)
006240     END-IF.
006250     IF  保険者番号左詰めＷ１(7) NOT = SPACE
006260         COMPUTE カウンタ = カウンタ  -  1
006270         MOVE 保険者番号左詰めＷ１(7)  TO  保険者番号右詰めＷ１(カウンタ)
006280     END-IF.
006290     IF  保険者番号左詰めＷ１(6) NOT = SPACE
006300         COMPUTE カウンタ = カウンタ  -  1
006310         MOVE 保険者番号左詰めＷ１(6)  TO  保険者番号右詰めＷ１(カウンタ)
006320     END-IF.
006330     IF  保険者番号左詰めＷ１(5) NOT = SPACE
006340         COMPUTE カウンタ = カウンタ  -  1
006350         MOVE 保険者番号左詰めＷ１(5)  TO  保険者番号右詰めＷ１(カウンタ)
006360     END-IF.
006370     IF  保険者番号左詰めＷ１(4) NOT = SPACE
006380         COMPUTE カウンタ = カウンタ  -  1
006390         MOVE 保険者番号左詰めＷ１(4)  TO  保険者番号右詰めＷ１(カウンタ)
006400     END-IF.
006410     IF  保険者番号左詰めＷ１(3) NOT = SPACE
006420         COMPUTE カウンタ = カウンタ  -  1
006430         MOVE 保険者番号左詰めＷ１(3)  TO  保険者番号右詰めＷ１(カウンタ)
006440     END-IF.
006450     IF  保険者番号左詰めＷ１(2) NOT = SPACE
006460         COMPUTE カウンタ = カウンタ  -  1
006470         MOVE 保険者番号左詰めＷ１(2)  TO  保険者番号右詰めＷ１(カウンタ)
006480     END-IF.
006490     IF  保険者番号左詰めＷ１(1) NOT = SPACE
006500         COMPUTE カウンタ = カウンタ  -  1
006510         MOVE 保険者番号左詰めＷ１(1)  TO  保険者番号右詰めＷ１(カウンタ)
006520     END-IF.
006530*
006540     MOVE 保険者番号右詰めＷ TO 保険者番号数字Ｗ.
006550*
006560*================================================================*
006570 作業ファイル１読込 SECTION.
006580*
006590     READ 作業ファイル１ NEXT
006600     AT END
006610         MOVE "YES" TO 終了フラグ
006620     NOT AT END
006630         MOVE 作１−施術和暦    TO 受−施術和暦
006640         MOVE 作１−施術年      TO 受−施術年
006650         MOVE 作１−施術月      TO 受−施術月
006660         MOVE 作１−患者番号    TO 受−患者番号
006670         MOVE 作１−枝番        TO 受−枝番
006680         READ 受診者情報Ｆ
006690         INVALID KEY
006700              MOVE NC"受診者"   TO ファイル名
006710              PERFORM エラー表示Ｒ
006720         END-READ
006730     END-READ.
006740*
004450*================================================================*
006930 レセプトＦ読込 SECTION.
006940*
006950     READ レセプトＦ NEXT
004490     AT END
004500         MOVE "YES" TO 終了フラグ
004510     END-READ.
006830*================================================================*
006840 レセ並び順降順ファイル作成 SECTION.
006850******************************************************************************
006860*  全印刷の場合、
006870*  レセプトを総括表の印字順番と逆に並べるため、順番を出力する。
006880******************************************************************************
006890*
006900     OPEN INPUT 作業ファイル１.
006910         MOVE NC"作１" TO ファイル名.
006920         PERFORM オープンチェック.
006930*
006940     MOVE SPACE TO 終了フラグ.
006950     MOVE ZERO  TO 順番Ｗ.      
006960     MOVE 9          TO 作１−請求和暦.
006970     MOVE 99         TO 作１−請求年.
006980     MOVE 99         TO 作１−請求月.
006990     MOVE 9          TO 作１−助成区分.
007000     MOVE 99999999   TO 作１−保険者番号.
007010     MOVE 99999999   TO 作１−保険者番号助成.
007020     MOVE 9          TO 作１−本人家族区分.
007030     MOVE HIGH-VALUE TO 作１−被保険者カナ.
007040     MOVE 999999     TO 作１−患者番号.
007050     MOVE HIGH-VALUE TO 作１−枝番.
007060     MOVE 9          TO 作１−施術和暦.
007070     MOVE 99         TO 作１−施術年.
007080     MOVE 99         TO 作１−施術月.
007090     START 作業ファイル１ KEY IS <=  作１−請求和暦年月
007100                                     作１−助成区分
007110                                     作１−保険者番号
007120                                     作１−本人家族区分
007130                                     作１−施術和暦年月
007140                                     作１−被保険者カナ
007150                                     作１−患者コード
007160                                     作１−保険者番号助成
007170                                     REVERSED
007180     END-START.
007190     IF 状態キー = "00"
007200*
007210         MOVE SPACE TO 終了フラグ
007220         PERFORM 作業ファイル１読込
007230         PERFORM UNTIL 終了フラグ = "YES"
007240             PERFORM 作２レコードセット
007250             PERFORM 作２ファイル書込
007260             PERFORM 作業ファイル１読込
007270         END-PERFORM
007280     END-IF.
007290*
007300     CLOSE 作業ファイル１.
007310*
007320*================================================================*
007330 レセ並び順ファイル作成 SECTION.
007340******************************************************************************
007350*  全印刷の場合、
007360*  レセプトを総括表の印字順番と同じ様に並べるため、順番を出力する。
007370******************************************************************************
007380*
007390     OPEN INPUT 作業ファイル１.
007400         MOVE NC"作１" TO ファイル名.
007410         PERFORM オープンチェック.
007420*
007430     MOVE SPACE TO 終了フラグ.
007440     MOVE ZERO  TO 順番Ｗ.      
007450     PERFORM 作業ファイル１読込.
007460     PERFORM UNTIL 終了フラグ = "YES"
007470             PERFORM 作２レコードセット
007480             PERFORM 作２ファイル書込
007490             PERFORM 作業ファイル１読込
007500     END-PERFORM.
007510*
007520     CLOSE 作業ファイル１.
007530*
007540*================================================================*
007550 作２レコードセット SECTION.
007560*
           MOVE 作１−レセ種別       TO 作２−レセ種別.
007570     MOVE 作１−施術和暦       TO 作２−施術和暦.
007580     MOVE 作１−施術年         TO 作２−施術年.
007590     MOVE 作１−施術月         TO 作２−施術月.
007600     MOVE 作１−患者コード     TO 作２−患者コード.
007610     MOVE 作１−保険種別       TO 作２−保険種別
006610     MOVE 作１−患者氏名       TO 作２−患者氏名.
006610     MOVE 作１−被保険者氏名   TO 作２−被保険者氏名.
007630     IF 作１−本人家族区分 = 1
007640         MOVE "本人"           TO 作２−本人家族
007650     ELSE
007660         MOVE "家族"           TO 作２−本人家族
007670     END-IF.
           IF (作１−保険種別 < 10) OR (作１−保険種別 > 60)
007592        MOVE 作１−保険者番号     TO 保険者番号ＷＲ
           ELSE
007592        MOVE 作１−保険者番号助成 TO 保険者番号ＷＲ
           END-IF.
           IF 保険者番号ＷＲ(1:2) = "00"
              MOVE 保険者番号ＷＲ(3:8)  TO 作２−請求保険者番号
           ELSE
              MOVE 保険者番号ＷＲ    TO 作２−請求保険者番号
           END-IF.
           MOVE 作１−費用額         TO 作２−費用額.
           MOVE 作１−請求額         TO 作２−請求額.
           MOVE 作１−負担額         TO 作２−負担額.
005210     MOVE 作１−レセ印刷区分   TO 作２−レセ印刷区分.
           IF 作１−レセ種別 = 7
              MOVE 1                 TO 作２−保険区分
006670        COMPUTE 生保順番Ｗ  = 生保順番Ｗ + 1
006680        MOVE 生保順番Ｗ            TO 作２−順番
           ELSE
              MOVE ZERO              TO 作２−保険区分
006670        COMPUTE 順番Ｗ  = 順番Ｗ + 1
006680        MOVE 順番Ｗ            TO 作２−順番
           END-IF.
007700*
007710*================================================================*
007720 作２ファイル書込 SECTION.
007730*
007740     WRITE 作２−レコード
007750     INVALID KEY
007760         MOVE NC"作２"  TO ファイル名
007770         PERFORM エラー表示
007780     END-WRITE.
007781*================================================================*
009192 助成レセまとめ判定 SECTION.
009193*---------------------------------------------------------------------------*
009194* 本体まとめ区分＝１
009195* の時は、フラグYES (金額を助成込みで印字）
009196*（例：横浜市の障害は、本体保険（国保系）のレセプト１枚で請求、助成レセはなし）
009197*---------------------------------------------------------------------------*
009198*
009199     MOVE SPACE TO 助成レセまとめフラグ.
009200*
009201     IF レセ−本体まとめ区分 = 1 
009202        MOVE "YES" TO 助成レセまとめフラグ
009203     END-IF.
007820*
007821*================================================================*
007822*================================================================*
007823******************************************************************
007824 END PROGRAM YCH6061.
007825******************************************************************
