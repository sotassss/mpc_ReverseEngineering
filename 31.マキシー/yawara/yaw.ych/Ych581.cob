000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YCH581.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*      総括表【ﾃﾞｰﾀ作成】新柔版
000100* 請求年月Ver.
000110*      MED = YCH580 
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2011-08-25
000140 DATE-COMPILED.          2011-08-25
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000490     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000500                             ORGANIZATION             IS  INDEXED
000510                             ACCESS MODE              IS  DYNAMIC
000520                             RECORD KEY               IS  受−施術和暦年月
000530                                                          受−患者コード
000540                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000550                                                          受−患者カナ
000560                                                          受−患者コード
000570                             ALTERNATE RECORD KEY     IS  受−患者コード
000580                                                          受−施術和暦年月
000590                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000600                                                          受−保険種別
000610                                                          受−保険者番号
000620                                                          受−患者コード
000630                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000640                                                          受−公費種別
000650                                                          受−費用負担者番号
000660                                                          受−患者コード
000670                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000680                                                          受−助成種別
000690                                                          受−費用負担者番号助成
000700                                                          受−患者コード
000710                             ALTERNATE RECORD KEY     IS  受−請求和暦年月
000720                                                          受−施術和暦年月
000730                                                          受−患者コード
000740                             FILE STATUS              IS  状態キー
000750                             LOCK        MODE         IS  AUTOMATIC.
000130     SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
000140                             ORGANIZATION             IS  INDEXED
000150                             ACCESS MODE              IS  DYNAMIC
000160                             RECORD KEY               IS  レセ−施術和暦年月
000170                                                          レセ−患者コード
000180                                                          レセ−レセ種別
000190                             ALTERNATE RECORD KEY     IS  レセ−患者コード
000200                                                          レセ−施術和暦年月
000210                                                          レセ−レセ種別
000220                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000230                                                          レセ−施術和暦年月
000240                                                          レセ−患者コード
000250                                                          レセ−レセ種別
000260                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000270                                                          レセ−レセ種別
000280                                                          レセ−請求保険者番号
000290                                                          レセ−患者コード
000300                                                          レセ−施術和暦年月
000310                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000320                                                          レセ−請求保険者番号
000330                                                          レセ−患者コード
000340                                                          レセ−レセ種別
000350                                                          レセ−施術和暦年月
000360                             FILE STATUS              IS  状態キー
000370                             LOCK        MODE         IS  AUTOMATIC.
000850     SELECT  作業ファイル１  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5801L.DAT"
000860                             ORGANIZATION             IS  SEQUENTIAL
000870                             ACCESS                   IS  SEQUENTIAL
000880                             FILE        STATUS       IS  状態キー
000890                             LOCK        MODE         IS  AUTOMATIC.
000900     SELECT  作業ファイル２  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5802L.DAT"
000910                             ORGANIZATION             IS  SEQUENTIAL
000920                             ACCESS                   IS  SEQUENTIAL
000930                             FILE        STATUS       IS  状態キー
000940                             LOCK        MODE         IS  AUTOMATIC.
000950     SELECT  作業ファイル３  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5803L.DAT"
000960                             ORGANIZATION             IS  INDEXED
000970                             ACCESS                   IS  DYNAMIC
000980                             RECORD      KEY          IS  作３−請求和暦年月
000990                                                          作３−助成区分
001000                                                          作３−保険者番号
001010                                                          作３−本人家族区分
001020                                                          作３−施術和暦年月
001030                                                          作３−被保険者カナ
001040                                                          作３−患者コード
001040                                                          作３−親子区分
001050                             FILE        STATUS       IS  状態キー
001060                             LOCK        MODE         IS  AUTOMATIC.
001070* レセ並び順用
001080     SELECT  作業ファイル４  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5912L.DAT"
001090                             ORGANIZATION             IS  INDEXED
001100                             ACCESS                   IS  DYNAMIC
001110                             RECORD      KEY          IS  作４−施術和暦年月
001120                                                          作４−患者コード
001130                                                          作４−保険種別
001140                             FILE        STATUS       IS  状態キー
001150                             LOCK        MODE         IS  AUTOMATIC.
001160******************************************************************
001170*                      DATA DIVISION                             *
001180******************************************************************
001190 DATA                    DIVISION.
001200 FILE                    SECTION.
001300*                           ［ＲＬ＝  ３２０］
001310 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001320     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
      *                          ［ＲＬ＝  １５３６］
       FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS.
           COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
001370*
001380 FD  作業ファイル１ RECORD  CONTAINS 200 CHARACTERS.
001390 01  作１−レコード.
001400     03  作１−レコードデータ.
001410         05  作１−請求和暦年月.
001420             07  作１−請求和暦            PIC 9.
001430             07  作１−請求年              PIC 9(2).
001440             07  作１−請求月              PIC 9(2).
001450         05  作１−施術和暦年月.
001460             07  作１−施術和暦            PIC 9.
001470             07  作１−施術年              PIC 9(2).
001480             07  作１−施術月              PIC 9(2).
001490         05  作１−助成区分                PIC 9.
001500         05  作１−保険者番号              PIC X(10).
001510         05  作１−記号                    PIC X(24).
001520         05  作１−番号                    PIC X(30).
001530         05  作１−被保険者カナ            PIC X(20).
001540         05  作１−患者カナ                PIC X(20).
001550         05  作１−本人家族区分            PIC 9.
001560         05  作１−費用額                  PIC 9(7).
001570         05  作１−負担額                  PIC 9(7).
001580         05  作１−請求額                  PIC 9(7).
001590         05  作１−患者コード.
001600             07 作１−患者番号             PIC 9(6).
001610             07 作１−枝番                 PIC X(1).
001620         05  作１−保険種別                PIC 9(2).
001630         05  作１−親保険種別              PIC 9(2).
001640         05  作１−老人レコード区分        PIC 9.
001650         05  作１−助成レコード区分        PIC 9.
001660         05  FILLER                        PIC X(50).
001670* 合計ファイル
001680 FD  作業ファイル２ RECORD  CONTAINS 32 CHARACTERS.
001690 01  作２−レコード.
001700     03  作２−レコードデータ.
001710         05  作２−件数合計                PIC 9(4).
001720         05  作２−費用額合計              PIC 9(8).
001730         05  作２−請求額合計              PIC 9(8).
001740         05  FILLER                        PIC X(12).
001750* 保険者番号順ファイル
001760 FD  作業ファイル３ RECORD  CONTAINS 64 CHARACTERS.
001770 01  作３−レコード.
001780     03  作３−レコードキー.
001790         05  作３−請求和暦年月.
001800             07  作３−請求和暦            PIC 9.
001810             07  作３−請求年              PIC 9(2).
001820             07  作３−請求月              PIC 9(2).
001830         05  作３−助成区分                PIC 9.
001840         05  作３−保険者番号              PIC 9(8).
001850         05  作３−本人家族区分            PIC 9.
001860         05  作３−施術和暦年月.
001870             07  作３−施術和暦            PIC 9.
001880             07  作３−施術年              PIC 9(2).
001890             07  作３−施術月              PIC 9(2).
001900         05  作３−被保険者カナ            PIC X(20).
001910         05  作３−患者コード.
001920             07 作３−患者番号             PIC 9(6).
001930             07 作３−枝番                 PIC X(1).
002339         05  作３−親子区分                PIC 9.
001940     03  作３−レコードデータ.
001950         05  FILLER                        PIC X(16).
001960*
001970 FD  作業ファイル４ RECORD  CONTAINS 32 CHARACTERS.
001980 01  作４−レコード.
001990     03  作４−レコードキー.
002000         05  作４−施術和暦年月.
002010             07  作４−施術和暦            PIC 9.
002020             07  作４−施術年              PIC 9(2).
002030             07  作４−施術月              PIC 9(2).
002040         05  作４−患者コード.
002050             07 作４−患者番号             PIC 9(6).
002060             07 作４−枝番                 PIC X(1).
002070         05  作４−保険種別                PIC 9(2).
002080     03  作４−レコードデータ.
002090         05  作４−順番                    PIC 9(4).
002100         05  FILLER                        PIC X(14).
002110*
002120*----------------------------------------------------------------*
002130******************************************************************
002140*                WORKING-STORAGE SECTION                         *
002150******************************************************************
002160 WORKING-STORAGE         SECTION.
002170 01 キー入力                           PIC X    VALUE SPACE.
002180 01 状態キー                           PIC X(2) VALUE SPACE.
002190 01 終了フラグ                         PIC X(3) VALUE SPACE.
002200 01 終了フラグ２                       PIC X(3) VALUE SPACE.
002210 01 実行キーＷ                         PIC X(3)  VALUE SPACE.
002220 01 施術記録有Ｗ                       PIC X(3) VALUE SPACE.
002230 01 カウンタ                           PIC 9(2)  VALUE ZERO.
002240**
002250 01 保険種別ＷＲ                       PIC 9(2) VALUE ZERO.
002260 01 印刷形式ＷＲ                       PIC 9    VALUE ZERO.
002270 01 保険者番号ＷＲ                     PIC X(10) VALUE SPACE.
002280 01 レセプト種類ＷＲ                   PIC X(4) VALUE SPACE.
002290 01 本人家族区分ＷＲ                   PIC 9    VALUE ZERO.
002300 01 施術和暦年月ＷＲ.
002310    03 施術和暦ＷＲ                    PIC 9    VALUE ZERO.
002320    03 施術年ＷＲ                      PIC 9(2) VALUE ZERO.
002330    03 施術月ＷＲ                      PIC 9(2) VALUE ZERO.
002340 01 請求和暦年月ＷＲ.
002350    03 請求和暦ＷＲ                    PIC 9    VALUE ZERO.
002360    03 請求年ＷＲ                      PIC 9(2) VALUE ZERO.
002370    03 請求月ＷＲ                      PIC 9(2) VALUE ZERO.
002380**
002390 01 ファイル名                         PIC N(8) VALUE SPACE.
002400 01 保険種別Ｗ                         PIC 9(2) VALUE ZERO.
002410 01 順番Ｗ                             PIC 9(4) VALUE ZERO.
002420*
002421** 助成レセまとめ用
002422 01 助成レセまとめフラグ               PIC X(3)  VALUE SPACE.
002423*
002430** 合計集計用
002440 01 合計Ｗ.
002450    03 件数合計Ｗ                      PIC 9(4) VALUE ZERO.
002460    03 費用額合計Ｗ                    PIC 9(8) VALUE ZERO.
002470    03 請求額合計Ｗ                    PIC 9(8) VALUE ZERO.
002480** エラーメッセージ用
002490 01 エラーメッセージＷ.
002500    03 エラー患者コードＷ              PIC X(7) VALUE SPACE.
002510    03 エラー区切りＷ                  PIC X(1) VALUE SPACE.
002520    03 エラー保険種別Ｗ                PIC X(2) VALUE SPACE.
002530    03 FILLER                          PIC X(10) VALUE SPACE.
002540*
002550** 保険者番号右詰め用
002560 01 保険者番号ＷＴ.
002570    03 保険者番号左詰めＷ.
002580      05 保険者番号左詰めＷ１          PIC X OCCURS 8 VALUE SPACE.
002590    03 保険者番号右詰めＷ.
002600      05 保険者番号右詰めＷ１          PIC X OCCURS 8 VALUE ZERO.
002610    03 保険者番号数字Ｗ                PIC 9(8)  VALUE ZERO.
002620    03 保険者番号Ｗ                    PIC X(8)  VALUE SPACE.
014774*
       01 複合プログラム名Ｗ     PIC X(8) VALUE "MOJI2".
002630**
002640 01 計算機西暦年Ｗ                     PIC 9(2).
002650* 日付ＷＯＲＫ
002660 01 計算機西暦.
002670    03 計算機西暦年                    PIC 9(4).
002680    03 計算機西暦月日                  PIC 9(4).
002690 01 計算機西暦Ｒ REDEFINES 計算機西暦.
002700    03 計算機世紀                      PIC 9(2).
002710    03 計算機日付                      PIC 9(6).
002720    03 計算機日付Ｒ REDEFINES 計算機日付.
002730       05 計算機年月                   PIC 9(4).
002740       05 計算機年月Ｒ REDEFINES 計算機年月.
002750         07 計算機年                   PIC 9(2).
002760         07 計算機月                   PIC 9(2).
002770       05 計算機日                     PIC 9(2).
002780*
002790******************************************************************
002800*                          連結項目                              *
002810******************************************************************
002820*
002830********************
002840* メッセージ表示キー *
002850********************
002860 01 連メ３−キー IS EXTERNAL.
002870    03  連メ３−メッセージ             PIC N(20).
002880    03  連メ３−メッセージ１           PIC X(20).
002890***
002900 01 連入−画面情報ＹＣＨ５８０ IS EXTERNAL.
002910    03 連入−印刷形式                  PIC 9.
002920    03 連入−請求和暦年月.
002930       05 連入−請求和暦               PIC 9.
002940       05 連入−請求年月.
002950         07 連入−請求年               PIC 9(2).
002960         07 連入−請求月               PIC 9(2).
002970*
003302************************
003303* 助成レセまとめ
003304************************
003305 01 連レセまとめ−キー IS EXTERNAL.
003306    03 連レセまとめ−施術和暦年月.
003307       05 連レセまとめ−施術和暦               PIC 9.
003308       05 連レセまとめ−施術年月.
003309          07 連レセまとめ−施術年              PIC 9(2).
003310          07 連レセまとめ−施術月              PIC 9(2).
003311    03 連レセまとめ−患者コード.
003312       05 連レセまとめ−患者番号               PIC 9(6).
003313       05 連レセまとめ−枝番                   PIC X(1).
003314**-------------------------------------------------------**
003315*   1:助成レセプトなしの本体まとめの判定
003316*   2:横浜・川崎用の社保助成レセかの判定
003317    03 連レセまとめ−判定区分                  PIC 9.
003318**-------------------------------------------------------**
003319*  / OUT /　 0:対象外、1:対象
003320    03 連レセまとめ−判定結果                  PIC 9.
003321**
      * 暗号複合用
       01 連暗号複合−暗号情報 IS EXTERNAL.
          03 連暗号複合−入力情報.
             05 連暗号複合−記号               PIC X(24).
             05 連暗号複合−番号               PIC X(30).
             05 連暗号複合−暗号化項目.
                07 連暗号複合−暗号患者番号    PIC X(6).
                07 連暗号複合−暗号判定記号    PIC X.
                07 連暗号複合−暗号判定番号    PIC X.
                07 連暗号複合−暗号記号        PIC X(24).
                07 連暗号複合−暗号番号        PIC X(30).
          03 連暗号複合−出力情報.
             05 連暗号複合−複合した記号       PIC X(24).
             05 連暗号複合−複合した番号       PIC X(30).
003322*
003323******************************************************************
003324*                      PROCEDURE  DIVISION                       *
003330******************************************************************
003340 PROCEDURE               DIVISION.
003350************
003360*           *
003370* 初期処理   *
003380*           *
003390************
003400     PERFORM 初期化.
003410************
003420*           *
003430* 主処理     *
003440*           *
003450************
003460     PERFORM 作業ファイル作成.
003470************
003480*           *
003490* 終了処理   *
003500*           *
003510************
003520     PERFORM 終了処理.
003530     MOVE ZERO TO PROGRAM-STATUS.
003540     EXIT PROGRAM.
003550*
003560*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003570*================================================================*
003580 初期化 SECTION.
003590*
003600     PERFORM ファイルオープン.
003610* 連結項目の待避
003620     MOVE 連入−印刷形式      TO 印刷形式ＷＲ.
003630     MOVE 連入−請求和暦      TO 請求和暦ＷＲ.
003640     MOVE 連入−請求年        TO 請求年ＷＲ.
003650     MOVE 連入−請求月        TO 請求月ＷＲ.
003660*
003670*================================================================*
003680 ファイルオープン SECTION.
003690*
003790     OPEN INPUT 受診者情報Ｆ.
003800         MOVE NC"受診者情報Ｆ" TO ファイル名.
003810         PERFORM オープンチェック.
006630     OPEN INPUT レセプトＦ.
006640             MOVE NC"レセ" TO ファイル名.
006650             PERFORM オープンチェック.
003850*================================================================*
003860 オープンチェック SECTION.
003870*
003880     IF 状態キー  NOT =  "00"
003890         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
003900         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003910         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003920                                                 UPON CONS
004380*-----------------------------------------*
004390         CALL "actcshm"  WITH C LINKAGE
004400*-----------------------------------------*
003930         ACCEPT  キー入力 FROM CONS
003940         PERFORM ファイル閉鎖
003950         MOVE 99 TO PROGRAM-STATUS
003960         EXIT PROGRAM.
003970*================================================================*
003980 ファイル閉鎖 SECTION.
003990*
004000     CLOSE 受診者情報Ｆ レセプトＦ.
004020*================================================================*
004030 終了処理 SECTION.
004040*
004050     PERFORM ファイル閉鎖.
004060*================================================================*
004070 エラー表示 SECTION.
004080*
004090     DISPLAY NC"状態キー" 状態キー  UPON CONS.
004100     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
004110     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
004120     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
004380*-----------------------------------------*
004390     CALL "actcshm"  WITH C LINKAGE.
004400*-----------------------------------------*
004130     ACCEPT  キー入力 FROM CONS.
004140     PERFORM ファイル閉鎖.
004150     MOVE 99 TO PROGRAM-STATUS.
004160     EXIT PROGRAM.
004170*================================================================*
004180 エラー表示Ｒ SECTION.
004190*
004200     DISPLAY NC"ファイル読込エラー" ファイル名     UPON CONS.
004210     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
004220     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
004380*-----------------------------------------*
004390     CALL "actcshm"  WITH C LINKAGE.
004400*-----------------------------------------*
004230     ACCEPT  キー入力 FROM CONS.
004240     PERFORM ファイル閉鎖.
004250     MOVE 99 TO PROGRAM-STATUS.
004260     EXIT PROGRAM.
004270*================================================================*
004280 作業ファイル作成 SECTION.
004290*
004300     PERFORM 保険者番号順ファイル作成.
004310*
004320     OPEN OUTPUT 作業ファイル１.
004330         MOVE NC"作１" TO ファイル名.
004340         PERFORM オープンチェック.
004350     OPEN INPUT  作業ファイル３.
004360         MOVE NC"作３" TO ファイル名.
004370         PERFORM オープンチェック.
004380*
004390     PERFORM 作業ファイル１作成.
004400*
004410     CLOSE 作業ファイル３ 作業ファイル１.
004420*
004430* 合計ファイル作成
004440     OPEN OUTPUT 作業ファイル２.
004450         MOVE NC"作２" TO ファイル名.
004460         PERFORM オープンチェック.
004470*
004480     IF  印刷形式ＷＲ = 1 OR 2
004490         PERFORM 作業ファイル２作成
004500     END-IF.
004510*
004520     CLOSE 作業ファイル２.
004530*
004540* レセ並び順ファイル作成
004550     OPEN OUTPUT 作業ファイル４.
004560         MOVE NC"作４" TO ファイル名.
004570         PERFORM オープンチェック.
004580*
004590     IF  印刷形式ＷＲ = ZERO
004600         PERFORM レセ並び順ファイル作成
004610     END-IF.
004620*
004630     CLOSE 作業ファイル４.
004640*
004650*================================================================*
004660 保険者番号順ファイル作成 SECTION.
004670**********************************************************************
004680**   受診者情報Ｆから、該当請求年月のデータを抽出し、
004690**   作業ファイル３(保険者番号順)に書き出す.
004700**********************************************************************
004710*
004720     OPEN OUTPUT 作業ファイル３.
004730         MOVE NC"作３" TO ファイル名.
004740         PERFORM オープンチェック.
004750*
004760     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
004770     MOVE 請求年ＷＲ    TO レセ−請求年.
004780     MOVE 請求月ＷＲ    TO レセ−請求月.
004790     MOVE ZERO          TO レセ−施術和暦.
004800     MOVE ZERO          TO レセ−施術年.
004810     MOVE ZERO          TO レセ−施術月.
004820     MOVE ZERO          TO レセ−患者番号.
004830     MOVE LOW-VALUE     TO レセ−枝番.
013730     MOVE ZERO          TO レセ−レセ種別.
004840     START レセプトＦ   KEY IS >= レセ−請求和暦年月
004850                                  レセ−施術和暦年月
004860                                  レセ−患者コード
000250                                  レセ−レセ種別
004870     END-START.
004880     IF 状態キー = "00"
004890         MOVE SPACE  TO 終了フラグ
004900         PERFORM レセプトＦ読込
004910         PERFORM UNTIL ( 終了フラグ = "YES" ) OR
004920                       ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
004930                       ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
004940                       ( レセ−請求月   NOT = 請求月ＷＲ   )
005430            PERFORM データチェック
012327**
012328            IF  実行キーＷ = "YES"
004960                 MOVE SPACE TO 作３−レコード
004970                 INITIALIZE    作３−レコード
004980                 MOVE レセ−請求和暦   TO  作３−請求和暦
004990                 MOVE レセ−請求年     TO  作３−請求年
005000                 MOVE レセ−請求月     TO  作３−請求月
005010                 MOVE レセ−施術和暦   TO  作３−施術和暦
005020                 MOVE レセ−施術年     TO  作３−施術年
005030                 MOVE レセ−施術月     TO  作３−施術月
005040                 IF  受−助成種別   = ZERO  OR 50
005050                     IF ( 受−保険種別   NOT = ZERO ) AND
005060                        ( 受−保険者番号 NOT = SPACE )
005070** 助成なし(生保親ありは助成なし扱い)
005080                        MOVE ZERO  TO   作３−助成区分
005090                     ELSE
005100** 生保単独
005110                        MOVE 2     TO   作３−助成区分
005120                     END-IF
005130                 ELSE
005140** 助成あり
005150                     MOVE 1        TO   作３−助成区分
005152**
005153*                  （助成まとめは、助成なし扱いにする）
005154                     PERFORM 助成レセまとめ判定
005155                     IF ( 助成レセまとめフラグ = "YES" ) 
005156                        MOVE ZERO  TO  作３−助成区分
005157                     END-IF
005166**
005167                 END-IF
005170*
005293                 IF ( 受−公費種別       = ZERO  ) AND
005294                    ( 受−費用負担者番号 = SPACE )
005295                     MOVE 受−保険者番号     TO 保険者番号Ｗ
005296                 ELSE
005297* 老人は、市町村番号
005298                     MOVE 受−費用負担者番号 TO 保険者番号Ｗ
005299                 END-IF
005300                 PERFORM 保険者番号右詰め
005301                 MOVE 保険者番号数字Ｗ   TO 作３−保険者番号
005302*
005303                 MOVE 受−本人家族区分   TO 作３−本人家族区分
005304                 MOVE 受−被保険者カナ   TO 作３−被保険者カナ
005305                 MOVE 受−患者コード     TO 作３−患者コード
005306*
                       EVALUATE レセ−レセ種別
                       WHEN 1
                       WHEN 2
                           MOVE ZERO           TO 作３−親子区分
                       WHEN 3
                           MOVE 1              TO 作３−親子区分
                       END-EVALUATE
      *
                       IF (レセ−レセ種別 = 3) AND (作３−助成区分 = ZERO)
                          CONTINUE
                       ELSE
005930*         / 横浜・川崎の小児医療の請求額０円は対象外にする /170412
006880                    IF (レセ−レセ種別 = 3) AND
                             (レセ−助成請求金額 = ZERO) AND
                             (受−助成種別 = "55") AND
                             (受−費用負担者番号助成(3:3) = "144" OR "145")
                              CONTINUE
                          ELSE
005307                       WRITE 作３−レコード
005308                       INVALID KEY
005309                          MOVE NC"作３"  TO ファイル名
005310                       PERFORM エラー表示
005311                       END-WRITE
                          END-IF
                       END-IF
                   END-IF
011503             PERFORM レセプトＦ読込
005315         END-PERFORM
005316     END-IF.
005410*
005420     CLOSE 作業ファイル３.
005430*
005440*================================================================*
005450 保険者番号右詰め SECTION.
005460*
005470     MOVE 保険者番号Ｗ    TO  保険者番号左詰めＷ.
005480     MOVE ZERO            TO  保険者番号右詰めＷ.
005490     MOVE ZERO            TO  保険者番号数字Ｗ.
005500*
005510     MOVE  9  TO  カウンタ.
005520*
005530     IF  保険者番号左詰めＷ１(8) NOT = SPACE
005540         COMPUTE カウンタ = カウンタ  -  1
005550         MOVE 保険者番号左詰めＷ１(8)  TO  保険者番号右詰めＷ１(カウンタ)
005560     END-IF.
005570     IF  保険者番号左詰めＷ１(7) NOT = SPACE
005580         COMPUTE カウンタ = カウンタ  -  1
005590         MOVE 保険者番号左詰めＷ１(7)  TO  保険者番号右詰めＷ１(カウンタ)
005600     END-IF.
005610     IF  保険者番号左詰めＷ１(6) NOT = SPACE
005620         COMPUTE カウンタ = カウンタ  -  1
005630         MOVE 保険者番号左詰めＷ１(6)  TO  保険者番号右詰めＷ１(カウンタ)
005640     END-IF.
005650     IF  保険者番号左詰めＷ１(5) NOT = SPACE
005660         COMPUTE カウンタ = カウンタ  -  1
005670         MOVE 保険者番号左詰めＷ１(5)  TO  保険者番号右詰めＷ１(カウンタ)
005680     END-IF.
005690     IF  保険者番号左詰めＷ１(4) NOT = SPACE
005700         COMPUTE カウンタ = カウンタ  -  1
005710         MOVE 保険者番号左詰めＷ１(4)  TO  保険者番号右詰めＷ１(カウンタ)
005720     END-IF.
005730     IF  保険者番号左詰めＷ１(3) NOT = SPACE
005740         COMPUTE カウンタ = カウンタ  -  1
005750         MOVE 保険者番号左詰めＷ１(3)  TO  保険者番号右詰めＷ１(カウンタ)
005760     END-IF.
005770     IF  保険者番号左詰めＷ１(2) NOT = SPACE
005780         COMPUTE カウンタ = カウンタ  -  1
005790         MOVE 保険者番号左詰めＷ１(2)  TO  保険者番号右詰めＷ１(カウンタ)
005800     END-IF.
005810     IF  保険者番号左詰めＷ１(1) NOT = SPACE
005820         COMPUTE カウンタ = カウンタ  -  1
005830         MOVE 保険者番号左詰めＷ１(1)  TO  保険者番号右詰めＷ１(カウンタ)
005840     END-IF.
005850*
005860     MOVE 保険者番号右詰めＷ TO 保険者番号数字Ｗ.
005870*
005880*================================================================*
005890*================================================================*
005900 作業ファイル１作成 SECTION.
005910*
005920     MOVE SPACE  TO 終了フラグ.
005930     PERFORM 作業ファイル３読込.
005940     PERFORM UNTIL  終了フラグ = "YES" 
005960*
008580         MOVE "YES"          TO 実行キーＷ
005970         EVALUATE 印刷形式ＷＲ 
005980* 全部
005990         WHEN ZERO 
006000             CONTINUE
006010* 助成以外
006020         WHEN 1 
006030             IF 作３−助成区分  NOT = ZERO
006040                 MOVE SPACE  TO 実行キーＷ
006050             END-IF
006060* 助成のみ
006070         WHEN 2 
006080             IF 作３−助成区分  NOT = 1
006090                 MOVE SPACE  TO 実行キーＷ
006100             END-IF
006110         WHEN OTHER
006120             CONTINUE
006130         END-EVALUATE
006140*
006240         IF  実行キーＷ = "YES"
006270*
006280*            ********
006290*            * 健保 *
006300*            ********
012372             IF 作３−親子区分 = ZERO
006310                 IF ( 受−保険種別   NOT = ZERO ) AND
006320                    ( 受−保険者番号 NOT = SPACE )
006330*                **********************
006340*                * 作業ファイル作成 *
006350*                **********************
006360                     IF ( 受−公費種別       = ZERO  ) AND
006370                        ( 受−費用負担者番号 = SPACE )
006400                         MOVE SPACE TO 作１−レコード
006410                         INITIALIZE 作１−レコード
006420                         MOVE 受−保険種別       TO 作１−保険種別
006430                         MOVE 受−保険者番号     TO 作１−保険者番号
006461*
005629                         MOVE レセ−合計         TO 作１−費用額
005630                         MOVE レセ−一部負担金   TO 作１−負担額
005631                         MOVE レセ−請求金額     TO 作１−請求額
006472*↑*
006473*
006474                         PERFORM 作１レコードセット
006480                         PERFORM 作１ファイル書込
006750                     END-IF
006760                 END-IF
006770*            ********
006780*            * 老人 *
006790*            ********
006800                 IF ( 受−公費種別       NOT = ZERO ) AND
006810                    ( 受−費用負担者番号 NOT = SPACE )
006820*                    **********************
006830*                    * 作業ファイル作成 *
006840*                    **********************
006870                     MOVE SPACE TO 作１−レコード
006880                     INITIALIZE 作１−レコード
006890                     MOVE 受−公費種別        TO 作１−保険種別
006900                     MOVE 受−費用負担者番号  TO 作１−保険者番号
006910                     MOVE  1                  TO 作１−老人レコード区分
005894                     MOVE レセ−合計          TO 作１−費用額
005895                     MOVE レセ−一部負担金    TO 作１−負担額
005896                     MOVE レセ−請求金額      TO 作１−請求額
006953*
006954                     PERFORM 作１レコードセット
006960                     PERFORM 作１ファイル書込
007230                 END-IF
                   END-IF
012369*            ********
012370*            * 助成 *
012371*            ********
012372             IF 作３−親子区分 = 1
005647                 MOVE SPACE TO 作１−レコード
005648                 INITIALIZE 作１−レコード
005649                 MOVE 受−助成種別       TO 作１−保険種別
                       IF 受−公費種別 NOT = ZERO
005956                     MOVE 受−公費種別        TO 作１−親保険種別
                       ELSE
005650                     MOVE 受−保険種別       TO 作１−親保険種別
                       END-IF
005651                 MOVE 受−費用負担者番号助成  TO  作１−保険者番号
005652                 MOVE  1                 TO 作１−助成レコード区分
005655** 助成の費用額は、0
005656                 MOVE ZERO               TO  作１−費用額
005657                 MOVE レセ−受給者負担額 TO  作１−負担額
005658                 MOVE レセ−助成請求金額 TO  作１−請求額
005671                 PERFORM 作１レコードセット
005672                 PERFORM 作１ファイル書込
005673*
                   END-IF
007240         END-IF
007250         PERFORM 作業ファイル３読込
007260      END-PERFORM.
007270*
007280*================================================================*
007290*================================================================*
007300 作業ファイル３読込 SECTION.
007310*
007320     READ 作業ファイル３ NEXT
007330     AT END
007340         MOVE "YES" TO 終了フラグ
007350     NOT AT END
006665         MOVE 作３−施術和暦    TO 受−施術和暦 レセ−施術和暦
006666         MOVE 作３−施術年      TO 受−施術年   レセ−施術年  
006667         MOVE 作３−施術月      TO 受−施術月   レセ−施術月  
006668         MOVE 作３−患者番号    TO 受−患者番号 レセ−患者番号
006669         MOVE 作３−枝番        TO 受−枝番     レセ−枝番    
006670         READ 受診者情報Ｆ
006671         INVALID KEY
006672              MOVE NC"受診者"   TO ファイル名
006673              PERFORM エラー表示Ｒ
006674         END-READ
               IF 作３−親子区分 = 1
                   MOVE 3          TO レセ−レセ種別
               ELSE
                  IF 受−公費種別 = 5
                      MOVE 2          TO レセ−レセ種別
                  ELSE
                      MOVE 1          TO レセ−レセ種別
                  END-IF
               END-IF
006670         READ レセプトＦ
006671         INVALID KEY
006672              MOVE NC"レセプト"   TO ファイル名
006673              PERFORM エラー表示Ｒ
007450         END-READ
007460     END-READ.
007470*
008550*================================================================*
008560 データチェック SECTION.
008570*
008580     MOVE SPACE          TO 実行キーＷ.
019520* *****************************************************************
019530* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
019540* *****************************************************************
019640     IF ( レセ−請求対象区分 NOT = ZERO ) AND
005778        ( レセ−償還払い区分 NOT = 1 )
              IF (レセ−レセ種別 = 3) AND ( レセ−会総括表印刷対象区分 = 1 )
                  CONTINUE
              ELSE
004090           MOVE レセ−施術和暦  TO 受−施術和暦
004100           MOVE レセ−施術年    TO 受−施術年
004110           MOVE レセ−施術月    TO 受−施術月
004120           MOVE レセ−患者番号  TO 受−患者番号
004130           MOVE レセ−枝番      TO 受−枝番
                 READ 受診者情報Ｆ
                 NOT INVALID KEY
005786**      健保のみ
005787              IF 受−保険分類 = 1
019880                 MOVE "YES"  TO 実行キーＷ
005789              END-IF
019950           END-READ
              END-IF
019960     END-IF.
009040*
008070*================================================================*
006930 レセプトＦ読込 SECTION.
006940*
006950     READ レセプトＦ NEXT
008110     AT END
008120         MOVE "YES" TO 終了フラグ
008130     END-READ.
008140*
008150*================================================================*
008160 作１レコードセット SECTION.
008170*
008180     MOVE 作３−助成区分     TO 作１−助成区分.
007311     MOVE レセ−請求和暦     TO 作１−請求和暦.
007312     MOVE レセ−請求年       TO 作１−請求年.
007313     MOVE レセ−請求月       TO 作１−請求月.
007314     MOVE レセ−施術和暦       TO 作１−施術和暦.
007320     MOVE レセ−施術年         TO 作１−施術年.
007330     MOVE レセ−施術月         TO 作１−施術月.
008250     MOVE 受−患者カナ       TO 作１−患者カナ.
008260     MOVE 受−患者コード     TO 作１−患者コード.
008270     MOVE 受−被保険者カナ   TO 作１−被保険者カナ.
008280     MOVE 受−本人家族区分   TO 作１−本人家族区分.
008290* 老人・助成の時は、受益者番号を番号に、記号は空白
008300     IF  作１−老人レコード区分 = 1
008310         MOVE SPACE               TO 作１−記号
008320         MOVE 受−受益者番号老人  TO 作１−番号
008330     ELSE
008340         IF 作１−助成レコード区分 = 1
008350            MOVE SPACE               TO 作１−記号
008360            MOVE 受−受益者番号助成  TO 作１−番号
008370         ELSE
008380*            MOVE 受−記号            TO 作１−記号
008390*            MOVE 受−番号            TO 作１−番号
      *    / 連暗号複合−入力情報セット /
                   MOVE 受−記号       TO 連暗号複合−記号
                   MOVE 受−番号       TO 連暗号複合−番号
                   MOVE 受−暗号化項目 TO 連暗号複合−暗号化項目
      *     
                   CALL   複合プログラム名Ｗ
                   CANCEL 複合プログラム名Ｗ
      *
                   MOVE 連暗号複合−複合した記号 TO 作１−記号
                   MOVE 連暗号複合−複合した番号 TO 作１−番号
008400         END-IF
008410     END-IF.
008420*
008680*================================================================*
008690 作１ファイル書込 SECTION.
008700*
008710     WRITE 作１−レコード.
008720     IF 状態キー  NOT =  "00"
008730         MOVE NC"作１"  TO ファイル名
008740         PERFORM エラー表示
008750     END-IF.
008760*
008770*================================================================*
008780 作業ファイル２作成 SECTION.
008790******************************************************************************
008800*  全印刷以外は、合計ページを印刷するため、合計のデータを作成する。
008810*  全データの合計件数、費用額合計、請求額合計を求める。
008820******************************************************************************
008830*
008840     OPEN INPUT  作業ファイル３.
008850         MOVE NC"作３" TO ファイル名.
008860         PERFORM オープンチェック.
008870*
008880     INITIALIZE 合計Ｗ.
008890     MOVE SPACE  TO 終了フラグ.
008900     PERFORM 作業ファイル３読込.
008910     PERFORM UNTIL  終了フラグ = "YES" 
008920         PERFORM データチェック
008970** 生保単独は対象外
008980         IF  作３−助成区分  = 2
008990             MOVE SPACE  TO 実行キーＷ
009000         END-IF
009010**
009020         IF  実行キーＷ = "YES"
009049*
009060*            ********
009070*            * 健保 *
009080*            ********
012372             IF 作３−親子区分 = ZERO
009090                 IF ( 受−保険種別   NOT = ZERO ) AND
009100                    ( 受−保険者番号 NOT = SPACE )
009110                     IF ( 受−公費種別       = ZERO  ) AND
009120                        ( 受−費用負担者番号 = SPACE )
009150                         COMPUTE 件数合計Ｗ   = 件数合計Ｗ   + 1
009179                         COMPUTE 費用額合計Ｗ = 費用額合計Ｗ + レセ−合計
009181                         COMPUTE 請求額合計Ｗ = 請求額合計Ｗ + レセ−請求金額
009290                     END-IF
009300                 END-IF
009310*            ********
009320*            * 老人 *
009330*            ********
009340                 IF ( 受−公費種別       NOT = ZERO ) AND
009350                    ( 受−費用負担者番号 NOT = SPACE )
009380                     COMPUTE 件数合計Ｗ   = 件数合計Ｗ   + 1
009408                     COMPUTE 費用額合計Ｗ = 費用額合計Ｗ + レセ−合計
009410                     COMPUTE 請求額合計Ｗ = 請求額合計Ｗ + レセ−請求金額
009510                 END-IF
009520             END-IF
012369*        ********
012370*        * 助成 *
012371*        ********
012372             IF 作３−親子区分 = 1
009380                 COMPUTE 件数合計Ｗ   = 件数合計Ｗ   + 1
009410                 COMPUTE 請求額合計Ｗ = 請求額合計Ｗ + レセ−助成請求金額
009520             END-IF
009520         END-IF
009530         PERFORM 作業ファイル３読込
009540      END-PERFORM.
009550*
009560*
009570* レコードセット
009580     MOVE SPACE TO 作２−レコード.
009590     INITIALIZE 作２−レコード.
009600     MOVE 件数合計Ｗ   TO  作２−件数合計.
009610     MOVE 費用額合計Ｗ TO  作２−費用額合計.
009620     MOVE 請求額合計Ｗ TO  作２−請求額合計.
009630     WRITE 作２−レコード.
009640     IF 状態キー  NOT =  "00"
009650         MOVE NC"作２"  TO ファイル名
009660         PERFORM エラー表示
009670     END-IF.
009680*
009690     CLOSE 作業ファイル３.
009700*
009710*================================================================*
009720 レセ並び順ファイル作成 SECTION.
009730******************************************************************************
009740*  全印刷の場合、
009750*  レセプトを総括表の印字順番と同じ様に並べるため、順番を出力する。
009760******************************************************************************
009770*
009780     OPEN INPUT 作業ファイル１.
009790         MOVE NC"作１" TO ファイル名.
009800         PERFORM オープンチェック.
009810*
009820     MOVE SPACE TO 終了フラグ.
009830     MOVE ZERO  TO 順番Ｗ.      
009840     PERFORM 作業ファイル１読込.
009850     PERFORM UNTIL 終了フラグ = "YES"
009860             PERFORM 作４レコードセット
009870             PERFORM 作４ファイル書込
009880             PERFORM 作業ファイル１読込
009890     END-PERFORM.
009900*
009910     CLOSE 作業ファイル１.
009920*
009930*================================================================*
009940 作４レコードセット SECTION.
009950*
009960     MOVE 作１−施術和暦       TO 作４−施術和暦.
009970     MOVE 作１−施術年         TO 作４−施術年.
009980     MOVE 作１−施術月         TO 作４−施術月.
009990     MOVE 作１−患者コード     TO 作４−患者コード.
010000     MOVE 作１−保険種別       TO 作４−保険種別.
      */助成レセプトを印刷しない場合の助成分は一連番号をカウントしない(印刷しない)/121201
           MOVE ZERO                 TO 作４−順番
           MOVE 作１−施術和暦年月   TO 受−施術和暦年月
           MOVE 作１−患者コード     TO 受−患者コード
           READ 受診者情報Ｆ
           NOT INVALID KEY
               IF (作４−保険種別 > 50) AND (受−助成レセ印刷対象区分 = 2 OR 6)
                   CONTINUE
               ELSE
010010             COMPUTE 順番Ｗ = 順番Ｗ + 1
010020             MOVE 順番Ｗ TO 作４−順番
               END-IF
           END-READ.
010030*
010040*================================================================*
010050 作４ファイル書込 SECTION.
010060*
010070     WRITE 作４−レコード
010080     INVALID KEY
010090         MOVE NC"作４"  TO ファイル名
010100         PERFORM エラー表示
010110     END-WRITE.
010120*================================================================*
010130 作業ファイル１読込 SECTION.
010140*
010150     READ 作業ファイル１ NEXT
010160     AT END
010170         MOVE "YES" TO 終了フラグ
010180     END-READ.
010190*
010232*================================================================*
009191*================================================================*
009192 助成レセまとめ判定 SECTION.
009193*---------------------------------------------------------------------------*
009194* 本体まとめ区分＝１
009195* の時は、フラグYES (金額を助成込みで印字）
009196*（例：横浜市の障害は、本体保険（国保系）のレセプト１枚で請求、助成レセはなし）
009197*---------------------------------------------------------------------------*
009198*
009199     MOVE SPACE TO 助成レセまとめフラグ.
009200*
009201     IF レセ−本体まとめ区分 = 1 
009202        MOVE "YES" TO 助成レセまとめフラグ
009203     END-IF.
009204*
009205*================================================================*
010233******************************************************************
010234 END PROGRAM YCH581.
010235******************************************************************
