000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YCH102.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*      提出FPD作成【FPD書込】新柔版
000091* 請求年月Ver.
000100*      MED = YCH100G 
000110*----------------------------------------------------------------*
000120 DATE-WRITTEN.           2011-08-26
000130 DATE-COMPILED.          2011-08-26
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000251     SELECT  制御情報マスタ  ASSIGN      TO        SEIGYOL
000252                             ORGANIZATION             IS  INDEXED
000253                             ACCESS MODE              IS  DYNAMIC
000254                             RECORD KEY               IS  制−制御区分
000255                             FILE STATUS              IS  状態キー
000256                             LOCK        MODE         IS  AUTOMATIC.
000257*
000260     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W1011L.DAT"
000270                             ORGANIZATION             IS  INDEXED
000280                             ACCESS                   IS  DYNAMIC
000290                             RECORD      KEY          IS  作１−請求和暦年月
000300                                                          作１−助成区分
000310                                                          作１−保険者番号
000320                                                          作１−本人家族区分
000330                                                          作１−施術和暦年月
000340                                                          作１−被保険者カナ
000350                                                          作１−患者コード
000360                             FILE        STATUS       IS  状態キー
000370                             LOCK        MODE         IS  AUTOMATIC.
000941*
000942     SELECT 会提出ファイル   ASSIGN      TO     FD-NAME
000943                             ORGANIZATION             IS  LINE SEQUENTIAL
000944                             ACCESS MODE              IS  SEQUENTIAL
000945                             FILE STATUS              IS  状態キー
000946                             LOCK      MODE           IS  AUTOMATIC.
000950******************************************************************
000960*                      DATA DIVISION                             *
000970******************************************************************
000980 DATA                    DIVISION.
000990 FILE                    SECTION.
001000*                           ［ＲＬ＝  ２５６］
001010 FD  制御情報マスタ      BLOCK   CONTAINS   1   RECORDS.
001020     COPY SEIGYO         OF  XFDLIB  JOINING   制   AS  PREFIX.
001160*
001161 FD  作業ファイル１ RECORD  CONTAINS 300 CHARACTERS.
001162 01  作１−レコード.
001163     03  作１−レコードキー.
001164         05  作１−請求和暦年月.
001165             07  作１−請求和暦            PIC 9.
001166             07  作１−請求年              PIC 9(2).
001167             07  作１−請求月              PIC 9(2).
001168         05  作１−助成区分                PIC 9.
001169         05  作１−保険者番号              PIC 9(8).
001170         05  作１−本人家族区分            PIC 9.
001171         05  作１−施術和暦年月.
001172             07  作１−施術和暦            PIC 9.
001173             07  作１−施術年              PIC 9(2).
001174             07  作１−施術月              PIC 9(2).
001175         05  作１−被保険者カナ            PIC X(20).
001176         05  作１−患者コード.
001177             07 作１−患者番号             PIC 9(6).
001178             07 作１−枝番                 PIC X(1).
001179     03  作１−レコードデータ.
001180         05  作１−会員番号              PIC 9(7).
001181         05  作１−続柄                  PIC 9.
001182         05  作１−老人                  PIC 9.
001183         05  作１−保険種別              PIC 9.
001184**         05  作１−助成区分              PIC 9.
001185         05  作１−委任区分              PIC 9.
001186         05  作１−地域区分              PIC 9.
001187         05  作１−加入区分              PIC 9.
001188         05  作１−請求フラグ            PIC 9.
001189         05  作１−部位数                PIC 9.
001190         05  作１−実日数                PIC 9(3).
001191**         05  作１−保険者番号            PIC 9(8).
001192         05  作１−請求年月データ.
001193             07  作１−請求年データ      PIC 9(4).
001194             07  作１−請求月データ      PIC 9(2).
001195         05  作１−患者生年月日.
001196             07  作１−患者年            PIC 9(4).
001197             07  作１−患者月            PIC 9(2).
001198             07  作１−患者日            PIC 9(2).
001203         05  作１−施術年月データ.
001204             07  作１−施術年データ      PIC 9(4).
001205             07  作１−施術月データ      PIC 9(2).
001206         05  作１−被保険者カナデータ.
001207             07  作１−被保険者カナ１    PIC X(14).
001208             07  作１−被保険者カナ２    PIC X(6).
001209         05  作１−患者カナ.
001210             07  作１−患者カナ１        PIC X(8).
001211             07  作１−患者カナ２        PIC X(12).
001212         05  作１−費用負担者番号助成    PIC 9(8).
001213         05  作１−番号.
001214             07  作１−番号１            PIC X(14).
001215             07  作１−番号２.
001216                 09  作１−番号２１      PIC X(6).
001217                 09  作１−番号２２      PIC X(8).
001218         05  作１−記号.
001219             07  作１−記号１            PIC X(14).
001220             07  作１−記号２.
001221                 09  作１−記号２１      PIC X(2).
001222                 09  作１−記号２２      PIC X(12).
001223             07  作１−記号３            PIC X(4).
001224         05  作１−費用額                PIC 9(9).
001225         05  作１−請求額                PIC 9(9).
001226         05  作１−助成請求額            PIC 9(5). 
001227         05  作１−保険者名称.
001228             07  作１−保険者名称１      PIC X(14).
001229             07  作１−保険者名称２      PIC X(14).
001230             07  作１−保険者名称３      PIC X(12).
001231         05  FILLER                      PIC X(35).
001232*
001840*
001841 FD  会提出ファイル RECORD  CONTAINS 253 CHARACTERS.
001842 01  会提−レコード.
001855     03  会提−レコードデータ.
001856         05  会提−会員番号              PIC 9(7).
001857         05  FILLER                      PIC X(7).
001858         05  タブ１                      PIC X.
001859         05  会提−続柄                  PIC 9.
001860         05  会提−老人                  PIC 9.
001861         05  会提−保険種別              PIC 9.
001862         05  会提−助成区分              PIC 9.
001863         05  会提−委任区分              PIC 9.
001864         05  会提−地域区分              PIC 9.
001865         05  会提−加入区分              PIC 9.
001866         05  会提−請求フラグ            PIC 9.
001867         05  会提−部位数                PIC 9.
001868         05  会提−実日数                PIC 9(3).
001869         05  FILLER                      PIC X(2).
001870         05  タブ２                      PIC X.
001871         05  会提−保険者番号            PIC 9(8).
001872         05  会提−請求年月データ.
001873             07  会提−請求年データ      PIC 9(4).
001874             07  会提−請求月データ      PIC 9(2).
001875         05  タブ３                      PIC X.
001876         05  会提−患者生年月日.
001877             07  会提−患者年            PIC 9(4).
001878             07  会提−患者月            PIC 9(2).
001879             07  会提−患者日            PIC 9(2).
001884         05  会提−施術年月データ.
001885             07  会提−施術年データ      PIC 9(4).
001886             07  会提−施術月データ      PIC 9(2).
001887         05  タブ４                      PIC X.
001888         05  会提−被保険者カナデータ.
001889             07  会提−被保険者カナ１    PIC X(14).
001890             07  タブ５                  PIC X.
001891             07  会提−被保険者カナ２    PIC X(6).
001892         05  会提−患者カナ.
001893             07  会提−患者カナ１        PIC X(8).
001894             07  タブ６                  PIC X.
001895             07  会提−患者カナ２        PIC X(12).
001896         05  FILLER                      PIC X(2).
001897         05  タブ７                      PIC X.
001898         05  FILLER                      PIC X(6).
001899         05  会提−費用負担者番号助成    PIC 9(8).
001900         05  タブ８                      PIC X.
001901         05  会提−番号.
001902             07  会提−番号１            PIC X(14).
001903             07  タブ９                  PIC X.
001904             07  会提−番号２.
001905                 09  会提−番号２１      PIC X(6).
001906                 09  会提−番号２２      PIC X(8).
001907         05  タブ１０                    PIC X.
001908         05  会提−記号.
001909             07  会提−記号１            PIC X(14).
001910             07  タブ１１                PIC X.
001911             07  会提−記号２.
001912                 09  会提−記号２１      PIC X(2).
001913                 09  会提−記号２２      PIC X(12).
001914             07  タブ１２                PIC X.
001915             07  会提−記号３            PIC X(4).
001916         05  タブ１３                    PIC X.
001917         05  会提−費用額                PIC 9(9).
001918         05  タブ１４                    PIC X.
001919         05  会提−請求額                PIC 9(9).
001920         05  会提−助成請求額            PIC 9(5). 
001921         05  タブ１５                    PIC X.
001922         05  会提−保険者名称.
001923             07  会提−保険者名称１      PIC X(14).
001924             07  タブ１６                PIC X.
001925             07  会提−保険者名称２      PIC X(14).
001926             07  タブ１７                PIC X.
001927             07  会提−保険者名称３      PIC X(12).
001928         05  タブ１８                    PIC X.
001929*
001999*----------------------------------------------------------------*
002000******************************************************************
002001*                WORKING-STORAGE SECTION                         *
002002******************************************************************
002003 WORKING-STORAGE         SECTION.
002004 01 キー入力                           PIC X    VALUE SPACE.
002005 01 状態キー                           PIC X(2) VALUE SPACE.
002006 01 終了フラグ                         PIC X(3) VALUE SPACE.
002007 01 終了フラグ２                       PIC X(3) VALUE SPACE.
002008 01 実行キーＷ                         PIC X(3)  VALUE SPACE.
002009 01 施術記録有Ｗ                       PIC X(3) VALUE SPACE.
002010 01 ファイル名                         PIC N(8) VALUE SPACE.
002011**
002180** エラーメッセージ用
002190 01 エラーメッセージＷ.
002200    03 エラー患者コードＷ              PIC X(7) VALUE SPACE.
002210    03 エラー区切りＷ                  PIC X(1) VALUE SPACE.
002220    03 エラー保険種別Ｗ                PIC X(2) VALUE SPACE.
002230    03 FILLER                          PIC X(10) VALUE SPACE.
002240*
002250* フロッピーパス用
002251 01 定期修正ＦＤＷ                     PIC X VALUE SPACE.
002252 01 FD-NAME                            PIC X(30) VALUE SPACE.
002260*
002410******************************************************************
002420*                          連結項目                              *
002430******************************************************************
      *
       01 連入−画面情報ＹＣＨ１００ IS EXTERNAL.
          03 連入−ドライブ                  PIC X(1).
      *
      *0:通常 1:固定パス
       01 連入−固定フラグ３２１１ IS EXTERNAL.
          03 連入−固定フラグ                PIC 9(1).
002440*
002450********************
002460* メッセージ表示キー *
002470********************
002474 01 連メ−キー IS EXTERNAL.
002475    03  連メ−メッセージ               PIC N(20).
004090*
004091 01 連メ７−キー IS EXTERNAL.
004092    03  連メ７−メッセージ１               PIC X(40).
004102    03  連メ７−メッセージ２               PIC X(40).
002930*
002940******************************************************************
002950*                      PROCEDURE  DIVISION                       *
002960******************************************************************
002970 PROCEDURE               DIVISION.
002980************
002990*           *
003000* 初期処理   *
003010*           *
003020************
003030     PERFORM 初期化.
003040************
003050*           *
003060* 主処理     *
003070*           *
003080************
003090     PERFORM 提出ファイル作成.
003100************
003110*           *
003120* 終了処理   *
003130*           *
003140************
003150     PERFORM 終了処理.
003160     MOVE ZERO TO PROGRAM-STATUS.
003170     EXIT PROGRAM.
003180*
003190*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003200*================================================================*
003210 初期化 SECTION.
003220*
003230     PERFORM ファイルオープン.
003312*
003314*================================================================*
003315 ファイルオープン SECTION.
003316*
003317** フロッピーパス設定
003318*     OPEN INPUT 制御情報マスタ.
003319*             MOVE NC"制御" TO ファイル名.
003320*             PERFORM オープンチェック.
003321*     MOVE ZERO TO 制−制御区分.
003322*     READ 制御情報マスタ
003323*     NOT INVALID KEY
003324*         MOVE 制−定期修正ＦＤ TO 定期修正ＦＤＷ
003325*     END-READ.
           IF 連入−固定フラグ = 1
002890        MOVE "C:\makishisys\Total.dat" TO FD-NAME
           ELSE
003324        MOVE 連入−ドライブ TO 定期修正ＦＤＷ
003326        IF 定期修正ＦＤＷ = SPACE
003327           MOVE "A" TO 定期修正ＦＤＷ
003328        END-IF
003330        STRING 定期修正ＦＤＷ   DELIMITED BY SIZE
003331               ":\Total.dat"    DELIMITED BY SIZE
003334              INTO FD-NAME
003335        END-STRING
           END-IF.
003336**
003338     OPEN OUTPUT 会提出ファイル.
002680     IF 状態キー  NOT =  "00"
002690         MOVE  "指定されたドライブがないか、" TO 連メ７−メッセージ１
               MOVE  "書き込むことが出来ません。" TO 連メ７−メッセージ２
002700         CALL   "MSG007"
002710         CANCEL "MSG007"
002720         MOVE 99 TO PROGRAM-STATUS
002730         EXIT PROGRAM
003348     END-IF.
003349*
003350     OPEN INPUT 作業ファイル１.
003351         MOVE NC"作１" TO ファイル名.
003352         PERFORM オープンチェック.
003470*================================================================*
003480 オープンチェック SECTION.
003490*
003500     IF 状態キー  NOT =  "00"
003510         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
003520         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003530         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003540                                                 UPON CONS
004380*-----------------------------------------*
004390         CALL "actcshm"  WITH C LINKAGE
004400*-----------------------------------------*
003550         ACCEPT  キー入力 FROM CONS
003560         PERFORM ファイル閉鎖
003570         MOVE 99 TO PROGRAM-STATUS
003580         EXIT PROGRAM.
003590*================================================================*
003600 ファイル閉鎖 SECTION.
003610*
003620     CLOSE  会提出ファイル 作業ファイル１.
      *            制御情報マスタ.
003640*================================================================*
003650 終了処理 SECTION.
003660*
003670     PERFORM ファイル閉鎖.
003680*================================================================*
003690 エラー表示 SECTION.
003700*
003710     DISPLAY NC"状態キー" 状態キー  UPON CONS.
003720     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
003730     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
003740     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
004380*-----------------------------------------*
004390     CALL "actcshm"  WITH C LINKAGE.
004400*-----------------------------------------*
003750     ACCEPT  キー入力 FROM CONS.
003760     PERFORM ファイル閉鎖.
003770     MOVE 99 TO PROGRAM-STATUS.
003780     EXIT PROGRAM.
003781*================================================================*
005860 提出ファイル作成 SECTION.
005870*
005871     MOVE SPACE   TO 終了フラグ.
005872     PERFORM 作業ファイル１読込.
005873     IF  終了フラグ = "YES"
005874         MOVE  NC"　　データが０件です。確認して下さい" TO 連メ−メッセージ
005875         CALL   "MSG001"
005876         CANCEL "MSG001"
005877         PERFORM ファイル閉鎖
005878         MOVE 99 TO PROGRAM-STATUS
005879         EXIT PROGRAM
005880     END-IF.
005881*
005882     PERFORM UNTIL 終了フラグ = "YES"
005894              PERFORM レコードセット
005895              PERFORM ファイル書込
005897         PERFORM 作業ファイル１読込
005898     END-PERFORM.
005903*
005904*================================================================*
005905 レコードセット SECTION.
005906*
005907     MOVE SPACE TO 会提−レコード.
005916     INITIALIZE    会提−レコード.
005926*
005972     MOVE 作１−会員番号     TO  会提−会員番号.
005975     MOVE 作１−続柄         TO  会提−続柄.
005976     MOVE 作１−老人         TO  会提−老人.
005977     MOVE 作１−保険種別     TO  会提−保険種別.
005978     MOVE 作１−助成区分     TO  会提−助成区分.
005979     MOVE 作１−委任区分     TO  会提−委任区分.
005980     MOVE 作１−地域区分     TO  会提−地域区分.
005981     MOVE 作１−加入区分     TO  会提−加入区分.
005982     MOVE 作１−請求フラグ   TO  会提−請求フラグ.
005983     MOVE 作１−部位数       TO  会提−部位数.
005984     MOVE 作１−実日数       TO  会提−実日数.
005987     MOVE 作１−保険者番号   TO  会提−保険者番号.
005988     MOVE 作１−請求年月データ  TO  会提−請求年月データ.
005992     MOVE 作１−患者生年月日    TO  会提−患者生年月日.
005996     MOVE 作１−施術年月データ  TO  会提−施術年月データ.
006000     MOVE 作１−被保険者カナ１  TO  会提−被保険者カナ１.
006003     MOVE 作１−被保険者カナ２  TO  会提−被保険者カナ２.
006005     MOVE 作１−患者カナ１      TO  会提−患者カナ１.
006007     MOVE 作１−患者カナ２      TO  会提−患者カナ２.
006011     MOVE 作１−費用負担者番号助成  TO  会提−費用負担者番号助成.
006014     MOVE 作１−番号１       TO  会提−番号１.
006016     MOVE 作１−番号２       TO  会提−番号２.
006021     MOVE 作１−記号１       TO  会提−記号１.
006023     MOVE 作１−記号２       TO  会提−記号２.
006027     MOVE 作１−記号３       TO  会提−記号３.
006029     MOVE 作１−費用額       TO  会提−費用額.
006031     MOVE 作１−請求額       TO  会提−請求額.
006032     MOVE 作１−助成請求額   TO  会提−助成請求額.
006035     MOVE 作１−保険者名称１ TO  会提−保険者名称１.
006037     MOVE 作１−保険者名称２ TO  会提−保険者名称２.
006039     MOVE 作１−保険者名称３ TO  会提−保険者名称３.
006040* ﾀﾌﾞ
006041     MOVE X"09"  TO  タブ１ タブ２ タブ３ タブ４ タブ５ タブ６
006042                     タブ７ タブ８ タブ９ タブ１０ タブ１１ タブ１２
006043                     タブ１３ タブ１４ タブ１５ タブ１６ タブ１７ タブ１８.
006047*
006105*================================================================*
006106 作業ファイル１読込 SECTION.
006107*
006108     READ 作業ファイル１ NEXT
006109     AT END
006110         MOVE "YES" TO 終了フラグ
006111     END-READ.
006112*
007830*================================================================*
007840 ファイル書込 SECTION.
007850*
007860     WRITE 会提−レコード.
007861     IF 状態キー  NOT =  "00"
007881         MOVE NC"会提出ファイル" TO ファイル名
007890         PERFORM エラー表示
007900     END-IF.
008232*================================================================*
008233******************************************************************
008234 END PROGRAM YCH102.
008235******************************************************************
