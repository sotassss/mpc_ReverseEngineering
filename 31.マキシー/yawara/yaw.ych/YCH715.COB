000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YCH715.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*         中央 カルテ裏印刷（柔ｳｨﾝﾄﾞｳｽﾞ95版）
000100*         MED = YCH713G YCH715P
000110*----------------------------------------------------------------*
000120 DATE-WRITTEN.           2000-03-03
000130 DATE-COMPILED.          2000-03-03
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000260     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000270                             ORGANIZATION             IS  INDEXED
000280                             ACCESS MODE              IS  DYNAMIC
000290                             RECORD KEY               IS  元－元号区分
000300                             FILE STATUS              IS  状態キー
000310                             LOCK        MODE         IS  AUTOMATIC.
000320     SELECT  制御情報マスタ  ASSIGN      TO        SEIGYOL
000330                             ORGANIZATION             IS  INDEXED
000340                             ACCESS MODE              IS  DYNAMIC
000350                             RECORD KEY               IS  制－制御区分
000360                             FILE STATUS              IS  状態キー
000370                             LOCK        MODE         IS  AUTOMATIC.
000380     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000390                             ORGANIZATION             IS  INDEXED
000400                             ACCESS MODE              IS  DYNAMIC
000410                             RECORD KEY               IS 受－施術和暦年月
000420                                                          受－患者コード
000430                             ALTERNATE RECORD KEY     IS 受－施術和暦年月
000440                                                          受－患者カナ
000450                                                          受－患者コード
000460                             ALTERNATE RECORD KEY     IS  受－患者コード
000470                                                         受－施術和暦年月
000480                             ALTERNATE RECORD KEY     IS 受－施術和暦年月
000490                                                          受－保険種別
000500                                                          受－保険者番号
000510                                                          受－患者コード
000520                             ALTERNATE RECORD KEY     IS 受－施術和暦年月
000530                                                          受－公費種別
000540                                                     受－費用負担者番号
000550                                                          受－患者コード
000560                             ALTERNATE RECORD KEY     IS 受－施術和暦年月
000570                                                          受－助成種別
000580                                                  受－費用負担者番号助成
000590                                                          受－患者コード
000600                             ALTERNATE RECORD KEY  IS 受－請求和暦年月
000610                                                      受－施術和暦年月
000620                                                      受－患者コード
000630                             FILE STATUS              IS  状態キー
000640                             LOCK        MODE         IS  AUTOMATIC.
000293     SELECT  長期継続者Ｆ    ASSIGN      TO        CHOKEIL
000294                             ORGANIZATION             IS INDEXED
000295                             ACCESS MODE              IS DYNAMIC
000296                             RECORD KEY               IS 長継－施術和暦年月
000297                                                         長継－患者コード
000298                             ALTERNATE RECORD KEY     IS 長継－患者コード
000299                                                         長継－施術和暦年月
000300                             FILE STATUS              IS 状態キー
000301                             LOCK      MODE           IS AUTOMATIC.
000440     SELECT  名称マスタ      ASSIGN      TO        MEISYOL
000450                             ORGANIZATION             IS  INDEXED
000460                             ACCESS MODE              IS  DYNAMIC
000470                             RECORD KEY               IS  名－区分コード
000480                                                          名－名称コード
000490                             FILE STATUS              IS  状態キー
000500                             LOCK        MODE         IS  AUTOMATIC.
000650     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W7114L.DAT"
000660                             ORGANIZATION             IS  INDEXED
000670                             ACCESS                   IS  DYNAMIC
000680                             RECORD      KEY    IS  作１－施術和暦年月日
000690                             FILE        STATUS       IS  状態キー
000700                             LOCK        MODE         IS  AUTOMATIC.
000710*
000720     SELECT  印刷ファイル    ASSIGN      TO     GS-PRTF002
000730                             SYMBOLIC    DESTINATION  IS "PRT"
000740                             FORMAT                   IS  定義体名Ｐ
000750                             GROUP                    IS  項目群名Ｐ
000760                             PROCESSING  MODE         IS  処理種別Ｐ
000770                             UNIT        CONTROL      IS  拡張制御Ｐ
000780                             FILE        STATUS       IS  通知情報Ｐ.
000790******************************************************************
000800*                      DATA DIVISION                             *
000810******************************************************************
000820 DATA                    DIVISION.
000830 FILE                    SECTION.
000840*                           ［ＲＬ＝  １２８］
000850 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
000860     COPY GENGOU          OF  XFDLIB  JOINING   元   AS  PREFIX.
000870*                           ［ＲＬ＝  ２５６］
000880 FD  制御情報マスタ          BLOCK   CONTAINS   1   RECORDS.
000890     COPY SEIGYO          OF  XFDLIB  JOINING   制   AS  PREFIX.
000900*                           ［ＲＬ＝  ３２０］
000910 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
000920     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
000686*                           ［ＲＬ＝  １２８］
000687 FD  長期継続者Ｆ          BLOCK   CONTAINS   1   RECORDS.
000688     COPY CHOKEI          OF  XFDLIB  JOINING   長継   AS  PREFIX.
001530*                           ［ＲＬ＝  １２８］
001540 FD  名称マスタ          BLOCK   CONTAINS   1   RECORDS.
001550     COPY MEISYO          OF  XFDLIB  JOINING   名   AS  PREFIX.
001000*                           ［ＲＬ＝  ２００］
001010 FD  作業ファイル１ RECORD  CONTAINS 200 CHARACTERS.
000950 01 作１－レコード.
000960    03 作１－レコードキー.
000970       05  作１－施術和暦年月日.
000980          07 作１－施術和暦               PIC 9.
000990          07 作１－施術年月.
001000             09 作１－施術年              PIC 9(2).
001010             09 作１－施術月              PIC 9(2).
001020          07 作１－施術日                 PIC 9(2).
001030    03 作１－レコードデータ.
001040       05 作１－患者コード.
001050          07 作１－患者番号               PIC 9(6).
001060          07 作１－枝番                   PIC X.
001070* ＊＊＊　日毎＋部位毎データ　＊＊＊
001080         05  作１－日チェック              PIC 9.
001090         05  作１－部位データ              OCCURS 5.
001100             07  作１－部位計              PIC 9(5).
001110         05  作１－電罨料.
001120             07  作１－罨法金額            PIC 9(4).
001130             07  作１－電療金額            PIC 9(4).
001140         05  作１－一部負担金              PIC 9(5).
001210         05  作１－金属料                  PIC 9(4).
001210         05  作１－運動料                  PIC 9(3).
001551         05  作１－コメント                PIC X(100).
001220         05  FILLER                        PIC X(40).
001160*
001170 FD  印刷ファイル.
001180     COPY YCH715P        OF  XMDLIB.
001190*----------------------------------------------------------------*
001200******************************************************************
001210*                WORKING-STORAGE SECTION                         *
001220******************************************************************
001230 WORKING-STORAGE         SECTION.
001240 01 キー入力                           PIC X     VALUE SPACE.
001250 01 状態キー                           PIC X(2)  VALUE SPACE.
001260 01 終了フラグ                         PIC X(3)  VALUE SPACE.
001270 01 終了フラグ２                       PIC X(3)  VALUE SPACE.
001280 01 初検フラグ                         PIC X(3)  VALUE SPACE.
001290 01 ファイル名                         PIC N(6)  VALUE SPACE.
001300 01 レセプトＰＧＷ                     PIC X(8)  VALUE SPACE.
001310 01 前和暦Ｗ                           PIC 9     VALUE ZERO.
001320 01 カレント元号Ｗ                     PIC 9(1)  VALUE ZERO.
001330 01 印刷位置ＣＮＴ                     PIC 9(2)  VALUE ZERO.
001340 01 部位ＣＮＴ                         PIC 9     VALUE ZERO.
001350 01 患者番号Ｗ                         PIC 9(6)  VALUE ZERO.
001360**
001370 01 遅延フラグ                         PIC X(3) VALUE SPACE.
001380 01 遅延回数Ｗ                         PIC 9(4) VALUE ZERO.
001390 01 遅延ＣＮＴ                         PIC 9(5) VALUE ZERO.
001400**
001210 01 オープンフラグ                     PIC X(3)   VALUE SPACE.
001410*
      */長期理由文用ワーク↓↓↓0401
       01 長期理由文２Ｗ.
          03 長期理由文Ｗ PIC N(40) OCCURS 16 VALUE SPACE.
001201 01 番号カウンタ                       PIC 9(2) VALUE ZERO.
      */0402
005080 01 月末日取得用Ｗ.
005090    03 請求終了日Ｗ                    PIC 9(2) VALUE ZERO.
005100    03 対象西暦Ｗ                      PIC 9(4) VALUE ZERO.
005110    03 商Ｗ                            PIC 9(3) VALUE ZERO.
005120    03 余Ｗ                            PIC 9(3) VALUE ZERO.
001329****
001330 01 長期負傷種別Ｗ                     PIC 9(2) VALUE ZERO.
001331 01 長期部位Ｗ                         PIC 9(2) VALUE ZERO.
001332 01 理由コードＷ                       PIC 9(3) VALUE ZERO.
001340 01 症状コードＷ                       PIC 9(2) VALUE ZERO.
001350 01 症状名Ｗ                           PIC N(4) VALUE SPACE.
001361 01 合成理由文Ｗ.
001369    03 長期番号Ｗ                      PIC X(2)   VALUE SPACE.
001360    03 理由文Ｗ                        PIC X(320) VALUE SPACE.
001362* 01 カウンタ                           PIC 9(2)  VALUE ZERO.
001363 01 全角空白                           PIC X(2)  VALUE X"8140".
001364 01 半角空白                           PIC X(2)  VALUE X"2020".
001365***
001366*** 症状用
001367 01 症状Ｗ.
001368    03 症状内容Ｗ                      PIC N(29) VALUE SPACE.
001401** 適用４０文字レセプト用
001402 01 理由４０桁Ｗ.
001403    03 理由４０桁１Ｗ                  PIC N(40) VALUE SPACE.
001404    03 理由４０桁２Ｗ                  PIC N(40) VALUE SPACE.
001405    03 理由４０桁３Ｗ                  PIC N(40) VALUE SPACE.
001405    03 理由４０桁４Ｗ                  PIC N(40) VALUE SPACE.
001405    03 理由４０桁５Ｗ                  PIC N(40) VALUE SPACE.
001401** 適用３５文字用
001402 01 理由３５桁Ｗ.
001403    03 理由３５桁１Ｗ                  PIC N(35) VALUE SPACE.
001404    03 理由３５桁２Ｗ                  PIC N(35) VALUE SPACE.
001405    03 理由３５桁３Ｗ                  PIC N(35) VALUE SPACE.
001405    03 理由３５桁４Ｗ                  PIC N(35) VALUE SPACE.
001405    03 理由３５桁５Ｗ                  PIC N(35) VALUE SPACE.
001405    03 理由３５桁６Ｗ                  PIC N(35) VALUE SPACE.
      *
002290 01 カウンタ                           PIC 9(2)  VALUE ZERO.
      */長期理由文用ワーク↑↑↑0401
001420****************
001430* 連結項目待避 *
001440****************
001450*    ************
001460*    * 印刷キー *
001470*    ************
001480 01 対象データＷＲ.
001490    03 施術和暦年月ＷＲ.
001500       05 施術和暦ＷＲ                  PIC 9(1)  VALUE ZERO.
001510       05 施術年ＷＲ                    PIC 9(2)  VALUE ZERO.
001520       05 施術月ＷＲ                    PIC 9(2)  VALUE ZERO.
001530    03 開始日ＷＲ                       PIC 9(2)  VALUE ZERO.
001540    03 終了日ＷＲ                       PIC 9(2)  VALUE ZERO.
001550    03 患者カナＷＲ                     PIC X(50) VALUE SPACE.
001560    03 患者コードＷＲ.
001570       05 患者番号ＷＲ                  PIC 9(6)  VALUE ZERO.
001580       05 枝番ＷＲ                      PIC X(1)  VALUE SPACE.
001590    03 印刷モードＦＷＲ                 PIC 9(1)  VALUE ZERO.
001600    03 日ごとモードＷＲ                 PIC 9(1)  VALUE ZERO.
001610    03 氏名モードＷＲ                   PIC 9(1)  VALUE ZERO.
001610    03 コメントモードＷＲ               PIC 9(1)  VALUE ZERO.
001620    03 印字位置ＣＮＴ                   PIC 9(2)  VALUE ZERO.
      */長期理由印刷0401
          03 長期モードＷＲ                   PIC 9(1)  VALUE ZERO.
      */金額行/0402
          03 料金モードＷＲ                   PIC 9(1)  VALUE ZERO.
          03 段数ＷＲ                         PIC 9(1)  VALUE ZERO.
002920    03 提出年月日ＷＲ.
002930       05 提出年ＷＲ                    PIC 9(2)  VALUE ZERO.
002940       05 提出月ＷＲ                    PIC 9(2)  VALUE ZERO.
002950       05 提出日ＷＲ                    PIC 9(2)  VALUE ZERO.
      */初件月明細/0405
          03 初検月明細モードＷＲ             PIC 9(1)  VALUE ZERO.
001630**************
001640* 受診者情報 *
001650**************
001660 01 受診者情報Ｗ.
001670    03 施術年月Ｗ.
001680       05 施術年Ｗ                     PIC 9(2)   VALUE ZERO.
001690       05 施術月Ｗ                     PIC 9(2)   VALUE ZERO.
001700    03 保険者番号Ｗ.
001710       05 印刷保険者番号Ｗ             PIC X(6)   VALUE SPACE.
001720       05 FILLER                       PIC X(4)   VALUE SPACE.
001730    03 退職保険者番号Ｗ.
001740       05 FILLER                       PIC X(2)   VALUE SPACE.
001750       05 退職印刷保険者番号Ｗ         PIC X(6)   VALUE SPACE.
001760       05 FILLER                       PIC X(2)   VALUE SPACE.
001770    03 市町村番号Ｗ.
001780       05 印刷市町村番号Ｗ             PIC X(8)   VALUE SPACE.
001790       05 FILLER                       PIC X(2)   VALUE SPACE.
001800    03 保険種別Ｗ                      PIC 9(2)   VALUE ZERO.
001810    03 患者情報Ｗ.
001820       05 患者カナＷ                   PIC X(20)  VALUE SPACE.
001830       05 患者氏名Ｗ                   PIC X(50)  VALUE SPACE.
001840************
001850* 料金情報 *
001860************
001870 01 料金情報Ｗ.
001880    03 初検加算Ｗ.
001890       05 時間外チェックＷ                PIC N(1) VALUE SPACE.
001900       05 休日チェックＷ                  PIC N(1) VALUE SPACE.
001910       05 深夜チェックＷ                  PIC N(1) VALUE SPACE.
001920    03 往療加算Ｗ.
001930       05 夜間チェックＷ                  PIC N(1) VALUE SPACE.
001940       05 暴風チェックＷ                  PIC N(1) VALUE SPACE.
001950    03 往療距離ＷＲ                       PIC 9(3)V9 VALUE ZERO.
001960    03 往療料ＷＲ                         OCCURS 3.
001970       05 往療月ＷＲ                      PIC 9(2)  VALUE ZERO.
001980       05 往療回数ＷＲ                    PIC 9(2)  VALUE ZERO.
001990       05 往療料金ＷＲ                    PIC 9(6)  VALUE ZERO.
002000    03 往療加算料ＷＲ                     PIC 9(5)  VALUE ZERO.
002010    03 金属副子チェックＷ.
002020       05 大チェックＷ                    PIC N(1) VALUE SPACE.
002030       05 中チェックＷ                    PIC N(1) VALUE SPACE.
002040       05 小チェックＷ                    PIC N(1) VALUE SPACE.
002050    03 金属副子料ＷＲ                     PIC 9(5) VALUE ZERO.
002060    03 初検料ＷＲ                         PIC 9(4) VALUE ZERO.
002060    03 相談料ＷＲ                         PIC 9(4) VALUE ZERO.
002070    03 再検料ＷＲ                         PIC 9(4) VALUE ZERO.
002080    03 初検加算料ＷＲ                     PIC 9(4) VALUE ZERO.
002090    03 初回処置料ＷＲ                     PIC 9(5) OCCURS 5 VALUE ZERO.
002100    03 初回処置料チェックＷ.
002110       05 整復料チェックＷ                PIC N(1) VALUE SPACE.
002120       05 固定料チェックＷ                PIC N(1) VALUE SPACE.
002130       05 施療料チェックＷ                PIC N(1) VALUE SPACE.
002140    03 情報提供料ＷＲ                     PIC 9(6) VALUE ZERO.
002150 01 明細.
002160    03 罨法料Ｗ                           PIC 9(4) VALUE ZERO.
002170    03 電療料Ｗ                           PIC 9(4) VALUE ZERO.
002180    03 一部負担金Ｗ                       PIC 9(5) VALUE ZERO.
002190    03 部位Ｗ                             OCCURS 5.
002200       05 部位計Ｗ                        PIC 9(5) VALUE ZERO.
002210       05 日数Ｗ                          PIC 9(2) VALUE ZERO.
002520    03 コメントＷ.
002530       05 コメント１Ｗ                    PIC X(50) VALUE SPACE.
002540       05 コメント２Ｗ                    PIC X(50) VALUE SPACE.
002220*******************************************************************
002230 01 印刷制御.
002240     03 定義体名Ｐ                     PIC X(8) VALUE SPACE.
002250     03 項目群名Ｐ                     PIC X(8) VALUE SPACE.
002260     03 処理種別Ｐ                     PIC X(2) VALUE SPACE.
002270     03 拡張制御Ｐ.
002280         05 端末制御Ｐ.
002290             07 移動方向Ｐ             PIC X(1) VALUE SPACE.
002300             07 移動行数Ｐ             PIC 9(3) VALUE ZERO.
002310         05 詳細制御Ｐ                 PIC X(2) VALUE SPACE.
002320     03 通知情報Ｐ                     PIC X(2) VALUE SPACE.
002330     03 ユニット名Ｐ                   PIC X(8) VALUE SPACE.
002340*
002350 01 計算機西暦年Ｗ                     PIC 9(2) VALUE ZERO.
002360* 日付ＷＯＲＫ
002370 01 和暦終了年Ｗ                       PIC 9(4) VALUE ZERO.
002380 01 計算機西暦.
002390    03 計算機西暦年                    PIC 9(4) VALUE ZERO.
002400    03 計算機西暦月日                  PIC 9(4) VALUE ZERO.
002410 01 計算機西暦Ｒ REDEFINES 計算機西暦.
002420    03 計算機世紀                      PIC 9(2).
002430    03 計算機日付                      PIC 9(6).
002440    03 計算機日付Ｒ REDEFINES 計算機日付.
002450       05 計算機年月                   PIC 9(4).
002460       05 計算機年月Ｒ REDEFINES 計算機年月.
002470         07 計算機年                   PIC 9(2).
002480         07 計算機月                   PIC 9(2).
002490       05 計算機日                     PIC 9(2).
002500*
002510******************************************************************
002520*                          連結項目                              *
002530******************************************************************
002540************
002550* 印刷キー *
002560************
002570 01 連印－対象データＹＣＨ７１３ IS EXTERNAL.
002580    03 連印－初診料.
002590       05 連印－初検料                    PIC 9(4).
002600       05 連印－初検加算料                PIC 9(4).
002610       05 連印－休日                      PIC 9.
002620       05 連印－深夜                      PIC 9.
002630       05 連印－時間外                    PIC 9.
          03 連印－相談料                       PIC 9(4).
002640    03 連印－再検料                       PIC 9(4).
002650    03 連印－初回処置                     OCCURS 5.
002660       05 連印－初回処置料                PIC 9(5).
002670    03 連印－施療料チェック               PIC N.
002680    03 連印－整復料チェック               PIC N.
002690    03 連印－固定料チェック               PIC N.
002700    03 連印－往療料加算.
002710       05 連印－夜間                      PIC 9.
002720       05 連印－暴風                      PIC 9.
002730       05 連印－往療加算料                PIC 9(5).
002740       05 連印－距離                      PIC 9(3)V9.
002750    03 連印－往療料                       OCCURS 3.
002760       05 連印－月                        PIC 9(2).
002770       05 連印－回数                      PIC 9(2).
002780       05 連印－往療料金額                PIC 9(6).
002790    03 連印－金属大                       PIC 9.
002800    03 連印－金属中                       PIC 9.
002810    03 連印－金属小                       PIC 9.
002820    03 連印－金額                         PIC 9(6).
002830    03 連印－情報提供料                   PIC 9(6).
002840    03 連印－施術料                       OCCURS 5.
002850       05 連印－施術金額                  PIC 9(6).
003520    03 連印－実日数                       PIC 9(2).
003520    03 連印－費用額                       PIC 9(6).
003520    03 連印－負担額                       PIC 9(6).
003520    03 連印－請求額                       PIC 9(6).
002860****************
002870* 画面入力情報 *
002880****************
002890 01 連入－入力データＹＣＨ７１３ IS EXTERNAL.
002900    03 連入－施術和暦年月.
002910       05 連入－施術和暦                  PIC 9(1).
002920       05 連入－施術年                    PIC 9(2).
002930       05 連入－施術月                    PIC 9(2).
002940    03 連入－開始日付.
002950       05 連入－開始日                    PIC 9(2).
002960    03 連入－患者コード.
002970       05 連入－患者番号                  PIC 9(6).
002980       05 連入－枝番                      PIC X(1).
002990    03 連入－印刷モードＦ                 PIC 9(1).
003000    03 連入－日ごとモード                 PIC 9(1).
003010    03 連入－氏名モード                   PIC 9(1).
003010    03 連入－コメントモード               PIC 9(1).
003020    03 連入－印刷行数                     PIC 9(2).
      */長期理由印刷0401
003280    03 連入－長期モード                   PIC 9(1).
      */金額行を印刷/0402
003280    03 連入－料金モード                   PIC 9(1).
          03 連入－金額行段数                   PIC 9(1).
          03 連入－請求年                       PIC 9(2).
          03 連入－請求月                       PIC 9(2).
          03 連入－請求日                       PIC 9(2).
      */初件月明細/0405
003280    03 連入－初検月明細モード             PIC 9(1).
      *
       01 連入－表示フラグＹＣＨ７１３ IS EXTERNAL GLOBAL.
          03 連入－プレビュー区分               PIC 9(1).
003030*
003040 01 連－共通キー IS EXTERNAL.
003050    03  連－カレント和暦               PIC 9.
003060*
003090**********************
003100* メッセージ表示キー *
003110**********************
003120 01 連メ－キー IS EXTERNAL.
003130    03  連メ－メッセージ                 PIC N(20).
006410*
000540************************************
000550* プリンタファイル作成用           *
000560************************************
000570 01 Ｈ連ＰＲＴＦ－作成データ IS EXTERNAL.
000580     03 Ｈ連ＰＲＴＦ－ファイル名           PIC X(8).
000590     03 Ｈ連ＰＲＴＦ－プレビュー区分       PIC 9.
000600     03 Ｈ連ＰＲＴＦ－帳票プログラム名     PIC X(8).
000610     03 Ｈ連ＰＲＴＦ－オーバレイ名         PIC X(8).
000993************************************
000994* プリンタファイル作成特殊用       *
000995************************************
000996 01 Ｈ連特殊ＰＲＴＦ－作成データ IS EXTERNAL.
000997     03 Ｈ連特殊ＰＲＴＦ－用紙種類         PIC X(8).
003100*
003160******************************************************************
003170*                      PROCEDURE  DIVISION                       *
003180******************************************************************
003190 PROCEDURE               DIVISION.
003200************
003210*           *
003220* 初期処理   *
003230*           *
003240************
002570     PERFORM プリンタファイル作成.
           MOVE SPACE TO オープンフラグ.
003250     PERFORM 初期化.
003260     PERFORM 制御情報取得.
003270************
003280*           *
003290* 主処理     *
003300*           *
003310************
003320* 印刷
003330     PERFORM 連結項目待避.
003340     EVALUATE 印刷モードＦＷＲ
003350     WHEN ZERO
003360         PERFORM 印刷セット１
003370         PERFORM 印刷処理１
003380     WHEN 1
003390         PERFORM 印刷セット２
003400         PERFORM 印刷処理２
003410     END-EVALUATE.
003420************
003430*           *
003440* 終了処理   *
003450*           *
003460************
003470     PERFORM 終了処理.
003480     PERFORM 遅延処理.
003490     EXIT PROGRAM.
003500*
003510*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002860*================================================================*
002870 プリンタファイル作成 SECTION.
002880*================================================================*
002890*   / 初期化 /
002900     MOVE SPACE TO Ｈ連ＰＲＴＦ－作成データ.
002910     INITIALIZE Ｈ連ＰＲＴＦ－作成データ.
002225     MOVE SPACE TO Ｈ連特殊ＰＲＴＦ－作成データ.
002226     INITIALIZE Ｈ連特殊ＰＲＴＦ－作成データ.
002920*
002930*
002940*--↓↓ 変更箇所 ↓↓--------------------------------------*
002230*   使用する用紙種別セット
           MOVE "KARUTE"              TO Ｈ連特殊ＰＲＴＦ－用紙種類.
002970*   使用するプリンタファイル名セット
002231     MOVE "PRTF002"             TO Ｈ連ＰＲＴＦ－ファイル名.
002972*
002973*   使用する帳票プログラム名セット
002974     MOVE "YCH715"             TO Ｈ連ＰＲＴＦ－帳票プログラム名.
002975*
002976*--↑↑-----------------------------------------------------*
002980*
002990*   / プレビュー区分セット /
003000     MOVE 連入－プレビュー区分 TO Ｈ連ＰＲＴＦ－プレビュー区分.
003010*
003020     CALL   "CRTPRTF".
003030     CANCEL "CRTPRTF".
003040*
003520*================================================================*
003530 初期化 SECTION.
003540*
003550     PERFORM ファイルオープン.
003560*    /* 現在日付取得 */
003570     ACCEPT 計算機日付 FROM DATE.
003580*    /* 1980～2079年の間で設定 */
003590     IF 計算機年 > 80
003600         MOVE 19 TO 計算機世紀
003610     ELSE
003620         MOVE 20 TO 計算機世紀
003630     END-IF.
003640     PERFORM カレント元号取得.
003650     PERFORM 和暦終了年取得.
003660     COMPUTE 計算機西暦年Ｗ = 計算機西暦年 - 1988.
003670*================================================================*
003680 カレント元号取得 SECTION.
003690*
003700     MOVE ZEROS TO 制－制御区分.
003710     READ 制御情報マスタ
003720     NOT INVALID KEY
003730         MOVE 制－カレント元号 TO カレント元号Ｗ
003740     END-READ.
003750*
003760*================================================================*
003770 和暦終了年取得 SECTION.
003780*
003790*     DISPLAY NC"カレント元号Ｗ"  カレント元号Ｗ UPON MSGBOX.
003800     MOVE カレント元号Ｗ TO 元－元号区分.
003810     READ 元号マスタ
003820     INVALID KEY
003830         DISPLAY NC"指定和暦が登録されていません" UPON CONS
003840         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003850                                                  UPON CONS
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
003860         ACCEPT  キー入力 FROM CONS
003870         PERFORM 終了処理
003880         EXIT PROGRAM
003890     NOT INVALID KEY
003900         COMPUTE 前和暦Ｗ = カレント元号Ｗ - 1
003910         MOVE 前和暦Ｗ TO 元－元号区分
003920         READ 元号マスタ
003930         INVALID KEY
003940             DISPLAY NC"指定和暦が登録されていません" UPON CONS
003950             DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003960                                                      UPON CONS
000080*-----------------------------------------*
000090             CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
003970             ACCEPT  キー入力 FROM CONS
003980             PERFORM 終了処理
003990             EXIT PROGRAM
004000         NOT INVALID KEY
004010             MOVE 元－終了西暦年 TO 和暦終了年Ｗ
004020         END-READ
004030     END-READ.
004040*
004050*================================================================*
004060 ファイルオープン SECTION.
004070*
004080     OPEN INPUT   元号マスタ
004090         MOVE NC"元号" TO ファイル名.
004100         PERFORM オープンチェック.
004110     OPEN INPUT   制御情報マスタ
004120         MOVE NC"制御情報" TO ファイル名.
004130         PERFORM オープンチェック.
004140     OPEN INPUT   受診者情報Ｆ.
004150         MOVE NC"受情" TO ファイル名.
004160         PERFORM オープンチェック.
004170     OPEN INPUT 作業ファイル１
004180         MOVE NC"作業１" TO ファイル名.
004190         PERFORM オープンチェック.
004200*     OPEN I-O   印刷ファイル
004210*         PERFORM エラー処理Ｐ.
002650     OPEN INPUT   長期継続者Ｆ.
002651         MOVE NC"長期継続者Ｆ" TO ファイル名.
002652         PERFORM オープンチェック.
007760     OPEN INPUT   名称マスタ
007770         MOVE NC"名称" TO ファイル名.
007780         PERFORM オープンチェック.
004220*================================================================*
004230 オープンチェック SECTION.
004240*
004250     IF 状態キー  NOT =  "00"
004260         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
004270         DISPLAY NC"状態キー：" 状態キー         UPON CONS
004280         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004290                                                 UPON CONS
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
004300         ACCEPT  キー入力 FROM CONS
004310         PERFORM ファイル閉鎖
004320         EXIT PROGRAM.
004330*================================================================*
004340 制御情報取得 SECTION.
004350*
004360     MOVE ZERO TO 制－制御区分
004370     READ 制御情報マスタ
004380     NOT INVALID KEY
004390         MOVE 制－遅延回数       TO 遅延回数Ｗ
004400     END-READ.
004410*
004420*================================================================*
004430 遅延処理 SECTION.
004440*
004450     PERFORM VARYING 遅延ＣＮＴ FROM 1 BY 1
004460                                UNTIL 遅延ＣＮＴ > 遅延回数Ｗ
004470         MOVE SPACE TO 遅延フラグ
004480     END-PERFORM.
004490*
004500*================================================================*
004510 連結項目待避 SECTION.
004520*
004530     MOVE 連入－施術和暦           TO 施術和暦ＷＲ.
004540     MOVE 連入－施術年             TO 施術年ＷＲ.
004550     MOVE 連入－施術月             TO 施術月ＷＲ.
004560     MOVE 連入－開始日             TO 開始日ＷＲ.
004570     MOVE 連入－患者番号           TO 患者番号ＷＲ.
004580     MOVE 連入－枝番               TO 枝番ＷＲ.
004590     MOVE 連入－印刷モードＦ       TO 印刷モードＦＷＲ.
004600     MOVE 連入－日ごとモード       TO 日ごとモードＷＲ.
004610     MOVE 連入－氏名モード         TO 氏名モードＷＲ.
004610     MOVE 連入－コメントモード     TO コメントモードＷＲ.
004620     MOVE 連入－印刷行数           TO 印刷位置ＣＮＴ.
      */0401
           MOVE 連入－長期モード         TO 長期モードＷＲ.
      */金額行の印刷/0402
           MOVE 連入－料金モード         TO 料金モードＷＲ.
           MOVE 連入－金額行段数         TO 段数ＷＲ.
           MOVE 連入－請求年             TO 提出年ＷＲ.
           MOVE 連入－請求月             TO 提出月ＷＲ.
           MOVE 連入－請求日             TO 提出日ＷＲ.
      */初件月明細/0405
           MOVE 連入－初検月明細モード   TO 初検月明細モードＷＲ.
004630*================================================================*
004640 印刷セット１ SECTION.
004650*
004660     PERFORM 項目初期化.
004670     PERFORM 受診者情報取得.
004680     IF 氏名モードＷＲ = 1
004690         MOVE 患者氏名Ｗ          TO 患者氏名
004700     END-IF.
004710     IF (日ごとモードＷＲ = 1) OR (コメントモードＷＲ = 1)
004720         PERFORM 印刷データセット
004730         PERFORM 印刷行数退避
004740     END-IF.
      */長期理由を印刷0401
           IF 長期モードＷＲ = 1
               PERFORM 長期理由文取得
               PERFORM VARYING カウンタ FROM 1 BY 1
                       UNTIL   カウンタ > 11
                   MOVE 長期理由文Ｗ(カウンタ) TO 長期理由文(カウンタ)
               END-PERFORM
           END-IF.
004750*================================================================*
004760 印刷セット２ SECTION.
004770*
004780     PERFORM 項目初期化.
004790********************
004800* 料金データセット *
004810********************
004820     PERFORM 連結項目退避
004830*    ****************************************************************
004840*    * 料金（月毎）（負傷毎）（逓減毎）については連結項目よりセット *
004850*    ****************************************************************
      */初検月明細の印刷を選択可に/0405
           IF 初検月明細モードＷＲ = 1
004860         MOVE 初検料ＷＲ          TO  初検料
               MOVE 相談料ＷＲ          TO  相談料
               IF 相談料ＷＲ NOT = ZERO
                   MOVE "初検時相談支援料" TO  相談料Ｔ
                   MOVE "円"               TO  相談料円
               END-IF
004870         MOVE 時間外チェックＷ    TO  時間外チェック
004880         MOVE 休日チェックＷ      TO  休日チェック
004890         MOVE 深夜チェックＷ      TO  深夜チェック
004900         MOVE 初検加算料ＷＲ      TO  初検加算
004910         MOVE 再検料ＷＲ          TO  再検料
004920         MOVE 施療料チェックＷ    TO  施療料チェック
004930         MOVE 整復料チェックＷ    TO  整復料チェック
004940         MOVE 固定料チェックＷ    TO  固定料チェック
004950         MOVE 初回処置料ＷＲ(1)   TO  初回処置料(1)
004960         MOVE 初回処置料ＷＲ(2)   TO  初回処置料(2)
004970         MOVE 初回処置料ＷＲ(3)   TO  初回処置料(3)
004980         MOVE 初回処置料ＷＲ(4)   TO  初回処置料(4)
004990         MOVE 初回処置料ＷＲ(5)   TO  初回処置料(5)
005000         MOVE 往療距離ＷＲ        TO  往療距離
005010         MOVE 往療月ＷＲ(1)       TO  往療月１
005020         MOVE 往療回数ＷＲ(1)     TO  往療回数１
005030         MOVE 往療料金ＷＲ(1)     TO  往療料１
005040         MOVE 夜間チェックＷ      TO  夜間チェック
005050         MOVE 暴風チェックＷ      TO  暴風チェック
005060         MOVE 往療加算料ＷＲ      TO  往療加算
005070         MOVE 情報提供料ＷＲ      TO  情報提供料
               IF 施術和暦年月ＷＲ < 43006
005080            MOVE 大チェックＷ     TO  大チェック
005090            MOVE 中チェックＷ     TO  中チェック
005100            MOVE 小チェックＷ     TO  小チェック
005110            MOVE 金属副子料ＷＲ   TO  金属料
               END-IF
           END-IF.
005120*
      */金額行を印刷/0402
           IF 料金モードＷＲ = 1
               PERFORM 請求期間取得
               PERFORM 合計金額行セット
           END-IF.
      *
005130*================================================================*
005140 項目初期化 SECTION.
005150*
005160     INITIALIZE 受診者情報Ｗ.
005170     INITIALIZE 料金情報Ｗ.
005180     INITIALIZE 明細.
005190     MOVE SPACE TO YCH715P.
005200     INITIALIZE YCH715P.
005210*================================================================*
005220 印刷データセット SECTION.
005240     MOVE 施術和暦ＷＲ       TO 作１－施術和暦.
005250     MOVE 施術年ＷＲ         TO 作１－施術年.
005260     MOVE 施術月ＷＲ         TO 作１－施術月.
005270     MOVE 開始日ＷＲ         TO 作１－施術日.
005280     START 作業ファイル１ KEY IS >= 作１－施術和暦年月日
005290     END-START.
005300     IF 状態キー = "00"
005310         PERFORM 作業ファイル１読込
005320         PERFORM UNTIL (作１－施術日 > 31) OR
005330                       (終了フラグ = "YES")
005340             IF 作１－日チェック = 1
005230                 IF 日ごとモードＷＲ = 1
005350                     MOVE 作１－施術月       TO 月(印刷位置ＣＮＴ)
005360                     MOVE "/"                TO スラッシュ(印刷位置ＣＮＴ)
005370                     MOVE 作１－施術日       TO 日(印刷位置ＣＮＴ)
005380                     MOVE 作１－部位計(1)    TO 後療１(印刷位置ＣＮＴ)
005390                     MOVE 作１－部位計(2)    TO 後療２(印刷位置ＣＮＴ)
005400                     MOVE 作１－部位計(3)    TO 後療３(印刷位置ＣＮＴ)
005410                     MOVE 作１－部位計(4)    TO 後療４(印刷位置ＣＮＴ)
005420                     MOVE 作１－部位計(5)    TO 後療５(印刷位置ＣＮＴ)
005430                     MOVE 作１－罨法金額     TO 冷罨温罨(印刷位置ＣＮＴ)
005440                     MOVE 作１－電療金額     TO 電療(印刷位置ＣＮＴ)
005450                     MOVE 作１－一部負担金   TO 一部負担金(印刷位置ＣＮＴ)
005440                     MOVE 作１－金属料       TO 金属副子(印刷位置ＣＮＴ)
                           IF 作１－金属料 NOT = ZERO
                              MOVE NC"金"          TO 金(印刷位置ＣＮＴ)
                           END-IF
005440                     MOVE 作１－運動料       TO 運動後療(印刷位置ＣＮＴ)
                           IF 作１－運動料 NOT = ZERO
                              MOVE NC"運"          TO 運(印刷位置ＣＮＴ)
                           END-IF
                       END-IF
005230                 IF コメントモードＷＲ = 1
                           MOVE 作１－コメント  TO コメントＷ
                           MOVE コメント１Ｗ    TO コメント１(印刷位置ＣＮＴ)
                           MOVE コメント２Ｗ    TO コメント２(印刷位置ＣＮＴ)
                       END-IF
005460                 COMPUTE 印刷位置ＣＮＴ = 印刷位置ＣＮＴ + 1
005470                 IF 印刷位置ＣＮＴ > 31
005480                     PERFORM 印刷処理１
005490                     PERFORM 改頁処理
005500                     PERFORM 項目初期化
005510                     MOVE 1      TO 印刷位置ＣＮＴ
005520                     MOVE SPACE  TO  連メ－キー
005530                     MOVE NC"新しい用紙をセットしてください" TO 連メ－メッセージ
005540                     CALL   "MSG001"
005550                     CANCEL "MSG001"
005560                 END-IF
005570             END-IF
005580             PERFORM 作業ファイル１読込
005590         END-PERFORM
005610     END-IF.
005620*================================================================*
005630 連結項目退避 SECTION.
005640*
005650     MOVE 連印－初検料     TO 初検料ＷＲ.
           MOVE 連印－相談料     TO 相談料ＷＲ.
005660     MOVE 連印－初検加算料 TO 初検加算料ＷＲ.
005670     IF 連印－休日 = 1
005680         MOVE NC"○"       TO 休日チェックＷ
005690     END-IF.
005700     IF 連印－深夜 = 1
005710         MOVE NC"○"       TO 深夜チェックＷ
005720     END-IF.
005730     IF 連印－時間外 = 1
005740         MOVE NC"○"       TO 時間外チェックＷ
005750     END-IF.
005760     MOVE 連印－再検料     TO 再検料ＷＲ.
005770     IF 連印－夜間 = 1
005780         MOVE NC"○"       TO 夜間チェックＷ
005790     END-IF.
005800     IF 連印－暴風 = 1
005810         MOVE NC"○"       TO 暴風チェックＷ
005820     END-IF.
005830     MOVE 連印－初回処置料(1)  TO 初回処置料ＷＲ(1).
005840     MOVE 連印－初回処置料(2)  TO 初回処置料ＷＲ(2).
005850     MOVE 連印－初回処置料(3)  TO 初回処置料ＷＲ(3).
005860     MOVE 連印－初回処置料(4)  TO 初回処置料ＷＲ(4).
005870     MOVE 連印－初回処置料(5)  TO 初回処置料ＷＲ(5).
005880     MOVE 連印－施療料チェック TO 施療料チェックＷ.
005890     MOVE 連印－整復料チェック TO 整復料チェックＷ.
005900     MOVE 連印－固定料チェック TO 固定料チェックＷ.
005910     MOVE 連印－月(1)         TO 往療月ＷＲ(1).
005920     MOVE 連印－回数(1)       TO 往療回数ＷＲ(1).
005930     MOVE 連印－距離          TO 往療距離ＷＲ.
005940     MOVE 連印－往療料金額(1) TO 往療料金ＷＲ(1).
005950     MOVE 連印－往療加算料    TO 往療加算料ＷＲ.
005960     MOVE 連印－情報提供料    TO 情報提供料ＷＲ.
005970     IF 連印－金属大 = 1
005980         MOVE NC"○"   TO 大チェックＷ
005990     END-IF.
006000     IF 連印－金属中 = 1
006010         MOVE NC"○"   TO 中チェックＷ
006020     END-IF.
006030     IF 連印－金属小 = 1
006040         MOVE NC"○"   TO 小チェックＷ
006050     END-IF.
006060     MOVE 連印－金額   TO 金属副子料ＷＲ.
006070*================================================================*
006080 受診者情報取得 SECTION.
006090*
006100**************************************************
006110* 連結データから受診者情報Ｆより以下の情報を取得 *
006120* ● 施術年 ..... 施術年Ｗに格納                 *
006130* ● 施術月 ..... 施術月Ｗに格納                 *
006140* ● 患者番号.... 患者番号Ｗに格納※ＦＤ連番用   *
006150* ● 患者氏名 ....患者氏名Ｗに格納               *
006160**************************************************
006170     MOVE 施術和暦ＷＲ       TO 受－施術和暦.
006180     MOVE 施術年ＷＲ         TO 受－施術年.
006190     MOVE 施術月ＷＲ         TO 受－施術月.
006200     MOVE 患者コードＷＲ     TO 受－患者コード.
006210     READ 受診者情報Ｆ
006220     INVALID KEY
006230         CONTINUE
006240*            /* ありえない */
006250     NOT INVALID KEY
006260         MOVE 受－施術年       TO 施術年Ｗ
006270         MOVE 受－施術月       TO 施術月Ｗ
006280         MOVE 受－患者番号     TO 患者番号Ｗ
006290         MOVE 受－患者氏名     TO 患者氏名Ｗ
006300     END-READ.
006310*================================================================*
006320 作業ファイル１読込 SECTION.
006330*
006340     READ 作業ファイル１ NEXT
006350     AT END
006360         MOVE "YES" TO 終了フラグ
006370     END-READ.
006380*
006390*================================================================*
006400 印刷処理１ SECTION.
007220*
004310     IF ( オープンフラグ NOT = "YES" )
004320        MOVE "YES" TO オープンフラグ
004330        OPEN I-O  印刷ファイル
004340        PERFORM エラー処理Ｐ
004350     END-IF.
006410*
006420     MOVE "YCH715P"  TO  定義体名Ｐ.
006430     MOVE "GRP001"   TO  項目群名Ｐ.
006440     MOVE SPACE      TO  処理種別Ｐ.
006450     MOVE SPACE      TO  拡張制御Ｐ.
006460     WRITE YCH715P.
006470     PERFORM エラー処理Ｐ.
006480*================================================================*
006490 印刷処理２ SECTION.
007220*
004310     IF ( オープンフラグ NOT = "YES" )
004320        MOVE "YES" TO オープンフラグ
004330        OPEN I-O  印刷ファイル
004340        PERFORM エラー処理Ｐ
004350     END-IF.
006500*
006510     MOVE "YCH715P"  TO  定義体名Ｐ.
006520     MOVE "GRP002"   TO  項目群名Ｐ.
006530     MOVE SPACE      TO  処理種別Ｐ.
006540     MOVE SPACE      TO  拡張制御Ｐ.
006550     WRITE YCH715P.
006560     PERFORM エラー処理Ｐ.
006570*================================================================*
006580 改頁処理  SECTION.
006590
006600     MOVE "YCH715P" TO  定義体名Ｐ.
006610     MOVE "CT"      TO  処理種別Ｐ.
006620     MOVE "PAGE"    TO  拡張制御Ｐ.
006630     MOVE SPACE     TO  項目群名Ｐ.
006640     WRITE YCH715P.
006650     PERFORM エラー処理Ｐ.
006660     MOVE SPACE     TO  拡張制御Ｐ.
006670*
006680*================================================================*
006690 エラー処理Ｐ SECTION.
006700*
006710     IF 通知情報Ｐ NOT = "00"
006720         DISPLAY NC"帳票エラー"              UPON CONS
006730         DISPLAY NC"項目群名Ｐ：" 項目群名Ｐ UPON CONS
006740         DISPLAY NC"通知情報Ｐ：" 通知情報Ｐ UPON CONS
006750         DISPLAY NC"拡張制御Ｐ：" 拡張制御Ｐ UPON CONS
006760         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
006770                                             UPON CONS
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
006780         ACCEPT  キー入力 FROM CONS
006790         PERFORM ファイル閉鎖
006800         MOVE 99 TO PROGRAM-STATUS
006810         EXIT PROGRAM
006820     END-IF.
006830*================================================================*
006840 ファイル閉鎖 SECTION.
006850*
002990     IF ( オープンフラグ = "YES" )
002991         CLOSE 印刷ファイル
003041     END-IF.
006870     CLOSE 元号マスタ      制御情報マスタ   受診者情報Ｆ
                 長期継続者Ｆ    名称マスタ.
006880*================================================================*
006890 終了処理 SECTION.
006900*
006910     PERFORM ファイル閉鎖.
006920*================================================================*
006930 印刷行数退避 SECTION.
006940     CLOSE 受診者情報Ｆ.
006950     PERFORM 遅延処理.
006960     OPEN I-O 受診者情報Ｆ.
006970         MOVE NC"受診" TO ファイル名.
006980         PERFORM オープンチェック.
006990     PERFORM 遅延処理.
007000*
007010     MOVE ZERO            TO 受－施術和暦.
007020     MOVE ZERO            TO 受－施術年.
007030     MOVE ZERO            TO 受－施術月.
007040     MOVE 作１－患者番号  TO 受－患者番号.
007050     MOVE 作１－枝番      TO 受－枝番.
007060*
007070     READ 受診者情報Ｆ
007080     NOT INVALID KEY
007090         MOVE 印刷位置ＣＮＴ TO 受－カルテ裏印刷行数
007100         REWRITE 受－レコード
007110         INVALID KEY
007120             MOVE NC"受Ｆ" TO ファイル名
007130             PERFORM エラー表示
007140         END-REWRITE
007150     END-READ.
007160     PERFORM 遅延処理.
007170     CLOSE 受診者情報Ｆ.
007180     PERFORM 遅延処理.
007190     OPEN INPUT 受診者情報Ｆ.
007200         MOVE NC"受診" TO ファイル名.
007210         PERFORM オープンチェック.
007220*================================================================*
007230 エラー表示 SECTION.
007240*
007250     DISPLAY ファイル名 NC"ファイル書込エラー"   UPON CONS.
007260     DISPLAY NC"状態キー：" 状態キー               UPON CONS.
007270     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
007280     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
000080*-----------------------------------------*
000090     CALL "actcshm"  WITH C LINKAGE.
000100*-----------------------------------------*
007290     ACCEPT  キー入力 FROM CONS.
007300     PERFORM ファイル閉鎖.
007310     EXIT PROGRAM.
007320*================================================================*
035270 長期理由文取得 SECTION.
003034     MOVE  ZERO   TO  カウンタ.
035280*
004502     MOVE 施術和暦ＷＲ   TO 長継－施術和暦
004503     MOVE 施術年ＷＲ     TO 長継－施術年.
004504     MOVE 施術月ＷＲ     TO 長継－施術月.
004505     MOVE 患者番号ＷＲ   TO 長継－患者番号.
004506     MOVE 枝番ＷＲ       TO 長継－枝番.
004507*
004508     READ 長期継続者Ｆ 
004509     INVALID KEY
                CONTINUE
004522     NOT INVALID KEY
004523         IF  (長継－理由文(1)  = ZERO )  AND
004524             (長継－理由文(2)  = ZERO )  AND
004525             (長継－理由文(3)  = ZERO )  AND
004526             (長継－理由文(4)  = ZERO )  AND
004527             (長継－理由文(5)  = ZERO )
                   CONTINUE
004535         ELSE
                   PERFORM 理由文セット
004537         END-IF
004538     END-READ.
035430*================================================================*
004541 理由文セット SECTION.
004542*
004543     PERFORM VARYING 番号カウンタ FROM 1 BY 1
004544             UNTIL   番号カウンタ  > 5 
004548*
004560         IF  長継－理由文(番号カウンタ) NOT = SPACE 
004561             PERFORM 番号転記
004595* 理由文
004598           MOVE 長継－理由文(番号カウンタ)  TO  理由文Ｗ
                 PERFORM 理由文分解４０桁
004631         END-IF
004632**
004637     END-PERFORM.
004647*
005590*================================================================*
005600 番号転記 SECTION.
005601*
005605      EVALUATE 番号カウンタ
005606      WHEN  1
005607          MOVE "①"  TO 長期番号Ｗ
005608      WHEN  2
005609          MOVE "②"  TO 長期番号Ｗ
005610      WHEN  3
005611          MOVE "③"  TO 長期番号Ｗ
005612      WHEN  4
005613          MOVE "④"  TO 長期番号Ｗ
005614      WHEN  5
005615          MOVE "⑤"  TO 長期番号Ｗ
005616      WHEN  OTHER
005617          CONTINUE
005618      END-EVALUATE.
005619*
005705*================================================================*
005706 理由文分解４０桁 SECTION.
005707**********
005708*** ４０桁
005709**********
005716* 合成した理由文を分割する。
005717     INITIALIZE 理由４０桁Ｗ.
005718     MOVE  合成理由文Ｗ  TO  理由４０桁Ｗ.
005945*半角スペースを全角にかえる(189ﾊﾞｲﾄより長いｴﾘｱに半角空白が入っているので)
005946     INSPECT 理由４０桁Ｗ REPLACING ALL 半角空白 BY 全角空白.
005719*
005720     IF  理由４０桁１Ｗ  NOT = SPACE
005721         COMPUTE カウンタ = カウンタ  +  1
005722         MOVE 理由４０桁１Ｗ  TO 長期理由文Ｗ(カウンタ)
005724     END-IF.
005725     IF  理由４０桁２Ｗ  NOT = SPACE
005726         COMPUTE カウンタ = カウンタ  +  1
005727         MOVE 理由４０桁２Ｗ  TO 長期理由文Ｗ(カウンタ)
005729     END-IF.
005730     IF  理由４０桁３Ｗ  NOT = SPACE
005731         COMPUTE カウンタ = カウンタ  +  1
005732         MOVE 理由４０桁３Ｗ  TO 長期理由文Ｗ(カウンタ)
005733     END-IF.
           IF  理由４０桁４Ｗ  NOT = SPACE
               COMPUTE カウンタ = カウンタ  +  1
               MOVE 理由４０桁４Ｗ  TO 長期理由文Ｗ(カウンタ)
           END-IF.
           IF  理由４０桁５Ｗ  NOT = SPACE
               COMPUTE カウンタ = カウンタ  +  1
               MOVE 理由４０桁５Ｗ  TO 長期理由文Ｗ(カウンタ)
           END-IF.
005734*
021810*================================================================*
022380  請求期間取得 SECTION.
022390*
022400     EVALUATE 施術月ＷＲ
022410     WHEN 4
022420     WHEN 6
022430     WHEN 9
022440     WHEN 11
022450         MOVE 30 TO 請求終了日Ｗ
022460     WHEN 2
022470         COMPUTE 対象西暦Ｗ = 和暦終了年Ｗ + 施術年ＷＲ
022480         DIVIDE 4 INTO 対象西暦Ｗ GIVING    商Ｗ
022490                                  REMAINDER 余Ｗ
022500         END-DIVIDE
022510         IF 余Ｗ = ZERO
022520             MOVE 29 TO 請求終了日Ｗ
022530         ELSE
022540             MOVE 28 TO 請求終了日Ｗ
022550         END-IF
022560     WHEN 1
022570     WHEN 3
022580     WHEN 5
022590     WHEN 7
022600     WHEN 8
022610     WHEN 10
022620     WHEN 12
022630         MOVE 31 TO 請求終了日Ｗ
022640     WHEN OTHER
022650         MOVE  NC"未知のエラー発生" TO 連メ－メッセージ
022660         CALL   "MSG001"
022670         CANCEL "MSG001"
022680     END-EVALUATE.
022681*================================================================*
021820 合計金額行セット SECTION.
021830*
021840     MOVE 施術年ＷＲ   TO 金額行年       (段数ＷＲ).
021850     MOVE 施術月ＷＲ   TO 金額行月       (段数ＷＲ).
021860     MOVE NC"年"       TO 金額行年表示   (段数ＷＲ).
021880     MOVE 提出年ＷＲ   TO 提出年         (段数ＷＲ).
021890     MOVE 提出月ＷＲ   TO 提出月         (段数ＷＲ).
021900     MOVE 提出日ＷＲ   TO 提出日         (段数ＷＲ).
021910     MOVE 施術年ＷＲ   TO 請求開始年     (段数ＷＲ)
021920                          請求終了年     (段数ＷＲ).
021930     MOVE 施術月ＷＲ   TO 請求開始月     (段数ＷＲ)
021940                          請求終了月     (段数ＷＲ).
021950     MOVE 1            TO 請求開始日     (段数ＷＲ).
021960     MOVE 請求終了日Ｗ TO 請求終了日     (段数ＷＲ).
021870     MOVE 連印－実日数 TO 実日数         (段数ＷＲ).
021970     MOVE 連印－費用額 TO 費用額         (段数ＷＲ).
021970     MOVE 連印－負担額 TO 負担額         (段数ＷＲ).
021970     MOVE 連印－請求額 TO 請求額         (段数ＷＲ).
022370*================================================================*
007330******************************************************************
007340 END PROGRAM YCH715.
007350******************************************************************
