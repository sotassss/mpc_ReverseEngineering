000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YCB592.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
                【再請求一覧表 印刷】
000100* 請求年月バージョン
000110*         MED = YCB580 YCB592P
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2012-11-15
000140 DATE-COMPILED.          2012-11-15
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000270     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000280                             ORGANIZATION             IS  INDEXED
000290                             ACCESS MODE              IS  DYNAMIC
000300                             RECORD KEY               IS  元−元号区分
000310                             FILE STATUS              IS  状態キー
000320                             LOCK        MODE         IS  AUTOMATIC.
000330     SELECT  制御情報マスタ  ASSIGN      TO        SEIGYOL
000340                             ORGANIZATION             IS  INDEXED
000350                             ACCESS MODE              IS  DYNAMIC
000360                             RECORD KEY               IS  制−制御区分
000370                             FILE STATUS              IS  状態キー
000380                             LOCK        MODE         IS  AUTOMATIC.
000390     SELECT  施術所情報マスタ ASSIGN     TO        SEJOHOL
000400                             ORGANIZATION             IS  INDEXED
000410                             ACCESS MODE              IS  DYNAMIC
000420                             RECORD KEY               IS  施情−施術所番号
000430                             FILE STATUS              IS  状態キー
000440                             LOCK        MODE         IS  AUTOMATIC.
000450     SELECT  保険者マスタ    ASSIGN      TO        HOKENSL
000460                             ORGANIZATION             IS  INDEXED
000470                             ACCESS MODE              IS  DYNAMIC
000480                             RECORD KEY               IS  保−保険種別
000490                                                          保−保険者番号
000500                             ALTERNATE RECORD KEY     IS  保−保険種別
000510                                                          保−保険者名称
000520                                                          保−保険者番号
000530                             FILE STATUS              IS  状態キー
000540                             LOCK        MODE         IS  AUTOMATIC.
000550     SELECT  市町村マスタ    ASSIGN      TO        SITYOSNL
000560                             ORGANIZATION             IS  INDEXED
000570                             ACCESS MODE              IS  DYNAMIC
000580                             RECORD KEY               IS  市−公費種別
000590                                                          市−市町村番号
000600                             ALTERNATE RECORD KEY     IS  市−公費種別
000610                                                          市−市町村名称
000620                                                          市−市町村番号
000630                             FILE STATUS              IS  状態キー
000640                             LOCK        MODE         IS  AUTOMATIC.
000650     SELECT  名称マスタ      ASSIGN      TO        MEISYOL
000660                             ORGANIZATION             IS  INDEXED
000670                             ACCESS MODE              IS  DYNAMIC
000680                             RECORD KEY               IS  名−区分コード
000690                                                          名−名称コード
000700                             FILE STATUS              IS  状態キー
000710                             LOCK        MODE         IS  AUTOMATIC.
000720     SELECT  作業ファイル１    ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W5901L.DAT"
000730                             ORGANIZATION             IS  INDEXED
000740                             ACCESS                   IS  DYNAMIC
000750                             RECORD      KEY          IS  作１−保険種別
000760                                                          作１−保険者番号
000770                                                          作１−施術和暦年月
000780                                                          作１−本人家族区分
000790                                                          作１−被保険者カナ
000800                                                          作１−患者コード
000810                             FILE        STATUS       IS  状態キー
000820                             LOCK        MODE         IS  AUTOMATIC.
000830     SELECT  作業ファイル２  ASSIGN      TO          "C:\MAKISHISYS\YAWOBJ\TEMP\W5902L.DAT"
000840                             ORGANIZATION             IS  INDEXED
000850                             ACCESS                   IS  DYNAMIC
000860                             RECORD      KEY          IS  作２−保険種別
000870                                                          作２−保険者番号
000880                             FILE        STATUS       IS  状態キー
000890                             LOCK        MODE         IS  AUTOMATIC.
000900     SELECT  印刷ファイル    ASSIGN      TO     GS-PRTF001
000910                             SYMBOLIC    DESTINATION  IS "PRT"
000920                             FORMAT                   IS  定義体名Ｐ
000930                             GROUP                    IS  項目群名Ｐ
000940                             PROCESSING  MODE         IS  処理種別Ｐ
000950                             UNIT        CONTROL      IS  拡張制御Ｐ
000960                             FILE        STATUS       IS  通知情報Ｐ.
000970******************************************************************
000980*                      DATA DIVISION                             *
000990******************************************************************
001000 DATA                    DIVISION.
001010 FILE                    SECTION.
001020*                           ［ＲＬ＝  １２８］
001030 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
001040     COPY GENGOU          OF  XFDLIB  JOINING   元   AS  PREFIX.
001050*                           ［ＲＬ＝  ２５６］
001060 FD  制御情報マスタ          BLOCK   CONTAINS   1   RECORDS.
001070     COPY SEIGYO          OF  XFDLIB  JOINING   制   AS  PREFIX.
001080*                           ［ＲＬ＝  ６４０］
001090 FD  施術所情報マスタ    BLOCK   CONTAINS   1   RECORDS.
001100     COPY SEJOHO         OF  XFDLIB  JOINING    施情 AS  PREFIX.
001110*                           ［ＲＬ＝  ３２０］
001120 FD  保険者マスタ        BLOCK   CONTAINS   1   RECORDS.
001130     COPY HOKENS          OF  XFDLIB  JOINING   保   AS  PREFIX.
001140*                           ［ＲＬ＝  ２５６］
001150 FD  市町村マスタ        BLOCK   CONTAINS   1   RECORDS.
001160     COPY SITYOSN         OF  XFDLIB  JOINING   市   AS  PREFIX.
001170*                           ［ＲＬ＝  １２８］
001180 FD  名称マスタ          BLOCK   CONTAINS   1   RECORDS.
001190     COPY MEISYO          OF  XFDLIB  JOINING   名   AS  PREFIX.
001040*                           ［ＲＬ＝  ２５６］
001050 FD  作業ファイル１ RECORD  CONTAINS 256 CHARACTERS.
001220 01  作１−レコード.
001230     03  作１−レコードキー.
001240         05  作１−保険種別                  PIC 9(2).
001250         05  作１−保険者番号                PIC X(10).
001260         05  作１−施術和暦年月.
001270             07  作１−施術和暦              PIC 9.
001280             07  作１−施術年                PIC 9(2).
001290             07  作１−施術月                PIC 9(2).
001300         05  作１−本人家族区分              PIC 9(1).
001310         05  作１−被保険者カナ              PIC X(50).
001320         05  作１−患者コード.
001330             07 作１−患者番号               PIC 9(6).
001340             07 作１−枝番                   PIC X(1).
001350     03  作１−レコードデータ.
001360         05  作１−受診者氏名                PIC X(50).
001370         05  作１−被保険者氏名              PIC X(50).
001380         05  作１−記号                      PIC X(24).
001390         05  作１−番号                      PIC X(30).
001400         05  作１−費用額                    PIC 9(7).
001410         05  作１−負担額                    PIC 9(7).
001420         05  作１−請求額                    PIC 9(7).
001430         05  FILLER                          PIC X(6).
001440*                           ［ＲＬ＝  ６４］
001450 FD  作業ファイル２ RECORD  CONTAINS 64 CHARACTERS.
001460 01  作２−レコード.
001470     03  作２−レコードキー.
001480         05  作２−保険種別                  PIC 9(2).
001490         05  作２−保険者番号                PIC X(10).
001500     03  作２−レコードデータ.
001510         05  作２−費用額                    PIC 9(9).
001520         05  作２−負担額                    PIC 9(9).
001530         05  作２−請求額                    PIC 9(9).
001540         05  FILLER                          PIC X(25).
001550*
001560 FD  印刷ファイル.
001570     COPY YCB592P        OF  XMDLIB.
001580*----------------------------------------------------------------*
001590******************************************************************
001600*                WORKING-STORAGE SECTION                         *
001610******************************************************************
001620 WORKING-STORAGE         SECTION.
001630 01 キー入力                           PIC X     VALUE SPACE.
001640 01 状態キー                           PIC X(2)  VALUE SPACE.
001650 01 終了フラグ                         PIC X(3)  VALUE SPACE.
001660 01 終了フラグ２                       PIC X(3)  VALUE SPACE.
001670 01 書込フラグ                         PIC X(4)  VALUE SPACE.
001680 01 作業フラグ                         PIC X(3)  VALUE SPACE.
001690 01 作業移動キー                       PIC X(4)  VALUE SPACE.
001700 01 終了行フラグ                       PIC X(3)  VALUE SPACE.
001210 01 オープンフラグ                     PIC X(3)   VALUE SPACE.
001710 01 ファイル名                         PIC N(2)  VALUE SPACE.
001720 01 備考Ｗ                             PIC X(20) VALUE SPACE.
001730 01 前和暦Ｗ                           PIC 9(1)  VALUE ZERO.
001740 01 保険種別Ｗ                         PIC 9(2)  VALUE ZERO.
001750 01 保険者番号Ｗ                       PIC X(10) VALUE SPACE.
001760 01 保険者名称Ｗ.
001770    03 保険者名称１Ｗ                  PIC X(16) VALUE SPACE.
001780    03 保険者名称２Ｗ                  PIC X(16) VALUE SPACE.
001790 01 記号Ｗ.
001800    03 記号１Ｗ                        PIC X(12) VALUE SPACE.
001810    03 記号２Ｗ                        PIC X(12) VALUE SPACE.
001820 01 番号Ｗ.
001830    03 番号１Ｗ                        PIC 9(10)  VALUE ZERO.
001840    03 番号２Ｗ                        PIC 9(10)  VALUE ZERO.
001850*
001860 01 行カウンタ                         PIC 9(2)  VALUE ZERO.
001870 01 頁カウンタ                         PIC 9(4)  VALUE ZERO.
001880 01 最大行数                           PIC 9(2)  VALUE ZERO.
001890 01 ヘッダ行数                         PIC 9(2)  VALUE ZERO.
001900 01 移動行数Ｗ                         PIC 9(2)  VALUE ZERO.
001910 01 カレント元号Ｗ                     PIC 9(1)  VALUE ZERO.
001920 01 保険名称Ｗ                         PIC N(2) VALUE SPACE.
001930*
001940 01 小計集計用Ｗ.
001950    03 費用額小計Ｗ                    PIC 9(8)  VALUE ZERO.
001960    03 負担額小計Ｗ                    PIC 9(8)  VALUE ZERO.
001970    03 請求額小計Ｗ                    PIC 9(8)  VALUE ZERO.
001980*
001990 01 総合計集計Ｗ.
002000    03 費用額合計Ｗ                    PIC 9(8) VALUE ZERO.
002010    03 負担額合計Ｗ                    PIC 9(8) VALUE ZERO.
002020    03 請求額合計Ｗ                    PIC 9(8) VALUE ZERO.
002030*
002040 01 請求和暦年月Ｗ.
002050     03 請求和暦Ｗ                     PIC 9(1)  VALUE ZERO.
002060     03 請求年月Ｗ.
002070        05 請求年Ｗ                    PIC 9(2)  VALUE ZERO.
002080        05 請求月Ｗ                    PIC 9(2)  VALUE ZERO.
002090*
002100 01 印刷施術年月Ｗ.
002110    03 印刷施術年Ｗ                    PIC X(2) VALUE SPACE.
002120    03 印刷施術月Ｗ                    PIC X(2) VALUE SPACE.
002130*
002140 01 ヘッダＷ.
002150    03 施術所住所１Ｗ                  PIC X(50) VALUE SPACE.
002160    03 施術所住所２Ｗ                  PIC X(50) VALUE SPACE.
002170    03 接骨院名Ｗ                      PIC X(50) VALUE SPACE.
002180    03 代表者名Ｗ                      PIC X(50) VALUE SPACE.
002190    03 代表者カナＷ                    PIC X(50) VALUE SPACE.
002200    03 国保チェックＷ                  PIC N(3)  VALUE SPACE.
002210    03 老人チェックＷ                  PIC N(3)  VALUE SPACE.
002220    03 県施術ＩＤＷ                    PIC X(12)  VALUE SPACE.
002230    03 施術所電話番号Ｗ                PIC X(15) VALUE SPACE.
002240    03 施術所郵便番号Ｗ.
002250       05 施術所郵便番号１Ｗ           PIC X(3)   VALUE SPACE.
002260       05 施術所郵便番号２Ｗ           PIC X(4)   VALUE SPACE.
002270    03 取引先情報Ｗ.
002280       05 取引先銀行名Ｗ.
002290          07 取引先銀行名１Ｗ          PIC X(14)  VALUE SPACE.
002300          07 取引先銀行名２Ｗ          PIC X(14)  VALUE SPACE.
002310       05 取引先銀行支店名Ｗ.
002320          07 取引先銀行支店名１Ｗ      PIC X(14)  VALUE SPACE.
002330          07 取引先銀行支店名２Ｗ      PIC X(14)  VALUE SPACE.
002340       05 預金種別Ｗ                   PIC 9(1)   VALUE ZERO.
002350       05 口座番号Ｗ                   PIC X(10)  VALUE SPACE.
002360       05 口座名義人Ｗ                 PIC X(66)  VALUE SPACE.
002370       05 口座名義人ＷＰ               PIC X(40)  VALUE SPACE.
002380       05 口座名義人カナＷ             PIC X(40)  VALUE SPACE.
002390       05 銀行名支店名Ｗ               PIC X(60)  VALUE SPACE.
002400       05 預金種別コメントＷ           PIC N(2)   VALUE SPACE.
002410*
002420 01 請求カレント和暦年月Ｗ.
002430     03 請求カレント和暦Ｗ             PIC 9(1)  VALUE ZERO.
002440     03 請求カレント年月Ｗ.
002450        05 請求カレント年Ｗ            PIC 9(2)  VALUE ZERO.
002460        05 請求カレント月Ｗ            PIC 9(2)  VALUE ZERO.
002470*
002480 01 印刷制御.
002490     03 定義体名Ｐ                     PIC X(8) VALUE SPACE.
002500     03 項目群名Ｐ                     PIC X(8) VALUE SPACE.
002510     03 処理種別Ｐ                     PIC X(2) VALUE SPACE.
002520     03 拡張制御Ｐ.
002530         05 端末制御Ｐ.
002540             07 移動方向Ｐ             PIC X(1).
002550             07 移動行数Ｐ             PIC 9(3).
002560         05 詳細制御Ｐ                 PIC X(2).
002570     03 通知情報Ｐ                     PIC X(2) VALUE SPACE.
002580     03 ユニット名Ｐ                   PIC X(8) VALUE SPACE.
002590*
002600 01 計算機西暦年Ｗ                     PIC 9(2).
002610* 日付ＷＯＲＫ
002620 01 和暦終了年Ｗ                       PIC 9(4).
002630 01 計算機西暦.
002640    03 計算機西暦年                    PIC 9(4).
002650    03 計算機西暦月日                  PIC 9(4).
002660 01 計算機西暦Ｒ REDEFINES 計算機西暦.
002670    03 計算機世紀                      PIC 9(2).
002680    03 計算機日付                      PIC 9(6).
002690    03 計算機日付Ｒ REDEFINES 計算機日付.
002700       05 計算機年月                   PIC 9(4).
002710       05 計算機年月Ｒ REDEFINES 計算機年月.
002720         07 計算機年                   PIC 9(2).
002730         07 計算機月                   PIC 9(2).
002740       05 計算機日                     PIC 9(2).
002750*
002760******************************************************************
002770*                          連結項目                              *
002780******************************************************************
002790*
002700 01 連入−画面情報ＹＣＢ５８０ IS EXTERNAL.
002710    03 連入−施術和暦年月.
002720       05 連入−施術和暦               PIC 9.
002730       05 連入−施術年月.
002740         07 連入−施術年               PIC 9(2).
002750         07 連入−施術月               PIC 9(2).
          03 連入−プレビュー区分            PIC 9.
002860**
002870 01 連メ−キー IS EXTERNAL.
002880    03  連メ−メッセージ               PIC N(20).
002560*
000540************************************
000550* プリンタファイル作成用           *
000560************************************
000570 01 Ｈ連ＰＲＴＦ−作成データ IS EXTERNAL.
000580     03 Ｈ連ＰＲＴＦ−ファイル名           PIC X(8).
000590     03 Ｈ連ＰＲＴＦ−プレビュー区分       PIC 9.
000600     03 Ｈ連ＰＲＴＦ−帳票プログラム名     PIC X(8).
000610     03 Ｈ連ＰＲＴＦ−オーバレイ名         PIC X(8).
002271*
002890******************************************************************
002900*                      PROCEDURE  DIVISION                       *
002910******************************************************************
002920 PROCEDURE               DIVISION.
002930************
002940*           *
002950* 初期処理   *
002960*           *
002970************
002560     MOVE SPACE TO オープンフラグ.
002570     PERFORM プリンタファイル作成.
002980     PERFORM 初期化.
002990************
003000*           *
003010* 主処理     *
003020*           *
003030************
003040     PERFORM 印刷処理.
003050************
003060*           *
003070* 終了処理   *
003080*           *
003090************
003100     PERFORM 終了処理.
003110     EXIT PROGRAM.
003120*
003130*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002860*================================================================*
002870 プリンタファイル作成 SECTION.
002880*================================================================*
002890*   / 初期化 /
002900     MOVE SPACE TO Ｈ連ＰＲＴＦ−作成データ.
002910     INITIALIZE Ｈ連ＰＲＴＦ−作成データ.
002920*
002930*
002940*--↓↓ 変更箇所 ↓↓--------------------------------------*
002970*   使用するプリンタファイル名セット
      *   印字調整有り
002971     MOVE "PRTF001"             TO Ｈ連ＰＲＴＦ−ファイル名.
002972*
002973*   使用する帳票プログラム名セット
002974     MOVE "YCB592"              TO Ｈ連ＰＲＴＦ−帳票プログラム名.
002975*
002976*--↑↑-----------------------------------------------------*
002980*
002990*   / プレビュー区分セット /
003000     MOVE 連入−プレビュー区分 TO Ｈ連ＰＲＴＦ−プレビュー区分.
003010*
003020     CALL   "CRTPRTF".
003030     CANCEL "CRTPRTF".
003040*
003140*================================================================*
003150 初期化 SECTION.
003160*
003700     OPEN INPUT   元号マスタ
003710         MOVE NC"元号" TO ファイル名.
003720         PERFORM オープンチェック.
003730     OPEN INPUT   制御情報マスタ
003740         MOVE NC"制御情報" TO ファイル名.
003750         PERFORM オープンチェック.
003760     OPEN INPUT 施術所情報マスタ.
003770             MOVE NC"施情" TO ファイル名.
003780             PERFORM オープンチェック.
003790     OPEN INPUT   保険者マスタ
003800         MOVE NC"保険" TO ファイル名.
003810         PERFORM オープンチェック.
003820     OPEN INPUT   市町村マスタ
003830         MOVE NC"市町" TO ファイル名.
003840         PERFORM オープンチェック.
003850     OPEN INPUT   名称マスタ
003860         MOVE NC"名称" TO ファイル名.
003870         PERFORM オープンチェック.
003880     OPEN INPUT   作業ファイル１.
003890         MOVE NC"作１" TO ファイル名.
003900         PERFORM オープンチェック.
003910     OPEN INPUT   作業ファイル２.
003920         MOVE NC"作２" TO ファイル名.
003930         PERFORM オープンチェック.
003960*================================================================*
003970 オープンチェック SECTION.
003980*
003990     IF 状態キー  NOT =  "00"
004000         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
004010         DISPLAY NC"状態キー：" 状態キー         UPON CONS
004020         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004030                                                 UPON CONS
004380*-----------------------------------------*
004390         CALL "actcshm"  WITH C LINKAGE
004400*-----------------------------------------*
004040         ACCEPT  キー入力 FROM CONS
004050         PERFORM ファイル閉鎖
004060         EXIT PROGRAM.
004070*================================================================*
004080 ファイル閉鎖 SECTION.
004090*
003570     IF ( オープンフラグ = "YES" )
003580         CLOSE 印刷ファイル
003590     ELSE
003600         MOVE  NC"　　データが０件です。確認して下さい。" TO 連メ−メッセージ
003610         CALL   "MSG001"
003620         CANCEL "MSG001"
003630     END-IF.
003640*
004100     CLOSE 元号マスタ     制御情報マスタ 施術所情報マスタ
004110           保険者マスタ   名称マスタ     市町村マスタ
004120           作業ファイル１ 作業ファイル２.
004130*================================================================*
004140 終了処理 SECTION.
004150*
004160     PERFORM ファイル閉鎖.
004170*================================================================*
004180 エラー表示 SECTION.
004190*
004200     DISPLAY NC"状態キー" 状態キー  UPON CONS.
004210     DISPLAY NC"５ファイル書込エラー：" ファイル名   UPON CONS.
004220     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
004230     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
000080*-----------------------------------------*
000090     CALL "actcshm"  WITH C LINKAGE.
000100*-----------------------------------------*
004240     ACCEPT  キー入力 FROM CONS.
004250     PERFORM ファイル閉鎖.
004260     EXIT PROGRAM.
004270*================================================================*
004280 印刷処理 SECTION.
004290*
004300     MOVE 32    TO 最大行数.
004310     MOVE 4     TO ヘッダ行数.
004320*
004330     MOVE SPACE TO 終了フラグ.
004340     MOVE ZERO  TO 行カウンタ.
004350     MOVE 1     TO 頁カウンタ.
004360     PERFORM 小計値初期化.
004370     INITIALIZE 総合計集計Ｗ.
004380     PERFORM 施術所情報マスタ読込.
004390     PERFORM 作業ファイル１読込.
004400     IF  終了フラグ = "YES"
004440         PERFORM ファイル閉鎖
004450         MOVE 99 TO PROGRAM-STATUS
004460         EXIT PROGRAM
004470     END-IF.
004480*
004490     MOVE 作１−保険種別   TO 保険種別Ｗ.
004500     MOVE 作１−保険者番号 TO 保険者番号Ｗ.
004510     PERFORM ヘッダ印刷処理.
004520*
004530     PERFORM UNTIL 終了フラグ = "YES"
004540         IF ( 行カウンタ >= 最大行数 )
004550             IF  (作１−保険者番号 NOT = 保険者番号Ｗ ) 
004560                 PERFORM フッタ１印刷処理
004570                 PERFORM 合計額累計
004580                 PERFORM 小計値初期化
004590             END-IF
004600             PERFORM 改頁処理
004610             COMPUTE 頁カウンタ = 頁カウンタ + 1
004620             PERFORM ヘッダ印刷処理
004630             PERFORM 明細１印刷処理
004640             MOVE 作１−保険種別   TO 保険種別Ｗ
004650             MOVE 作１−保険者番号 TO 保険者番号Ｗ
004660             PERFORM 作業ファイル１読込
004670         ELSE
004680             IF  (作１−保険者番号 NOT = 保険者番号Ｗ )
004690                 PERFORM フッタ１印刷処理
004700                 PERFORM 合計額累計
004710                 PERFORM 小計値初期化
004720                 PERFORM 明細１印刷処理
004730             ELSE
004740*
004750                 PERFORM 明細１印刷処理
004760             END-IF
004770             MOVE 作１−保険種別   TO 保険種別Ｗ
004780             MOVE 作１−保険者番号 TO 保険者番号Ｗ
004790             PERFORM 作業ファイル１読込
004800         END-IF
004810     END-PERFORM.
004820*
004830     PERFORM フッタ１印刷処理.
004840     PERFORM 合計額累計.
004850     PERFORM フッタ２印刷処理.
004860*
004870*================================================================*
004880 施術所情報マスタ読込 SECTION.
004890*
004900     MOVE ZERO TO 施情−施術所番号.
004910     READ 施術所情報マスタ
004920     INVALID KEY
004930         MOVE NC"施情" TO ファイル名
004940         PERFORM エラー表示
004950         PERFORM ファイル閉鎖
004960         MOVE 99 TO PROGRAM-STATUS
004970         EXIT PROGRAM
004980     NOT INVALID KEY
004990*         MOVE 施情−新柔整師番号     TO 登録番号コードＷ
005000         MOVE 施情−郵便番号１       TO 施術所郵便番号１Ｗ
005010         MOVE 施情−郵便番号２       TO 施術所郵便番号２Ｗ
005020         MOVE 施情−住所１           TO 施術所住所１Ｗ
005030         MOVE 施情−住所２           TO 施術所住所２Ｗ
005040         MOVE 施情−接骨院名         TO 接骨院名Ｗ
005050         MOVE 施情−代表者カナ       TO 代表者カナＷ
005060         MOVE 施情−代表者名         TO 代表者名Ｗ
005070         MOVE 施情−電話番号         TO 施術所電話番号Ｗ
005360*
005370     END-READ.
005380*================================================================*
005390 作業ファイル１読込 SECTION.
005400*
005410     READ 作業ファイル１ NEXT
005420     AT END
005430         MOVE "YES" TO 終了フラグ
005440     END-READ.
005450*================================================================*
005460 作業ファイル２読込 SECTION.
005470*
005480     READ 作業ファイル２ NEXT
005490     AT END
005500         MOVE "YES" TO 終了フラグ２
005510     END-READ.
005520*================================================================*
005530 改頁処理  SECTION.
005540*
005550     MOVE "YCB592P" TO  定義体名Ｐ.
005560     MOVE "CT"      TO  処理種別Ｐ.
005570     MOVE "PAGE"    TO  拡張制御Ｐ.
005580     MOVE SPACE     TO  項目群名Ｐ.
005590     WRITE YCB592P.
005600     PERFORM エラー処理Ｐ.
005610     MOVE SPACE     TO  拡張制御Ｐ.
005620*
005630     CLOSE  印刷ファイル.
006060     MOVE SPACE     TO オープンフラグ.
005660*
005670*================================================================*
005680 ヘッダ印刷処理 SECTION.
005690*
005700     PERFORM ヘッダセット.
005710     PERFORM ヘッダ印字.
005720*================================================================*
005730 ヘッダセット SECTION.
005740*
005750     MOVE SPACE  TO YCB592P.
005760     INITIALIZE YCB592P.
005770*
005780* 対象請求和暦を取得
005790     MOVE 連入−施術和暦  TO 元−元号区分.
005800     READ 元号マスタ
005810     INVALID KEY
005820         MOVE SPACE         TO 請求和暦
005830     NOT INVALID KEY
005840         MOVE 元−元号名称  TO 請求和暦
005850     END-READ.
005860     MOVE 連入−施術年      TO 請求年.
005870     MOVE 連入−施術月      TO 請求月.
005880*
005890     MOVE 頁カウンタ        TO 頁.
005900     MOVE 施術所郵便番号１Ｗ      TO 郵便番号１.
005910     MOVE 施術所郵便番号２Ｗ      TO 郵便番号２.
005920     MOVE 施術所住所１Ｗ    TO 住所１.
005930     MOVE 施術所住所２Ｗ    TO 住所２.
005940     MOVE 接骨院名Ｗ        TO 施術所名.
005950     MOVE 代表者名Ｗ        TO 代表者名.
005960     MOVE 施術所電話番号Ｗ        TO 電話番号.
005970*
005980*================================================================*
005990 ヘッダ印字  SECTION.
006000*
006050     IF ( オープンフラグ NOT = "YES" )
006060        MOVE "YES" TO オープンフラグ
006070        OPEN I-O  印刷ファイル
006080        PERFORM エラー処理Ｐ
006090     END-IF.
006010     MOVE "YCB592P" TO  定義体名Ｐ.
006020     MOVE SPACE     TO  処理種別Ｐ.
006030     MOVE "HEAD01"  TO  項目群名Ｐ.
006040     WRITE YCB592P.
006050     PERFORM エラー処理Ｐ.
006060     INITIALIZE YCB592P.
006070     MOVE ヘッダ行数 TO 行カウンタ.
006080*================================================================*
006090 明細１印刷処理 SECTION.
006100*
006110     PERFORM 明細１セット.
006120     PERFORM 明細１印字.
006130*================================================================*
006140 明細１セット SECTION.
006150*
006160     MOVE SPACE  TO YCB592P.
006170     INITIALIZE YCB592P.
006180*
006190     MOVE 作１−施術年      TO 施術年.
006200     MOVE 作１−施術月      TO 施術月.
006210     MOVE 作１−保険者番号  TO 保険者番号.
006220*
006230** 保険者名称 (老人・助成は市町村マスタ検索)
006240     IF  (作１−保険種別 = 05)  OR  ((作１−保険種別 >= 50) AND (作１−保険種別 <= 60))
006250         MOVE 作１−保険種別     TO 市−公費種別
006260         MOVE 作１−保険者番号   TO 市−市町村番号
006270         READ 市町村マスタ
006280         INVALID KEY
006290             MOVE SPACE          TO 保険者名称Ｗ
006300         NOT INVALID KEY
006310             MOVE 市−市町村名称 TO 保険者名称Ｗ
006320         END-READ
006330     ELSE
006340         MOVE 作１−保険種別     TO 保−保険種別
006350         MOVE 作１−保険者番号   TO 保−保険者番号
006360         READ 保険者マスタ
006370         INVALID KEY
006380             MOVE SPACE          TO 保険者名称Ｗ
006390         NOT INVALID KEY
006400             MOVE 保−保険者名称 TO 保険者名称Ｗ
006410         END-READ
006420     END-IF.
006430     MOVE 保険者名称１Ｗ         TO 保険者名称１.
006440     MOVE 保険者名称２Ｗ         TO 保険者名称２.
006450*
006460     MOVE 作１−記号             TO 記号Ｗ.
006470     MOVE 記号１Ｗ               TO 記号１.
006480     MOVE 記号２Ｗ               TO 記号２.
006490     MOVE 作１−番号             TO 番号Ｗ.
006500     MOVE 番号１Ｗ               TO 番号１.
006510     MOVE 番号２Ｗ               TO 番号２.
006520     MOVE 作１−被保険者氏名     TO 被保険者氏名.
006530     MOVE 作１−受診者氏名       TO 患者氏名.
006540     MOVE 作１−費用額           TO 費用額.
006550*     MOVE 作２−負担額           TO 負担額.
006560     MOVE 作１−請求額           TO 請求額.
006570*
006580*================================================================*
006590 明細１印字  SECTION.
006600*
006610     MOVE "YCB592P"  TO  定義体名Ｐ.
006620     MOVE SPACE      TO  処理種別Ｐ.
006630     MOVE "GRP001"  TO  項目群名Ｐ.
006640     WRITE YCB592P.
006650     PERFORM エラー処理Ｐ.
006660     INITIALIZE YCB592P.
006670     COMPUTE 行カウンタ = 行カウンタ + 1.
006680*================================================================*
006690 フッタ１印刷処理 SECTION.
006700*
006710     PERFORM フッタ１セット.
006720     PERFORM フッタ１印字.
006730*================================================================*
006740 フッタ１セット SECTION.
006750*
006760     MOVE SPACE  TO YCB592P.
006770     INITIALIZE YCB592P.
006780*
006790     MOVE 保険種別Ｗ     TO 作２−保険種別.
006800     MOVE 保険者番号Ｗ   TO 作２−保険者番号.
006810     READ 作業ファイル２
006820     NOT INVALID KEY
006830         MOVE 作２−費用額   TO 費用額小計Ｗ
006840*         MOVE 作２−負担額   TO 負担額小計Ｗ
006850         MOVE 作２−請求額   TO 請求額小計Ｗ
006860     END-READ.
006870     MOVE 費用額小計Ｗ   TO 費用額小計.
006880*     MOVE 負担額小計Ｗ   TO 負担額小計.
006890     MOVE 請求額小計Ｗ   TO 請求額小計.
006900*
006910*================================================================*
006920 フッタ１印字 SECTION.
006930*
006940     MOVE "YCB592P"  TO  定義体名Ｐ.
006950     MOVE SPACE     TO  処理種別Ｐ.
006960     MOVE "FOOT01"   TO  項目群名Ｐ.
006970     WRITE YCB592P.
006980     PERFORM エラー処理Ｐ.
006990     INITIALIZE YCB592P.
007000     COMPUTE 行カウンタ = 行カウンタ + 1.
007010*================================================================*
007020 フッタ２印刷処理 SECTION.
007030*
007040     PERFORM フッタ２セット.
007050     PERFORM フッタ２印字.
007060*================================================================*
007070 フッタ２セット SECTION.
007080*
007090     MOVE SPACE  TO YCB592P.
007100     INITIALIZE YCB592P.
007110* 総合計
007120     MOVE 費用額合計Ｗ     TO 費用額合計.
007130*     MOVE 負担額合計Ｗ    TO 負担額合計.
007140     MOVE 請求額合計Ｗ     TO 請求額合計.
007150*
007160*================================================================*
007170 フッタ２印字 SECTION.
007180*
007190     MOVE "YCB592P"  TO  定義体名Ｐ.
007200     MOVE SPACE     TO  処理種別Ｐ.
007210     MOVE "FOOT02"   TO  項目群名Ｐ.
007220     WRITE YCB592P.
007230     PERFORM エラー処理Ｐ.
007240     INITIALIZE YCB592P.
007250*================================================================*
007260 合計額累計 SECTION.
007270*
007280     COMPUTE 費用額合計Ｗ   = 費用額合計Ｗ + 費用額小計Ｗ.
007290*     COMPUTE 負担額合計Ｗ   = 負担額合計Ｗ + 負担額小計Ｗ.
007300     COMPUTE 請求額合計Ｗ   = 請求額合計Ｗ + 請求額小計Ｗ.
007310*
007320*================================================================*
007330 小計値初期化 SECTION.
007340*
007350     MOVE ZERO  TO 費用額小計Ｗ.
007360     MOVE ZERO  TO 負担額小計Ｗ.
007370     MOVE ZERO  TO 請求額小計Ｗ.
007380*================================================================*
007390 エラー処理Ｐ SECTION.
007400*
007410     IF 通知情報Ｐ NOT = "00"
007420         DISPLAY NC"帳票エラー"              UPON CONS
007430         DISPLAY NC"項目群名Ｐ：" 項目群名Ｐ UPON CONS
007440         DISPLAY NC"通知情報Ｐ：" 通知情報Ｐ UPON CONS
007450         DISPLAY NC"拡張制御Ｐ：" 拡張制御Ｐ UPON CONS
007460         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
007470                                             UPON CONS
004380*-----------------------------------------*
004390         CALL "actcshm"  WITH C LINKAGE
004400*-----------------------------------------*
007480         ACCEPT  キー入力 FROM CONS
007490         PERFORM ファイル閉鎖
007500         EXIT PROGRAM
007510     END-IF.
007520*================================================================*
007530******************************************************************
007540 END PROGRAM YCB592.
007550******************************************************************
