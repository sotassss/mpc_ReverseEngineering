000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YCB591.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
                【再請求一覧表 データ作成】
000100* 請求年月バージョン
000110*         MED = YCB580 YCB592P
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2012-11-15
000140 DATE-COMPILED.          2012-11-15
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000270     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000280                             ORGANIZATION             IS  INDEXED
000290                             ACCESS MODE              IS  DYNAMIC
000300                             RECORD KEY               IS 受−施術和暦年月
000310                                                          受−患者コード
000320                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000330                                                          受−患者カナ
000340                                                          受−患者コード
000350                             ALTERNATE RECORD KEY     IS  受−患者コード
000360                                                         受−施術和暦年月
000370                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000380                                                          受−保険種別
000390                                                          受−保険者番号
000400                                                          受−患者コード
000410                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000420                                                          受−公費種別
000430                                                     受−費用負担者番号
000440                                                          受−患者コード
000450                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000460                                                          受−助成種別
000470                                                  受−費用負担者番号助成
000480                                                          受−患者コード
000490                             ALTERNATE RECORD KEY  IS 受−請求和暦年月
000500                                                      受−施術和暦年月
000510                                                      受−患者コード
000520                             FILE STATUS              IS  状態キー
000530                             LOCK        MODE         IS  AUTOMATIC.
000130     SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
000140                             ORGANIZATION             IS  INDEXED
000150                             ACCESS MODE              IS  DYNAMIC
000160                             RECORD KEY               IS  レセ−施術和暦年月
000170                                                          レセ−患者コード
000180                                                          レセ−レセ種別
000190                             ALTERNATE RECORD KEY     IS  レセ−患者コード
000200                                                          レセ−施術和暦年月
000210                                                          レセ−レセ種別
000220                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000230                                                          レセ−施術和暦年月
000240                                                          レセ−患者コード
000250                                                          レセ−レセ種別
000260                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000270                                                          レセ−レセ種別
000280                                                          レセ−請求保険者番号
000290                                                          レセ−患者コード
000300                                                          レセ−施術和暦年月
000310                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000320                                                          レセ−請求保険者番号
000330                                                          レセ−患者コード
000340                                                          レセ−レセ種別
000350                                                          レセ−施術和暦年月
000360                             FILE STATUS              IS  状態キー
000370                             LOCK        MODE         IS  AUTOMATIC.
000241     SELECT  生保情報Ｆ      ASSIGN      TO        SEIHOJL
000242                             ORGANIZATION             IS INDEXED
000243                             ACCESS MODE              IS DYNAMIC
000244                             RECORD KEY               IS 生保−施術和暦年月
000245                                                         生保−患者コード
000255                             ALTERNATE RECORD KEY     IS 生保−患者コード
000265                                                         生保−施術和暦年月
000277                             FILE STATUS              IS 状態キー
000278                             LOCK        MODE         IS AUTOMATIC.
000241     SELECT  労災情報Ｆ      ASSIGN      TO        ROUSAIJL
000242                             ORGANIZATION             IS INDEXED
000243                             ACCESS MODE              IS DYNAMIC
000244                             RECORD KEY               IS 労災−施術和暦年月
000245                                                         労災−患者コード
000255                             ALTERNATE RECORD KEY     IS 労災−患者コード
000265                                                         労災−施術和暦年月
000277                             FILE STATUS              IS 状態キー
000278                             LOCK        MODE         IS AUTOMATIC.
000720     SELECT  作業ファイル１    ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W5901L.DAT"
000730                             ORGANIZATION             IS  INDEXED
000740                             ACCESS                   IS  DYNAMIC
000750                             RECORD      KEY          IS  作１−保険種別
000760                                                          作１−保険者番号
000770                                                          作１−施術和暦年月
000780                                                          作１−本人家族区分
000790                                                          作１−被保険者カナ
000800                                                          作１−患者コード
000810                             FILE        STATUS       IS  状態キー
000820                             LOCK        MODE         IS  AUTOMATIC.
000830     SELECT  作業ファイル２  ASSIGN      TO          "C:\MAKISHISYS\YAWOBJ\TEMP\W5902L.DAT"
000840                             ORGANIZATION             IS  INDEXED
000850                             ACCESS                   IS  DYNAMIC
000860                             RECORD      KEY          IS  作２−保険種別
000870                                                          作２−保険者番号
000880                             FILE        STATUS       IS  状態キー
000890                             LOCK        MODE         IS  AUTOMATIC.
000900******************************************************************
000910*                      DATA DIVISION                             *
000920******************************************************************
000930 DATA                    DIVISION.
000940 FILE                    SECTION.
000950*                           ［ＲＬ＝  ３２０］
000960 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
000970     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
      *                          ［ＲＬ＝  １５３６］
       FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS.
           COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
001080* 
000280 FD  生保情報Ｆ          BLOCK   CONTAINS   1   RECORDS.
000281     COPY SEIHOJ          OF  XFDLIB  JOINING   生保   AS  PREFIX.
001700*
000280 FD  労災情報Ｆ          BLOCK   CONTAINS   1   RECORDS.
000281     COPY ROUSAIJ         OF  XFDLIB  JOINING   労災   AS  PREFIX.
001040*                           ［ＲＬ＝  ２５６］
001050 FD  作業ファイル１ RECORD  CONTAINS 256 CHARACTERS.
001060 01  作１−レコード.
001070     03  作１−レコードキー.
001080         05  作１−保険種別                  PIC 9(2).
001090         05  作１−保険者番号                PIC X(10).
001100         05  作１−施術和暦年月.
001110             07  作１−施術和暦              PIC 9.
001120             07  作１−施術年                PIC 9(2).
001130             07  作１−施術月                PIC 9(2).
001140         05  作１−本人家族区分              PIC 9(1).
001150         05  作１−被保険者カナ              PIC X(50).
001160         05  作１−患者コード.
001170             07 作１−患者番号               PIC 9(6).
001180             07 作１−枝番                   PIC X(1).
001190     03  作１−レコードデータ.
001200         05  作１−受診者氏名                PIC X(50).
001210         05  作１−被保険者氏名              PIC X(50).
001220         05  作１−記号                      PIC X(24).
001230         05  作１−番号                      PIC X(30).
001240         05  作１−費用額                    PIC 9(7).
001250         05  作１−負担額                    PIC 9(7).
001260         05  作１−請求額                    PIC 9(7).
001270         05  FILLER                          PIC X(6).
001280*                           ［ＲＬ＝  ６４］
001290 FD  作業ファイル２ RECORD  CONTAINS 64 CHARACTERS.
001300 01  作２−レコード.
001310     03  作２−レコードキー.
001320         05  作２−保険種別                  PIC 9(2).
001330         05  作２−保険者番号                PIC X(10).
001340     03  作２−レコードデータ.
001350         05  作２−費用額                    PIC 9(9).
001360         05  作２−負担額                    PIC 9(9).
001370         05  作２−請求額                    PIC 9(9).
001380         05  FILLER                          PIC X(25).
001390*----------------------------------------------------------------*
001400******************************************************************
001410*                WORKING-STORAGE SECTION                         *
001420******************************************************************
001430 WORKING-STORAGE         SECTION.
001440 01 キー入力                           PIC X    VALUE SPACE.
001450 01 状態キー                           PIC X(2) VALUE SPACE.
001460 01 終了フラグ                         PIC X(3) VALUE SPACE.
001470 01 終了フラグ２                       PIC X(3) VALUE SPACE.
001480 01 実行キーＷ                         PIC X(3)  VALUE SPACE.
001490 01 施術記録有Ｗ                       PIC X(3)  VALUE SPACE.
001500*
001510 01 保険種別ＷＲ                       PIC 9(2) VALUE ZERO.
001520 01 保険者番号ＷＲ                     PIC X(10) VALUE SPACE.
001530 01 請求和暦年月ＷＲ.
001540    03 請求和暦ＷＲ                    PIC 9(1) VALUE ZERO.
001550    03 請求年ＷＲ                      PIC 9(2) VALUE ZERO.
001560    03 請求月ＷＲ                      PIC 9(2) VALUE ZERO.
001570 01 処理移動フラグ                     PIC X(4) VALUE SPACE.
001580 01 ファイル名                         PIC N(10) VALUE SPACE.
001590*
001600*/ 集計用.
001610 01 請求和暦年月Ｗ.
001620    03 請求和暦Ｗ                      PIC 9(1)  VALUE ZERO.
001630    03 請求年Ｗ                        PIC 9(2)  VALUE ZERO.
001640    03 請求月Ｗ                        PIC 9(2)  VALUE ZERO.
001650 01 保険種別Ｗ                         PIC 9(2)  VALUE ZERO.
001660 01 保険者番号Ｗ                       PIC X(10) VALUE SPACE.
001670 01 集計作業用Ｗ.
001680    03 費用額Ｗ                        PIC 9(9)  VALUE ZERO.
001690    03 負担額Ｗ                        PIC 9(9)  VALUE ZERO.
001700    03 請求額Ｗ                        PIC 9(9)  VALUE ZERO.
001710*
       01 複合プログラム名Ｗ     PIC X(8) VALUE "MOJI2".
      *
001720******************************************************************
001730*                          連結項目                              *
001740******************************************************************
001750*
002700 01 連入−画面情報ＹＣＢ５８０ IS EXTERNAL.
002710    03 連入−施術和暦年月.
002720       05 連入−施術和暦                PIC 9.
002730       05 連入−施術年月.
002740         07 連入−施術年                PIC 9(2).
002750         07 連入−施術月                PIC 9(2).
          03 連入−プレビュー区分            PIC 9.
      *
      * 暗号複合用
       01 連暗号複合−暗号情報 IS EXTERNAL.
          03 連暗号複合−入力情報.
             05 連暗号複合−記号               PIC X(24).
             05 連暗号複合−番号               PIC X(30).
             05 連暗号複合−暗号化項目.
               07 連暗号複合−暗号患者番号     PIC X(6).
               07 連暗号複合−暗号判定記号     PIC X.
               07 連暗号複合−暗号判定番号     PIC X.
               07 連暗号複合−暗号記号         PIC X(24).
               07 連暗号複合−暗号番号         PIC X(30).
          03 連暗号複合−出力情報.
             05 連暗号複合−複合した記号       PIC X(24).
             05 連暗号複合−複合した番号       PIC X(30).
      * 
002140******************************************************************
002150*                      PROCEDURE  DIVISION                       *
002160******************************************************************
002170 PROCEDURE               DIVISION.
002180************
002190*           *
002200* 初期処理   *
002210*           *
002220************
002230     PERFORM ファイルオープン.
002240     PERFORM 連結項目待避.
002250************
002260*           *
002270* 主処理     *
002280*           *
002290************
002300     PERFORM 作業ファイル１作成.
002310     PERFORM 作業ファイル２作成.
002320************
002330*           *
002340* 終了処理   *
002350*           *
002360************
002370     PERFORM 終了処理.
002380     MOVE ZERO TO PROGRAM-STATUS.
002390     EXIT PROGRAM.
002400*
002410*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002420*================================================================*
002430 連結項目待避 SECTION.
002440*
002450* 連結項目の待避
002460     MOVE 連入−施術和暦      TO 請求和暦ＷＲ.
002470     MOVE 連入−施術年        TO 請求年ＷＲ.
002480     MOVE 連入−施術月        TO 請求月ＷＲ.
002490*================================================================*
002500 ファイルオープン SECTION.
002510*
002520     OPEN INPUT 受診者情報Ｆ.
002530         MOVE NC"受診者情報Ｆ" TO ファイル名.
002540         PERFORM オープンチェック.
006630     OPEN INPUT レセプトＦ.
006640             MOVE NC"レセ" TO ファイル名.
006650             PERFORM オープンチェック.
006630     OPEN INPUT 生保情報Ｆ.
006640         MOVE NC"生保" TO ファイル名.
006650         PERFORM オープンチェック.
007900     OPEN INPUT 労災情報Ｆ.
007910         MOVE NC"労災情報Ｆ" TO ファイル名.
007920         PERFORM オープンチェック.
002610*================================================================*
002620 オープンチェック SECTION.
002630*
002640     IF 状態キー  NOT =  "00"
002650         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
002660         DISPLAY NC"状態キー：" 状態キー         UPON CONS
002670         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
002680                                                 UPON CONS
004380*-----------------------------------------*
004390         CALL "actcshm"  WITH C LINKAGE
004400*-----------------------------------------*
002690         ACCEPT  キー入力 FROM CONS
002700         PERFORM ファイル閉鎖
002710         MOVE 99 TO PROGRAM-STATUS
002720         EXIT PROGRAM.
002730*================================================================*
002740 作業ファイル１作成 SECTION.
002750*
002760     OPEN OUTPUT 作業ファイル１.
002770         MOVE NC"作業" TO ファイル名.
002780         PERFORM オープンチェック.
002790*
004760     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
004770     MOVE 請求年ＷＲ    TO レセ−請求年.
004780     MOVE 請求月ＷＲ    TO レセ−請求月.
004790     MOVE ZERO          TO レセ−施術和暦.
004800     MOVE ZERO          TO レセ−施術年.
004810     MOVE ZERO          TO レセ−施術月.
004820     MOVE ZERO          TO レセ−患者番号.
004830     MOVE LOW-VALUE     TO レセ−枝番.
013730     MOVE ZERO          TO レセ−レセ種別.
004840     START レセプトＦ   KEY IS >= レセ−請求和暦年月
004850                                  レセ−施術和暦年月
004860                                  レセ−患者コード
000250                                  レセ−レセ種別
002910     END-START.
002920     IF 状態キー = "00"
002930         MOVE SPACE  TO 終了フラグ
002940         PERFORM レセプトＦ読込
002950         PERFORM UNTIL ( 終了フラグ = "YES" ) OR
004920                       ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
004930                       ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
004940                       ( レセ−請求月   NOT = 請求月ＷＲ   )
002990             PERFORM データチェック
003000***
003010             IF 実行キーＷ = "YES"
      */国保老人助成を対象にする/071221
                       IF レセ−請求区分 = 2
003070                     MOVE SPACE TO 作１−レコード
003080                     INITIALIZE 作１−レコード
                           EVALUATE TRUE
                           WHEN レセ−レセ種別 = 2
003090                         MOVE 受−公費種別       TO 作１−保険種別
003100                         MOVE 受−費用負担者番号 TO 作１−保険者番号
      */助成レコード作成
                           WHEN レセ−レセ種別 = 3
                               MOVE 受−助成種別            TO 作１−保険種別
                               MOVE 受−費用負担者番号助成  TO 作１−保険者番号
                           WHEN レセ−レセ種別 = 7
003960                         MOVE 受−保険種別     TO 作１−保険種別
                               MOVE 受−施術和暦年月 TO 生保−施術和暦年月
                               MOVE 受−患者コード   TO 生保−患者コード
                               READ 生保情報Ｆ
                               NOT INVALID KEY
003950                            MOVE 生保−負担者番号   TO 作１−保険者番号
                               END-READ
      */国保老人助成を対象にする/071221
                           WHEN OTHER
003090                         MOVE 受−保険種別       TO 作１−保険種別
003100                         MOVE 受−保険者番号     TO 作１−保険者番号
003140                     END-EVALUATE
003110                     PERFORM レコードセット
003120                     PERFORM 健保料金計算
003130                     PERFORM ファイル書込
003150                 END-IF
003160             END-IF
003170             PERFORM レセプトＦ読込
003180         END-PERFORM
003190     END-IF.
003200     CLOSE 作業ファイル１.
003210*================================================================*
003220 レセプトＦ読込 SECTION.
003230*
003240     READ レセプトＦ NEXT
003250     AT END
003260         MOVE "YES" TO 終了フラグ
003270     END-READ.
003280*================================================================*
003290 データチェック SECTION.
003300*
003310     MOVE SPACE          TO 実行キーＷ.
019520* *****************************************************************
019530* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
019540* *****************************************************************
019640     IF ( レセ−請求対象区分 NOT = ZERO ) AND
005778        ( レセ−償還払い区分 NOT = 1 )
              IF (レセ−レセ種別 = 3) AND ( レセ−会総括表印刷対象区分 = 1 )
                  CONTINUE
              ELSE
004090           MOVE レセ−施術和暦  TO 受−施術和暦
004100           MOVE レセ−施術年    TO 受−施術年
004110           MOVE レセ−施術月    TO 受−施術月
004120           MOVE レセ−患者番号  TO 受−患者番号
004130           MOVE レセ−枝番      TO 受−枝番
                 READ 受診者情報Ｆ
                 NOT INVALID KEY
019880              MOVE "YES"  TO 実行キーＷ
019950           END-READ
              END-IF
019960     END-IF.
003770*
003850*================================================================*
003860 レコードセット SECTION.
003870*
003880     MOVE 受−本人家族区分  TO 作１−本人家族区分.
003890     MOVE 受−被保険者カナ  TO 作１−被保険者カナ.
003900     MOVE 受−患者コード    TO 作１−患者コード.
003910     MOVE 受−施術和暦      TO 作１−施術和暦.
003920     MOVE 受−施術年        TO 作１−施術年.
003930     MOVE 受−施術月        TO 作１−施術月.
003940     MOVE 受−患者氏名      TO 作１−受診者氏名.
003950     MOVE 受−被保険者氏名  TO 作１−被保険者氏名.
003960*     MOVE 受−記号          TO 作１−記号.
003970*     MOVE 受−番号          TO 作１−番号.
      *-----------------------------------------------------------------*
           MOVE SPACE TO 連暗号複合−暗号情報.
      *
      *        / 連暗号複合−入力情報セット /
           MOVE 受−記号       TO 連暗号複合−記号.
           MOVE 受−番号       TO 連暗号複合−番号.
           MOVE 受−暗号化項目 TO 連暗号複合−暗号化項目.
      *
           CALL   複合プログラム名Ｗ.
           CANCEL 複合プログラム名Ｗ.
      *
           MOVE 連暗号複合−複合した記号 TO 作１−記号.
           MOVE 連暗号複合−複合した番号 TO 作１−番号.
      *
           IF レセ−レセ種別 = 4
              MOVE 受−施術和暦年月 TO 労災−施術和暦年月
              MOVE 受−患者コード   TO 労災−患者コード
              READ 労災情報Ｆ
              NOT INVALID KEY
012130            MOVE 労災−労働保険番号 TO 作１−番号
              END-READ
           END-IF.
004160*================================================================*
004170 健保料金計算 SECTION.
004180*
           EVALUATE TRUE
           WHEN レセ−レセ種別 = 3
               MOVE レセ−一部負担金   TO 作１−費用額
               MOVE レセ−受給者負担額 TO 作１−負担額
               MOVE レセ−助成請求金額 TO 作１−請求額
           WHEN OTHER
004260         MOVE レセ−合計         TO 作１−費用額
004270         MOVE レセ−一部負担金   TO 作１−負担額
004280         MOVE レセ−請求金額     TO 作１−請求額
           END-EVALUATE.
004550*================================================================*
004560 ファイル書込 SECTION.
004570*
004580     WRITE 作１−レコード
004590     INVALID KEY
004600         MOVE NC"作業"  TO ファイル名
004610         PERFORM エラー表示
004620     END-WRITE.
004630*
004640*================================================================*
004650 作業ファイル２作成 SECTION.
004660*
004670     OPEN INPUT  作業ファイル１.
004680     OPEN OUTPUT 作業ファイル２.
004690*
004700     PERFORM 作業ファイル２作成処理.
004710*
004720     CLOSE 作業ファイル２.
004730     CLOSE 作業ファイル１.
004740*
004750*================================================================*
004760 作業ファイル２作成処理 SECTION.
004770*
004780     MOVE ZERO       TO 作１−保険種別.
004790     MOVE SPACE      TO 作１−保険者番号.
004800     MOVE ZERO       TO 作１−本人家族区分.
004810     MOVE SPACE      TO 作１−被保険者カナ.
004820     MOVE ZERO       TO 作１−患者番号.
004830     MOVE SPACE      TO 作１−枝番.
004840     MOVE ZERO       TO 作１−施術和暦年月.
004850     START 作業ファイル１ KEY IS >= 作１−保険種別
004860                                    作１−保険者番号
004870                                    作１−施術和暦年月
004880                                    作１−本人家族区分
004890                                    作１−被保険者カナ
004900                                    作１−患者コード
004910     END-START.
004920     IF 状態キー = "00"
004930*
004940         MOVE SPACE TO 終了フラグ
004950         PERFORM 作業ファイル１読込
004960*
004970         PERFORM UNTIL 終了フラグ = "YES"
004980             MOVE ZERO               TO 費用額Ｗ
004990             MOVE ZERO               TO 負担額Ｗ
005000             MOVE ZERO               TO 請求額Ｗ
005010             MOVE 作１−保険種別     TO 保険種別Ｗ
005020             MOVE 作１−保険者番号   TO 保険者番号Ｗ
005030*
005040             PERFORM UNTIL ( 終了フラグ = "YES" ) OR
005050                           ( 作１−保険種別     NOT = 保険種別Ｗ ) OR
005060                           ( 作１−保険者番号   NOT = 保険者番号Ｗ )
005070                PERFORM 作業レコード２累計
005080                PERFORM 作業ファイル１読込
005090             END-PERFORM
005100             PERFORM 作業レコード２セット
005110             PERFORM 作業ファイル２書込
005120         END-PERFORM
005130     END-IF.
005140*================================================================*
005150 作業ファイル１読込 SECTION.
005160*
005170     READ 作業ファイル１ NEXT
005180     AT END
005190         MOVE "YES" TO 終了フラグ
005200     END-READ.
005210*================================================================*
005220 作業レコード２累計 SECTION.
005230*
005240     COMPUTE 費用額Ｗ = 費用額Ｗ + 作１−費用額.
005250     COMPUTE 負担額Ｗ = 負担額Ｗ + 作１−負担額.
005260     COMPUTE 請求額Ｗ = 請求額Ｗ + 作１−請求額.
005270*===============================================================*
005280 作業レコード２セット SECTION.
005290*
005300     MOVE SPACE TO 作２−レコード.
005310     INITIALIZE    作２−レコード.
005320     MOVE 保険種別Ｗ     TO 作２−保険種別.
005330     MOVE 保険者番号Ｗ   TO 作２−保険者番号.
005340     MOVE 費用額Ｗ       TO 作２−費用額.
005350     MOVE 負担額Ｗ       TO 作２−負担額.
005360     MOVE 請求額Ｗ       TO 作２−請求額.
005370*===============================================================*
005380 作業ファイル２書込 SECTION.
005390*
005400     WRITE 作２−レコード
005410     INVALID KEY
005420**         DISPLAY NC"状態キー" 状態キー  UPON MSGBOX
005430         MOVE NC"作２" TO ファイル名
005440         PERFORM エラー表示
005450         PERFORM ファイル閉鎖
005460         MOVE 99 TO PROGRAM-STATUS
005470         EXIT PROGRAM.
005480*================================================================*
005490 エラー表示 SECTION.
005500*
005510     DISPLAY NC"状態キー" 状態キー  UPON CONS.
005520     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
005530     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
005540     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
004380*-----------------------------------------*
004390     CALL "actcshm"  WITH C LINKAGE.
004400*-----------------------------------------*
005550     ACCEPT  キー入力 FROM CONS.
005560     PERFORM ファイル閉鎖.
005570     MOVE 99 TO PROGRAM-STATUS.
005580     EXIT PROGRAM.
005590*================================================================*
005600 ファイル閉鎖 SECTION.
005610*
005620     CLOSE 受診者情報Ｆ レセプトＦ 生保情報Ｆ 労災情報Ｆ.
005630*================================================================*
005640 終了処理 SECTION.
005650*
005660     PERFORM ファイル閉鎖.
005670*================================================================*
005680******************************************************************
005690 END PROGRAM YCB591.
005700******************************************************************
