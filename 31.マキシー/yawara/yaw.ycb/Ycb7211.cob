000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YCB7211.
000060 AUTHOR.                 池田 幸子
000070*
000080*----------------------------------------------------------------*
000090*         中部柔整師協会 カルテ裏【データ作成】［受診者・年月別］
000100*         MED = YCB720 YCB7221P
000110*----------------------------------------------------------------*
000120 DATE-WRITTEN.           2012-11-12
000130 DATE-COMPILED.          2012-11-12
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000260     SELECT  施術記録Ｆ      ASSIGN      TO        SEKIROKL
000270                             ORGANIZATION             IS  INDEXED
000280                             ACCESS MODE              IS  DYNAMIC
000290                             RECORD KEY           IS 施記−施術和暦年月日
000300                                                     施記−患者コード
000310                             ALTERNATE RECORD KEY IS 施記−患者コード
000320                                                     施記−施術和暦年月日
000330                             FILE STATUS              IS  状態キー
000340                             LOCK        MODE         IS  AUTOMATIC.
000350     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000360                             ORGANIZATION             IS  INDEXED
000370                             ACCESS MODE              IS  DYNAMIC
000380                             RECORD KEY               IS  受−施術和暦年月
000390                                                          受−患者コード
000400                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000410                                                          受−患者カナ
000420                                                          受−患者コード
000430                             ALTERNATE RECORD KEY     IS  受−患者コード
000440                                                          受−施術和暦年月
000450                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000460                                                          受−保険種別
000470                                                          受−保険者番号
000480                                                          受−患者コード
000490                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000500                                                          受−公費種別
000510                                                          受−費用負担者番号
000520                                                          受−患者コード
000530                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000540                                                          受−助成種別
000550                                                          受−費用負担者番号助成
000560                                                          受−患者コード
000570                             ALTERNATE RECORD KEY     IS  受−請求和暦年月
000580                                                          受−施術和暦年月
000590                                                          受−患者コード
000600                             FILE STATUS              IS  状態キー
000610                             LOCK        MODE         IS  AUTOMATIC.
000620     SELECT  負傷データＦ    ASSIGN      TO        HUSYOUL
000630                             ORGANIZATION             IS  INDEXED
000640                             ACCESS MODE              IS  DYNAMIC
000650                             RECORD KEY               IS 負−施術和暦年月
000660                                                         負−患者コード
000670                             ALTERNATE RECORD KEY     IS 負−患者コード
000680                                                         負−施術和暦年月
000690                             FILE STATUS              IS  状態キー
000700                             LOCK        MODE         IS  AUTOMATIC.
000710     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000720                             ORGANIZATION             IS  INDEXED
000730                             ACCESS MODE              IS  DYNAMIC
000740                             RECORD KEY               IS  元−元号区分
000750                             FILE STATUS              IS  状態キー
000760                             LOCK        MODE         IS  AUTOMATIC.
000400     SELECT  メモファイル    ASSIGN      TO        MEMOL
000410                             ORGANIZATION             IS  INDEXED
000420                             ACCESS MODE              IS  DYNAMIC
000430                             RECORD KEY               IS  メモ−制御区分
                                                                メモ−患者コード
                                                                メモ−施術和暦年月日
000360                             ALTERNATE RECORD KEY     IS  メモ−制御区分
                                                                メモ−施術和暦年月日
                                                                メモ−患者コード
000360                             ALTERNATE RECORD KEY     IS  メモ−患者コード
                                                                メモ−施術和暦年月日
                                                                メモ−制御区分
000440                             FILE STATUS              IS  状態キー
000450                             LOCK        MODE         IS  AUTOMATIC.
000690     SELECT  会計データＦ    ASSIGN      TO        KAIKEIL
000700                             ORGANIZATION             IS  INDEXED
000710                             ACCESS MODE              IS  DYNAMIC
000089                             RECORD KEY               IS  会−施術和暦年月日
000090                                                          会−患者コード
000092                             ALTERNATE RECORD KEY     IS  会−患者コード
000093                                                          会−施術和暦年月日
000790                             FILE STATUS              IS  状態キー
000800                             LOCK        MODE         IS  AUTOMATIC.
000770     SELECT  作業ファイル１    ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W72111L.DAT"
000780                             ORGANIZATION             IS  INDEXED
000790                             ACCESS                   IS  DYNAMIC
000800                             RECORD      KEY    IS  作１−施術和暦年月日
000810                             FILE        STATUS       IS  状態キー
000820                             LOCK        MODE         IS  AUTOMATIC.
000830******************************************************************
000840*                      DATA DIVISION                             *
000850******************************************************************
000860 DATA                    DIVISION.
000870 FILE                    SECTION.
000880*                           ［ＲＬ＝  ２５６］
000890 FD  施術記録Ｆ          BLOCK   CONTAINS   1   RECORDS.
000900     COPY SEKIROK         OF  XFDLIB  JOINING   施記 AS  PREFIX.
000910*                           ［ＲＬ＝  ３２０］
000920 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
000930     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
000940*                           ［ＲＬ＝  ６４０］
000950 FD  負傷データＦ        BLOCK   CONTAINS   1   RECORDS.
000960     COPY HUSYOU          OF  XFDLIB  JOINING   負   AS  PREFIX.
000970*                           ［ＲＬ＝  １２８］
000980 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
000990     COPY GENGOU          OF  XFDLIB  JOINING   元   AS  PREFIX.
000600*                           ［ＲＬ＝  ８３２］
000610 FD  メモファイル        BLOCK CONTAINS 1     RECORDS.
000620     COPY MEMO           OF    XFDLIB JOINING メモ AS PREFIX.
001060*                           ［ＲＬ＝  ５１２］
001070 FD  会計データＦ        BLOCK   CONTAINS   1   RECORDS.
001080     COPY KAIKEI     OF  XFDLIB  JOINING   会   AS  PREFIX.
001000*                           ［ＲＬ＝  ２５６］
001010 FD  作業ファイル１ RECORD  CONTAINS 256 CHARACTERS.
001020 01 作１−レコード.
001030    03 作１−レコードキー.
001040       05  作１−施術和暦年月日.
001050          07 作１−施術和暦               PIC 9.
001060          07 作１−施術年月.
001070             09 作１−施術年              PIC 9(2).
001080             09 作１−施術月              PIC 9(2).
001090          07 作１−施術日                 PIC 9(2).
001100    03 作１−レコードデータ.
001110       05 作１−患者コード.
001120          07 作１−患者番号               PIC 9(6).
001130          07 作１−枝番                   PIC X.
001080       05  作１−日チェック               PIC 9.
             05 作１−費用額                    PIC 9(6).
001200       05 作１−一部負担金                PIC 9(5).
001551       05 作１−コメント                  PIC X(100).
      *
             05 作１−初検料                    PIC 9(5).
             05 作１−初検加算.
                07 作１−初検時間外             PIC 9.
                07 作１−初検休日               PIC 9.
                07 作１−初検深夜               PIC 9.
             05 作１−初検加算料                PIC 9(5).
             05 作１−再検料                    PIC 9(5).
             05 作１−往療.
                07 作１−往療距離               PIC 9(2)V9.
                07 作１−往療回数               PIC 9(2).
                07 作１−往療料                 PIC 9(5).
                07 作１−往療加算料             PIC 9(5).
                07 作１−往療加算.
                   09 作１−往療夜間            PIC 9.
                   09 作１−往療難路            PIC 9.
                   09 作１−往療暴風雨          PIC 9.
                   09 作１−往療時間外          PIC 9.
             05 作１−初回処置料計              PIC 9(6).
             05 作１−後療料                    PIC 9(6).
             05 作１−冷罨法料                  PIC 9(5).
             05 作１−温罨法料                  PIC 9(5).
             05 作１−電療料                    PIC 9(5).
             05 作１−大                        PIC 9(1).
             05 作１−中                        PIC 9(1).
             05 作１−小                        PIC 9(1).
             05 作１−金属副子加算料            PIC 9(5).
             05 作１−運動後療料                PIC 9(4).
001210       05 FILLER                          PIC X(54).
001220*
001230*----------------------------------------------------------------*
001240******************************************************************
001250*                WORKING-STORAGE SECTION                         *
001260******************************************************************
001270 WORKING-STORAGE         SECTION.
001280 01 キー入力                         PIC X     VALUE SPACE.
001290 01 状態キー                         PIC X(2)  VALUE SPACE.
001300 01 実行キーＷ                       PIC X(3)  VALUE SPACE.
001310 01 終了フラグ                       PIC X(3)  VALUE SPACE.
001320 01 終了フラグ２                     PIC X(3) VALUE SPACE.
001330 01 施術記録有Ｗ                     PIC X(3) VALUE SPACE.
001340 01 施術和暦年月ＷＲ.
001350    03 施術和暦ＷＲ                  PIC 9(1)  VALUE ZERO.
001360    03 施術年月ＷＲ.
001370       05 施術年ＷＲ                 PIC 9(2)  VALUE ZERO.
001380       05 施術月ＷＲ                 PIC 9(2)  VALUE ZERO.
001390       05 施術日ＷＲ                 PIC 9(2)  VALUE ZERO.
001400 01 患者コードＷＲ.
001410    03 患者番号ＷＲ                  PIC 9(6)  VALUE ZERO.
001420    03 枝番ＷＲ                      PIC X     VALUE SPACE.
001430 01 印刷モードＦＷＲ                 PIC 9(1)  VALUE ZERO.
001440 01 日ごとモードＷＲ                 PIC 9(1)  VALUE ZERO.
001450 01 氏名モードＷＲ                   PIC 9(1)  VALUE ZERO.
001460 01 年月モードＷＲ                   PIC 9(1)  VALUE ZERO.
001480 01 合計モードＷＲ                   PIC 9(1)  VALUE ZERO.
001630 01 コメントモードＷＲ               PIC 9(1)  VALUE ZERO.
       01 開始日ＷＲ                       PIC 9(2)  VALUE ZERO.
       01 終了日ＷＲ                       PIC 9(2)  VALUE ZERO.
001490 01 施術日数Ｗ                       PIC 9(2)  VALUE ZERO.
001500 01 ファイル名                       PIC N(10) VALUE SPACE.
001510 01 部位ＣＮＴ                       PIC 9(2)  VALUE ZERO.
001520 01 カウンタ                         PIC 9(2)  VALUE ZERO.
001530*
001540 01 退避項目ＧＷ.
001550   03 レセプト種類Ｗ                 PIC X(4).
001560   03 レセプト種類ＧＷ               PIC X(4).
001570   03 レセプト種別ＧＷ               PIC 9(2).
001580*
001590*
001600 01 明細.
001610    03 初検料ＷＲ                    PIC 9(4)  VALUE ZERO.
001620    03 初検加算料ＷＲ                PIC 9(4)  VALUE ZERO.
001630    03 休日ＷＲ                      PIC 9     VALUE ZERO.
001640    03 深夜ＷＲ                      PIC 9     VALUE ZERO.
001650    03 時間外ＷＲ                    PIC 9     VALUE ZERO.
001660    03 診療時ＷＲ                    PIC 9(2)  VALUE ZERO.
001670    03 診療分ＷＲ                    PIC 9(2)  VALUE ZERO.
001680    03 再検料ＷＲ                    PIC 9(4)  VALUE ZERO.
001690    03 往療夜間ＷＲ                  PIC 9     VALUE ZERO.
001700    03 往療難路ＷＲ                  PIC 9     VALUE ZERO.
001710    03 往療暴風ＷＲ                  PIC 9     VALUE ZERO.
001720    03 往療回数ＷＲ                  PIC 9(2)  VALUE ZERO.
001730    03 往療距離ＷＲ                  PIC 9(3)V9 VALUE ZERO.
001740    03 往療料ＷＲ                    PIC 9(6)  VALUE ZERO.
001750    03 往療加算料ＷＲ                PIC 9(5)  VALUE ZERO.
001760    03 電罨料ＷＲ                    PIC 9(4)  VALUE ZERO.
001770    03 一部負担金ＷＲ                PIC 9(5)  VALUE ZERO.
003140    03 運動後療料ＷＲ                PIC 9(4)  VALUE ZERO.
001780    03 施術情報提供料ＷＲ            PIC 9(6)  VALUE ZERO.
001790    03 部位Ｗ                        OCCURS 9.
001800       05 初回処置料ＷＲ             PIC 9(4)  VALUE ZERO.
001810       05 後療料ＷＲ                 PIC 9(4)  VALUE ZERO.
001820       05 冷罨料ＷＲ                 PIC 9(4)  VALUE ZERO.
001830       05 温罨料ＷＲ                 PIC 9(4)  VALUE ZERO.
001840       05 電療料ＷＲ                 PIC 9(4)  VALUE ZERO.
001850       05 部位計ＷＲ                 PIC 9(4)  VALUE ZERO.
001860       05 金属ＷＲ                   PIC 9     VALUE ZERO.
001870    03 日数ＷＲ                      PIC 9(2)  OCCURS 5 VALUE ZERO.
001880    03 冷罨法料３０ＷＲ              PIC 9(4)  VALUE ZERO.
001890    03 冷罨法料３８ＷＲ              PIC 9(4)  VALUE ZERO.
001900    03 冷罨法料４０ＷＲ              PIC 9(4)  VALUE ZERO.
001910    03 冷罨法料４５ＷＲ              PIC 9(4)  VALUE ZERO.
001920    03 冷罨法料４８ＷＲ              PIC 9(4)  VALUE ZERO.
001930    03 温罨法料３０ＷＲ              PIC 9(4)  VALUE ZERO.
001940    03 温罨法料３８ＷＲ              PIC 9(4)  VALUE ZERO.
001950    03 温罨法料４０ＷＲ              PIC 9(4)  VALUE ZERO.
001960    03 温罨法料４５ＷＲ              PIC 9(4)  VALUE ZERO.
001970    03 温罨法料４８ＷＲ              PIC 9(4)  VALUE ZERO.
001980    03 電療料３０ＷＲ                PIC 9(4)  VALUE ZERO.
001990    03 電療料３８ＷＲ                PIC 9(4)  VALUE ZERO.
002000    03 電療料４０ＷＲ                PIC 9(4)  VALUE ZERO.
002010    03 電療料４５ＷＲ                PIC 9(4)  VALUE ZERO.
002020    03 電療料４８ＷＲ                PIC 9(4)  VALUE ZERO.
002030    03 後療料３０ＷＲ                PIC 9(4)  VALUE ZERO.
002040    03 後療料３８ＷＲ                PIC 9(4)  VALUE ZERO.
002050    03 後療料４０ＷＲ                PIC 9(4)  VALUE ZERO.
002060    03 後療料４５ＷＲ                PIC 9(4)  VALUE ZERO.
002070    03 後療料４８ＷＲ                PIC 9(4)  VALUE ZERO.
002080    03 冷罨料計ＷＲ                  PIC 9(4)  VALUE ZERO.
002090    03 温罨料計ＷＲ                  PIC 9(4)  VALUE ZERO.
002100    03 電療料計ＷＲ                  PIC 9(4)  VALUE ZERO.
002110*
002120 01 保険種別Ｗ                       PIC 9(2)  VALUE ZERO.
002130 01 公費種別Ｗ                       PIC 9(2)  VALUE ZERO.
002140 01 助成種別Ｗ                       PIC 9(2)  VALUE ZERO.
002150 01 負担額Ｗ２                       PIC 9(5)  VALUE ZERO.
002160 01 費用額Ｗ２                       PIC 9(5)  VALUE ZERO.
002170 01 施術累計回数Ｗ                   PIC 9(3)  VALUE ZERO.
002180*
002190 01 負傷和暦年月日Ｗ.
002200     03 負傷和暦年月Ｗ.
002210         05 負傷和暦Ｗ               PIC 9.
002220         05 負傷年月Ｗ.
002230            07 負傷年Ｗ              PIC 9(2).
002240            07 負傷月Ｗ              PIC 9(2).
002250     03 負傷日Ｗ                     PIC 9(2).
002260*
002270 01 開始和暦年月日Ｗ.
002280     03 開始和暦年月Ｗ.
002290         05 開始和暦Ｗ               PIC 9.
002300         05 開始年月Ｗ.
002310            07 開始年Ｗ              PIC 9(2).
002320            07 開始月Ｗ              PIC 9(2).
002330     03 開始日Ｗ                     PIC 9(2).
002340*
002350 01 終了和暦年月日Ｗ.
002360     03 終了和暦年月Ｗ.
002370         05 終了和暦Ｗ               PIC 9.
002380         05 終了年月Ｗ.
002390            07 終了年Ｗ              PIC 9(2).
002400            07 終了月Ｗ              PIC 9(2).
002410     03 終了日Ｗ                     PIC 9(2).
002420*
002440 01 計算和暦年月Ｗ.
002450     03 計算和暦Ｗ                     PIC 9.
002460     03 計算年月Ｗ.
002470        05 計算年Ｗ                    PIC 9(2).
002480        05 計算月Ｗ                    PIC 9(2).
002490     03 計算日Ｗ                       PIC 9(2).
002500*
002510 01 実日数Ｗ                           PIC 9(2) VALUE ZERO.
002520 01 部位数Ｗ                           PIC 9 VALUE ZERO.
002530 01 最大登録数Ｗ                       PIC 9 VALUE ZERO.
002540 01 部位長Ｗ                           PIC 9(2) VALUE 1.
002550 01 商Ｗ                               PIC 9(4) VALUE ZERO.
002560 01 剰余Ｗ                             PIC 9(4) VALUE ZERO.
002570 01 負傷名Ｗ                           PIC N(6) VALUE SPACE.
002580*
002590 01 絶対値Ｗ                           PIC 9(6).
002600 01 絶対値月Ｗ                         PIC 9(3).
002610 01 絶対値月Ｗ２                       PIC 9(3).
002620 01 負傷経過日Ｗ                       PIC 9(3).
002630 01 西暦年Ｗ                           PIC 9(4).
002640 01 西暦年差Ｗ                         PIC 9(2).
002650 01 西暦年差２Ｗ                       PIC 9(2).
002660 01 閏年回数Ｗ                         PIC 9(2).
002670 01 通常年回数Ｗ                       PIC 9(2).
002680 01 通常年絶対値Ｗ                     PIC 9(6).
002690 01 閏年絶対値Ｗ                       PIC 9(6).
002700 01 年絶対値Ｗ                         PIC 9(6).
002710 01 負傷絶対値Ｗ                       PIC 9(6).
002720 01 施術絶対値Ｗ                       PIC 9(6).
       01 加算年Ｗ                           PIC 9(4).
002730*
      */冷罨法の合計を多部位逓減後の合計に変更(長期逓減はありえない。端数は四捨五入）
      *
       01 多部位ＣＮＴ                       PIC 9(2) VALUE ZERO.
      *
       01 施術和暦年月日ＣＷ.
         03 施術和暦年月ＣＷ.
           05 施術和暦ＣＷ                   PIC 9.
           05 施術年月ＣＷ.
              07 施術年ＣＷ                  PIC 9(2).
              07 施術月ＣＷ                  PIC 9(2).
         03 施術日ＣＷ                       PIC 9(2).
      *
       01 逓減率ＳＫＷ.
         03 逓減率ＫＷ  OCCURS 9.
           05 多部位逓減率ＫＷ               PIC 9(3) VALUE ZERO.
      *
      */エラー表示の修正
       01 エラー表示Ｆ                       PIC 9(1)  VALUE ZERO.
      *
002740******************************************************************
002750*                          連結項目                              *
002760******************************************************************
002770*
002780****************
002790* 画面入力情報 *
002800****************
002810 01 連入−入力データＹＣＢ７２０ IS EXTERNAL.
002820    03 連入−施術和暦年月.
002830       05 連入−施術和暦                  PIC 9(1).
002840       05 連入−施術年                    PIC 9(2).
002850       05 連入−施術月                    PIC 9(2).
002860    03 連入−開始日付.
002870       05 連入−開始日                    PIC 9(2).
002880    03 連入−終了日付.
002890       05 連入−終了日                    PIC 9(2).
002900    03 連入−患者コード.
002910       05 連入−患者番号                  PIC 9(6).
002920       05 連入−枝番                      PIC X(1).
002930    03 連入−印刷モードＦ                 PIC 9(1).
002940    03 連入−日ごとモード                 PIC 9(1).
002950    03 連入−氏名モード                   PIC 9(1).
          03 連入−コメントモード               PIC 9(1).
          03 連入−印刷段数                     PIC 9(2).
      */連続時の氏名印刷の有無0302
          03 連入−処理モード                   PIC X(4).
006570*
006580 01 連レ−キー IS EXTERNAL.
006590    03 連レ−保険種別                  PIC 9(2).
006600*
006610 01 連メ−キー IS EXTERNAL.
006620    03  連メ−メッセージ                 PIC N(20).
      *
009182* 確認メッセージ用 (２行)
009183 01 連メ７−キー IS EXTERNAL.
009184    03  連メ７−メッセージ１             PIC X(40).
009185    03  連メ７−メッセージ２             PIC X(40).
      *
006700******************************************************************
006710*                      PROCEDURE  DIVISION                       *
006720******************************************************************
006730 PROCEDURE               DIVISION.
006740************
006750*           *
006760* 初期処理   *
006770*           *
006780************
006790     PERFORM ファイルオープン.
006800     PERFORM 連結項目待避.
006810************
006820*           *
006830* 主処理     *
006840*           *
006850************
006890     IF (日ごとモードＷＲ = 1) OR (コメントモードＷＲ = 1)
006900         PERFORM 作業ファイル作成
006910     END-IF.
006920************
006930*           *
006940* 終了処理   *
006950*           *
006960************
006970     PERFORM 終了処理.
006980     MOVE ZERO TO PROGRAM-STATUS.
006990     EXIT PROGRAM.
007000*
007010*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
007020*================================================================*
007030 連結項目待避 SECTION.
007040*
007050     MOVE 連入−患者番号       TO 患者番号ＷＲ.
007060     MOVE 連入−枝番           TO 枝番ＷＲ.
007070     MOVE 連入−施術和暦       TO 施術和暦ＷＲ.
007080     MOVE 連入−施術年         TO 施術年ＷＲ.
007090     MOVE 連入−施術月         TO 施術月ＷＲ.
007100     MOVE 連入−印刷モードＦ   TO 印刷モードＦＷＲ.
007110     MOVE 連入−日ごとモード   TO 日ごとモードＷＲ.
007120     MOVE 連入−氏名モード     TO 氏名モードＷＲ.
004540     MOVE 連入−コメントモード TO コメントモードＷＲ.
      *
           MOVE 連入−開始日         TO 開始日ＷＲ.
           MOVE 連入−終了日         TO 終了日ＷＲ.
007160*================================================================*
007170 ファイルオープン SECTION.
007180*
007190     OPEN INPUT 施術記録Ｆ.
007200         MOVE NC"施記" TO ファイル名.
007210         PERFORM オープンチェック.
007220     OPEN INPUT 受診者情報Ｆ.
007230         MOVE NC"受診" TO ファイル名.
007240         PERFORM オープンチェック.
007250     OPEN INPUT 負傷データＦ.
007260         MOVE NC"負傷" TO ファイル名.
007270         PERFORM オープンチェック.
007280     OPEN INPUT 元号マスタ.
007290         MOVE NC"元号" TO ファイル名.
007300         PERFORM オープンチェック.
002780     OPEN INPUT メモファイル.
002790         MOVE NC"メモ"         TO ファイル名.
002800         PERFORM オープンチェック.
003060     OPEN INPUT 会計データＦ.
003070         MOVE NC"会計" TO ファイル名.
003080         PERFORM オープンチェック.
007310*================================================================*
007320 オープンチェック SECTION.
007330*
007340     IF 状態キー  NOT =  "00"
007350         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
007360         DISPLAY NC"状態キー：" 状態キー         UPON CONS
007370         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
007380                                                 UPON CONS
003131*-----------------------------------------*
003132         CALL "actcshm"  WITH C LINKAGE
003133*-----------------------------------------*
007390         ACCEPT  キー入力 FROM CONS
007400         PERFORM ファイル閉鎖
007410         MOVE 99 TO PROGRAM-STATUS
007420         EXIT PROGRAM.
007430*================================================================*
007440 エラー表示 SECTION.
007450*
007460     DISPLAY NC"状態キー" 状態キー                              UPON CONS.
007470     DISPLAY NC"ファイル書込エラー：" ファイル名                UPON CONS.
007480     DISPLAY NC"システム管理者に連絡してください"               UPON CONS.
007490     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
003131*-----------------------------------------*
003132     CALL "actcshm"  WITH C LINKAGE.
003133*-----------------------------------------*
007500     ACCEPT  キー入力 FROM CONS.
007510     PERFORM ファイル閉鎖.
007520     MOVE 99 TO PROGRAM-STATUS.
007530     EXIT PROGRAM.
007540*================================================================*
008520 データチェック SECTION.
008530*
008540     MOVE SPACE  TO 実行キーＷ.
008550* *****************************************************************
008560* * 負傷部位有無チェック：部位数 = 0 の場合データ作成対象としない *
008570* *****************************************************************
008580     MOVE 受−施術和暦   TO 負−施術和暦.
008590     MOVE 受−施術年     TO 負−施術年.
008600     MOVE 受−施術月     TO 負−施術月.
008610     MOVE 受−患者番号   TO 負−患者番号.
008620     MOVE 受−枝番       TO 負−枝番.
008630     READ 負傷データＦ
008640     INVALID KEY
008650         MOVE SPACE  TO 実行キーＷ
008660     NOT INVALID KEY
008670         IF 負−部位数 NOT = ZERO
008680*        *************************************************************
008690*        * 施術記録チェック：通院数 = 0 の場合データ作成対象としない *
008700*        *************************************************************
008710             MOVE 負−患者番号  TO 施記−患者番号
008720             MOVE 負−枝番      TO 施記−枝番
008730             MOVE 負−施術和暦  TO 施記−施術和暦
008740             MOVE 負−施術年    TO 施記−施術年
008750             MOVE 負−施術月    TO 施記−施術月
008760             MOVE ZERO          TO 施記−施術日
008770             START 施術記録Ｆ   KEY IS >= 施記−患者コード
008780                                          施記−施術和暦年月日
008790             END-START
008800             IF 状態キー = "00"
008810                 MOVE SPACE TO 終了フラグ２
008820                 MOVE SPACE TO 施術記録有Ｗ
008830                 PERFORM 施術記録Ｆ読込
008840                 PERFORM UNTIL ( 終了フラグ２         = "YES"          ) OR
008850                               ( 施記−患者コード NOT = 負−患者コード ) OR
008860                               ( 施記−施術和暦   NOT = 負−施術和暦   ) OR
008870                               ( 施記−施術年     NOT = 負−施術年     ) OR
008880                               ( 施記−施術月     NOT = 負−施術月     ) OR
008890                               ( 施術記録有Ｗ         = "YES"          )
008900                     MOVE "YES"  TO 施術記録有Ｗ
008910                     MOVE "YES"  TO 実行キーＷ
008920                 END-PERFORM
008930             ELSE
008940                 MOVE SPACE  TO 実行キーＷ
008950             END-IF
008960         ELSE
008970             MOVE  NC"データがありません。" TO 連メ−メッセージ
008980             CALL   "MSG001"
008990             CANCEL "MSG001"
009000             MOVE SPACE  TO 実行キーＷ
009010         END-IF
009020     END-READ.
009030*
009380*================================================================*
009390 レセプト呼出し２ SECTION.
009400*
009180     MOVE 受−患者番号  TO 会−患者番号.
009190     MOVE 受−枝番      TO 会−枝番.
009200     MOVE 受−施術和暦  TO 会−施術和暦.
009210     MOVE 受−施術年    TO 会−施術年.
009220     MOVE 受−施術月    TO 会−施術月.
009230     MOVE 施術日ＷＲ    TO 会−施術日.
009240     READ 会計データＦ
           INVALID KEY
               MOVE SPACE     TO 会−レコード
           END-READ.
009710*
009750     MOVE 会−一部負担金 TO 一部負担金ＷＲ.
009770*
011430*================================================================*
011440 項目ごと計算 SECTION.
011450***********************************************
011460* 料金データセット                            *
011470***********************************************
011480     PERFORM VARYING カウンタ FROM 1 BY 1 UNTIL カウンタ > 9
011490         MOVE 会−初回処置料(カウンタ) TO 初回処置料ＷＲ(カウンタ)
011500     END-PERFORM.
011510     MOVE 会−後療料１     TO 後療料ＷＲ(1).
011520     MOVE 会−後療料２     TO 後療料ＷＲ(2).
011530     MOVE 会−後療料３８   TO 後療料３８ＷＲ.
011540     MOVE 会−後療料３０   TO 後療料３０ＷＲ.
011550     COMPUTE 後療料ＷＲ(3)  = 後療料３８ＷＲ   + 後療料３０ＷＲ.
011560     MOVE 会−後療料４５   TO 後療料４５ＷＲ.
011570     MOVE 会−後療料４８   TO 後療料４８ＷＲ.
011580     MOVE 会−後療料４０   TO 後療料４０ＷＲ.
011590     COMPUTE 後療料ＷＲ(4)  = 後療料４５ＷＲ   + 後療料４８ＷＲ   + 後療料４０ＷＲ.
011600     PERFORM VARYING カウンタ FROM 1 BY 1 UNTIL カウンタ > 9
011610         COMPUTE 部位計ＷＲ(カウンタ) = 後療料ＷＲ(カウンタ) + 初回処置料ＷＲ(カウンタ)
011620     END-PERFORM.
011630********************
011640* 逓減毎料金セット *
011650********************
011660     MOVE 会−冷罨法料１             TO 冷罨料ＷＲ(1).
011670     MOVE 会−冷罨法料２             TO 冷罨料ＷＲ(2).
011680     MOVE 会−冷罨法料３８           TO 冷罨法料３８ＷＲ.
011690     MOVE 会−冷罨法料３０           TO 冷罨法料３０ＷＲ.
011700     COMPUTE 冷罨料ＷＲ(3)   = 冷罨法料３８ＷＲ  + 冷罨法料３０ＷＲ.
011710     MOVE 会−冷罨法料４５           TO 冷罨法料４５ＷＲ.
011720     MOVE 会−冷罨法料４８           TO 冷罨法料４８ＷＲ.
011730     MOVE 会−冷罨法料４０           TO 冷罨法料４０ＷＲ.
011740     COMPUTE 冷罨料ＷＲ(4)   = 冷罨法料４５ＷＲ  + 冷罨法料４８ＷＲ  + 冷罨法料４０ＷＲ.
011750     COMPUTE 冷罨料計ＷＲ = 冷罨料ＷＲ(1) + 冷罨料ＷＲ(2) + 冷罨料ＷＲ(3) + 冷罨料ＷＲ(4).
011760*
011770     MOVE 会−温罨法料１             TO 温罨料ＷＲ(1).
011780     MOVE 会−温罨法料２             TO 温罨料ＷＲ(2).
011790     MOVE 会−温罨法料３８           TO 温罨法料３８ＷＲ.
011800     MOVE 会−温罨法料３０           TO 温罨法料３０ＷＲ.
011810     COMPUTE 温罨料ＷＲ(3)   = 温罨法料３８ＷＲ  + 温罨法料３０ＷＲ.
011820     MOVE 会−温罨法料４５           TO 温罨法料４５ＷＲ.
011830     MOVE 会−温罨法料４８           TO 温罨法料４８ＷＲ.
011840     MOVE 会−温罨法料４０           TO 温罨法料４０ＷＲ.
011850     COMPUTE 温罨料ＷＲ(4)   = 温罨法料４５ＷＲ  + 温罨法料４８ＷＲ  + 温罨法料４０ＷＲ.
011860     COMPUTE 温罨料計ＷＲ = 温罨料ＷＲ(1) + 温罨料ＷＲ(2) + 温罨料ＷＲ(3) + 温罨料ＷＲ(4).
011870*
011880     MOVE 会−電療料１             TO 電療料ＷＲ(1).
011890     MOVE 会−電療料２             TO 電療料ＷＲ(2).
011900     MOVE 会−電療料３８           TO 電療料３８ＷＲ.
011910     MOVE 会−電療料３０           TO 電療料３０ＷＲ.
011920     COMPUTE 電療料ＷＲ(3)  = 電療料３８ＷＲ  + 電療料３０ＷＲ.
011930     MOVE 会−電療料４５           TO 電療料４５ＷＲ.
011940     MOVE 会−電療料４８           TO 電療料４８ＷＲ.
011950     MOVE 会−電療料４０           TO 電療料４０ＷＲ.
011960     COMPUTE 電療料ＷＲ(4)  = 電療料４５ＷＲ  + 電療料４８ＷＲ  + 電療料４０ＷＲ.
011970     COMPUTE 電療料計ＷＲ = 電療料ＷＲ(1) + 電療料ＷＲ(2) + 電療料ＷＲ(3) + 電療料ＷＲ(4).
011980*
011990     COMPUTE 電罨料ＷＲ = 冷罨料計ＷＲ + 温罨料計ＷＲ + 電療料計ＷＲ.
012000*
012080*================================================================*
012090 施術記録Ｆ読込 SECTION.
012100*
012110     READ 施術記録Ｆ NEXT
012120     AT END
012130         MOVE "YES" TO 終了フラグ２
012140     END-READ.
012150*================================================================*
012160 受診者情報Ｆ読込 SECTION.
012170*
012180     READ 受診者情報Ｆ NEXT
012190     AT END
012200         MOVE "YES" TO 終了フラグ
012210     END-READ.
012220*================================================================*
012230 ファイル書込 SECTION.
012240*
012250     WRITE 作１−レコード
012260     INVALID KEY
012270         MOVE NC"作業"  TO ファイル名
012280         PERFORM エラー表示
012290     END-WRITE.
012300*================================================================*
012310 ファイル閉鎖 SECTION.
012320*
012330     CLOSE 施術記録Ｆ 受診者情報Ｆ 負傷データＦ 
                 元号マスタ メモファイル 会計データＦ.
012340*================================================================*
012350 終了処理 SECTION.
012360*
012370     PERFORM ファイル閉鎖.
012380*================================================================*
012390 西暦年取得 SECTION.
012400*
012410     MOVE 計算和暦Ｗ TO 元−元号区分.
012420     READ 元号マスタ
012430     INVALID KEY
012440         DISPLAY NC"指定和暦が登録されていません" UPON CONS
012450         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
012460                                                  UPON CONS
003131*-----------------------------------------*
003132         CALL "actcshm"  WITH C LINKAGE
003133*-----------------------------------------*
012470         ACCEPT  キー入力 FROM CONS
012480         PERFORM 終了処理
012490         EXIT PROGRAM
012500     NOT INVALID KEY
012510         COMPUTE 西暦年Ｗ = 元−開始西暦年 + 計算年Ｗ - 1
012520     END-READ.
012530*
012540*================================================================*
012550 日付絶対値計算 SECTION.
012560*
012570** 1989年を基準とし、当年まで閏年が何回あるかを計算
012580** 元号マスタの西暦開始年確認。
012590*     COMPUTE 西暦年差Ｗ   = 西暦年Ｗ - 1989 - 1.
012600*     DIVIDE 4 INTO 西暦年差Ｗ GIVING 閏年回数Ｗ
012610*                            REMAINDER 剰余Ｗ
012620*     END-DIVIDE.
      */基準年1989年から当年までの閏年の回数を計算(当年は含まない)
      */始めのうるう年の1992年から数える
007500     COMPUTE 西暦年差Ｗ   = 西暦年Ｗ - 1989 - 1.
           MOVE 1992 TO 加算年Ｗ.
           MOVE ZERO TO 閏年回数Ｗ.
           PERFORM UNTIL 西暦年Ｗ <= 加算年Ｗ
               COMPUTE 加算年Ｗ   = 加算年Ｗ   + 4
               COMPUTE 閏年回数Ｗ = 閏年回数Ｗ + 1
           END-PERFORM.
012630*
012640     COMPUTE 通常年回数Ｗ     = 西暦年差Ｗ     - 閏年回数Ｗ.
012650     COMPUTE 通常年絶対値Ｗ   = 通常年回数Ｗ   * 365.
012660     COMPUTE 閏年絶対値Ｗ     = 閏年回数Ｗ     * 366.
012670     COMPUTE 年絶対値Ｗ       = 通常年絶対値Ｗ + 閏年絶対値Ｗ.
012680* 当年がうるう年かどうかを調べる（２月の末日計算）
012690*     COMPUTE 西暦年差２Ｗ     = 西暦年Ｗ + 1.
012700*     DIVIDE 4 INTO 西暦年差２Ｗ GIVING 閏年回数Ｗ
012710*                                REMAINDER 剰余Ｗ
012720*     END-DIVIDE.
012730     DIVIDE 4 INTO 西暦年Ｗ GIVING 閏年回数Ｗ
012740                            REMAINDER 剰余Ｗ
012750     END-DIVIDE.
012760*
012770     MOVE ZERO TO 絶対値月Ｗ.
012780*
012790* 前月までの絶対値を累計
012800     COMPUTE 絶対値月Ｗ２ = 計算月Ｗ * 30.
012810*
012820     EVALUATE 計算月Ｗ
012830     WHEN 3
012840         COMPUTE 絶対値月Ｗ   = 絶対値月Ｗ２ - 1
012850     WHEN 2
012860     WHEN 6
012870     WHEN 7
012880         COMPUTE 絶対値月Ｗ   = 絶対値月Ｗ２ + 1
012890     WHEN 8
012900         COMPUTE 絶対値月Ｗ   = 絶対値月Ｗ２ + 2
012910     WHEN 9
012920     WHEN 10
012930         COMPUTE 絶対値月Ｗ   = 絶対値月Ｗ２ + 3
012940     WHEN 11
012950     WHEN 12
012960         COMPUTE 絶対値月Ｗ   = 絶対値月Ｗ２ + 4
012970     WHEN OTHER
012980         MOVE 絶対値月Ｗ２ TO 絶対値月Ｗ
012990     END-EVALUATE.
013000*
013010     IF ( 剰余Ｗ = ZERO ) AND
013020        ( 計算月Ｗ > 2  )
013030         COMPUTE 絶対値月Ｗ = 絶対値月Ｗ + 1
013040     END-IF.
013050*
013060     COMPUTE 絶対値Ｗ   = 年絶対値Ｗ + 絶対値月Ｗ + 計算日Ｗ.
013070*
      *================================================================*
007990 作業ファイル作成 SECTION.
008000*
008010     OPEN OUTPUT 作業ファイル１.
008020         MOVE NC"作業" TO ファイル名.
008030         PERFORM オープンチェック.
008040*
008050     MOVE 施術和暦ＷＲ  TO 受−施術和暦.
008060     MOVE 施術年ＷＲ    TO 受−施術年.
008070     MOVE 施術月ＷＲ    TO 受−施術月.
008080     MOVE 患者番号ＷＲ  TO 受−患者番号.
008090     MOVE 枝番ＷＲ      TO 受−枝番.
008100     START 受診者情報Ｆ   KEY IS >= 受−施術和暦年月
008110                                    受−患者コード
008120     END-START.
008130     IF 状態キー = "00"
008140         PERFORM 受診者情報Ｆ読込
008150         PERFORM データチェック
008160         IF 実行キーＷ = "YES"
                   MOVE 受−患者番号  TO 施記−患者番号
                   MOVE 受−枝番      TO 施記−枝番
                   MOVE 受−施術和暦  TO 施記−施術和暦
                   MOVE 受−施術年    TO 施記−施術年
                   MOVE 受−施術月    TO 施記−施術月
                   MOVE ZERO          TO 施記−施術日
                   START 施術記録Ｆ   KEY IS >= 施記−患者コード
                                                施記−施術和暦年月日
                   END-START
                   IF 状態キー = "00"
                       MOVE SPACE TO 終了フラグ２
                       PERFORM 施術記録Ｆ読込
                       PERFORM UNTIL ( 終了フラグ２         = "YES"          ) OR
                                     ( 施記−患者コード NOT = 受−患者コード ) OR
                                     ( 施記−施術和暦   NOT = 受−施術和暦   ) OR
                                     ( 施記−施術年     NOT = 受−施術年     ) OR
                                     ( 施記−施術月     NOT = 受−施術月     ) OR
                                     ( 施記−施術日         > 終了日ＷＲ     )
      *
                           MOVE 施記−施術日 TO 施術日ＷＲ
008180                     PERFORM レセプト呼出し２
008190                     PERFORM 項目ごと計算
008200                     PERFORM 作業ファイルセット
008210                     PERFORM ファイル書込
      *
                           PERFORM 施術記録Ｆ読込
008220                 END-PERFORM
                   END-IF
008260         END-IF
008270     END-IF.
008280*
008290     CLOSE 作業ファイル１.
008300*================================================================*
008310 作業ファイルセット SECTION.
008320*
008330     INITIALIZE 作１−レコード.
008050     MOVE 施記−施術和暦     TO 作１−施術和暦.
008060     MOVE 施記−施術年       TO 作１−施術年.
008070     MOVE 施記−施術月       TO 作１−施術月.
008370     MOVE 施記−施術日       TO 作１−施術日.
008380     MOVE 施記−患者コード   TO 作１−患者コード.
008390     MOVE 一部負担金ＷＲ     TO 作１−一部負担金.
           MOVE 会−費用額         TO 作１−費用額.
      *
           MOVE 会−初検料       TO 作１−初検料      .
           MOVE 会−時間外       TO 作１−初検時間外  .
           MOVE 会−休日         TO 作１−初検休日    .
           MOVE 会−深夜         TO 作１−初検深夜    .
           MOVE 会−初検加算料   TO 作１−初検加算料  .
           MOVE 会−再検料       TO 作１−再検料      .
           MOVE 会−往療距離     TO 作１−往療距離    .
           MOVE 会−往療回数     TO 作１−往療回数    .
           MOVE 会−往療料       TO 作１−往療料      .
           MOVE 会−往療加算料   TO 作１−往療加算料  .
           MOVE 会−夜間         TO 作１−往療夜間    .
           MOVE 会−難路         TO 作１−往療難路    .
           MOVE 会−暴風雨雪     TO 作１−往療暴風雨  .
           MOVE 会−時間外       TO 作１−往療時間外  .
      */運動後療料/180518
           MOVE 会−金属副子加算料 TO 作１−金属副子加算料.
           MOVE 会−運動後療料   TO 作１−運動後療料  .
           MOVE 会−初回処置料合計 TO 作１−初回処置料計.
      */後療料
           COMPUTE 作１−後療料 = 
                   会−後療料１   + 会−後療料２   +
                   会−後療料３８ + 会−後療料３０ +
                   会−後療料４５ + 会−後療料４８ + 会−後療料４０.
      */冷罨法
           COMPUTE 作１−冷罨法料 =
                   会−冷罨法料１   + 会−冷罨法料２   +
                   会−冷罨法料３８ + 会−冷罨法料３０ +
                   会−冷罨法料４５ + 会−冷罨法料４８ + 会−冷罨法料４０.
      */温罨法
           COMPUTE 作１−温罨法料 =
                   会−温罨法料１   + 会−温罨法料２   +
                   会−温罨法料３８ + 会−温罨法料３０ +
                   会−温罨法料４５ + 会−温罨法料４８ + 会−温罨法料４０.
      */電療料
           COMPUTE 作１−電療料 =
                   会−電療料１   + 会−電療料２   +
　                 会−電療料３８ + 会−電療料３０ + 
　                 会−電療料４５ + 会−電療料４８ + 会−電療料４０.
      */金属副子
           MOVE 会−大 TO 作１−大.
           MOVE 会−中 TO 作１−中.
           MOVE 会−小 TO 作１−小.
      *
008280     MOVE 施記−施術和暦   TO メモ−施術和暦.
008290     MOVE 施記−施術年     TO メモ−施術年.
008300     MOVE 施記−施術月     TO メモ−施術月.
008310     MOVE 施記−施術日     TO メモ−施術日.
           MOVE 1                TO メモ−制御区分.
004130     MOVE 施記−患者コード TO メモ−患者コード.
           READ メモファイル
           NOT INVALID KEY
               MOVE メモ−施術コメント TO 作１−コメント
           END-READ.
010250*================================================================*
013080******************************************************************
013090 END PROGRAM YCB7211.
013100******************************************************************
