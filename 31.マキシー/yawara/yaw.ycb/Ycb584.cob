000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YCB584.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*      保険医療費総括表【ﾃﾞｰﾀ作成】柔+ｳｨﾝﾄﾞｳｽﾞ版
000100* 請求年月Ver.のみ
000110*      MED = YCB580  YCB585P
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2012-11-14
000140 DATE-COMPILED.          2012-11-14
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000420     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000430                             ORGANIZATION             IS  INDEXED
000440                             ACCESS MODE              IS  DYNAMIC
000450                             RECORD KEY               IS 受−施術和暦年月
000460                                                          受−患者コード
000470                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000480                                                          受−患者カナ
000490                                                          受−患者コード
000500                             ALTERNATE RECORD KEY     IS  受−患者コード
000510                                                         受−施術和暦年月
000520                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000530                                                          受−保険種別
000540                                                          受−保険者番号
000550                                                          受−患者コード
000560                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000570                                                          受−公費種別
000580                                                     受−費用負担者番号
000590                                                          受−患者コード
000600                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000610                                                          受−助成種別
000620                                                  受−費用負担者番号助成
000630                                                          受−患者コード
000640                             ALTERNATE RECORD KEY  IS 受−請求和暦年月
000650                                                      受−施術和暦年月
000660                                                      受−患者コード
000670                             FILE STATUS              IS  状態キー
000680                             LOCK        MODE         IS  AUTOMATIC.
000130     SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
000140                             ORGANIZATION             IS  INDEXED
000150                             ACCESS MODE              IS  DYNAMIC
000160                             RECORD KEY               IS  レセ−施術和暦年月
000170                                                          レセ−患者コード
000180                                                          レセ−レセ種別
000190                             ALTERNATE RECORD KEY     IS  レセ−患者コード
000200                                                          レセ−施術和暦年月
000210                                                          レセ−レセ種別
000220                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000230                                                          レセ−施術和暦年月
000240                                                          レセ−患者コード
000250                                                          レセ−レセ種別
000260                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000270                                                          レセ−レセ種別
000280                                                          レセ−請求保険者番号
000290                                                          レセ−患者コード
000300                                                          レセ−施術和暦年月
000310                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000320                                                          レセ−請求保険者番号
000330                                                          レセ−患者コード
000340                                                          レセ−レセ種別
000350                                                          レセ−施術和暦年月
000360                             FILE STATUS              IS  状態キー
000370                             LOCK        MODE         IS  AUTOMATIC.
000771*
000902     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W5841L.DAT"
000903                             ORGANIZATION             IS  INDEXED
000904                             ACCESS                   IS  DYNAMIC
000905                             RECORD      KEY          IS  作１−分類コード
                                                                作１−保険種別順序
000908                                                          作１−保険者番号
000909                                                          作１−特別区分
000909                                                          作１−本人家族区分
000908                                                          作１−被保険者カナ
000910                                                          作１−患者カナ
000911                                                          作１−患者コード
000912                                                          作１−施術和暦年月
000913                             FILE        STATUS       IS  状態キー
000914                             LOCK        MODE         IS  AUTOMATIC.
001000*
001010******************************************************************
001020*                      DATA DIVISION                             *
001030******************************************************************
001040 DATA                    DIVISION.
001050 FILE                    SECTION.
001120*                           ［ＲＬ＝  ３２０］
001130 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001140     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
      *                          ［ＲＬ＝  １５３６］
       FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS.
           COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
001180*
001181****
001190 FD  作業ファイル１ RECORD  CONTAINS 160 CHARACTERS.
001200 01  作１−レコード.
001210     03  作１−レコードキー.
001261         05  作１−分類コード              PIC 9(2).
               05  作１−保険種別順序            PIC 9(2).
001271         05  作１−保険者番号              PIC X(8).
001280         05  作１−特別区分                PIC 9.
001280         05  作１−本人家族区分            PIC 9.
001260         05  作１−被保険者カナ            PIC X(50).
001300         05  作１−患者カナ                PIC X(50).
001310         05  作１−患者コード.
001320             07 作１−患者番号             PIC 9(6).
001330             07 作１−枝番                 PIC X(1).
001340         05  作１−施術和暦年月.
001350             07  作１−施術和暦            PIC 9.
001360             07  作１−施術年              PIC 9(2).
001370             07  作１−施術月              PIC 9(2).
001380     03  作１−レコードデータ.
001400         05  作１−保険種別                PIC 9(2).
               05  作１−費用額                  PIC 9(6).
               05  作１−請求額                  PIC 9(6).
001260         05  作１−月遅れ区分              PIC 9.
001598         05  FILLER                        PIC X(19).
001599*
001600*----------------------------------------------------------------*
001601******************************************************************
001610*                WORKING-STORAGE SECTION                         *
001620******************************************************************
001630 WORKING-STORAGE         SECTION.
001640 01 キー入力                           PIC X    VALUE SPACE.
001650 01 状態キー                           PIC X(2) VALUE SPACE.
001660 01 終了フラグ                         PIC X(3) VALUE SPACE.
001670 01 終了フラグ２                       PIC X(3) VALUE SPACE.
001680 01 実行キーＷ                         PIC X(3) VALUE SPACE.
001690 01 施術記録有Ｗ                       PIC X(3) VALUE SPACE.
001700 01 ファイル名                         PIC N(8) VALUE SPACE.
001710 01 カウンタ                           PIC 9(2) VALUE ZERO.
001720 01 カウンタ２                         PIC 9(2) VALUE ZERO.
001730 01 対象外フラグ                       PIC X(3) VALUE SPACE.
001740 01 分類フラグ                         PIC X(3) VALUE SPACE.
001750 01 作成フラグ                         PIC X(3) VALUE SPACE.
001760 01 脱出フラグ                         PIC X(3) VALUE SPACE.
001770 01 負担割合Ｗ                         PIC 9    VALUE ZERO.
001780 01 順番Ｗ                             PIC 9(4) VALUE ZERO.
001781 01 助成レコードフラグ                 PIC X(3) VALUE SPACE.
001782 01 保険種別Ｗ                         PIC 9(2) VALUE ZERO.
001790*
001800** 退避用
001801 01 返戻区分ＷＲ                       PIC 9 VALUE ZERO.
001802 01 分類コードＷＲ                     PIC 9 VALUE ZERO.
001830 01 請求和暦年月ＷＲ.
001840    03 請求和暦ＷＲ                    PIC 9    VALUE ZERO.
001850    03 請求年ＷＲ                      PIC 9(2) VALUE ZERO.
001860    03 請求月ＷＲ                      PIC 9(2) VALUE ZERO.
001790*
001870** エラーメッセージ用
001880 01 エラーメッセージＷ.
001890    03 エラー患者コードＷ              PIC X(7) VALUE SPACE.
001900    03 エラー区切りＷ                  PIC X(1) VALUE SPACE.
001910    03 エラー保険種別Ｗ                PIC X(2) VALUE SPACE.
001920    03 FILLER                          PIC X(10) VALUE SPACE.
001930*
001940*****************************************************************
001950 01 計算機西暦年Ｗ                     PIC 9(2).
001960* 日付ＷＯＲＫ
001970 01 計算機西暦.
001980    03 計算機西暦年                    PIC 9(4).
001990    03 計算機西暦月日                  PIC 9(4).
002000 01 計算機西暦Ｒ REDEFINES 計算機西暦.
002010    03 計算機世紀                      PIC 9(2).
002020    03 計算機日付                      PIC 9(6).
002030    03 計算機日付Ｒ REDEFINES 計算機日付.
002040       05 計算機年月                   PIC 9(4).
002050       05 計算機年月Ｒ REDEFINES 計算機年月.
002060         07 計算機年                   PIC 9(2).
002070         07 計算機月                   PIC 9(2).
002080       05 計算機日                     PIC 9(2).
002090*
002100******************************************************************
002110*                          連結項目                              *
002120******************************************************************
002130*
002140********************
002150* メッセージ表示キー *
002160********************
002170 01 連メ−キー IS EXTERNAL.
002180    03  連メ−メッセージ               PIC N(20).
002190*
002200 01 連メ３−キー IS EXTERNAL.
002210    03  連メ３−メッセージ             PIC N(20).
002220    03  連メ３−メッセージ１           PIC X(20).
002230****************
002240* 画面入力情報 *
002250****************
002700 01 連入−画面情報ＹＣＢ５８０ IS EXTERNAL.
002710    03 連入−施術和暦年月.
002720       05 連入−施術和暦                PIC 9.
002730       05 連入−施術年月.
002740         07 連入−施術年                PIC 9(2).
002750         07 連入−施術月                PIC 9(2).
          03 連入−プレビュー区分             PIC 9.
002363*
002660******************************************************************
002670*                      PROCEDURE  DIVISION                       *
002680******************************************************************
002690 PROCEDURE               DIVISION.
002700************
002710*           *
002720* 初期処理   *
002730*           *
002740************
002750     PERFORM 初期化.
002760************
002770*           *
002780* 主処理     *
002790*           *
002800************
002810     PERFORM 作業ファイル作成.
002820************
002830*           *
002840* 終了処理   *
002850*           *
002860************
002870     PERFORM 終了処理.
002880     MOVE ZERO TO PROGRAM-STATUS.
002890     EXIT PROGRAM.
002900*
002910*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002920*================================================================*
002930 初期化 SECTION.
002940*
002950     PERFORM ファイルオープン.
002960* 連結項目の待避
002970     MOVE 連入−施術和暦      TO 請求和暦ＷＲ.
002980     MOVE 連入−施術年        TO 請求年ＷＲ.
002990     MOVE 連入−施術月        TO 請求月ＷＲ.
003000*
003010*================================================================*
003020 ファイルオープン SECTION.
003030*
003070     OPEN INPUT 受診者情報Ｆ.
003080         MOVE NC"受診者情報Ｆ" TO ファイル名.
003090         PERFORM オープンチェック.
006630     OPEN INPUT レセプトＦ.
006640             MOVE NC"レセ" TO ファイル名.
006650             PERFORM オープンチェック.
003121*
003130*================================================================*
003140 オープンチェック SECTION.
003150*
003160     IF 状態キー  NOT =  "00"
003170         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
003180         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003190         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003200                                                 UPON CONS
004380*-----------------------------------------*
004390         CALL "actcshm"  WITH C LINKAGE
004400*-----------------------------------------*
003210         ACCEPT  キー入力 FROM CONS
003220         PERFORM ファイル閉鎖
003230         MOVE 99 TO PROGRAM-STATUS
003240         EXIT PROGRAM.
003250*================================================================*
003260 ファイル閉鎖 SECTION.
003270*
003280     CLOSE 受診者情報Ｆ レセプトＦ.
003290*================================================================*
003300 終了処理 SECTION.
003310*
003320     PERFORM ファイル閉鎖.
003330*================================================================*
003340 エラー表示 SECTION.
003350*
003360     DISPLAY NC"状態キー" 状態キー  UPON CONS.
003370     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
003380     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
003390     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
004380*-----------------------------------------*
004390     CALL "actcshm"  WITH C LINKAGE.
004400*-----------------------------------------*
003400     ACCEPT  キー入力 FROM CONS.
003410     PERFORM ファイル閉鎖.
003420     MOVE 99 TO PROGRAM-STATUS.
003430     EXIT PROGRAM.
003440*================================================================*
003450 作業ファイル作成 SECTION.
003460*
003470     OPEN OUTPUT 作業ファイル１.
003480         MOVE NC"作１" TO ファイル名.
003490         PERFORM オープンチェック.
003500*
004760     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
004770     MOVE 請求年ＷＲ    TO レセ−請求年.
004780     MOVE 請求月ＷＲ    TO レセ−請求月.
004790     MOVE ZERO          TO レセ−施術和暦.
004800     MOVE ZERO          TO レセ−施術年.
004810     MOVE ZERO          TO レセ−施術月.
004820     MOVE ZERO          TO レセ−患者番号.
004830     MOVE LOW-VALUE     TO レセ−枝番.
013730     MOVE ZERO          TO レセ−レセ種別.
004840     START レセプトＦ   KEY IS >= レセ−請求和暦年月
004850                                  レセ−施術和暦年月
004860                                  レセ−患者コード
000250                                  レセ−レセ種別
003620     END-START.
003630     IF 状態キー = "00"
003640         MOVE SPACE  TO 終了フラグ
003650         PERFORM レセプトＦ読込
003660         PERFORM UNTIL ( 終了フラグ = "YES" ) OR
004920                       ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
004930                       ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
004940                       ( レセ−請求月   NOT = 請求月ＷＲ   )
005430            PERFORM データチェック
012328            IF  実行キーＷ = "YES"
003730*
003732               IF レセ−レセ種別 =  1 OR 2 OR 3
      */返戻分を対象にしない/080219
                        IF レセ−請求区分 = 2
                           MOVE SPACE TO 実行キーＷ
                        END-IF
003737                  IF  実行キーＷ = "YES"
003738                     PERFORM レコードセット
003739                  END-IF
003741               END-IF
                  END-IF
003850            PERFORM レセプトＦ読込
003860         END-PERFORM
003870     END-IF.
003880*
003890     CLOSE 作業ファイル１.
003900*
004046*================================================================*
004050 データチェック SECTION.
004060*
004070     MOVE SPACE  TO 実行キーＷ.
019520* *****************************************************************
019530* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
019540* *****************************************************************
019640     IF ( レセ−請求対象区分 NOT = ZERO ) AND
005778        ( レセ−償還払い区分 NOT = 1 )
              IF (レセ−レセ種別 = 3) AND ( レセ−会総括表印刷対象区分 = 1 )
                  CONTINUE
              ELSE
004090           MOVE レセ−施術和暦  TO 受−施術和暦
004100           MOVE レセ−施術年    TO 受−施術年
004110           MOVE レセ−施術月    TO 受−施術月
004120           MOVE レセ−患者番号  TO 受−患者番号
004130           MOVE レセ−枝番      TO 受−枝番
                 READ 受診者情報Ｆ
                 NOT INVALID KEY
019880              MOVE "YES"  TO 実行キーＷ
019950           END-READ
              END-IF
019960     END-IF.
004530*
004540*================================================================*
004640 レセプトＦ読込 SECTION.
004650*
004660     READ レセプトＦ NEXT
004670     AT END
004680         MOVE "YES" TO 終了フラグ
004690     END-READ.
004700*
004780*================================================================*
004790 レコードセット SECTION.
004800*
004801     MOVE SPACE TO 助成レコードフラグ.
004828********
004829***  / 助成 /
004830     IF (レセ−レセ種別 = 3)
004844         MOVE "YES" TO 助成レコードフラグ
004853     END-IF.
004854*
004803*
004811     PERFORM レコード明細セット.
004827*
      */名古屋市福祉分は本体分と別に集計
           PERFORM 名古屋市福祉判定.
005442*================================================================*
005443 レコード明細セット SECTION.
005456*
005473     MOVE SPACE TO 作１−レコード.
005474     INITIALIZE 作１−レコード.
005478     MOVE 受−施術和暦       TO 作１−施術和暦.
005479     MOVE 受−施術年         TO 作１−施術年.
005480     MOVE 受−施術月         TO 作１−施術月.
005485     MOVE 受−被保険者カナ   TO 作１−被保険者カナ.
005481     MOVE 受−患者コード     TO 作１−患者コード.
005485     MOVE 受−患者カナ       TO 作１−患者カナ.
005527     MOVE 受−本人家族区分   TO 作１−本人家族区分.
005482*
005483     IF 助成レコードフラグ  = "YES"
005542         PERFORM 分類取得
005517         MOVE 受−費用負担者番号助成  TO 作１−保険者番号
005518         MOVE 受−助成種別            TO 作１−保険種別
               MOVE レセ−一部負担金        TO 作１−費用額
               MOVE レセ−助成請求金額      TO 作１−請求額
005524     ELSE
005526         IF 受−公費種別 = ZERO
005531             MOVE 受−保険者番号      TO 作１−保険者番号
005532             MOVE 受−保険種別        TO 作１−保険種別
                   MOVE レセ−合計          TO 作１−費用額
                   MOVE レセ−請求金額      TO 作１−請求額
005533         ELSE
005534             MOVE 1                   TO 作１−本人家族区分
005538             MOVE 受−費用負担者番号  TO 作１−保険者番号
005539             MOVE 受−公費種別        TO 作１−保険種別
                   MOVE レセ−合計          TO 作１−費用額
                   MOVE レセ−請求金額      TO 作１−請求額
005540         END-IF
005541*
005542         PERFORM 分類取得
005543*
005545     END-IF.
005564*
           IF 受−公費種別 = 05
               IF 受−施術和暦年月 < 42004
                   EVALUATE 受−老人負担金免除
                   WHEN 3
                       MOVE 2          TO 作１−特別区分
                   WHEN OTHER
                       MOVE 1          TO 作１−特別区分
                   END-EVALUATE
               ELSE
                   EVALUATE 受−特別区分
                   WHEN 1
                       MOVE 1           TO 作１−特別区分
                   WHEN 3
                       MOVE 3           TO 作１−特別区分
               END-IF
           ELSE
               EVALUATE 受−保険種別
               WHEN 01
                   EVALUATE 受−特別区分
                   WHEN 1
                   WHEN 2
                       MOVE 1      TO 作１−特別区分
                   WHEN 3
                       MOVE 2      TO 作１−特別区分
                   WHEN 6
                       MOVE 5      TO 作１−特別区分
                   WHEN OTHER
                       IF レセ−負担割合 = 3
                           MOVE 4  TO 作１−特別区分
                       ELSE
                           MOVE 3  TO 作１−特別区分
                       END-IF
                   END-EVALUATE
               WHEN 08
                   EVALUATE 受−特別区分
                   WHEN 1
                   WHEN 2
                       MOVE 2      TO 作１−特別区分
                   WHEN 3
                       MOVE 3      TO 作１−特別区分
                   WHEN 6
                       MOVE 5      TO 作１−特別区分
                   WHEN OTHER
                       IF 受−本人家族区分 = 1
                           MOVE 1  TO 作１−特別区分
                       ELSE
                           MOVE 4  TO 作１−特別区分
                       END-IF
                   END-EVALUATE
               END-EVALUATE
           END-IF.
      *
005649     IF レセ−請求区分 = 1
005650        MOVE  1      TO   作１−月遅れ区分
005651     ELSE
005652        MOVE  ZERO   TO   作１−月遅れ区分
005653     END-IF.
005654*
005655****
005656     WRITE 作１−レコード.
005657     IF 状態キー  NOT =  "00"
005658         MOVE NC"作１"  TO ファイル名
005659         PERFORM エラー表示
005660     END-IF.
005661*
005705*================================================================*
005706 分類取得 SECTION.
005707***************************************************
005708* 分類コード
005711*  1:国保
005712*  2:退職
      *  3:国保組合
005713*  4:２７老人（国保）
005713*  5:２７老人（社保）
005713*  6:後期高齢者
005709*  7:社保・日雇・船員
005710*  8:組合
      *  9:共済・自衛官
005715* 10:４１老
      * 11:母子
      * 12:障害
005714* 13:被爆者
      * 14:乳児
      * 15:その他
005716*
005718***************************************************
005719*
005720     IF 助成レコードフラグ  = "YES"
005723         EVALUATE 受−助成種別
               WHEN 51
005724             MOVE 10 TO  作１−分類コード
005725         WHEN 52
005726             MOVE 11 TO  作１−分類コード
005725         WHEN 53
005726             MOVE 12 TO  作１−分類コード
005725         WHEN 54
005726             MOVE 13 TO  作１−分類コード
005725         WHEN 55
005726             MOVE 14 TO  作１−分類コード
               WHEN 60
      */名古屋市福祉の対応/080417
      *             IF 受−費用負担者番号助成 = "89231005"
005726*                 MOVE 20 TO  作１−分類コード
      *             ELSE
005726                 MOVE 15 TO  作１−分類コード
      *             END-IF
005727         END-EVALUATE
      */名古屋市福祉分は本体分と別に集計
      **         IF (( 受−公費種別 = ZERO ) AND
      **             ( 受−助成種別 = 51 OR 60 ) AND
      **             ( 受−老人負担金免除 = 1 ) AND
      **             ( 受−費用負担者番号助成(3:3) = "234" ))
005726**             MOVE 20 TO  作１−分類コード
      **         END-IF
005728     ELSE
005729         IF 受−公費種別 = ZERO
005730*       / 健保 /
005731             EVALUATE 受−保険種別
005741             WHEN 01
005745                 IF ( 受−保険者番号(3:1) = "3" )
      */国保系(国保、退職、国保組合)、老人系(国保老人、社保老人)を改頁しないで続きに印刷する/0801
005748*                     MOVE 3 TO  作１−分類コード
005748                     MOVE 1 TO  作１−分類コード
                           MOVE 3 TO  作１−保険種別順序
005749                 ELSE
005750                     MOVE 1 TO  作１−分類コード
                           MOVE 1 TO  作１−保険種別順序
005752                 END-IF
005732             WHEN 02
005733             WHEN 06
005734             WHEN 07
005736                 MOVE 7 TO  作１−分類コード
005737             WHEN 03
005740                 MOVE 8 TO  作１−分類コード
005738             WHEN 04
005739             WHEN 09
005740                 MOVE 9 TO  作１−分類コード
005753             WHEN 08
      */国保系(国保、退職、国保組合)、老人系(国保老人、社保老人)を改頁しないで続きに印刷する/0801
005755*                 MOVE 2 TO  作１−分類コード
005755                 MOVE 1 TO  作１−分類コード
                       MOVE 2 TO  作１−保険種別順序
005765             END-EVALUATE
005766*
005767         ELSE
005768*       / 老人 /
                   IF 受−保険種別 = 01 OR 08
      */国保系(国保、退職、国保組合)、老人系(国保老人、社保老人)を改頁しないで続きに印刷する/0801
005770                 MOVE 4 TO  作１−分類コード
                       MOVE 1 TO  作１−保険種別順序
                   ELSE
      */国保系(国保、退職、国保組合)、老人系(国保老人、社保老人)を改頁しないで続きに印刷する/0801
005770*                 MOVE 5 TO  作１−分類コード
005770                 MOVE 4 TO  作１−分類コード
                       MOVE 2 TO  作１−保険種別順序
                   END-IF
                   IF 受−保険種別 = 05
005770                 MOVE 6 TO  作１−分類コード
                   END-IF
      */名古屋市福祉分は本体分と別に集計
005210**             IF (( 受−老人負担金免除 = 1 ) AND
005220**                 ( 受−費用負担者番号(3:3) = "234" ))
005230**               OR
005240**                (( 受−助成負担金免除 = 1 ) AND
005250**                 ( 受−費用負担者番号(3:3) = "234" ))
005726**                 MOVE 20 TO  作１−分類コード
      **             END-IF
005778         END-IF
005779     END-IF.
005780*
005806*================================================================*
       名古屋市福祉判定 SECTION.
      *
      */(レコードセット済み)
005210     IF (( 受−老人負担金免除      = 1     ) AND
005220         ( 受−費用負担者番号(3:3) = "234" )) OR
005240        (( 受−助成負担金免除      = 1     ) AND
005250         ( 受−費用負担者番号(3:3) = "234" ))
005726         MOVE 20               TO 作１−分類コード
               MOVE レセ−一部負担金 TO 作１−請求額
      */国保系(国保、退職、国保組合)、老人系(国保老人、社保老人)を改頁しないで続きに印刷する/0801
               MOVE ZERO             TO 作１−保険種別順序
005656         WRITE 作１−レコード
005657         IF 状態キー  NOT =  "00"
005658             MOVE NC"作１"  TO ファイル名
005659             PERFORM エラー表示
005660         END-IF
           END-IF.
      */後期高齢
005210     IF (( 受−助成負担金免除      = 1     ) AND
005250         ( 受−費用負担者番号(1:5) = "39231" ))
005726         MOVE 20               TO 作１−分類コード
               MOVE レセ−一部負担金 TO 作１−請求額
      */国保系(国保、退職、国保組合)、老人系(国保老人、社保老人)を改頁しないで続きに印刷する/0801
               MOVE ZERO             TO 作１−保険種別順序
005656         WRITE 作１−レコード
005657         IF 状態キー  NOT =  "00"
005658             MOVE NC"作１"  TO ファイル名
005659             PERFORM エラー表示
005660         END-IF
           END-IF.
           IF (( 受−公費種別 = ZERO ) AND
               ( 受−助成種別 = 51 OR 60 ) AND
               ( 受−老人負担金免除 = 1 ) AND
               ( 受−費用負担者番号助成(3:3) = "234" ))
005726         MOVE 20                 TO 作１−分類コード
               MOVE レセ−合計         TO 作１−費用額
               MOVE レセ−助成請求金額 TO 作１−請求額
      */国保系(国保、退職、国保組合)、老人系(国保老人、社保老人)を改頁しないで続きに印刷する/0801
               MOVE ZERO               TO 作１−保険種別順序
005656         WRITE 作１−レコード
005657         IF 状態キー  NOT =  "00"
005658             MOVE NC"作１"  TO ファイル名
005659             PERFORM エラー表示
005660         END-IF
           END-IF.
005442*================================================================*
005911******************************************************************
005912 END PROGRAM YCB584.
005913******************************************************************
