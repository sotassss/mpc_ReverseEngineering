000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YCB7221.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*         中部柔整師協会 カルテ裏【印刷】
000100*         MED = YCB720 YCB7221P
000110*----------------------------------------------------------------*
000120 DATE-WRITTEN.           2012-11-13
000130 DATE-COMPILED.          2012-11-13
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000320     SELECT  制御情報マスタ  ASSIGN      TO        SEIGYOL
000330                             ORGANIZATION             IS  INDEXED
000340                             ACCESS MODE              IS  DYNAMIC
000350                             RECORD KEY               IS  制−制御区分
000360                             FILE STATUS              IS  状態キー
000370                             LOCK        MODE         IS  AUTOMATIC.
000380     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000390                             ORGANIZATION             IS  INDEXED
000400                             ACCESS MODE              IS  DYNAMIC
000410                             RECORD KEY               IS 受−施術和暦年月
000420                                                          受−患者コード
000430                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000440                                                          受−患者カナ
000450                                                          受−患者コード
000460                             ALTERNATE RECORD KEY     IS  受−患者コード
000470                                                         受−施術和暦年月
000480                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000490                                                          受−保険種別
000500                                                          受−保険者番号
000510                                                          受−患者コード
000520                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000530                                                          受−公費種別
000540                                                     受−費用負担者番号
000550                                                          受−患者コード
000560                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000570                                                          受−助成種別
000580                                                  受−費用負担者番号助成
000590                                                          受−患者コード
000600                             ALTERNATE RECORD KEY  IS 受−請求和暦年月
000610                                                      受−施術和暦年月
000620                                                      受−患者コード
000630                             FILE STATUS              IS  状態キー
000640                             LOCK        MODE         IS  AUTOMATIC.
000620     SELECT  負傷データＦ    ASSIGN      TO        HUSYOUL
000630                             ORGANIZATION             IS  INDEXED
000640                             ACCESS MODE              IS  DYNAMIC
000650                             RECORD KEY               IS 負−施術和暦年月
000660                                                         負−患者コード
000670                             ALTERNATE RECORD KEY     IS 負−患者コード
000680                                                         負−施術和暦年月
000690                             FILE STATUS              IS  状態キー
000700                             LOCK        MODE         IS  AUTOMATIC.
000260     SELECT  施術記録Ｆ      ASSIGN      TO        SEKIROKL
000270                             ORGANIZATION             IS  INDEXED
000280                             ACCESS MODE              IS  DYNAMIC
000290                             RECORD KEY           IS 施記−施術和暦年月日
000300                                                     施記−患者コード
000310                             ALTERNATE RECORD KEY IS 施記−患者コード
000320                                                     施記−施術和暦年月日
000330                             FILE STATUS              IS  状態キー
000340                             LOCK        MODE         IS  AUTOMATIC.
000130     SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
000140                             ORGANIZATION             IS  INDEXED
000150                             ACCESS MODE              IS  DYNAMIC
000160                             RECORD KEY               IS  レセ−施術和暦年月
000170                                                          レセ−患者コード
000180                                                          レセ−レセ種別
000190                             ALTERNATE RECORD KEY     IS  レセ−患者コード
000200                                                          レセ−施術和暦年月
000210                                                          レセ−レセ種別
000220                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000230                                                          レセ−施術和暦年月
000240                                                          レセ−患者コード
000250                                                          レセ−レセ種別
000260                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000270                                                          レセ−レセ種別
000280                                                          レセ−請求保険者番号
000290                                                          レセ−患者コード
000300                                                          レセ−施術和暦年月
000310                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000320                                                          レセ−請求保険者番号
000330                                                          レセ−患者コード
000340                                                          レセ−レセ種別
000350                                                          レセ−施術和暦年月
000360                             FILE STATUS              IS  状態キー
000370                             LOCK        MODE         IS  AUTOMATIC.
000650     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W72111L.DAT"
000660                             ORGANIZATION             IS  INDEXED
000670                             ACCESS                   IS  DYNAMIC
000680                             RECORD      KEY    IS  作１−施術和暦年月日
000690                             FILE        STATUS       IS  状態キー
000700                             LOCK        MODE         IS  AUTOMATIC.
000710*
000720     SELECT  印刷ファイル    ASSIGN      TO     GS-PRTF002
000730                             SYMBOLIC    DESTINATION  IS "PRT"
000740                             FORMAT                   IS  定義体名Ｐ
000750                             GROUP                    IS  項目群名Ｐ
000760                             PROCESSING  MODE         IS  処理種別Ｐ
000770                             UNIT        CONTROL      IS  拡張制御Ｐ
000780                             FILE        STATUS       IS  通知情報Ｐ.
000790******************************************************************
000800*                      DATA DIVISION                             *
000810******************************************************************
000820 DATA                    DIVISION.
000830 FILE                    SECTION.
000870*                           ［ＲＬ＝  ２５６］
000880 FD  制御情報マスタ          BLOCK   CONTAINS   1   RECORDS.
000890     COPY SEIGYO          OF  XFDLIB  JOINING   制   AS  PREFIX.
000900*                           ［ＲＬ＝  ３２０］
000910 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
000920     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
000940*                           ［ＲＬ＝  ６４０］
000950 FD  負傷データＦ        BLOCK   CONTAINS   1   RECORDS.
000960     COPY HUSYOU          OF  XFDLIB  JOINING   負   AS  PREFIX.
000880*                           ［ＲＬ＝  ２５６］
000890 FD  施術記録Ｆ          BLOCK   CONTAINS   1   RECORDS.
000900     COPY SEKIROK         OF  XFDLIB  JOINING   施記 AS  PREFIX.
      *                          ［ＲＬ＝  １５３６］
       FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS.
           COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
000930*                           ［ＲＬ＝  ２５６］
000940 FD  作業ファイル１ RECORD  CONTAINS 256 CHARACTERS.
000950 01 作１−レコード.
000960    03 作１−レコードキー.
000970       05  作１−施術和暦年月日.
000980          07 作１−施術和暦               PIC 9.
000990          07 作１−施術年月.
001000             09 作１−施術年              PIC 9(2).
001010             09 作１−施術月              PIC 9(2).
001020          07 作１−施術日                 PIC 9(2).
001030    03 作１−レコードデータ.
001040       05 作１−患者コード.
001050          07 作１−患者番号               PIC 9(6).
001060          07 作１−枝番                   PIC X.
001080       05 作１−日チェック                PIC 9.
             05 作１−費用額                    PIC 9(6).
001130       05 作１−一部負担金                PIC 9(5).
001551       05 作１−コメント                  PIC X(100).
      *
             05 作１−初検料                    PIC 9(5).
             05 作１−初検加算.
                07 作１−初検時間外             PIC 9.
                07 作１−初検休日               PIC 9.
                07 作１−初検深夜               PIC 9.
             05 作１−初検加算料                PIC 9(5).
             05 作１−再検料                    PIC 9(5).
             05 作１−往療.
                07 作１−往療距離               PIC 9(2)V9.
                07 作１−往療回数               PIC 9(2).
                07 作１−往療料                 PIC 9(5).
                07 作１−往療加算料             PIC 9(5).
                07 作１−往療加算.
                   09 作１−往療夜間            PIC 9.
                   09 作１−往療難路            PIC 9.
                   09 作１−往療暴風雨          PIC 9.
                   09 作１−往療時間外          PIC 9.
             05 作１−初回処置料計              PIC 9(6).
             05 作１−後療料                    PIC 9(6).
             05 作１−冷罨法料                  PIC 9(5).
             05 作１−温罨法料                  PIC 9(5).
             05 作１−電療料                    PIC 9(5).
             05 作１−大                        PIC 9(1).
             05 作１−中                        PIC 9(1).
             05 作１−小                        PIC 9(1).
             05 作１−金属副子加算料            PIC 9(5).
             05 作１−運動後療料                PIC 9(4).
001210       05 FILLER                          PIC X(54).
001150*
001160 FD  印刷ファイル.
001170     COPY YCB7221P        OF  XMDLIB.
001180*----------------------------------------------------------------*
001190******************************************************************
001200*                WORKING-STORAGE SECTION                         *
001210******************************************************************
001220 WORKING-STORAGE         SECTION.
001230 01 キー入力                           PIC X     VALUE SPACE.
001240 01 状態キー                           PIC X(2)  VALUE SPACE.
001250 01 終了フラグ                         PIC X(3)  VALUE SPACE.
001260 01 終了フラグ２                       PIC X(3)  VALUE SPACE.
001270 01 初検フラグ                         PIC X(3)  VALUE SPACE.
001280 01 ファイル名                         PIC N(6)  VALUE SPACE.
001290 01 レセプトＰＧＷ                     PIC X(8)  VALUE SPACE.
001300 01 前和暦Ｗ                           PIC 9     VALUE ZERO.
001310 01 カレント元号Ｗ                     PIC 9(1)  VALUE ZERO.
001320 01 日位置ＣＮＴ                       PIC 9(2)  VALUE ZERO.
001330 01 部位ＣＮＴ                         PIC 9     VALUE ZERO.
001340 01 患者番号Ｗ                         PIC 9(6)  VALUE ZERO.
       01 行カウンタ                         PIC 9(2)  VALUE ZERO.
       01 文字ＣＮＴ                         PIC 9(2)  VALUE ZERO.
002520 01 部位数Ｗ                           PIC 9     VALUE ZERO.
001210 01 オープンフラグ                     PIC X(3)   VALUE SPACE.
001350**
001360 01 遅延フラグ                         PIC X(3) VALUE SPACE.
001370 01 遅延回数Ｗ                         PIC 9(4) VALUE ZERO.
001380 01 遅延ＣＮＴ                         PIC 9(5) VALUE ZERO.
001390**
       01 費用額Ｗ                           PIC 9(6) VALUE ZERO.
       01 請求額Ｗ                           PIC 9(6) VALUE ZERO.
       01 負担額Ｗ                           PIC 9(6) VALUE ZERO.
      *
       01 コメントＷ.
         03 コメントＷ１                     PIC X(30) VALUE SPACE.
         03 コメントＷ２                     PIC X(30) VALUE SPACE.
         03 コメントＷ３                     PIC X(30) VALUE SPACE.
       01 改頁フラグ                         PIC 9(1) VALUE ZERO.
001400*
001540 01 退避項目ＧＷ.
001550   03 レセプト種類Ｗ                 PIC X(4) VALUE SPACE.
001560   03 レセプト種類ＧＷ               PIC X(4) VALUE SPACE.
001570   03 レセプト種別ＧＷ               PIC 9(2) VALUE ZERO.
      *
       01 往療加算回数Ｗ                   PIC 9(2) VALUE ZERO.
      * 01 終了和暦年月日Ｗ                 PIC 9(7) VALUE ZERO.
      *
       01 部位カウンタ                     PIC 9(2) VALUE ZERO.
       01 部位カウンタ２                   PIC 9(2) VALUE ZERO.
      *
       01 継続部位数Ｗ                       PIC 9(2) VALUE ZERO.
      *
       01 開始和暦年月日Ｗ.
         03 開始和暦年月Ｗ.
           05 開始和暦Ｗ                     PIC 9.
           05 開始年月Ｗ.
              07 開始年Ｗ                    PIC 9(2).
              07 開始月Ｗ                    PIC 9(2).
         03 開始日Ｗ                         PIC 9(2).
      *
       01 終了和暦年月日Ｗ.
         03 終了和暦年月Ｗ.
           05 終了和暦Ｗ                     PIC 9.
           05 終了年月Ｗ.
              07 終了年Ｗ                    PIC 9(2).
              07 終了月Ｗ                    PIC 9(2).
         03 終了日Ｗ                         PIC 9(2).
      *
       01 後療可能算定数ＫＷ                 PIC 9(2) VALUE ZERO.
       01 転帰区分Ｗ                         PIC 9 VALUE ZERO.
001410****************
001420* 連結項目待避 *
001430****************
001440*    ************
001450*    * 印刷キー *
001460*    ************
001470 01 対象データＷＲ.
001480    03 施術和暦年月ＷＲ.
001490       05 施術和暦ＷＲ                  PIC 9(1)  VALUE ZERO.
001500       05 施術年ＷＲ                    PIC 9(2)  VALUE ZERO.
001510       05 施術月ＷＲ                    PIC 9(2)  VALUE ZERO.
001520    03 開始日ＷＲ                       PIC 9(2)  VALUE ZERO.
001530    03 終了日ＷＲ                       PIC 9(2)  VALUE ZERO.
001540    03 患者カナＷＲ                     PIC X(50) VALUE SPACE.
001550    03 患者コードＷＲ.
001560       05 患者番号ＷＲ                  PIC 9(6)  VALUE ZERO.
001570       05 枝番ＷＲ                      PIC X(1)  VALUE SPACE.
001580    03 印刷モードＦＷＲ                 PIC 9(1)  VALUE ZERO.
001590    03 日ごとモードＷＲ                 PIC 9(1)  VALUE ZERO.
001600    03 氏名モードＷＲ                   PIC 9(1)  VALUE ZERO.
001610    03 年月モードＷＲ                   PIC 9(1)  VALUE ZERO.
001630    03 下部モードＷＲ                   PIC 9(1)  VALUE ZERO.
001630    03 合計モードＷＲ                   PIC 9(1)  VALUE ZERO.
001630    03 コメントモードＷＲ               PIC 9(1)  VALUE ZERO.
001640    03 印字位置ＣＮＴ                   PIC 9     VALUE ZERO.
          03 開始段数ＷＲ                     PIC 9(2)  VALUE ZERO.
001650**************
001660* 受診者情報 *
001670**************
001680 01 受診者情報Ｗ.
001690    03 施術年月Ｗ.
001700       05 施術年Ｗ                     PIC 9(2)   VALUE ZERO.
001710       05 施術月Ｗ                     PIC 9(2)   VALUE ZERO.
001720    03 保険者番号Ｗ.
001730       05 印刷保険者番号Ｗ             PIC X(6)   VALUE SPACE.
001740       05 FILLER                       PIC X(4)   VALUE SPACE.
001750    03 退職保険者番号Ｗ.
001760       05 FILLER                       PIC X(2)   VALUE SPACE.
001770       05 退職印刷保険者番号Ｗ         PIC X(6)   VALUE SPACE.
001780       05 FILLER                       PIC X(2)   VALUE SPACE.
001790    03 市町村番号Ｗ.
001800       05 印刷市町村番号Ｗ             PIC X(8)   VALUE SPACE.
001810       05 FILLER                       PIC X(2)   VALUE SPACE.
001820    03 保険種別Ｗ                      PIC 9(2)   VALUE ZERO.
001830    03 患者情報Ｗ.
001840       05 患者カナＷ                   PIC X(50)  VALUE SPACE.
001850       05 患者氏名Ｗ                   PIC X(50)  VALUE SPACE.
002160 01 明細.
002170    03 電罨料Ｗ                           PIC 9(4) VALUE ZERO.
002180    03 一部負担金Ｗ                       PIC 9(5) VALUE ZERO.
002190    03 部位Ｗ                             OCCURS 4.
002200       05 部位計Ｗ                        PIC 9(4) VALUE ZERO.
002210       05 日数Ｗ                          PIC 9(2) VALUE ZERO.
002220*******************************************************************
002230 01 印刷制御.
002240     03 定義体名Ｐ                     PIC X(8) VALUE SPACE.
002250     03 項目群名Ｐ                     PIC X(8) VALUE SPACE.
002260     03 処理種別Ｐ                     PIC X(2) VALUE SPACE.
002270     03 拡張制御Ｐ.
002280         05 端末制御Ｐ.
002290             07 移動方向Ｐ             PIC X(1) VALUE SPACE.
002300             07 移動行数Ｐ             PIC 9(3) VALUE ZERO.
002310         05 詳細制御Ｐ                 PIC X(2) VALUE SPACE.
002320     03 通知情報Ｐ                     PIC X(2) VALUE SPACE.
002330     03 ユニット名Ｐ                   PIC X(8) VALUE SPACE.
002340*
002350 01 計算機西暦年Ｗ                     PIC 9(2) VALUE ZERO.
002360* 日付ＷＯＲＫ
002370 01 和暦終了年Ｗ                       PIC 9(4) VALUE ZERO.
002380 01 計算機西暦.
002390    03 計算機西暦年                    PIC 9(4) VALUE ZERO.
002400    03 計算機西暦月日                  PIC 9(4) VALUE ZERO.
002410 01 計算機西暦Ｒ REDEFINES 計算機西暦.
002420    03 計算機世紀                      PIC 9(2).
002430    03 計算機日付                      PIC 9(6).
002440    03 計算機日付Ｒ REDEFINES 計算機日付.
002450       05 計算機年月                   PIC 9(4).
002460       05 計算機年月Ｒ REDEFINES 計算機年月.
002470         07 計算機年                   PIC 9(2).
002480         07 計算機月                   PIC 9(2).
002490       05 計算機日                     PIC 9(2).
002500*
002510******************************************************************
002520*                          連結項目                              *
002530******************************************************************
002820****************
002830* 画面入力情報 *
002840****************
002850 01 連入−入力データＹＣＢ７２０ IS EXTERNAL.
002860    03 連入−施術和暦年月.
002870       05 連入−施術和暦                  PIC 9(1).
002880       05 連入−施術年                    PIC 9(2).
002890       05 連入−施術月                    PIC 9(2).
002900    03 連入−開始日付.
002910       05 連入−開始日                    PIC 9(2).
002920    03 連入−終了日付.
002930       05 連入−終了日                    PIC 9(2).
002940    03 連入−患者コード.
002950       05 連入−患者番号                  PIC 9(6).
002960       05 連入−枝番                      PIC X(1).
002970    03 連入−印刷モードＦ                 PIC 9(1).
002980    03 連入−日ごとモード                 PIC 9(1).
002990    03 連入−氏名モード                   PIC 9(1).
          03 連入−コメントモード               PIC 9(1).
          03 連入−印刷段数                     PIC 9(2).
      */連続時の氏名印刷の有無0302
          03 連入−処理モード                   PIC X(4).
003070*
006580 01 連レ−キー IS EXTERNAL.
006590    03 連レ−保険種別                       PIC 9(2).
006600*
006040**********************
006050* メッセージ表示キー *
006060**********************
006070 01 連メ−キー IS EXTERNAL.
006080    03  連メ−メッセージ                 PIC N(20).
      *
004100 01 連メ５１−キー IS EXTERNAL.
004110    03  連メ５１−メッセージ               PIC N(20).
004120    03  連メ５１−初期値                   PIC X.
004121    03  連メ５１−返り値                   PIC X.
003020*
       01 連入−表示フラグＹＣＢ７２０ IS EXTERNAL GLOBAL.
          03 連入−プレビュー区分               PIC 9(1).
000540************************************
000550* プリンタファイル作成用           *
000560************************************
000570 01 Ｈ連ＰＲＴＦ−作成データ IS EXTERNAL.
000580     03 Ｈ連ＰＲＴＦ−ファイル名           PIC X(8).
000590     03 Ｈ連ＰＲＴＦ−プレビュー区分       PIC 9.
000600     03 Ｈ連ＰＲＴＦ−帳票プログラム名     PIC X(8).
000610     03 Ｈ連ＰＲＴＦ−オーバレイ名         PIC X(8).
000993************************************
000994* プリンタファイル作成特殊用       *
000995************************************
000996 01 Ｈ連特殊ＰＲＴＦ−作成データ IS EXTERNAL.
000997     03 Ｈ連特殊ＰＲＴＦ−用紙種類         PIC X(8).
007308*
003110******************************************************************
003120*                      PROCEDURE  DIVISION                       *
003130******************************************************************
003140 PROCEDURE               DIVISION.
003150************
003160*           *
003170* 初期処理   *
003180*           *
003190************
002570     PERFORM プリンタファイル作成.
003200     PERFORM 初期化.
003210     PERFORM 制御情報取得.
003220************
003230*           *
003240* 主処理     *
003250*           *
003260************
003270* 印刷
           MOVE ZERO TO 改頁フラグ.
003280     PERFORM 連結項目待避.
003290     PERFORM 印刷セット.
      *
      */改頁して印刷していた場合、次ページの印刷を選ぶ。：１
      */改頁して印刷しなかった場合、無条件で印刷しない。：２
      */改頁をしていなかった場合、無条件で印刷する。    ：０
           EVALUATE 改頁フラグ
           WHEN 1
               MOVE  NC"　　　次の用紙をセットしてください" TO 連メ５１−メッセージ
               MOVE "Y" TO 連メ５１−初期値
               CALL   "MSG0051"
               CANCEL "MSG0051"
           WHEN 2
               MOVE "N" TO 連メ５１−返り値
           WHEN ZERO
               MOVE "Y" TO 連メ５１−返り値
           WHEN OTHER
               MOVE "Y" TO 連メ５１−返り値
           END-EVALUATE
      *
           IF 連メ５１−返り値 = "Y"
003300         PERFORM 印刷処理
               PERFORM 印刷行数退避
           END-IF.
003310************
003320*           *
003330* 終了処理   *
003340*           *
003350************
003360     PERFORM 終了処理.
003370     PERFORM 遅延処理.
003380     EXIT PROGRAM.
003390*
003400*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002860*================================================================*
002870 プリンタファイル作成 SECTION.
002880*================================================================*
002890*   / 初期化 /
002900     MOVE SPACE TO Ｈ連ＰＲＴＦ−作成データ.
002910     INITIALIZE Ｈ連ＰＲＴＦ−作成データ.
002225     MOVE SPACE TO Ｈ連特殊ＰＲＴＦ−作成データ.
002226     INITIALIZE Ｈ連特殊ＰＲＴＦ−作成データ.
002920*
002930*
002940*--↓↓ 変更箇所 ↓↓--------------------------------------*
002230*   使用する用紙種別セット
           MOVE "KARUTE"              TO Ｈ連特殊ＰＲＴＦ−用紙種類.
002970*   使用するプリンタファイル名セット
002231     MOVE "PRTF002"             TO Ｈ連ＰＲＴＦ−ファイル名.
002972*
002973*   使用する帳票プログラム名セット
002974     MOVE "YCB7221"             TO Ｈ連ＰＲＴＦ−帳票プログラム名.
002975*
002976*--↑↑-----------------------------------------------------*
002980*
002990*   / プレビュー区分セット /
003000     MOVE 連入−プレビュー区分 TO Ｈ連ＰＲＴＦ−プレビュー区分.
003000*     MOVE 1 TO Ｈ連ＰＲＴＦ−プレビュー区分.
003010*
003020     CALL   "CRTPRTF".
003030     CANCEL "CRTPRTF".
003040*
003410*================================================================*
003420 初期化 SECTION.
003430*
004000     OPEN INPUT   制御情報マスタ
004010         MOVE NC"制御情報" TO ファイル名.
004020         PERFORM オープンチェック.
004030     OPEN INPUT   受診者情報Ｆ.
004040         MOVE NC"受情" TO ファイル名.
004050         PERFORM オープンチェック.
007250     OPEN INPUT 負傷データＦ.
007260         MOVE NC"負傷" TO ファイル名.
007270         PERFORM オープンチェック.
007190     OPEN INPUT 施術記録Ｆ.
007200         MOVE NC"施記" TO ファイル名.
007210         PERFORM オープンチェック.
007870     OPEN INPUT レセプトＦ.
007880         MOVE NC"レセ"         TO ファイル名.
007890         PERFORM オープンチェック.
004060     OPEN INPUT 作業ファイル１
004070         MOVE NC"作業１" TO ファイル名.
004080         PERFORM オープンチェック.
004110*================================================================*
004120 オープンチェック SECTION.
004130*
004140     IF 状態キー  NOT =  "00"
004150         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
004160         DISPLAY NC"状態キー：" 状態キー         UPON CONS
004170         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004180                                                 UPON CONS
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
004190         ACCEPT  キー入力 FROM CONS
004200         PERFORM ファイル閉鎖
004210         EXIT PROGRAM.
004220*================================================================*
004230 制御情報取得 SECTION.
004240*
004250     MOVE ZERO TO 制−制御区分
004260     READ 制御情報マスタ
004270     NOT INVALID KEY
004280         MOVE 制−遅延回数       TO 遅延回数Ｗ
004290     END-READ.
004300*
004310*================================================================*
004320 遅延処理 SECTION.
004330*
004340     PERFORM VARYING 遅延ＣＮＴ FROM 1 BY 1
004350               UNTIL 遅延ＣＮＴ > 遅延回数Ｗ
004360         MOVE SPACE TO 遅延フラグ
004370     END-PERFORM.
004380*
004390*================================================================*
004400 連結項目待避 SECTION.
004410*
004420     MOVE 連入−施術和暦           TO 施術和暦ＷＲ.
004430     MOVE 連入−施術年             TO 施術年ＷＲ.
004440     MOVE 連入−施術月             TO 施術月ＷＲ.
004450     MOVE 連入−開始日             TO 開始日ＷＲ.
004460     MOVE 連入−終了日             TO 終了日ＷＲ.
004470     MOVE 連入−患者番号           TO 患者番号ＷＲ.
004480     MOVE 連入−枝番               TO 枝番ＷＲ.
004490     MOVE 連入−印刷モードＦ       TO 印刷モードＦＷＲ.
004500     MOVE 連入−日ごとモード       TO 日ごとモードＷＲ.
004510     MOVE 連入−氏名モード         TO 氏名モードＷＲ.
004540     MOVE 連入−コメントモード     TO コメントモードＷＲ.
           MOVE 連入−印刷段数           TO 開始段数ＷＲ.
004560*================================================================*
004570 印刷セット SECTION.
004580*
004590     PERFORM 項目初期化.
004600     PERFORM 受診者情報取得.
004640     IF 氏名モードＷＲ = 1
004650         MOVE 患者氏名Ｗ     TO 患者氏名
      *         MOVE 患者番号ＷＲ   TO 患者番号
      *         MOVE 枝番ＷＲ       TO 枝番
004660     END-IF.
004800     IF (日ごとモードＷＲ   = 1) OR
              (コメントモードＷＲ = 1)
               PERFORM 明細行セット
               PERFORM 月毎金額セット
004890     END-IF.
004900*================================================================*
005310 項目初期化 SECTION.
005320*
005330     INITIALIZE 受診者情報Ｗ.
005350     INITIALIZE 明細.
005360     MOVE SPACE TO YCB7221P.
005370     INITIALIZE YCB7221P.
005380*================================================================*
005390 明細行セット SECTION.
005480     MOVE 施術和暦ＷＲ       TO 作１−施術和暦
005490     MOVE 施術年ＷＲ         TO 作１−施術年
005500     MOVE 施術月ＷＲ         TO 作１−施術月
005510     MOVE 開始日ＷＲ         TO 作１−施術日
005520     START 作業ファイル１ KEY IS >= 作１−施術和暦年月日
005530     END-START
005540     IF 状態キー = "00"
005550         PERFORM 作業ファイル１読込
               IF 開始段数ＷＲ = ZERO
                   MOVE 1 TO 行カウンタ
               ELSE
                   MOVE 開始段数ＷＲ TO 行カウンタ
               END-IF
               PERFORM UNTIL 終了フラグ = "YES"
005470             IF 日ごとモードＷＲ = 1
                       MOVE 作１−費用額       TO 費用額      (行カウンタ)
                       MOVE 作１−施術月       TO 施術月      (行カウンタ)
                       MOVE 作１−施術日       TO 施術日      (行カウンタ)
                       MOVE 作１−一部負担金   TO 負担額      (行カウンタ)
                       MOVE 作１−初検加算料   TO 初検加算料  (行カウンタ)
                       IF 施術和暦年月ＷＲ < 43006
                           MOVE 作１−初回処置料計 TO 初回処置料計(行カウンタ)
                       ELSE
                           COMPUTE 初回処置料計(行カウンタ) = 作１−初回処置料計 + 作１−金属副子加算料
                                                            + 作１−運動後療料
                       END-IF
                       MOVE 作１−後療料       TO 後療料計    (行カウンタ)
                       MOVE 作１−電療料       TO 電療料計    (行カウンタ)
                       COMPUTE 初検再検往療料(行カウンタ) = 作１−初検料 + 
                              作１−再検料 + 作１−往療料 + 作１−往療加算料
                       COMPUTE 罨法料計(行カウンタ) = 作１−冷罨法料 +
                                                      作１−温罨法料
      *
005690             END-IF
                   IF コメントモードＷＲ = 1
                       MOVE 作１−コメント       TO コメントＷ
                       MOVE コメントＷ１         TO コメント１(行カウンタ)
                       MOVE コメントＷ２         TO コメント２(行カウンタ)
                       MOVE コメントＷ３         TO コメント３(行カウンタ)
                   END-IF
      *
                   COMPUTE 行カウンタ = 行カウンタ + 1
005700             PERFORM 作業ファイル１読込
      *
      */明細行が３１行で改頁
                   IF (行カウンタ > 31) AND (終了フラグ = SPACE)
                       IF 改頁フラグ NOT = ZERO
                           MOVE  NC"　　　次の用紙をセットしてください" TO 連メ５１−メッセージ
                           MOVE "Y" TO 連メ５１−初期値
                           CALL   "MSG0051"
                           CANCEL "MSG0051"
                       ELSE
                           MOVE "Y" TO 連メ５１−返り値
                       END-IF
                       IF 連メ５１−返り値 = "Y"
                           MOVE 1 TO 改頁フラグ
                           PERFORM 印刷処理
                           PERFORM 改頁処理
                           PERFORM 印刷行数退避
                           MOVE SPACE TO YCB7221P
　                         INITIALIZE YCB7221P
                           MOVE 1 TO 行カウンタ
      *                   */改頁後は氏名を印刷する
                           MOVE 患者氏名Ｗ TO 患者氏名
                       ELSE
                           MOVE 2 TO 改頁フラグ
                           MOVE "YES" TO 終了フラグ
                       END-IF
                   END-IF
005710         END-PERFORM
005720     END-IF.
005740*================================================================*
006890 受診者情報取得 SECTION.
006900*
006910**************************************************
006920* 連結データから受診者情報Ｆより以下の情報を取得 *
006930* ● 施術年 ..... 施術年Ｗに格納                 *
006940* ● 施術月 ..... 施術月Ｗに格納                 *
006950* ● 患者番号.... 患者番号Ｗに格納※ＦＤ連番用   *
006960* ● 患者氏名 ....患者氏名Ｗに格納               *
006970**************************************************
006980     MOVE 施術和暦ＷＲ       TO 受−施術和暦.
006990     MOVE 施術年ＷＲ         TO 受−施術年.
007000     MOVE 施術月ＷＲ         TO 受−施術月.
007010     MOVE 患者コードＷＲ     TO 受−患者コード.
007020     READ 受診者情報Ｆ
007030     INVALID KEY
007040         CONTINUE
007050*            /* ありえない */
007060     NOT INVALID KEY
007070         MOVE 受−施術年       TO 施術年Ｗ
007080         MOVE 受−施術月       TO 施術月Ｗ
007090         MOVE 受−患者番号     TO 患者番号Ｗ
007100         MOVE 受−患者氏名     TO 患者氏名Ｗ
007110     END-READ.
007120*================================================================*
007130 作業ファイル１読込 SECTION.
007140*
007150     READ 作業ファイル１ NEXT
007160     AT END
007170         MOVE "YES" TO 終了フラグ
007180     END-READ.
007190*
007200*================================================================*
007210 印刷処理 SECTION.
007220*
004310     IF ( オープンフラグ NOT = "YES" )
004320        MOVE "YES" TO オープンフラグ
004330        OPEN I-O  印刷ファイル
004340        PERFORM エラー処理Ｐ
004350     END-IF.
013440*
007230     MOVE "YCB7221P"  TO  定義体名Ｐ.
007240     MOVE "GRP001"   TO  項目群名Ｐ.
007250     WRITE YCB7221P.
007260     PERFORM エラー処理Ｐ.
007270*================================================================*
007280 エラー処理Ｐ SECTION.
007290*
007300     IF 通知情報Ｐ NOT = "00"
007310         DISPLAY NC"帳票エラー"              UPON CONS
007320         DISPLAY NC"項目群名Ｐ：" 項目群名Ｐ UPON CONS
007330         DISPLAY NC"通知情報Ｐ：" 通知情報Ｐ UPON CONS
007340         DISPLAY NC"拡張制御Ｐ：" 拡張制御Ｐ UPON CONS
007350         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
007360                                             UPON CONS
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
007370         ACCEPT  キー入力 FROM CONS
007380         PERFORM ファイル閉鎖
007390         MOVE 99 TO PROGRAM-STATUS
007400         EXIT PROGRAM
007410     END-IF.
007420*================================================================*
007430 ファイル閉鎖 SECTION.
007440*
002990     IF ( オープンフラグ = "YES" )
002991         CLOSE 印刷ファイル
003041     END-IF.
007460     CLOSE 制御情報マスタ 受診者情報Ｆ 
                 負傷データＦ  施術記録Ｆ     レセプトＦ.
007470*================================================================*
007480 終了処理 SECTION.
007490*
007500     PERFORM ファイル閉鎖.
007510*================================================================*
006860 印刷行数退避 SECTION.
006870     CLOSE 受診者情報Ｆ.
006880     PERFORM 遅延処理.
006890     OPEN I-O 受診者情報Ｆ.
006900         MOVE NC"受診" TO ファイル名.
006910         PERFORM オープンチェック.
006920     PERFORM 遅延処理.
006930*
006940     MOVE ZERO            TO 受−施術和暦.
006950     MOVE ZERO            TO 受−施術年.
006960     MOVE ZERO            TO 受−施術月.
006970     MOVE 患者コードＷＲ  TO 受−患者コード.
006990*
007000     READ 受診者情報Ｆ
007010     NOT INVALID KEY
               IF 行カウンタ > 31
                   MOVE 1 TO 行カウンタ
               END-IF
007020         MOVE 行カウンタ TO 受−カルテ裏印刷行数
007030         REWRITE 受−レコード
007040         INVALID KEY
007050             MOVE NC"受Ｆ" TO ファイル名
007060             PERFORM エラー表示
007070         END-REWRITE
007080     END-READ.
007090     PERFORM 遅延処理.
007100     CLOSE 受診者情報Ｆ.
007110     PERFORM 遅延処理.
007120     OPEN INPUT 受診者情報Ｆ.
007130         MOVE NC"受診" TO ファイル名.
007140         PERFORM オープンチェック.
007530*================================================================*
007620 エラー表示 SECTION.
007630*
007640     DISPLAY ファイル名   NC"ファイル書込エラー"   UPON CONS.
007650     DISPLAY NC"状態キー：" 状態キー               UPON CONS.
007660     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
007670     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
                                                         UPON CONS.
000080*-----------------------------------------*
000090     CALL "actcshm"  WITH C LINKAGE.
000100*-----------------------------------------*
007680     ACCEPT  キー入力 FROM CONS.
007690     PERFORM ファイル閉鎖.
007700     EXIT PROGRAM.
007290*================================================================*
012090 施術記録Ｆ読込 SECTION.
012100*
012110     READ 施術記録Ｆ NEXT
012120     AT END
012130         MOVE "YES" TO 終了フラグ２
012140     END-READ.
009040*================================================================*
009050 レセプト呼出し１ SECTION.
009060*
           IF 受−助成種別 NOT = ZERO
              MOVE  3   TO レセ−レセ種別
           ELSE
              IF 受−公費種別 NOT = ZERO
                 MOVE  2   TO レセ−レセ種別
              ELSE
                 MOVE  1   TO レセ−レセ種別
              END-IF
           END-IF.
005200     MOVE 受−施術和暦  TO レセ−施術和暦.
005210     MOVE 受−施術年    TO レセ−施術年.  
005220     MOVE 受−施術月    TO レセ−施術月.  
005230     MOVE 受−患者番号  TO レセ−患者番号.
005240     MOVE 受−枝番      TO レセ−枝番.    
           READ レセプトＦ.
009370*
023480*================================================================*
003740 改頁処理  SECTION.
003750*
003822     CLOSE  印刷ファイル.
004320     MOVE SPACE TO オープンフラグ.
003823*     OPEN I-O   印刷ファイル.
003824*     PERFORM エラー処理Ｐ.
003825*
007610*================================================================*
       月毎金額セット SECTION.
      *
006980     MOVE 施術和暦ＷＲ       TO 受−施術和暦.
006990     MOVE 施術年ＷＲ         TO 受−施術年.
007000     MOVE 施術月ＷＲ         TO 受−施術月.
007010     MOVE 患者コードＷＲ     TO 受−患者コード.
007020     READ 受診者情報Ｆ
007030     INVALID KEY
007040         CONTINUE
007050*            /* ありえない */
007060     NOT INVALID KEY
007700         PERFORM レセプト呼出し１
           END-READ.
      *
           IF 施術和暦年月ＷＲ < 43006
               COMPUTE 金属情報提供料 = レセ−金属副子加算料 + レセ−施術情報提供料
           ELSE
               MOVE レセ−施術情報提供料 TO 金属情報提供料
           END-IF.
      *
009040*================================================================*
007520******************************************************************
007530 END PROGRAM YCB7221.
007540******************************************************************
