000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YGN588.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*         群馬県用　決議書印刷（柔+ｳｨﾝﾄﾞｳｽﾞ版）
000100*         MED = YGN580 YGN588P
000110*----------------------------------------------------------------*
000120 DATE-WRITTEN.           2016-05-27
000130 DATE-COMPILED.          2016-05-27
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000380     SELECT  施術所情報マスタ ASSIGN      TO        SEJOHOL
000390                             ORGANIZATION             IS  INDEXED
000400                             ACCESS MODE              IS  DYNAMIC
000410                             RECORD KEY               IS  施情−施術所番号
000420                             FILE STATUS              IS  状態キー
000430                             LOCK        MODE         IS  AUTOMATIC.
000450     SELECT  保険者マスタ    ASSIGN      TO        HOKENSL
000460                             ORGANIZATION             IS  INDEXED
000470                             ACCESS MODE              IS  DYNAMIC
000480                             RECORD KEY               IS  保−保険種別
000490                                                          保−保険者番号
000500                             ALTERNATE RECORD KEY     IS  保−保険種別
000510                                                          保−保険者名称
000520                                                          保−保険者番号
000530                             FILE STATUS              IS  状態キー
000540                             LOCK        MODE         IS  AUTOMATIC.
000260     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000270                             ORGANIZATION             IS  INDEXED
000280                             ACCESS MODE              IS  DYNAMIC
000290                             RECORD KEY               IS  元−元号区分
000300                             FILE STATUS              IS  状態キー
000310                             LOCK        MODE         IS  AUTOMATIC.
000720     SELECT  作業ファイル１  ASSIGN      TO      "C:\MAKISHISYS\YAWOBJ\TEMP\W5871L.DAT"
000730                             ORGANIZATION             IS  INDEXED
000740                             ACCESS                   IS  DYNAMIC
000750                             RECORD      KEY          IS  作１−保険者番号
000800                             FILE        STATUS       IS  状態キー
000810                             LOCK        MODE         IS  AUTOMATIC.
000640     SELECT  印刷ファイル    ASSIGN      TO     GS-PRTF001
000650                             SYMBOLIC    DESTINATION  IS "PRT"
000660                             FORMAT                   IS  定義体名Ｐ
000670                             GROUP                    IS  項目群名Ｐ
000680                             PROCESSING  MODE         IS  処理種別Ｐ
000690                             UNIT        CONTROL      IS  拡張制御Ｐ
000700                             FILE        STATUS       IS  通知情報Ｐ.
000710******************************************************************
000720*                      DATA DIVISION                             *
000730******************************************************************
000740 DATA                    DIVISION.
000750 FILE                    SECTION.
000820*                           ［ＲＬ＝  １２８］
000830 FD  施術所情報マスタ          BLOCK   CONTAINS   1   RECORDS.
000840     COPY SEJOHO         OF  XFDLIB  JOINING   施情   AS  PREFIX.
000910*                           ［ＲＬ＝  ３２０］
000920 FD  保険者マスタ        BLOCK   CONTAINS   1   RECORDS.
000930     COPY HOKENS          OF  XFDLIB  JOINING   保   AS  PREFIX.
000760*                           ［ＲＬ＝  １２８］
000770 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
000780     COPY GENGOU          OF  XFDLIB  JOINING   元   AS  PREFIX.
000880*
001030 FD  作業ファイル１ RECORD  CONTAINS  64 CHARACTERS.
001040 01  作１−レコード.
001050     03  作１−レコードキー.
001690         05  作１−保険者番号.
001700             07 作１−法別番号               PIC X(2).
001710             07 作１−保番.
001720                09 作１−都道府県番号        PIC X(2).
001730                09 作１−保番号              PIC X(3).
001740                09 作１−検証番号            PIC X.
001750                09 FILLER                    PIC X(2).
001190     03  作１−レコードデータ.
001790         05  作１−保険種別                  PIC 9(2).
001790         05  作１−本人件数                  PIC 9(4).
001800         05  作１−本人日数                  PIC 9(4).
001200         05  作１−本人請求額                PIC 9(7).
001790         05  作１−家族件数                  PIC 9(4).
001800         05  作１−家族日数                  PIC 9(4).
001200         05  作１−家族請求額                PIC 9(7).
001210         05  FILLER                          PIC X(22).
000990*
001000 FD  印刷ファイル.
001010     COPY YGN588P        OF  XMDLIB.
001020*----------------------------------------------------------------*
001030******************************************************************
001040*                WORKING-STORAGE SECTION                         *
001050******************************************************************
001060 WORKING-STORAGE         SECTION.
001070 01 キー入力                           PIC X     VALUE SPACE.
001080 01 状態キー                           PIC X(2)  VALUE SPACE.
001090 01 終了フラグ                         PIC X(3)  VALUE SPACE.
001100 01 ファイル名                         PIC N(2)  VALUE SPACE.
001110 01 カウンタ                           PIC 9(2)  VALUE ZERO.
001210 01 オープンフラグ                     PIC X(3)   VALUE SPACE.
001150*
001160****************
001170* 連結項目待避 *
001180****************
001190 01 施術和暦年月ＷＲ.
001200    03 施術和暦ＷＲ                    PIC 9(1)  VALUE ZERO.
001210    03 施術年月ＷＲ.
001220       05 施術年ＷＲ                   PIC 9(2)  VALUE ZERO.
001230       05 施術月ＷＲ                   PIC 9(2)  VALUE ZERO.
002030 01 保険者番号ＷＲ.
002040    03 保険者法別番号Ｗ                PIC X(2)  VALUE SPACE.
002050    03 保険者番号１Ｗ.
002060       05 保険者県コードＷ             PIC X(2)  VALUE SPACE.
002070       05 保険者番号２Ｗ               PIC X(6)  VALUE SPACE.
001300*
001310**************
001320* 施術所情報 *
001330**************
001340 01 施術所情報Ｗ.
001350    03 柔整師番号Ｗ                    PIC X(22)  VALUE SPACE.
001360    03 接骨師会会員番号Ｗ              PIC X(16)  VALUE SPACE.
001370    03 代表者カナＷ                    PIC X(50)  VALUE SPACE.
001380    03 代表者名Ｗ                      PIC X(50)  VALUE SPACE.
001390    03 接骨院名Ｗ                      PIC X(50)  VALUE SPACE.
001410*
001420**************
001430* 費用額情報 *
001440**************
001450 01 合計Ｗ.
001470    05 本人件数Ｗ                      PIC 9(4)  VALUE ZERO.
001470    05 本人日数Ｗ                      PIC 9(4)  VALUE ZERO.
001480    05 本人請求額Ｗ                    PIC 9(8)  VALUE ZERO.
001490    05 家族件数Ｗ                      PIC 9(4)  VALUE ZERO.
001490    05 家族日数Ｗ                      PIC 9(4)  VALUE ZERO.
001500    05 家族請求額Ｗ                    PIC 9(8)  VALUE ZERO.
002090 01 保険種別Ｗ                         PIC 9(2)  VALUE ZERO.
002000 01 請求先名称Ｗ                       PIC X(30) VALUE SPACE.
002000 01 支部名Ｗ                           PIC X(30) VALUE SPACE.
001510*
001520 01 印刷制御.
001530     03 定義体名Ｐ                     PIC X(8) VALUE SPACE.
001540     03 項目群名Ｐ                     PIC X(8) VALUE SPACE.
001550     03 処理種別Ｐ                     PIC X(2) VALUE SPACE.
001560     03 拡張制御Ｐ.
001570         05 端末制御Ｐ.
001580             07 移動方向Ｐ             PIC X(1).
001590             07 移動行数Ｐ             PIC 9(3).
001600         05 詳細制御Ｐ                 PIC X(2).
001610     03 通知情報Ｐ                     PIC X(2) VALUE SPACE.
001620     03 ユニット名Ｐ                   PIC X(8) VALUE SPACE.
001630*
001800******************************************************************
001810*                          連結項目                              *
001820******************************************************************
001830*
       01 連入−入力データＹＧＮ５８０ IS EXTERNAL.
          03 連入−施術和暦年月.
             05 連入−施術和暦               PIC 9(1).
             05 連入−施術年月.
                07 連入−施術年              PIC 9(2).
                07 連入−施術月              PIC 9(2).
003240*
       01 連入−入力データＹＧＮ５８５ IS EXTERNAL GLOBAL.
          03 連入−請求和暦年月日.
             05 連入−請求和暦               PIC 9(1).
             05 連入−請求年                 PIC 9(2).
             05 連入−請求月                 PIC 9(2).
             05 連入−請求日                 PIC 9(2).
          03 連入−保険者番号                PIC X(10).
      *
       01 連入−画面情報ＹＧＮ５８０１ IS EXTERNAL.
          03 連入−元号訂正区分            PIC 9.
      *
       01 連入−プレビュー IS EXTERNAL GLOBAL.
          03 連入−プレビュー区分            PIC 9.
      *
001960*
001970 01 連メ−キー IS EXTERNAL.
001980    03  連メ−メッセージ               PIC N(20).
001990*
000540************************************
000550* プリンタファイル作成用           *
000560************************************
000570 01 Ｈ連ＰＲＴＦ−作成データ IS EXTERNAL.
000580     03 Ｈ連ＰＲＴＦ−ファイル名           PIC X(8).
000590     03 Ｈ連ＰＲＴＦ−プレビュー区分       PIC 9.
000600     03 Ｈ連ＰＲＴＦ−帳票プログラム名     PIC X(8).
000610     03 Ｈ連ＰＲＴＦ−オーバレイ名         PIC X(8).
002560*
002000******************************************************************
002010*                      PROCEDURE  DIVISION                       *
002020******************************************************************
002030 PROCEDURE               DIVISION.
002040************
002050*           *
002060* 初期処理   *
002070*           *
002080************
002560     MOVE SPACE TO オープンフラグ.
002570     PERFORM プリンタファイル作成.
002090     PERFORM 初期化.
002100************
002110*           *
002120* 主処理     *
002130*           *
002140************
002150     PERFORM 連結項目待避.
002160     PERFORM 印刷セット.
002170     PERFORM 印刷処理.
002180************
002190*           *
002200* 終了処理   *
002210*           *
002220************
002230     PERFORM 終了処理.
002240     MOVE ZERO  TO PROGRAM-STATUS.
002250     EXIT PROGRAM.
002260*
002270*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002860*================================================================*
002870 プリンタファイル作成 SECTION.
002880*================================================================*
002890*   / 初期化 /
002900     MOVE SPACE TO Ｈ連ＰＲＴＦ−作成データ.
002910     INITIALIZE Ｈ連ＰＲＴＦ−作成データ.
002920*
002930*
002940*--↓↓ 変更箇所 ↓↓--------------------------------------*
002970*   使用するプリンタファイル名セット
002971     MOVE "PRTF001"             TO Ｈ連ＰＲＴＦ−ファイル名.
002972*
002973*   使用する帳票プログラム名セット
002974     MOVE "YGN588"             TO Ｈ連ＰＲＴＦ−帳票プログラム名.
002975*
002976*--↑↑-----------------------------------------------------*
002980*
002990*   / プレビュー区分セット /
003000     MOVE 連入−プレビュー区分 TO Ｈ連ＰＲＴＦ−プレビュー区分.
003010*
003020     CALL   "CRTPRTF".
003030     CANCEL "CRTPRTF".
003040*
002280*================================================================*
002290 初期化 SECTION.
002300*================================================================*
002530     OPEN INPUT   施術所情報マスタ
002540         MOVE NC"施情" TO ファイル名.
002550         PERFORM オープンチェック.
003620     OPEN INPUT 保険者マスタ.
003630         MOVE NC"保険者マスタ" TO ファイル名.
003640         PERFORM オープンチェック.
002470     OPEN INPUT   元号マスタ
002480         MOVE NC"元号" TO ファイル名.
002490         PERFORM オープンチェック.
002590     OPEN INPUT   作業ファイル１.
002600         MOVE NC"作１" TO ファイル名.
002610         PERFORM オープンチェック.
002640*
002650*================================================================*
002660 オープンチェック SECTION.
002670*
002680     IF 状態キー  NOT =  "00"
002690         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
002700         DISPLAY NC"状態キー：" 状態キー         UPON CONS
002710         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
002720                                                 UPON CONS
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
002730         ACCEPT  キー入力 FROM CONS
002740         PERFORM ファイル閉鎖
002750         EXIT PROGRAM.
002760*
003150*================================================================*
003160 連結項目待避 SECTION.
003170*================================================================*
003180     MOVE 連入−施術和暦           TO 施術和暦ＷＲ.
003190     MOVE 連入−施術年             TO 施術年ＷＲ.
003200     MOVE 連入−施術月             TO 施術月ＷＲ.
003200     MOVE 連入−保険者番号         TO 保険者番号ＷＲ.
003250*
003260*================================================================*
003270 印刷セット SECTION.
003280*================================================================*
003290     PERFORM 項目初期化.
003300*
003310     PERFORM 施術所情報取得.
003320     PERFORM 請求額情報取得.
003310     PERFORM 請求先情報取得.
003330*
003340********************
003350* 印刷情報セット *
003360********************
003370*
           MOVE 支部名Ｗ          TO 支部名.
      */元号修正/↓↓↓20190514
037370     IF (施術和暦ＷＲ > 4) AND (連入−元号訂正区分 = ZERO)
              MOVE 施術和暦ＷＲ     TO 元−元号区分
037380        READ 元号マスタ
037390        NOT INVALID KEY
037400            MOVE 元−元号名称   TO 施術和暦
037410        END-READ
              MOVE "===="             TO 施術和暦訂正
           END-IF.
      */元号修正/↑↑↑20190514
           MOVE 施術年ＷＲ        TO 施術年.
           MOVE 施術月ＷＲ        TO 施術月.
003380     MOVE 本人件数Ｗ        TO 本人件数.
003380     MOVE 本人日数Ｗ        TO 本人日数.
003390     MOVE 本人請求額Ｗ      TO 本人費用額.
003400     MOVE 家族件数Ｗ        TO 家族件数.
003400     MOVE 家族日数Ｗ        TO 家族日数.
003410     MOVE 家族請求額Ｗ      TO 家族費用額.
003630*
003590     MOVE 柔整師番号Ｗ(6:4) TO 柔整師番号.
003592*
003600     MOVE 代表者名Ｗ        TO 代表者名.
003670*
003680*****     PERFORM テスト印字処理.
003690*
003700*================================================================*
003710 項目初期化 SECTION.
003720*================================================================*
003730     INITIALIZE 施術所情報Ｗ.
003740     INITIALIZE 合計Ｗ.
003750     MOVE SPACE TO YGN588P.
003760*****     INITIALIZE YGN588P.
003770*
003780*================================================================*
003790 施術所情報取得 SECTION.
003800*================================================================*
003810**************************************************
003820* 本院データを使用し、以下の情報を取得           *
003830* ● 柔整師番号.. 柔整師番号Ｗに格納             *
003840* ● 会員番号 ... 接骨師会会員番号Ｗに格納       *
003850* ● 代表者名 ... 代表者名Ｗに格納               *
003860**************************************************
003870     MOVE ZERO  TO 施情−施術所番号.
003880     READ 施術所情報マスタ
003890     INVALID KEY
003900         CONTINUE
003910     NOT INVALID KEY
003970         MOVE 施情−新柔整師番号     TO 柔整師番号Ｗ
003980         MOVE 施情−接骨師会会員番号 TO 接骨師会会員番号Ｗ
003990*
004000         MOVE 施情−代表者カナ       TO 代表者カナＷ
004010         MOVE 施情−代表者名         TO 代表者名Ｗ
004020         MOVE 施情−接骨院名         TO 接骨院名Ｗ
004030     END-READ.
004130*
004140*================================================================*
004150 請求額情報取得 SECTION.
004160*================================================================*
004170     MOVE SPACE TO 終了フラグ.
004180*
           MOVE 保険者番号ＷＲ    TO 作１−保険者番号.
004190     READ 作業ファイル１
004200     NOT INVALID KEY
004300*
004330         MOVE 作１−保険種別    TO 保険種別Ｗ
               MOVE 作１−本人件数    TO 本人件数Ｗ
004330         MOVE 作１−本人日数    TO 本人日数Ｗ
004340         MOVE 作１−本人請求額  TO 本人請求額Ｗ
004350         MOVE 作１−家族件数    TO 家族件数Ｗ
004350         MOVE 作１−家族日数    TO 家族日数Ｗ
004360         MOVE 作１−家族請求額  TO 家族請求額Ｗ
004390*
004400     END-READ.
004410*
006222*================================================================*
006230 請求先情報取得 SECTION.
006240*
006250*********************************************************
006260* 連結データから保険者マスタより請求先を取得する。      *
006270* ● 請求先...... 請求先名称Ｗに格納                    *
006280*********************************************************
006290     MOVE 保険種別Ｗ          TO 保−保険種別.
006300     MOVE 保険者番号ＷＲ      TO 保−保険者番号.
006310     READ 保険者マスタ
006320     INVALID KEY
006330         MOVE SPACE           TO 請求先名称Ｗ 支部名Ｗ
006340     NOT INVALID KEY
006350         UNSTRING 保−保険者名称  DELIMITED BY "全国健康保険協会" OR "支部"
                   INTO 請求先名称Ｗ 支部名Ｗ
               END-UNSTRING
006360     END-READ.
004500*================================================================*
004510 印刷処理 SECTION.
004520*================================================================*
006050     IF ( オープンフラグ NOT = "YES" )
006060        MOVE "YES" TO オープンフラグ
006070        OPEN I-O  印刷ファイル
006080        PERFORM エラー処理Ｐ
006090     END-IF.
003640*
004530     MOVE "YGN588P"  TO  定義体名Ｐ.
004540     MOVE "SCREEN"   TO  項目群名Ｐ.
004550     WRITE YGN588P.
004560     PERFORM エラー処理Ｐ.
004570*
004580*================================================================*
004590 終了処理 SECTION.
004600*================================================================*
004610*
004620     PERFORM ファイル閉鎖.
004630*
004640*================================================================*
004650 ファイル閉鎖 SECTION.
004660*
003570     IF ( オープンフラグ = "YES" )
003580         CLOSE 印刷ファイル
003590     ELSE
003600         MOVE  NC"　　データが０件です。確認して下さい。" TO 連メ−メッセージ
003610         CALL   "MSG001"
003620         CANCEL "MSG001"
003630     END-IF.
003640*
004670     CLOSE 施術所情報マスタ 保険者マスタ  作業ファイル１ 元号マスタ.
004690*
004700*================================================================*
004710*================================================================*
004720 エラー表示 SECTION.
004730*
004740     DISPLAY NC"状態キー" 状態キー  UPON CONS.
004750     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
004760     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
004770     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
004380*-----------------------------------------*
004390     CALL "actcshm"  WITH C LINKAGE.
004400*-----------------------------------------*
004780     ACCEPT  キー入力 FROM CONS.
004790     PERFORM ファイル閉鎖.
004800     EXIT PROGRAM.
004810*
004820*================================================================*
004830 エラー処理Ｐ SECTION.
004840*
004850     IF 通知情報Ｐ NOT = "00"
004860         DISPLAY NC"帳票エラー"              UPON CONS
004870         DISPLAY NC"項目群名Ｐ：" 項目群名Ｐ UPON CONS
004880         DISPLAY NC"通知情報Ｐ：" 通知情報Ｐ UPON CONS
004890         DISPLAY NC"拡張制御Ｐ：" 拡張制御Ｐ UPON CONS
004900         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004910                                             UPON CONS
004380*-----------------------------------------*
004390         CALL "actcshm"  WITH C LINKAGE
004400*-----------------------------------------*
004920         ACCEPT  キー入力 FROM CONS
004930         PERFORM ファイル閉鎖
004940         EXIT PROGRAM
004950     END-IF.
004960*
004970*================================================================*
004980*================================================================*
004990 テスト印字処理 SECTION.
005000*
005010     MOVE ALL "9"    TO 本人件数 本人日数 本人費用額 家族件数 家族日数 家族費用額.
005080     MOVE ALL "X"    TO 柔整師番号.
005100     MOVE ALL "静"   TO 代表者名.
005120*
005130*================================================================*
005140******************************************************************
005150 END PROGRAM YGN588.
005160******************************************************************
