000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YGN583.
000060 AUTHOR.                 池田  幸子
000070*
000080*----------------------------------------------------------------*
000090*   【柔+ｳｨﾝﾄﾞｳｽﾞ版　群馬用 支給申請総括表】
000100*   【データ作成】
000110*         MED = YGN580 YGN583P 
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2016-05-19
000140 DATE-COMPILED.          2016-05-19
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000270     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000280                             ORGANIZATION             IS  INDEXED
000290                             ACCESS MODE              IS  DYNAMIC
000300                             RECORD KEY               IS  受−施術和暦年月
000310                                                          受−患者コード
000320                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000330                                                          受−患者カナ
000340                                                          受−患者コード
000350                             ALTERNATE RECORD KEY     IS  受−患者コード
000360                                                          受−施術和暦年月
000370                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000380                                                          受−保険種別
000390                                                          受−保険者番号
000400                                                          受−患者コード
000410                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000420                                                          受−公費種別
000430                                                          受−費用負担者番号
000440                                                          受−患者コード
000450                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000460                                                          受−助成種別
000470                                                          受−費用負担者番号助成
000480                                                          受−患者コード
000490                             ALTERNATE RECORD KEY     IS  受−請求和暦年月
000500                                                          受−施術和暦年月
000510                                                          受−患者コード
000520                             FILE STATUS              IS  状態キー
000530                             LOCK        MODE         IS  AUTOMATIC.
000130     SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
000140                             ORGANIZATION             IS  INDEXED
000150                             ACCESS MODE              IS  DYNAMIC
000160                             RECORD KEY               IS  レセ−施術和暦年月
000170                                                          レセ−患者コード
000180                                                          レセ−レセ種別
000190                             ALTERNATE RECORD KEY     IS  レセ−患者コード
000200                                                          レセ−施術和暦年月
000210                                                          レセ−レセ種別
000220                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000230                                                          レセ−施術和暦年月
000240                                                          レセ−患者コード
000250                                                          レセ−レセ種別
000260                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000270                                                          レセ−レセ種別
000280                                                          レセ−請求保険者番号
000290                                                          レセ−患者コード
000300                                                          レセ−施術和暦年月
000310                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000320                                                          レセ−請求保険者番号
000330                                                          レセ−患者コード
000340                                                          レセ−レセ種別
000350                                                          レセ−施術和暦年月
000360                             FILE STATUS              IS  状態キー
000370                             LOCK        MODE         IS  AUTOMATIC.
001180     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W5831L.DAT"
001190                             ORGANIZATION             IS  INDEXED
001200                             ACCESS                   IS  DYNAMIC
001210                             RECORD      KEY          IS  作１−保険者番号
001220                                                          作１−保険種別
001230                                                          作１−区分
001240                             FILE        STATUS       IS  状態キー
001250                             LOCK        MODE         IS  AUTOMATIC.
001260******************************************************************
001270*                      DATA DIVISION                             *
001280******************************************************************
001290 DATA                    DIVISION.
001300 FILE                    SECTION.
001310*                           ［ＲＬ＝  ７６８］
001320 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001330     COPY JUSINJ         OF  XFDLIB  JOINING    受   AS  PREFIX.
      *                          ［ＲＬ＝  １５３６］
       FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS.
           COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
001550*
001560 FD  作業ファイル１ RECORD  CONTAINS 32 CHARACTERS.
001570 01  作１−レコード.
001580   03  作１−レコードキー.
001650     05  作１−保険者番号.
001660       07 作１−法別番号               PIC X(2).
001670       07 作１−保番.
001680         09 作１−都道府県番号         PIC X(2).
001690         09 作１−保番号               PIC X(3).
001700         09 作１−検証番号             PIC X.
001710         09 FILLER                     PIC X(2).
001590     05  作１−保険種別                PIC 9(2).
001730     05  作１−区分                    PIC 9(1).
001720   03  作１−レコードデータ.
001730     05  作１−件数                    PIC 9(4).
001750     05  作１−費用額                  PIC 9(8).
001780     05  FILLER                        PIC X(7).
001790*
001800******************************************************************
001810*                WORKING-STORAGE SECTION                         *
001820******************************************************************
001830 WORKING-STORAGE         SECTION.
001840 01 キー入力                           PIC X(1) VALUE SPACE.
001850 01 状態キー                           PIC X(2) VALUE SPACE.
001860 01 終了フラグ１                       PIC X(3) VALUE SPACE.
001870 01 終了フラグ３                       PIC X(3) VALUE SPACE.
001880 01 確認入力Ｗ                         PIC X(1) VALUE SPACE.
001890 01 ファイル名Ｗ                       PIC N(2) VALUE SPACE.
001900 01 ファイル名                         PIC N(2) VALUE SPACE.
001910 01 カレント元号Ｗ                     PIC 9(1) VALUE ZERO.
001930 01 実行キーＷ                         PIC X(4) VALUE SPACE.
001940 01 前和暦Ｗ                           PIC 9(1) VALUE ZERO.
001950 01 行桁フラグ                         PIC X(3) VALUE SPACE.
001960 01 処理移動キー                       PIC X(4) VALUE SPACE.
001970*
002070 01 印刷形式Ｗ                         PIC 9(1)  VALUE ZERO.
002080 01 元号名称Ｗ                         PIC N(2)  VALUE SPACE.
002090*
002100**************
002110* 保険者情報 *
002120**************
002130*
002620 01 費用額ＰＧＷ                     PIC X(8) VALUE SPACE.
002630 01 負担額ＰＧＷ                     PIC X(8) VALUE SPACE.
002640 01 負担額ＰＧ老人Ｗ                 PIC X(8) VALUE SPACE.
002650 01 負担額ＰＧ助成Ｗ                 PIC X(8) VALUE SPACE.
002660 01 請求和暦年月ＷＲ.
002670    03 請求和暦ＷＲ                  PIC 9     VALUE ZERO.
002680    03 請求年ＷＲ                    PIC 9(2)  VALUE ZERO.
002690    03 請求月ＷＲ                    PIC 9(2)  VALUE ZERO.
002700*
002710 01 保険種別Ｗ                       PIC 9(2) VALUE ZERO.
002720 01 法別番号Ｗ                       PIC X(2) VALUE SPACE.
002730 01 検証番号ＷＰ                     PIC 9    VALUE ZERO.
002740*
002750 01 対象件数Ｗ                       PIC 9(4) VALUE ZERO.
002760*
002770 01 総括表.
002780   03 合計件数Ｗ                     PIC 9(5)  VALUE ZERO.
002800   03 合計費用額Ｗ                   PIC 9(9)  VALUE ZERO.
002950*
002960 01 終了フラグ２                     PIC X(3) VALUE SPACE.
002970*
002980 01 状態フラグ                       PIC 9(1) VALUE ZERO.
003120*
003130******************************************************************
003140*                          連結項目                              *
003150******************************************************************
003160*
003170****************
003180* 画面入力情報 *
003190****************
       01 連入−入力データＹＧＮ５８０ IS EXTERNAL.
          03 連入−施術和暦年月.
             05 連入−施術和暦               PIC 9(1).
             05 連入−施術年月.
                07 連入−施術年              PIC 9(2).
                07 連入−施術月              PIC 9(2).
003270*
003280 01 連印−印刷情報ＹＧＮ５８３ IS EXTERNAL.
003290    03 連印−合計件数                  PIC 9(5).
003310    03 連印−合計費用額                PIC 9(9).
003770*
003780******************************************************************
003790*                      PROCEDURE  DIVISION                       *
003800******************************************************************
003810 PROCEDURE               DIVISION.
003820************
003830*           *
003840* 初期処理   *
003850*           *
003860************
003870     PERFORM 初期化.
003880************
003890*           *
003900* 主処理     *
003910*           *
003920************
003930     PERFORM 作業ファイル作成.
003940     PERFORM 連結項目退避.
003950************
003960*           *
003970* 終了処理   *
003980*           *
003990************
004000     PERFORM 終了処理.
004010     MOVE ZERO TO PROGRAM-STATUS.
004020     EXIT PROGRAM.
004030*
004040*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
004050*================================================================*
004060 初期化 SECTION.
004070*
004080     OPEN INPUT 受診者情報Ｆ.
004090             MOVE NC"受診" TO ファイル名Ｗ.
004100             PERFORM オープンチェック.
006630     OPEN INPUT レセプトＦ.
006640             MOVE NC"レセ" TO ファイル名.
006650             PERFORM オープンチェック.
004290*
004440* 連結項目の待避
004450     MOVE 連入−施術和暦 TO 請求和暦ＷＲ.
004460     MOVE 連入−施術年   TO 請求年ＷＲ.
004470     MOVE 連入−施術月   TO 請求月ＷＲ.
004480*================================================================*
004490 オープンチェック SECTION.
004500*
004510     IF 状態キー  NOT =  "00"
004520         DISPLAY ファイル名Ｗ NC"Ｆオープンエラー" UPON CONS
004530         DISPLAY NC"状態キー：" 状態キー           UPON CONS
004540         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004550                                                   UPON CONS
004380*-----------------------------------------*
004390         CALL "actcshm"  WITH C LINKAGE
004400*-----------------------------------------*
004560         ACCEPT  キー入力 FROM CONS
004570         PERFORM ファイル閉鎖
004580         EXIT PROGRAM.
005120*================================================================*
005130 ファイル閉鎖 SECTION.
005140*
005150     CLOSE  受診者情報Ｆ レセプトＦ.
005170*================================================================*
005180 終了処理 SECTION.
005190*
005200     PERFORM ファイル閉鎖.
005210*================================================================*
005220* データ作成 SECTION.
005230 作業ファイル作成 SECTION.
005240*
005270     OPEN OUTPUT 作業ファイル１.
005280         MOVE NC"作１" TO ファイル名.
005290         PERFORM オープンチェック.
005300     CLOSE 作業ファイル１.
005310     OPEN I-O 作業ファイル１.
005320         MOVE NC"作１" TO ファイル名.
005330         PERFORM オープンチェック.
005400*  /*県内国保・退職と中央建設・全国建設
007470     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
007480     MOVE 請求年ＷＲ    TO レセ−請求年.
007490     MOVE 請求月ＷＲ    TO レセ−請求月.
013730     MOVE 1             TO レセ−レセ種別.
007500     MOVE ZERO          TO レセ−請求保険者番号.
007500     MOVE ZERO          TO レセ−施術和暦.
007510     MOVE ZERO          TO レセ−施術年.
007520     MOVE ZERO          TO レセ−施術月.
007530     MOVE ZERO          TO レセ−患者番号.
007540     MOVE SPACE         TO レセ−枝番.
007550     START レセプトＦ   KEY IS >= レセ−請求和暦年月
000250                                  レセ−レセ種別
007560                                  レセ−請求保険者番号
007570                                  レセ−患者コード
                                        レセ−施術和暦年月
005560     END-START.
005570     IF 状態キー = "00"
005580         MOVE SPACE  TO 終了フラグ３
005590         PERFORM レセプトＦ読込
005630         PERFORM UNTIL ( 終了フラグ３ = "YES" ) OR
007630                       ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
007640                       ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
007650                       ( レセ−請求月   NOT = 請求月ＷＲ   ) OR
                             ( レセ−レセ種別 NOT = 1 )
005700             PERFORM データチェック
005710             IF 実行キーＷ = "YES"
005670                 IF (( 受−保険種別 = 1 ) AND ( 受−法別番号 = "10" )) OR
005870                    (( 受−保険種別 = 8 ) AND ( 受−都道府県番号 = "10" )) OR
                          ( 受−保険者番号 = "133264" OR "133298" )
005750                     PERFORM 作業ファイル作成１
005760                     PERFORM 合計処理
005770                 END-IF
005780             END-IF
005790             PERFORM レセプトＦ読込
005800         END-PERFORM
006280     END-IF.
006760     CLOSE 作業ファイル１.
007400*================================================================*
007410 レセプトＦ読込 SECTION.
007420*
007430     READ レセプトＦ NEXT
007440     AT END
007450         MOVE "YES" TO 終了フラグ３
007460     END-READ.
007710*================================================================*
007720 エラー表示 SECTION.
007730*
007740     DISPLAY NC"ファイル書込エラー：" ファイル名Ｗ UPON CONS.
007750     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
007760     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
003331*-----------------------------------------*
003332     CALL "actcshm"  WITH C LINKAGE.
003333*-----------------------------------------*
007770     ACCEPT  キー入力 FROM CONS.
007780*================================================================*
007790 エラー表示Ｒ SECTION.
007800*
007810     DISPLAY NC"ファイル読込エラー" ファイル名Ｗ UPON CONS.
007820     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
003331*-----------------------------------------*
003332     CALL "actcshm"  WITH C LINKAGE.
003333*-----------------------------------------*
007830     ACCEPT  キー入力 FROM CONS.
007840     PERFORM ファイル閉鎖.
007850     EXIT PROGRAM.
007860*================================================================*
007870 エラー表示その他 SECTION.
007880*
007890     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
007900     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
003331*-----------------------------------------*
003332     CALL "actcshm"  WITH C LINKAGE.
003333*-----------------------------------------*
007910     ACCEPT  キー入力 FROM CONS.
007920     PERFORM ファイル閉鎖.
007930     EXIT PROGRAM.
007940*================================================================*
007950 データチェック SECTION.
007960*
007970     MOVE SPACE          TO 実行キーＷ.
008590* *****************************************************************
019530* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
008610* *****************************************************************
019640     IF ( レセ−請求対象区分 NOT = ZERO ) AND
005778        ( レセ−償還払い区分 NOT = 1 )
004090           MOVE レセ−施術和暦  TO 受−施術和暦
004100           MOVE レセ−施術年    TO 受−施術年
004110           MOVE レセ−施術月    TO 受−施術月
004120           MOVE レセ−患者番号  TO 受−患者番号
004130           MOVE レセ−枝番      TO 受−枝番
                 READ 受診者情報Ｆ
                 NOT INVALID KEY
019880               MOVE "YES"  TO 実行キーＷ
019950           END-READ
019960     END-IF.
008430*
008510*================================================================*
008520 作業ファイル作成１ SECTION.
008530*
008540     MOVE SPACE TO 作１−レコード.
008550     INITIALIZE    作１−レコード.
008600     EVALUATE 受−保険種別
008610     WHEN 1
008620         MOVE 受−保険者番号   TO 作１−保険者番号
028984         EVALUATE 受−特別区分
               WHEN 1
               WHEN 2
                   MOVE 1            TO 作１−区分
               WHEN 3
                   MOVE 2            TO 作１−区分
               WHEN 6
                   MOVE 5            TO 作１−区分
               WHEN OTHER
                   IF 受−本人家族区分 = 1
                       MOVE 3        TO 作１−区分
                   ELSE
                       MOVE 4        TO 作１−区分
                   END-IF
014730         END-EVALUATE
008630     WHEN OTHER
008640         MOVE 受−保番         TO 作１−保険者番号
028984         IF 受−特別区分 = 6
                   MOVE 5            TO 作１−区分
               ELSE
                   IF 受−本人家族区分 = 1
                       MOVE 3        TO 作１−区分
                   ELSE
                       MOVE 4        TO 作１−区分
                   END-IF
               END-IF
008650     END-EVALUATE.
           IF 作１−保険者番号(3:1) = "3"
               MOVE 9                TO 作１−保険種別
           ELSE
               MOVE 受−保険種別     TO 作１−保険種別
           END-IF.
008680     READ 作業ファイル１
008690     INVALID KEY
008700         MOVE 1                TO 作１−件数 
008770         MOVE レセ−合計       TO 作１−費用額
008810         WRITE 作１−レコード
008820         INVALID KEY
008830             DISPLAY NC"状態キー" 状態キー
008840             MOVE NC"作１"  TO ファイル名Ｗ
008850             PERFORM エラー表示
008860         END-WRITE
008870     NOT INVALID KEY
008880         COMPUTE 作１−件数   = 作１−件数   + 1
008950         COMPUTE 作１−費用額 = 作１−費用額 + レセ−合計
008990         REWRITE 作１−レコード
009000         INVALID KEY
009010             DISPLAY NC"状態キー" 状態キー
009020             MOVE NC"作１"  TO ファイル名Ｗ
009030             PERFORM エラー表示
009040         END-REWRITE
009050     END-READ.
009060*================================================================*
009070 合計処理 SECTION.
009080*
009090     COMPUTE 合計件数Ｗ   = 合計件数Ｗ   + 1.
009110     COMPUTE 合計費用額Ｗ = 合計費用額Ｗ + レセ−合計.
009220*================================================================*
009230 連結項目退避 SECTION.
009240*
009250     MOVE 合計件数Ｗ        TO 連印−合計件数.
009270     MOVE 合計費用額Ｗ      TO 連印−合計費用額.
009350*================================================================*
009360******************************************************************
009370 END PROGRAM YGN583.
009380******************************************************************
