000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YGN593.
000060 AUTHOR.                 池田  幸子
000070*
000080*----------------------------------------------------------------*
000090*        【柔+ｳｨﾝﾄﾞｳｽﾞ版　群馬用 療養費請求書集計表】
000100*        【データ作成】
000110*         MED = YGN580 YGN594P 
      *
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2025-01-15
000140 DATE-COMPILED.          2025-01-15
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000270     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000280                             ORGANIZATION             IS  INDEXED
000290                             ACCESS MODE              IS  DYNAMIC
000300                             RECORD KEY               IS 受−施術和暦年月
000310                                                         受−患者コード
000320                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000330                                                         受−患者カナ
000340                                                         受−患者コード
000350                             ALTERNATE RECORD KEY     IS 受−患者コード
000360                                                         受−施術和暦年月
000370                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000380                                                         受−保険種別
000390                                                         受−保険者番号
000400                                                         受−患者コード
000410                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000420                                                         受−公費種別
000430                                                         受−費用負担者番号
000440                                                         受−患者コード
000450                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000460                                                         受−助成種別
000470                                                         受−費用負担者番号助成
000480                                                         受−患者コード
000490                             ALTERNATE RECORD KEY     IS 受−請求和暦年月
000500                                                         受−施術和暦年月
000510                                                         受−患者コード
000520                             FILE STATUS              IS 状態キー
000530                             LOCK        MODE         IS AUTOMATIC.
000130     SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
000140                             ORGANIZATION             IS  INDEXED
000150                             ACCESS MODE              IS  DYNAMIC
000160                             RECORD KEY               IS  レセ−施術和暦年月
000170                                                          レセ−患者コード
000180                                                          レセ−レセ種別
000190                             ALTERNATE RECORD KEY     IS  レセ−患者コード
000200                                                          レセ−施術和暦年月
000210                                                          レセ−レセ種別
000220                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000230                                                          レセ−施術和暦年月
000240                                                          レセ−患者コード
000250                                                          レセ−レセ種別
000260                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000270                                                          レセ−レセ種別
000280                                                          レセ−請求保険者番号
000290                                                          レセ−患者コード
000300                                                          レセ−施術和暦年月
000310                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000320                                                          レセ−請求保険者番号
000330                                                          レセ−患者コード
000340                                                          レセ−レセ種別
000350                                                          レセ−施術和暦年月
000360                             FILE STATUS              IS  状態キー
000370                             LOCK        MODE         IS  AUTOMATIC.
001080     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W5931L.DAT"
001090                             ORGANIZATION             IS  INDEXED
001100                             ACCESS                   IS  DYNAMIC
001110                             RECORD      KEY          IS  作１−印刷順番
001120                                                          作１−区分
001130                             FILE        STATUS       IS  状態キー
001140                             LOCK        MODE         IS  AUTOMATIC.
001150******************************************************************
001160*                      DATA DIVISION                             *
001170******************************************************************
001180 DATA                    DIVISION.
001190 FILE                    SECTION.
001200*                           ［ＲＬ＝  ７６８］
001210 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001220     COPY JUSINJ         OF  XFDLIB  JOINING    受   AS  PREFIX.
      *                          ［ＲＬ＝  １５３６］
       FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS.
           COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
001410*
001420 FD  作業ファイル１ RECORD  CONTAINS 32 CHARACTERS.
001430 01  作１−レコード.
001440   03  作１−レコードキー.
001450     05  作１−印刷順番                PIC 9(2).
001460     05  作１−区分                    PIC 9(1).
001470   03  作１−レコードデータ.
001480     05  作１−件数                    PIC 9(4).
001480     05  作１−日数                    PIC 9(5).
001500     05  作１−費用額                  PIC 9(9).
001520     05  作１−請求額                  PIC 9(9).
001530     05  FILLER                        PIC X(2).
001540*
001550******************************************************************
001560*                WORKING-STORAGE SECTION                         *
001570******************************************************************
001580 WORKING-STORAGE         SECTION.
001590 01 キー入力                           PIC X(1) VALUE SPACE.
001600 01 状態キー                           PIC X(2) VALUE SPACE.
001610 01 終了フラグ１                       PIC X(3) VALUE SPACE.
001620 01 終了フラグ３                       PIC X(3) VALUE SPACE.
001630 01 確認入力Ｗ                         PIC X(1) VALUE SPACE.
001640 01 ファイル名Ｗ                       PIC N(2) VALUE SPACE.
001650 01 ファイル名                         PIC N(2) VALUE SPACE.
001660 01 カレント元号Ｗ                     PIC 9(1) VALUE ZERO.
001670 01 実行キーＷ                         PIC X(4) VALUE SPACE.
001680 01 前和暦Ｗ                           PIC 9(1) VALUE ZERO.
001690 01 行桁フラグ                         PIC X(3) VALUE SPACE.
001700 01 処理移動キー                       PIC X(4) VALUE SPACE.
001710*
001740 01 施術記録有Ｗ                       PIC X(3) VALUE SPACE.
001750 01 請求和暦年月Ｗ.
001760     03 請求和暦Ｗ                     PIC 9.
001770     03 請求年月Ｗ.
001780        05 請求年Ｗ                    PIC 9(2).
001790        05 請求月Ｗ                    PIC 9(2).
001800 01 請求日Ｗ                           PIC 9(2).
001810 01 印刷形式Ｗ                         PIC 9(1)  VALUE ZERO.
001820 01 元号名称Ｗ                         PIC N(2)  VALUE SPACE.
001830 01 例カウンタ                         PIC 9(2)  VALUE ZERO.
001840*
001850**************
001860* 保険者情報 *
001870**************
002080*
002410 01 請求和暦年月ＷＲ.
002420    03 請求和暦ＷＲ                  PIC 9     VALUE ZERO.
002430    03 請求年ＷＲ                    PIC 9(2)  VALUE ZERO.
002440    03 請求月ＷＲ                    PIC 9(2)  VALUE ZERO.
002450*
002520 01 対象件数Ｗ                       PIC 9(4) VALUE ZERO.
002530*
002610 01 終了フラグ２                     PIC X(3) VALUE SPACE.
002620*
002630 01 状態フラグ                       PIC 9(1) VALUE ZERO.
002720*
002730 01 総括表.
002740   03 小計１件数Ｗ                   PIC 9(5)  VALUE ZERO.
002740   03 小計１日数Ｗ                   PIC 9(6)  VALUE ZERO.
002760   03 小計１合計額Ｗ                 PIC 9(9)  VALUE ZERO.
002780   03 小計１請求額Ｗ                 PIC 9(9)  VALUE ZERO.
002740   03 返戻小計１件数Ｗ               PIC 9(5)  VALUE ZERO.
002740   03 返戻小計１日数Ｗ               PIC 9(6)  VALUE ZERO.
002760   03 返戻小計１合計額Ｗ             PIC 9(9)  VALUE ZERO.
002780   03 返戻小計１請求額Ｗ             PIC 9(9)  VALUE ZERO.
002740   03 小計２件数Ｗ                   PIC 9(5)  VALUE ZERO.
002740   03 小計２日数Ｗ                   PIC 9(6)  VALUE ZERO.
002760   03 小計２合計額Ｗ                 PIC 9(9)  VALUE ZERO.
002780   03 小計２請求額Ｗ                 PIC 9(9)  VALUE ZERO.
002740   03 返戻小計２件数Ｗ               PIC 9(5)  VALUE ZERO.
002740   03 返戻小計２日数Ｗ               PIC 9(6)  VALUE ZERO.
002760   03 返戻小計２合計額Ｗ             PIC 9(9)  VALUE ZERO.
002780   03 返戻小計２請求額Ｗ             PIC 9(9)  VALUE ZERO.
002740   03 合計件数Ｗ                     PIC 9(5)  VALUE ZERO.
002740   03 合計日数Ｗ                     PIC 9(6)  VALUE ZERO.
002760   03 合計合計額Ｗ                   PIC 9(9)  VALUE ZERO.
002780   03 合計請求額Ｗ                   PIC 9(9)  VALUE ZERO.
002740   03 返戻合計件数Ｗ                 PIC 9(5)  VALUE ZERO.
002740   03 返戻合計日数Ｗ                 PIC 9(6)  VALUE ZERO.
002760   03 返戻合計合計額Ｗ               PIC 9(9)  VALUE ZERO.
002780   03 返戻合計請求額Ｗ               PIC 9(9)  VALUE ZERO.
002790*
002800******************************************************************
002810*                          連結項目                              *
002820******************************************************************
002830*
002840****************
002850* 画面入力情報 *
002860****************
       01 連入−入力データＹＧＮ５８０ IS EXTERNAL GLOBAL.
          03 連入−施術和暦年月.
             05 連入−施術和暦               PIC 9(1).
             05 連入−施術年月.
                07 連入−施術年              PIC 9(2).
                07 連入−施術月              PIC 9(2).
       01 連入−入力データＹＧＮ５８５ IS EXTERNAL GLOBAL.
          03 連入−請求和暦年月日.
             05 連入−請求和暦               PIC 9(1).
             05 連入−請求年                 PIC 9(2).
             05 連入−請求月                 PIC 9(2).
             05 連入−請求日                 PIC 9(2).
          03 連入−保険者番号                PIC X(10).
002930*
002940 01 連印−印刷情報ＹＧＮ５９３ IS EXTERNAL.
002950    03 連印−小計１件数                PIC 9(5).
002950    03 連印−小計１日数                PIC 9(6).
002970    03 連印−小計１合計額              PIC 9(9).
002990    03 連印−小計１請求額              PIC 9(9).
002950    03 連印−返戻小計１件数            PIC 9(5).
002950    03 連印−返戻小計１日数            PIC 9(6).
002970    03 連印−返戻小計１合計額          PIC 9(9).
002990    03 連印−返戻小計１請求額          PIC 9(9).
002950    03 連印−小計２件数                PIC 9(5).
002950    03 連印−小計２日数                PIC 9(6).
002970    03 連印−小計２合計額              PIC 9(9).
002990    03 連印−小計２請求額              PIC 9(9).
002950    03 連印−返戻小計２件数            PIC 9(5).
002950    03 連印−返戻小計２日数            PIC 9(6).
002970    03 連印−返戻小計２合計額          PIC 9(9).
002990    03 連印−返戻小計２請求額          PIC 9(9).
002950    03 連印−合計件数                  PIC 9(5).
002950    03 連印−合計日数                  PIC 9(6).
002970    03 連印−合計合計額                PIC 9(9).
002990    03 連印−合計請求額                PIC 9(9).
002950    03 連印−返戻合計件数              PIC 9(5).
002950    03 連印−返戻合計日数              PIC 9(6).
002970    03 連印−返戻合計合計額            PIC 9(9).
002990    03 連印−返戻合計請求額            PIC 9(9).
003000*
003390******************************************************************
003400*                      PROCEDURE  DIVISION                       *
003410******************************************************************
003420 PROCEDURE               DIVISION.
003430************
003440*           *
003450* 初期処理   *
003460*           *
003470************
003480     PERFORM 初期化.
003490************
003500*           *
003510* 主処理     *
003520*           *
003530************
003540     PERFORM 作業ファイル作成.
003550     PERFORM 連結項目退避.
003560************
003570*           *
003580* 終了処理   *
003590*           *
003600************
003610     PERFORM 終了処理.
003620     MOVE ZERO TO PROGRAM-STATUS.
003630     EXIT PROGRAM.
003640*
003650*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003660*================================================================*
003670 初期化 SECTION.
003680*
003690     OPEN INPUT 受診者情報Ｆ.
003700             MOVE NC"受診" TO ファイル名Ｗ.
003710             PERFORM オープンチェック.
006630     OPEN INPUT レセプトＦ.
006640             MOVE NC"レセ" TO ファイル名.
006650             PERFORM オープンチェック.
003870*
003890* 連結項目の待避
003900     MOVE 連入−施術和暦 TO 請求和暦ＷＲ.
003910     MOVE 連入−施術年   TO 請求年ＷＲ.
003920     MOVE 連入−施術月   TO 請求月ＷＲ.
003930*================================================================*
003940 オープンチェック SECTION.
003950*
003960     IF 状態キー  NOT =  "00"
003970         DISPLAY ファイル名Ｗ NC"Ｆオープンエラー" UPON CONS
003980         DISPLAY NC"状態キー：" 状態キー           UPON CONS
003990         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004000                                                   UPON CONS
004380*-----------------------------------------*
004390         CALL "actcshm"  WITH C LINKAGE
004400*-----------------------------------------*
004010         ACCEPT  キー入力 FROM CONS
004020         PERFORM ファイル閉鎖
004030         EXIT PROGRAM.
004190*================================================================*
004200 ファイル閉鎖 SECTION.
004210*
004220     CLOSE  受診者情報Ｆ レセプトＦ.
004240*================================================================*
004250 終了処理 SECTION.
004260*
004270     PERFORM ファイル閉鎖.
004280*================================================================*
004290* データ作成 SECTION.
004300 作業ファイル作成 SECTION.
004310*
004340     OPEN OUTPUT 作業ファイル１.
004350         MOVE NC"作１" TO ファイル名.
004360         PERFORM オープンチェック.
004370     CLOSE 作業ファイル１.
004380     OPEN I-O 作業ファイル１.
004390         MOVE NC"作１" TO ファイル名.
004400         PERFORM オープンチェック.
004440*
004760     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
004770     MOVE 請求年ＷＲ    TO レセ−請求年.
004780     MOVE 請求月ＷＲ    TO レセ−請求月.
004790     MOVE ZERO          TO レセ−施術和暦.
004800     MOVE ZERO          TO レセ−施術年.
004810     MOVE ZERO          TO レセ−施術月.
004820     MOVE ZERO          TO レセ−患者番号.
004830     MOVE LOW-VALUE     TO レセ−枝番.
013730     MOVE ZERO          TO レセ−レセ種別.
004840     START レセプトＦ   KEY IS >= レセ−請求和暦年月
004850                                  レセ−施術和暦年月
004860                                  レセ−患者コード
000250                                  レセ−レセ種別
004560     END-START.
004570     IF 状態キー = "00"
004580         MOVE SPACE  TO 終了フラグ３
004590         PERFORM レセプトＦ読込
004630         PERFORM UNTIL ( 終了フラグ３ = "YES" ) OR
004920                       ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
004930                       ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
004940                       ( レセ−請求月   NOT = 請求月ＷＲ   )
005430            PERFORM データチェック
012328            IF  実行キーＷ = "YES"
004650*労災も含めない
004670*                 IF レセ−レセ種別 = 1 OR 2 OR 3 OR 4 OR 7
004670                 IF レセ−レセ種別 = 1 OR 2 OR 7
004740                     PERFORM 作業ファイル作成１
004750                     PERFORM 合計計算
004760                 END-IF
004670                 IF レセ−レセ種別 = 3
004740                     PERFORM 作業ファイル作成助成
004750                     PERFORM 合計計算
004760                 END-IF
004770             END-IF
004780             PERFORM レセプトＦ読込
004790         END-PERFORM
004810     END-IF.
004820     CLOSE 作業ファイル１.
005460*================================================================*
005470 レセプトＦ読込 SECTION.
005480*
005490     READ レセプトＦ NEXT
005500     AT END
005510         MOVE "YES" TO 終了フラグ３
005520     END-READ.
005770*================================================================*
005780 エラー表示 SECTION.
005790*
005800     DISPLAY NC"ファイル書込エラー：" ファイル名Ｗ UPON CONS.
005810     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
005820     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
004380*-----------------------------------------*
004390     CALL "actcshm"  WITH C LINKAGE.
004400*-----------------------------------------*
005830     ACCEPT  キー入力 FROM CONS.
005840*================================================================*
005850 エラー表示Ｒ SECTION.
005860*
005870     DISPLAY NC"ファイル読込エラー" ファイル名Ｗ UPON CONS.
005880     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
004380*-----------------------------------------*
004390     CALL "actcshm"  WITH C LINKAGE.
004400*-----------------------------------------*
005890     ACCEPT  キー入力 FROM CONS.
005900     PERFORM ファイル閉鎖.
005910     EXIT PROGRAM.
005920*================================================================*
005930 エラー表示その他 SECTION.
005940*
005950     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
005960     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
004380*-----------------------------------------*
004390     CALL "actcshm"  WITH C LINKAGE.
004400*-----------------------------------------*
005970     ACCEPT  キー入力 FROM CONS.
005980     PERFORM ファイル閉鎖.
005990     EXIT PROGRAM.
006000*================================================================*
006010 データチェック SECTION.
006020*
006030     MOVE SPACE          TO 実行キーＷ.
005300     IF ( レセ−レセ印刷対象区分 NOT = 1 )
005310        MOVE "YES"  TO 実行キーＷ
005320     END-IF.
019520* *****************************************************************
019530* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
019540* *****************************************************************
005360     IF 実行キーＷ  = "YES"
005370*      (再度、実行キーＷ SPACE)
005380        MOVE SPACE  TO 実行キーＷ
019640        IF レセ−請求対象区分 NOT = ZERO
004090           MOVE レセ−施術和暦  TO 受−施術和暦
004100           MOVE レセ−施術年    TO 受−施術年
004110           MOVE レセ−施術月    TO 受−施術月
004120           MOVE レセ−患者番号  TO 受−患者番号
004130           MOVE レセ−枝番      TO 受−枝番
                 READ 受診者情報Ｆ
                 NOT INVALID KEY
019880              MOVE "YES"  TO 実行キーＷ
019950           END-READ
              END-IF
019960     END-IF.
006490*
006570*================================================================*
006580 作業ファイル作成１ SECTION.
006590*
006600     MOVE SPACE TO 作１−レコード.
006610     INITIALIZE    作１−レコード.
           PERFORM 印刷順番セット.
006790     IF レセ−請求区分 = 2
006800         MOVE 1                  TO 作１−区分
006810     ELSE
006820         MOVE ZERO               TO 作１−区分
006830     END-IF.    
006840     READ 作業ファイル１
006850     INVALID KEY
006860         MOVE 1                TO 作１−件数 
006930         MOVE レセ−レセ実日数 TO 作１−日数
006930         MOVE レセ−合計       TO 作１−費用額
006950         MOVE レセ−請求金額   TO 作１−請求額
006970         WRITE 作１−レコード
006980         INVALID KEY
006990             DISPLAY NC"状態キー" 状態キー
007000             MOVE NC"作１"  TO ファイル名Ｗ
007010             PERFORM エラー表示
007020         END-WRITE
007030     NOT INVALID KEY
007040         COMPUTE 作１−件数   = 作１−件数   + 1
007110         COMPUTE 作１−日数   = 作１−日数   + レセ−レセ実日数
007110         COMPUTE 作１−費用額 = 作１−費用額 + レセ−合計
007130         COMPUTE 作１−請求額 = 作１−請求額 + レセ−請求金額
007150         REWRITE 作１−レコード
007160         INVALID KEY
007170             DISPLAY NC"状態キー" 状態キー
007180             MOVE NC"作１"  TO ファイル名Ｗ
007190             PERFORM エラー表示
007200         END-REWRITE
007210     END-READ.
006570*================================================================*
006580 作業ファイル作成助成 SECTION.
006590*
006600     MOVE SPACE TO 作１−レコード.
006610     INITIALIZE    作１−レコード.
           PERFORM 印刷順番セット.
006790     IF レセ−請求区分 = 2
006800         MOVE 1                  TO 作１−区分
006810     ELSE
006820         MOVE ZERO               TO 作１−区分
006830     END-IF.    
006840     READ 作業ファイル１
006850     INVALID KEY
006860         MOVE 1                  TO 作１−件数 
006930         MOVE レセ−レセ実日数   TO 作１−日数
006930         MOVE レセ−合計         TO 作１−費用額
006950         MOVE レセ−助成請求金額 TO 作１−請求額
006970         WRITE 作１−レコード
006980         INVALID KEY
006990             DISPLAY NC"状態キー" 状態キー
007000             MOVE NC"作１"  TO ファイル名Ｗ
007010             PERFORM エラー表示
007020         END-WRITE
007030     NOT INVALID KEY
007040         COMPUTE 作１−件数   = 作１−件数   + 1
007110         COMPUTE 作１−日数   = 作１−日数   + レセ−レセ実日数
007110         COMPUTE 作１−費用額 = 作１−費用額 + レセ−合計
007130         COMPUTE 作１−請求額 = 作１−請求額 + レセ−助成請求金額
007150         REWRITE 作１−レコード
007160         INVALID KEY
007170             DISPLAY NC"状態キー" 状態キー
007180             MOVE NC"作１"  TO ファイル名Ｗ
007190             PERFORM エラー表示
007200         END-REWRITE
007210     END-READ.
007220*================================================================*
007230 印刷順番セット SECTION.
007240*
007124**///  印刷順序の変更はこのSECTIONで   ////**
007125*
007126**************************************************************************
007127*  印刷順序:保険名称(保険種別コード)                                     *
007128*                                                                        *
007129*    11:社保県内本人(2) 日雇県内本人(6) 船員県内本人(7)                  *
007134*    12:社保県内家族(2) 日雇県内家族(6) 船員県内家族(7)                  *
007129*    13:社保県外本人(2) 日雇県外本人(6) 船員県外本人(7)                  *
007134*    14:社保県外家族(2) 日雇県外家族(6) 船員県外家族(7)                  *
007130*    21:組合県内本人(3)                                                  *
007135*    22:組合県内家族(3)                                                  *
007130*    23:組合県外本人(3)                                                  *
007135*    24:組合県外家族(3)                                                  *
007139*    31:共済本人(4) 自衛官本人(9)                                        *
007140*    32:共済家族(4) 自衛官家族(9)                                        *
007141*    41:国保県内(1) 退職県内(8)                                          *
007144*    42:国保県外(1) 退職県外(8)                                          *
007145*    51:後高県内(5)                                                      *
007145*    52:後高県外(5)                                                      *
007146*                                                                        *
007147*    61:助成    (51〜60)                                                 *
007147*    71:生保    (85)                                                     *
007147*    81:労災    (70)                                                     *
007150*                                                                        *
007151**************************************************************************
007152*
            EVALUATE レセ−レセ種別
            WHEN 1
007154         EVALUATE  受−保険種別
007155* 国保
007156         WHEN  01
                  IF 受−保険者番号(1:2) = "10"
007165               MOVE  41  TO  作１−印刷順番
007166            ELSE 
007167               MOVE  42  TO  作１−印刷順番
007168            END-IF
007162* 社保
007176* 日雇
007169* 船員
007163         WHEN  02
007177         WHEN  06
007170         WHEN  07
                  IF 受−保険者番号(3:2) = "10"
007164               IF 受−本人家族区分 = 1
007165                  MOVE  11  TO  作１−印刷順番
007166               ELSE 
007167                  MOVE  12  TO  作１−印刷順番
007168               END-IF
                  ELSE
007164               IF 受−本人家族区分 = 1
007165                  MOVE  13  TO  作１−印刷順番
007166               ELSE 
007167                  MOVE  14  TO  作１−印刷順番
007168               END-IF
007168            END-IF
007183* 組合
007184         WHEN  03
                  IF 受−保険者番号(3:2) = "10"
007164               IF 受−本人家族区分 = 1
007165                  MOVE  21  TO  作１−印刷順番
007166               ELSE 
007167                  MOVE  22  TO  作１−印刷順番
007168               END-IF
                  ELSE
007164               IF 受−本人家族区分 = 1
007165                  MOVE  23  TO  作１−印刷順番
007166               ELSE 
007167                  MOVE  24  TO  作１−印刷順番
007168               END-IF
007168            END-IF
007190* 共済・自衛官
007191         WHEN  04
007192         WHEN  09
007193            IF 受−本人家族区分 = 1
007194               MOVE  31  TO  作１−印刷順番
007195            ELSE
007196               MOVE  32  TO  作１−印刷順番
007197            END-IF
007201* 退職国保
007202         WHEN  08
                  IF 受−保険者番号(3:2) = "10"
007204               MOVE  41  TO  作１−印刷順番
007205            ELSE
007206               MOVE  42  TO  作１−印刷順番
007207            END-IF
               END-EVALUATE
007208*
            WHEN 2
               IF 受−費用負担者番号(3:2) = "10"
007204            MOVE  51  TO  作１−印刷順番
007205         ELSE
007206            MOVE  52  TO  作１−印刷順番
007207         END-IF
            WHEN 3
008480         MOVE  61  TO  作１−印刷順番
            WHEN 7
007158         MOVE  71  TO  作１−印刷順番
007218     END-EVALUATE.
008175*
007220*================================================================*
007230 合計計算 SECTION.
007240*
           IF レセ−レセ種別 NOT = 3 AND 7
006790        IF レセ−請求区分 = 2
                 IF 受−保険種別 NOT = 1 AND 5 AND 8
007250              COMPUTE 返戻小計１件数Ｗ   = 返戻小計１件数Ｗ   + 1
007320              COMPUTE 返戻小計１日数Ｗ   = 返戻小計１日数Ｗ   + レセ−レセ実日数
007320              COMPUTE 返戻小計１合計額Ｗ = 返戻小計１合計額Ｗ + レセ−合計
007340              COMPUTE 返戻小計１請求額Ｗ = 返戻小計１請求額Ｗ + レセ−請求金額
                 ELSE
007250              COMPUTE 返戻小計２件数Ｗ   = 返戻小計２件数Ｗ + 1
007320              COMPUTE 返戻小計２日数Ｗ   = 返戻小計２日数Ｗ   + レセ−レセ実日数
007320              COMPUTE 返戻小計２合計額Ｗ = 返戻小計２合計額Ｗ + レセ−合計
007340              COMPUTE 返戻小計２請求額Ｗ = 返戻小計２請求額Ｗ + レセ−請求金額
                 END-IF
007250           COMPUTE 返戻合計件数Ｗ   = 返戻合計件数Ｗ   + 1
007320           COMPUTE 返戻合計日数Ｗ   = 返戻合計日数Ｗ   + レセ−レセ実日数
007320           COMPUTE 返戻合計合計額Ｗ = 返戻合計合計額Ｗ + レセ−合計
007340           COMPUTE 返戻合計請求額Ｗ = 返戻合計請求額Ｗ + レセ−請求金額
              ELSE
                 IF 受−保険種別 NOT = 1 AND 5 AND 8
007250              COMPUTE 小計１件数Ｗ   = 小計１件数Ｗ   + 1
007320              COMPUTE 小計１日数Ｗ   = 小計１日数Ｗ   + レセ−レセ実日数
007320              COMPUTE 小計１合計額Ｗ = 小計１合計額Ｗ + レセ−合計
007340              COMPUTE 小計１請求額Ｗ = 小計１請求額Ｗ + レセ−請求金額
                 ELSE
007250              COMPUTE 小計２件数Ｗ   = 小計２件数Ｗ   + 1
007320              COMPUTE 小計２日数Ｗ   = 小計２日数Ｗ   + レセ−レセ実日数
007320              COMPUTE 小計２合計額Ｗ = 小計２合計額Ｗ + レセ−合計
007340              COMPUTE 小計２請求額Ｗ = 小計２請求額Ｗ + レセ−請求金額
                 END-IF
007250           COMPUTE 合計件数Ｗ   = 合計件数Ｗ   + 1
007320           COMPUTE 合計日数Ｗ   = 合計日数Ｗ   + レセ−レセ実日数
007320           COMPUTE 合計合計額Ｗ = 合計合計額Ｗ + レセ−合計
007340           COMPUTE 合計請求額Ｗ = 合計請求額Ｗ + レセ−請求金額
              END-IF
           END-IF.
007360*================================================================*
007370 連結項目退避 SECTION.
007380*
           INITIALIZE 連印−印刷情報ＹＧＮ５９３.
007390     MOVE 小計１件数Ｗ          TO 連印−小計１件数.
007390     MOVE 小計１日数Ｗ          TO 連印−小計１日数.
007410     MOVE 小計１合計額Ｗ        TO 連印−小計１合計額.
007430     MOVE 小計１請求額Ｗ        TO 連印−小計１請求額.
007390     MOVE 返戻小計１件数Ｗ      TO 連印−返戻小計１件数.
007390     MOVE 返戻小計１日数Ｗ      TO 連印−返戻小計１日数.
007410     MOVE 返戻小計１合計額Ｗ    TO 連印−返戻小計１合計額.
007430     MOVE 返戻小計１請求額Ｗ    TO 連印−返戻小計１請求額.
007390     MOVE 小計２件数Ｗ          TO 連印−小計２件数.
007390     MOVE 小計２日数Ｗ          TO 連印−小計２日数.
007410     MOVE 小計２合計額Ｗ        TO 連印−小計２合計額.
007430     MOVE 小計２請求額Ｗ        TO 連印−小計２請求額.
007390     MOVE 返戻小計２件数Ｗ      TO 連印−返戻小計２件数.
007390     MOVE 返戻小計２日数Ｗ      TO 連印−返戻小計２日数.
007410     MOVE 返戻小計２合計額Ｗ    TO 連印−返戻小計２合計額.
007430     MOVE 返戻小計２請求額Ｗ    TO 連印−返戻小計２請求額.
007390     MOVE 合計件数Ｗ            TO 連印−合計件数.
007390     MOVE 合計日数Ｗ            TO 連印−合計日数.
007410     MOVE 合計合計額Ｗ          TO 連印−合計合計額.
007430     MOVE 合計請求額Ｗ          TO 連印−合計請求額.
007390     MOVE 返戻合計件数Ｗ        TO 連印−返戻合計件数.
007390     MOVE 返戻合計日数Ｗ        TO 連印−返戻合計日数.
007410     MOVE 返戻合計合計額Ｗ      TO 連印−返戻合計合計額.
007430     MOVE 返戻合計請求額Ｗ      TO 連印−返戻合計請求額.
007440*================================================================*
007450******************************************************************
007460 END PROGRAM YGN593.
007470******************************************************************
