000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YGN581.
000060 AUTHOR.                 池田  幸子
000070*
000080*----------------------------------------------------------------*
000090*        【柔+ｳｨﾝﾄﾞｳｽﾞ版　群馬用 施術療養費請求書集計表】
000100*        【データ作成】
000110*         MED = YGN580 YGN582P 
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2016-05-18
000140 DATE-COMPILED.          2016-05-18
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000270     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000280                             ORGANIZATION             IS  INDEXED
000290                             ACCESS MODE              IS  DYNAMIC
000300                             RECORD KEY               IS 受−施術和暦年月
000310                                                         受−患者コード
000320                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000330                                                         受−患者カナ
000340                                                         受−患者コード
000350                             ALTERNATE RECORD KEY     IS 受−患者コード
000360                                                         受−施術和暦年月
000370                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000380                                                         受−保険種別
000390                                                         受−保険者番号
000400                                                         受−患者コード
000410                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000420                                                         受−公費種別
000430                                                         受−費用負担者番号
000440                                                         受−患者コード
000450                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000460                                                         受−助成種別
000470                                                         受−費用負担者番号助成
000480                                                         受−患者コード
000490                             ALTERNATE RECORD KEY     IS 受−請求和暦年月
000500                                                         受−施術和暦年月
000510                                                         受−患者コード
000520                             FILE STATUS              IS 状態キー
000530                             LOCK        MODE         IS AUTOMATIC.
000130     SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
000140                             ORGANIZATION             IS  INDEXED
000150                             ACCESS MODE              IS  DYNAMIC
000160                             RECORD KEY               IS  レセ−施術和暦年月
000170                                                          レセ−患者コード
000180                                                          レセ−レセ種別
000190                             ALTERNATE RECORD KEY     IS  レセ−患者コード
000200                                                          レセ−施術和暦年月
000210                                                          レセ−レセ種別
000220                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000230                                                          レセ−施術和暦年月
000240                                                          レセ−患者コード
000250                                                          レセ−レセ種別
000260                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000270                                                          レセ−レセ種別
000280                                                          レセ−請求保険者番号
000290                                                          レセ−患者コード
000300                                                          レセ−施術和暦年月
000310                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000320                                                          レセ−請求保険者番号
000330                                                          レセ−患者コード
000340                                                          レセ−レセ種別
000350                                                          レセ−施術和暦年月
000360                             FILE STATUS              IS  状態キー
000370                             LOCK        MODE         IS  AUTOMATIC.
001080     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W58021L.DAT"
001090                             ORGANIZATION             IS  INDEXED
001100                             ACCESS                   IS  DYNAMIC
001110                             RECORD      KEY          IS  作１−保険種別
001130                             FILE        STATUS       IS  状態キー
001140                             LOCK        MODE         IS  AUTOMATIC.
001150******************************************************************
001160*                      DATA DIVISION                             *
001170******************************************************************
001180 DATA                    DIVISION.
001190 FILE                    SECTION.
001200*                           ［ＲＬ＝  ７６８］
001210 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001220     COPY JUSINJ         OF  XFDLIB  JOINING    受   AS  PREFIX.
      *                          ［ＲＬ＝  １５３６］
       FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS.
           COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
001410*
001420 FD  作業ファイル１ RECORD  CONTAINS 32 CHARACTERS.
001430 01  作１−レコード.
001440   03  作１−レコードキー.
001450     05  作１−保険種別                PIC 9(2).
001470   03  作１−レコードデータ.
001480     05  作１−件数                    PIC 9(4).
001490     05  作１−日数                    PIC 9(5).
001500     05  作１−費用額                  PIC 9(8).
001530     05  FILLER                        PIC X(13).
001540*
001550******************************************************************
001560*                WORKING-STORAGE SECTION                         *
001570******************************************************************
001580 WORKING-STORAGE         SECTION.
001590 01 キー入力                           PIC X(1) VALUE SPACE.
001600 01 状態キー                           PIC X(2) VALUE SPACE.
001610 01 終了フラグ１                       PIC X(3) VALUE SPACE.
002610 01 終了フラグ２                     PIC X(3) VALUE SPACE.
001620 01 終了フラグ３                       PIC X(3) VALUE SPACE.
001640 01 ファイル名Ｗ                       PIC N(2) VALUE SPACE.
001650 01 ファイル名                         PIC N(2) VALUE SPACE.
001670 01 実行キーＷ                         PIC X(4) VALUE SPACE.
001710*
001750 01 請求和暦年月Ｗ.
001760     03 請求和暦Ｗ                     PIC 9.
001770     03 請求年月Ｗ.
001780        05 請求年Ｗ                    PIC 9(2).
001790        05 請求月Ｗ                    PIC 9(2).
001800 01 請求日Ｗ                           PIC 9(2).
001810 01 印刷形式Ｗ                         PIC 9(1)  VALUE ZERO.
001840*
001850**************
001860* 保険者情報 *
001870**************
002360*
002410 01 請求和暦年月ＷＲ.
002420    03 請求和暦ＷＲ                  PIC 9     VALUE ZERO.
002430    03 請求年ＷＲ                    PIC 9(2)  VALUE ZERO.
002440    03 請求月ＷＲ                    PIC 9(2)  VALUE ZERO.
002450*
002460 01 保険種別Ｗ                       PIC 9(2) VALUE ZERO.
002470 01 公費種別Ｗ                       PIC 9(2) VALUE ZERO.
002480 01 助成種別Ｗ                       PIC 9(2) VALUE ZERO.
002490 01 本人家族区分Ｗ                   PIC 9(1) VALUE ZERO.
002510*
002520 01 対象件数Ｗ                       PIC 9(4) VALUE ZERO.
002620*
002630 01 状態フラグ                       PIC 9(1) VALUE ZERO.
002720*
002730 01 総括表.
002740   03 小計件数Ｗ                     PIC 9(4)  VALUE ZERO.
002750   03 小計日数Ｗ                     PIC 9(5)  VALUE ZERO.
002750   03 小計平均日数Ｗ                 PIC 9(2)  VALUE ZERO.
002760   03 小計費用額Ｗ                   PIC 9(9)  VALUE ZERO.
002770   03 小計平均費用額Ｗ               PIC 9(6)  VALUE ZERO.
002740   03 総合計件数Ｗ                   PIC 9(4)  VALUE ZERO.
002750   03 総合計日数Ｗ                   PIC 9(5)  VALUE ZERO.
002750   03 総合計平均日数Ｗ               PIC 9(2)  VALUE ZERO.
002760   03 総合計費用額Ｗ                 PIC 9(9)  VALUE ZERO.
002770   03 総合計平均費用額Ｗ             PIC 9(6)  VALUE ZERO.
002790*
002800******************************************************************
002810*                          連結項目                              *
002820******************************************************************
002830*
002840****************
002850* 画面入力情報 *
002860****************
       01 連入−入力データＹＧＮ５８０ IS EXTERNAL.
          03 連入−施術和暦年月.
             05 連入−施術和暦               PIC 9(1).
             05 連入−施術年月.
                07 連入−施術年              PIC 9(2).
                07 連入−施術月              PIC 9(2).
002930*
002940 01 連印−印刷情報ＹＧＮ５８１ IS EXTERNAL.
002950    03 連印−小計件数                  PIC 9(4).
002960    03 連印−小計日数                  PIC 9(6).
002960    03 連印−小計平均日数              PIC 9(2).
002970    03 連印−小計費用額                PIC 9(9).
002980    03 連印−小計平均費用額            PIC 9(6).
002950    03 連印−総合計件数                PIC 9(4).
002960    03 連印−総合計日数                PIC 9(6).
002960    03 連印−総合計平均日数            PIC 9(2).
002970    03 連印−総合計費用額              PIC 9(9).
002980    03 連印−総合計平均費用額          PIC 9(6).
003000*
003390******************************************************************
003400*                      PROCEDURE  DIVISION                       *
003410******************************************************************
003420 PROCEDURE               DIVISION.
003430************
003440*           *
003450* 初期処理   *
003460*           *
003470************
003480     PERFORM 初期化.
003490************
003500*           *
003510* 主処理     *
003520*           *
003530************
003540     PERFORM 作業ファイル作成.
003550     PERFORM 連結項目退避.
003560************
003570*           *
003580* 終了処理   *
003590*           *
003600************
003610     PERFORM 終了処理.
003620     MOVE ZERO TO PROGRAM-STATUS.
003630     EXIT PROGRAM.
003640*
003650*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003660*================================================================*
003670 初期化 SECTION.
003680*
003690     OPEN INPUT 受診者情報Ｆ.
003700             MOVE NC"受診" TO ファイル名Ｗ.
003710             PERFORM オープンチェック.
006630     OPEN INPUT レセプトＦ.
006640             MOVE NC"レセ" TO ファイル名.
006650             PERFORM オープンチェック.
003870*
003890* 連結項目の待避
003900     MOVE 連入−施術和暦 TO 請求和暦ＷＲ.
003910     MOVE 連入−施術年   TO 請求年ＷＲ.
003920     MOVE 連入−施術月   TO 請求月ＷＲ.
003930*================================================================*
003940 オープンチェック SECTION.
003950*
003960     IF 状態キー  NOT =  "00"
003970         DISPLAY ファイル名Ｗ NC"Ｆオープンエラー" UPON CONS
003980         DISPLAY NC"状態キー：" 状態キー           UPON CONS
003990         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004000                                                   UPON CONS
004380*-----------------------------------------*
004390         CALL "actcshm"  WITH C LINKAGE
004400*-----------------------------------------*
004010         ACCEPT  キー入力 FROM CONS
004020         PERFORM ファイル閉鎖
004030         EXIT PROGRAM.
004190*================================================================*
004200 ファイル閉鎖 SECTION.
004210*
004220     CLOSE  受診者情報Ｆ レセプトＦ.
004240*================================================================*
004250 終了処理 SECTION.
004260*
004270     PERFORM ファイル閉鎖.
004280*================================================================*
004290* データ作成 SECTION.
004300 作業ファイル作成 SECTION.
004310*
004340     OPEN OUTPUT 作業ファイル１.
004350         MOVE NC"作１" TO ファイル名.
004360         PERFORM オープンチェック.
004370     CLOSE 作業ファイル１.
004380     OPEN I-O 作業ファイル１.
004390         MOVE NC"作１" TO ファイル名.
004400         PERFORM オープンチェック.
004440*
004760     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
004770     MOVE 請求年ＷＲ    TO レセ−請求年.
004780     MOVE 請求月ＷＲ    TO レセ−請求月.
004790     MOVE ZERO          TO レセ−施術和暦.
004800     MOVE ZERO          TO レセ−施術年.
004810     MOVE ZERO          TO レセ−施術月.
004820     MOVE ZERO          TO レセ−患者番号.
004830     MOVE LOW-VALUE     TO レセ−枝番.
013730     MOVE ZERO          TO レセ−レセ種別.
004840     START レセプトＦ   KEY IS >= レセ−請求和暦年月
004850                                  レセ−施術和暦年月
004860                                  レセ−患者コード
000250                                  レセ−レセ種別
004560     END-START.
004570     IF 状態キー = "00"
004580         MOVE SPACE  TO 終了フラグ３
004590         PERFORM レセプトＦ読込
004630         PERFORM UNTIL ( 終了フラグ３ = "YES" ) OR
004920                       ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
004930                       ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
004940                       ( レセ−請求月   NOT = 請求月ＷＲ   )
005430            PERFORM データチェック
012328            IF  実行キーＷ = "YES"
004650*
004660*               */労災のみ対象
      *               */助成対象外
004670                 IF レセ−レセ種別 = 1 OR 2 OR 4
004740                     PERFORM 作業ファイル作成１
004750                     PERFORM 合計計算
004760                 END-IF
004770             END-IF
004780             PERFORM レセプトＦ読込
004790         END-PERFORM
004750         PERFORM 平均計算
004810     END-IF.
004820     CLOSE 作業ファイル１.
005460*================================================================*
005470 レセプトＦ読込 SECTION.
005480*
005490     READ レセプトＦ NEXT
005500     AT END
005510         MOVE "YES" TO 終了フラグ３
005520     END-READ.
005770*================================================================*
005780 エラー表示 SECTION.
005790*
005800     DISPLAY NC"ファイル書込エラー：" ファイル名Ｗ UPON CONS.
005810     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
005820     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
004380*-----------------------------------------*
004390     CALL "actcshm"  WITH C LINKAGE.
004400*-----------------------------------------*
005830     ACCEPT  キー入力 FROM CONS.
005840*================================================================*
005850 エラー表示Ｒ SECTION.
005860*
005870     DISPLAY NC"ファイル読込エラー" ファイル名Ｗ UPON CONS.
005880     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
004380*-----------------------------------------*
004390     CALL "actcshm"  WITH C LINKAGE.
004400*-----------------------------------------*
005890     ACCEPT  キー入力 FROM CONS.
005900     PERFORM ファイル閉鎖.
005910     EXIT PROGRAM.
005920*================================================================*
005930 エラー表示その他 SECTION.
005940*
005950     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
005960     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
004380*-----------------------------------------*
004390     CALL "actcshm"  WITH C LINKAGE.
004400*-----------------------------------------*
005970     ACCEPT  キー入力 FROM CONS.
005980     PERFORM ファイル閉鎖.
005990     EXIT PROGRAM.
006000*================================================================*
006010 データチェック SECTION.
006020*
006030     MOVE SPACE          TO 実行キーＷ.
019520* *****************************************************************
019530* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
019540* *****************************************************************
019640     IF ( レセ−請求対象区分 NOT = ZERO ) AND
005778        ( レセ−償還払い区分 NOT = 1 ) AND
              ( レセ−請求区分     NOT = 2 )
              IF (レセ−レセ種別 = 3) AND ( レセ−会総括表印刷対象区分 = 1 )
                  CONTINUE
              ELSE
004090           MOVE レセ−施術和暦  TO 受−施術和暦
004100           MOVE レセ−施術年    TO 受−施術年
004110           MOVE レセ−施術月    TO 受−施術月
004120           MOVE レセ−患者番号  TO 受−患者番号
004130           MOVE レセ−枝番      TO 受−枝番
                 READ 受診者情報Ｆ
                 NOT INVALID KEY
019880              MOVE "YES"  TO 実行キーＷ
019950           END-READ
              END-IF
019960     END-IF.
006490*
006570*================================================================*
006580 作業ファイル作成１ SECTION.
006590*
006600     MOVE SPACE TO 作１−レコード.
006610     INITIALIZE    作１−レコード.
006620     IF ( 受−保険種別 = 5 )
006630         MOVE 05                 TO 作１−保険種別
006640     ELSE
006680         EVALUATE 受−保険種別
006690         WHEN 01
                   IF 受−保険者番号 = "133033" OR "133280" OR "133231" OR "133264" OR "133298"
                       MOVE 03     TO 作１−保険種別
                   ELSE
006710                 MOVE 01     TO 作１−保険種別
006700             END-IF
               WHEN 08
006710             MOVE 01         TO 作１−保険種別
006690         WHEN 06
006700         WHEN 07
006710             MOVE 02         TO 作１−保険種別
006720         WHEN 09
006730             MOVE 04         TO 作１−保険種別
006740         WHEN OTHER
006750             MOVE 受−保険種別   TO 作１−保険種別
006760         END-EVALUATE
006780     END-IF.
006840     READ 作業ファイル１
006850     INVALID KEY
006860         MOVE 1                TO 作１−件数 
006870         MOVE レセ−レセ実日数 TO 作１−日数
006930         MOVE レセ−合計       TO 作１−費用額
006970         WRITE 作１−レコード
006980         INVALID KEY
006990             DISPLAY NC"状態キー" 状態キー
007000             MOVE NC"作１"  TO ファイル名Ｗ
007010             PERFORM エラー表示
007020         END-WRITE
007030     NOT INVALID KEY
007040         COMPUTE 作１−件数   = 作１−件数 + 1
007050         COMPUTE 作１−日数   = 作１−日数 + レセ−レセ実日数
007110         COMPUTE 作１−費用額 = 作１−費用額 + レセ−合計
007150         REWRITE 作１−レコード
007160         INVALID KEY
007170             DISPLAY NC"状態キー" 状態キー
007180             MOVE NC"作１"  TO ファイル名Ｗ
007190             PERFORM エラー表示
007200         END-REWRITE
007210     END-READ.
007220*================================================================*
007230 合計計算 SECTION.
007240*
007250     IF レセ−レセ種別 NOT = 4
               COMPUTE 小計件数Ｗ = 小計件数Ｗ + 1
007260         COMPUTE 小計日数Ｗ = 小計日数Ｗ + レセ−レセ実日数
007320         COMPUTE 小計費用額Ｗ = 小計費用額Ｗ + レセ−合計
           END-IF.
007240*
007250     COMPUTE 総合計件数Ｗ = 総合計件数Ｗ + 1.
007260     COMPUTE 総合計日数Ｗ = 総合計日数Ｗ + レセ−レセ実日数.
007320     COMPUTE 総合計費用額Ｗ = 総合計費用額Ｗ + レセ−合計.
007220*================================================================*
007230 平均計算 SECTION.
007240*
007250     IF 小計件数Ｗ NOT = ZERO
007260         COMPUTE 小計平均日数Ｗ = 小計日数Ｗ / 小計件数Ｗ
007320         COMPUTE 小計平均費用額Ｗ = 小計費用額Ｗ / 小計件数Ｗ
           END-IF.
007240*
007250     IF 総合計件数Ｗ NOT = ZERO
007260         COMPUTE 総合計平均日数Ｗ = 総合計日数Ｗ / 総合計件数Ｗ
007320         COMPUTE 総合計平均費用額Ｗ = 総合計費用額Ｗ / 総合計件数Ｗ
           END-IF.
007360*================================================================*
007370 連結項目退避 SECTION.
007380*
007390     MOVE 小計件数Ｗ          TO 連印−小計件数.
007400     MOVE 小計日数Ｗ          TO 連印−小計日数.
007400     MOVE 小計平均日数Ｗ      TO 連印−小計平均日数.
007410     MOVE 小計費用額Ｗ        TO 連印−小計費用額.
007410     MOVE 小計平均費用額Ｗ    TO 連印−小計平均費用額.
007380*
007390     MOVE 総合計件数Ｗ          TO 連印−総合計件数.
007400     MOVE 総合計日数Ｗ          TO 連印−総合計日数.
007400     MOVE 総合計平均日数Ｗ      TO 連印−総合計平均日数.
007410     MOVE 総合計費用額Ｗ        TO 連印−総合計費用額.
007410     MOVE 総合計平均費用額Ｗ    TO 連印−総合計平均費用額.
007440*================================================================*
007450******************************************************************
007460 END PROGRAM YGN581.
007470******************************************************************
