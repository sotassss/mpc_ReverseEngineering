000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YGN587.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*         群馬県用　決議書【ﾃﾞｰﾀ作成】柔+ｳｨﾝﾄﾞｳｽﾞ版
000100* 請求年月バージョン
000110*         MED = YNK580 YGN588P
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2016-05-25
000140 DATE-COMPILED.          2016-05-25
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000270     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000280                             ORGANIZATION          IS  INDEXED
000290                             ACCESS MODE           IS  DYNAMIC
000300                             RECORD KEY            IS  受−施術和暦年月
000310                                                       受−患者コード
000320                             ALTERNATE RECORD KEY  IS  受−施術和暦年月
000330                                                       受−患者カナ
000340                                                       受−患者コード
000350                             ALTERNATE RECORD KEY  IS  受−患者コード
000360                                                       受−施術和暦年月
000370                             ALTERNATE RECORD KEY  IS  受−施術和暦年月
000380                                                       受−保険種別
000390                                                       受−保険者番号
000400                                                       受−患者コード
000410                             ALTERNATE RECORD KEY  IS  受−施術和暦年月
000420                                                       受−公費種別
000430                                                       受−費用負担者番号
000440                                                       受−患者コード
000450                             ALTERNATE RECORD KEY  IS  受−施術和暦年月
000460                                                       受−助成種別
000470                                                       受−費用負担者番号助成
000480                                                       受−患者コード
000490                             ALTERNATE RECORD KEY  IS  受−請求和暦年月
000500                                                       受−施術和暦年月
000510                                                       受−患者コード
000520                             FILE STATUS           IS  状態キー
000530                             LOCK        MODE      IS  AUTOMATIC.
000130     SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
000140                             ORGANIZATION             IS  INDEXED
000150                             ACCESS MODE              IS  DYNAMIC
000160                             RECORD KEY               IS  レセ−施術和暦年月
000170                                                          レセ−患者コード
000180                                                          レセ−レセ種別
000190                             ALTERNATE RECORD KEY     IS  レセ−患者コード
000200                                                          レセ−施術和暦年月
000210                                                          レセ−レセ種別
000220                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000230                                                          レセ−施術和暦年月
000240                                                          レセ−患者コード
000250                                                          レセ−レセ種別
000260                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000270                                                          レセ−レセ種別
000280                                                          レセ−請求保険者番号
000290                                                          レセ−患者コード
000300                                                          レセ−施術和暦年月
000310                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000320                                                          レセ−請求保険者番号
000330                                                          レセ−患者コード
000340                                                          レセ−レセ種別
000350                                                          レセ−施術和暦年月
000360                             FILE STATUS              IS  状態キー
000370                             LOCK        MODE         IS  AUTOMATIC.
000540     SELECT  負傷データＦ    ASSIGN      TO        HUSYOUL
000550                             ORGANIZATION          IS  INDEXED
000560                             ACCESS MODE           IS  DYNAMIC
000570                             RECORD KEY            IS  負−施術和暦年月
000580                                                       負−患者コード
000590                             ALTERNATE RECORD KEY  IS  負−患者コード
000600                                                       負−施術和暦年月
000610                             FILE STATUS           IS  状態キー
000620                             LOCK        MODE      IS  AUTOMATIC.
000630     SELECT  施術記録Ｆ      ASSIGN      TO        SEKIROKL
000640                             ORGANIZATION          IS  INDEXED
000650                             ACCESS MODE           IS  DYNAMIC
000660                             RECORD KEY            IS  施記−施術和暦年月日
000670                                                       施記−患者コード
000680                             ALTERNATE RECORD KEY  IS  施記−患者コード
000690                                                       施記−施術和暦年月日
000700                             FILE STATUS           IS  状態キー
000710                             LOCK        MODE      IS  AUTOMATIC.
000720     SELECT  作業ファイル１  ASSIGN      TO      "C:\MAKISHISYS\YAWOBJ\TEMP\W5871L.DAT"
000730                             ORGANIZATION          IS  INDEXED
000740                             ACCESS                IS  DYNAMIC
000750                             RECORD      KEY       IS  作１−保険者番号
000800                             FILE        STATUS    IS  状態キー
000810                             LOCK        MODE      IS  AUTOMATIC.
000880******************************************************************
000890*                      DATA DIVISION                             *
000900******************************************************************
000910 DATA                    DIVISION.
000920 FILE                    SECTION.
000930*                           ［ＲＬ＝  ３２０］
000940 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
000950     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
      *                          ［ＲＬ＝  １５３６］
       FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS.
           COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
000960*                           ［ＲＬ＝  ２５６］
000970 FD  施術記録Ｆ          BLOCK   CONTAINS   1   RECORDS.
000980     COPY SEKIROK         OF  XFDLIB  JOINING   施記 AS  PREFIX.
000990*                           ［ＲＬ＝  １２８］
001000 FD  負傷データＦ        BLOCK   CONTAINS   1   RECORDS.
001010     COPY HUSYOU          OF  XFDLIB  JOINING   負   AS  PREFIX.
001020*
001030 FD  作業ファイル１ RECORD  CONTAINS  64 CHARACTERS.
001040 01  作１−レコード.
001050     03  作１−レコードキー.
001690         05  作１−保険者番号.
001700             07 作１−法別番号               PIC X(2).
001710             07 作１−保番.
001720                09 作１−都道府県番号        PIC X(2).
001730                09 作１−保番号              PIC X(3).
001740                09 作１−検証番号            PIC X.
001750                09 FILLER                    PIC X(2).
001190     03  作１−レコードデータ.
001790         05  作１−保険種別                  PIC 9(2).
001790         05  作１−本人件数                  PIC 9(4).
001800         05  作１−本人日数                  PIC 9(4).
001200         05  作１−本人請求額                PIC 9(7).
001790         05  作１−家族件数                  PIC 9(4).
001800         05  作１−家族日数                  PIC 9(4).
001200         05  作１−家族請求額                PIC 9(7).
001210         05  FILLER                          PIC X(22).
001220*
001330*----------------------------------------------------------------*
001340******************************************************************
001350*                WORKING-STORAGE SECTION                         *
001360******************************************************************
001370 WORKING-STORAGE         SECTION.
001380 01 キー入力                           PIC X     VALUE SPACE.
001390 01 状態キー                           PIC X(2)  VALUE SPACE.
001400 01 終了フラグ                         PIC X(3)  VALUE SPACE.
001410 01 終了フラグ２                       PIC X(3)  VALUE SPACE.
001420 01 実行キーＷ                         PIC X(3)  VALUE SPACE.
001430 01 ファイル名                         PIC N(2)  VALUE SPACE.
001440*
001500**************
001510* 費用額情報 *
001520**************
001530 01 合計Ｗ.
001540    03 本人件数Ｗ                      PIC 9(4)  VALUE ZERO.
001540    03 本人日数Ｗ                      PIC 9(4)  VALUE ZERO.
001550    03 本人請求額Ｗ                    PIC 9(8)  VALUE ZERO.
001560    03 家族件数Ｗ                      PIC 9(4)  VALUE ZERO.
001560    03 家族日数Ｗ                      PIC 9(4)  VALUE ZERO.
001570    03 家族請求額Ｗ                    PIC 9(8)  VALUE ZERO.
001580*
001590****************
001600* 連結項目待避 *
001610****************
001620 01 請求和暦年月Ｗ.
001630    03 請求和暦Ｗ                      PIC 9(1)  VALUE ZERO.
001640    03 請求年Ｗ                        PIC 9(2)  VALUE ZERO.
001650    03 請求月Ｗ                        PIC 9(2)  VALUE ZERO.
001660*
001670******************************************************************
001680*                          連結項目                              *
001690******************************************************************
001700*
       01 連入−入力データＹＧＮ５８０ IS EXTERNAL.
          03 連入−施術和暦年月.
             05 連入−施術和暦               PIC 9(1).
             05 連入−施術年月.
                07 連入−施術年              PIC 9(2).
                07 連入−施術月              PIC 9(2).
001830*
002160******************************************************************
002170*                      PROCEDURE  DIVISION                       *
002180******************************************************************
002190 PROCEDURE               DIVISION.
002200************
002210*           *
002220* 初期処理   *
002230*           *
002240************
002250     PERFORM 連結項目待避.
002260     PERFORM ファイルオープン.
002270************
002280*           *
002290* 主処理     *
002300*           *
002310************
002320     PERFORM 作業ファイル１作成.
002340************
002350*           *
002360* 終了処理   *
002370*           *
002380************
002390     PERFORM 終了処理.
002400     MOVE ZERO TO PROGRAM-STATUS.
002410     EXIT PROGRAM.
002420*
002430*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002440*================================================================*
002450 連結項目待避 SECTION.
002460*================================================================*
002470     MOVE 連入−施術和暦 TO 請求和暦Ｗ.
002480     MOVE 連入−施術年   TO 請求年Ｗ.
002490     MOVE 連入−施術月   TO 請求月Ｗ.
002500*
002510*================================================================*
002520 ファイルオープン SECTION.
002530*================================================================*
002540     OPEN INPUT 受診者情報Ｆ.
002550         MOVE NC"受診者情報Ｆ" TO ファイル名.
002560         PERFORM オープンチェック.
006630     OPEN INPUT レセプトＦ.
006640         MOVE NC"レセ" TO ファイル名.
006650         PERFORM オープンチェック.
002570     OPEN INPUT 負傷データＦ.
002580         MOVE NC"負傷データＦ" TO ファイル名.
002590         PERFORM オープンチェック.
002600     OPEN INPUT 施術記録Ｆ.
002610         MOVE NC"施術記録Ｆ" TO ファイル名.
002620         PERFORM オープンチェック.
002630*
002640*================================================================*
002650 オープンチェック SECTION.
002660*
002670     IF 状態キー  NOT =  "00"
002680         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
002690         DISPLAY NC"状態キー：" 状態キー         UPON CONS
002700         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
002710                                                 UPON CONS
004380*-----------------------------------------*
004390         CALL "actcshm"  WITH C LINKAGE
004400*-----------------------------------------*
002720         ACCEPT  キー入力 FROM CONS
002730         PERFORM ファイル閉鎖
002740         MOVE 99 TO PROGRAM-STATUS
002750         EXIT PROGRAM.
002760*
002770*================================================================*
002780 作業ファイル１作成 SECTION.
002790*================================================================*
002800     OPEN OUTPUT 作業ファイル１.
002810         MOVE NC"作１" TO ファイル名.
002820         PERFORM オープンチェック.
005220     CLOSE 作業ファイル１.
005230     OPEN I-O 作業ファイル１.
005240         MOVE NC"作１" TO ファイル名.
005250         PERFORM オープンチェック.
002830*
002840     PERFORM 作業ファイル１作成１.
002850*
002860     CLOSE 作業ファイル１.
002870*
002880*================================================================*
002890 作業ファイル１作成１ SECTION.
002900*================================================================*
007470     MOVE 請求和暦Ｗ    TO レセ−請求和暦.
007480     MOVE 請求年Ｗ      TO レセ−請求年.
007490     MOVE 請求月Ｗ      TO レセ−請求月.
013730     MOVE 1             TO レセ−レセ種別.
007500     MOVE ZERO          TO レセ−請求保険者番号.
007500     MOVE ZERO          TO レセ−施術和暦.
007510     MOVE ZERO          TO レセ−施術年.
007520     MOVE ZERO          TO レセ−施術月.
007530     MOVE ZERO          TO レセ−患者番号.
007540     MOVE SPACE         TO レセ−枝番.
007550     START レセプトＦ   KEY IS >= レセ−請求和暦年月
000250                                  レセ−レセ種別
007560                                  レセ−請求保険者番号
007570                                  レセ−患者コード
                                        レセ−施術和暦年月
003020     END-START.
003030     IF 状態キー = "00"
003040        MOVE SPACE  TO 終了フラグ
003050        PERFORM レセプトＦ読込
003060        PERFORM UNTIL ( 終了フラグ = "YES" ) OR
007630                      ( レセ−請求和暦 NOT = 請求和暦Ｗ ) OR
007640                      ( レセ−請求年   NOT = 請求年Ｗ   ) OR
007650                      ( レセ−請求月   NOT = 請求月Ｗ   ) OR
                            ( レセ−レセ種別 NOT = 1 )
003080*
003090           PERFORM データチェック
003100           IF 実行キーＷ = "YES"
003120              PERFORM 作１レコードセット
003140           END-IF
003150           PERFORM レセプトＦ読込
003160        END-PERFORM
003170     END-IF.
003180*
003190*================================================================*
003200 レセプトＦ読込 SECTION.
003210*
003220     READ レセプトＦ NEXT
003230     AT END
003240         MOVE "YES" TO 終了フラグ
003250     END-READ.
003260*
003270*================================================================*
003280 データチェック SECTION.
003290*================================================================*
003300*
003600     MOVE SPACE TO 実行キーＷ.
019640     IF ( レセ−請求対象区分 NOT = ZERO ) AND
005778        ( レセ−償還払い区分 NOT = 1 )
004090           MOVE レセ−施術和暦  TO 受−施術和暦
004100           MOVE レセ−施術年    TO 受−施術年
004110           MOVE レセ−施術月    TO 受−施術月
004120           MOVE レセ−患者番号  TO 受−患者番号
004130           MOVE レセ−枝番      TO 受−枝番
                 READ 受診者情報Ｆ
                 NOT INVALID KEY
                    IF 受−保険種別 = 2 OR 6 OR 7
019880                  MOVE "YES"  TO 実行キーＷ
019950              END-IF
                 END-READ
019960     END-IF.
003650*
004410*================================================================*
004420 作１レコードセット SECTION.
004430*
004440     MOVE SPACE TO 作１−レコード.
004450     INITIALIZE    作１−レコード.
004460*
           MOVE 受−保険者番号    TO 作１−保険者番号.
004560*
008250     READ 作業ファイル１
008260     INVALID KEY
               MOVE 受−保険種別        TO 作１−保険種別
               IF 受−本人家族区分 = 1
008270            MOVE 1                TO 作１−本人件数 
009470            MOVE レセ−レセ実日数 TO 作１−本人日数
008290            MOVE レセ−請求金額   TO 作１−本人請求額
               ELSE
008270            MOVE 1                TO 作１−家族件数 
009470            MOVE レセ−レセ実日数 TO 作１−家族日数
008290            MOVE レセ−請求金額   TO 作１−家族請求額
               END-IF
008300         WRITE 作１−レコード
008310         INVALID KEY
008320             DISPLAY NC"状態キー" 状態キー
008330             MOVE NC"作１"  TO ファイル名
008340             PERFORM エラー表示
008350         END-WRITE
008360     NOT INVALID KEY
               IF 受−本人家族区分 = 1
008370            COMPUTE 作１−本人件数   = 作１−本人件数   + 1
009470            COMPUTE 作１−本人日数   = 作１−本人日数   + レセ−レセ実日数
008390            COMPUTE 作１−本人請求額 = 作１−本人請求額 + レセ−請求金額
008400         ELSE
008370            COMPUTE 作１−家族件数   = 作１−家族件数   + 1
009470            COMPUTE 作１−家族日数   = 作１−家族日数   + レセ−レセ実日数
008390            COMPUTE 作１−家族請求額 = 作１−家族請求額 + レセ−請求金額
               END-IF
               REWRITE 作１−レコード
008410         INVALID KEY
008420             DISPLAY NC"状態キー" 状態キー
008430             MOVE NC"作１"  TO ファイル名
008440             PERFORM エラー表示
008450         END-REWRITE
008460     END-READ.
004580*
005390*================================================================*
005400 終了処理 SECTION.
005410*================================================================*
005420     PERFORM ファイル閉鎖.
005430*
005440*================================================================*
005450 ファイル閉鎖 SECTION.
005460*
005470     CLOSE 受診者情報Ｆ 負傷データＦ 施術記録Ｆ レセプトＦ.
005480*
005490*================================================================*
005500 エラー表示 SECTION.
005510*================================================================*
005520     DISPLAY NC"状態キー" 状態キー  UPON CONS.
005530     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
005540     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
005550     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
003331*-----------------------------------------*
003332     CALL "actcshm"  WITH C LINKAGE.
003333*-----------------------------------------*
005560     ACCEPT  キー入力 FROM CONS.
005570     PERFORM ファイル閉鎖.
005580     MOVE 99 TO PROGRAM-STATUS.
005590     EXIT PROGRAM.
005600*================================================================*
005610******************************************************************
005620 END PROGRAM YGN587.
005630******************************************************************
