000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YGN584.
000060 AUTHOR.                 池田  幸子
000070*
000080*----------------------------------------------------------------*
000090*   【柔+ｳｨﾝﾄﾞｳｽﾞ版　群馬用 支給申請総括表】
000100*   【データ作成、印刷】
000110*         MED = YGN580 YGN584P 
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2016-05-19
000140 DATE-COMPILED.          2016-05-19
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000390     SELECT  施術所情報マスタ ASSIGN     TO        SEJOHOL
000400                             ORGANIZATION             IS  INDEXED
000410                             ACCESS MODE              IS  DYNAMIC
000420                             RECORD KEY               IS  施情−施術所番号
000430                             FILE STATUS              IS  状態キー
000440                             LOCK        MODE         IS  AUTOMATIC.
000450     SELECT  保険者マスタ    ASSIGN      TO        HOKENSL
000460                             ORGANIZATION             IS  INDEXED
000470                             ACCESS MODE              IS  DYNAMIC
000480                             RECORD KEY               IS  保−保険種別
000490                                                          保−保険者番号
000500                             ALTERNATE RECORD KEY     IS  保−保険種別
000510                                                          保−保険者名称
000520                                                          保−保険者番号
000530                             FILE STATUS              IS  状態キー
000540                             LOCK        MODE         IS  AUTOMATIC.
000810     SELECT  ＩＤ管理マスタ  ASSIGN      TO        IDKANRL
000820                             ORGANIZATION             IS  INDEXED
000830                             ACCESS MODE              IS  DYNAMIC
000840                             RECORD KEY               IS  ＩＤ管−ＩＤ区分
000850                                                          ＩＤ管−施術所番号
000860                                                          ＩＤ管−保険種別
000870                                                          ＩＤ管−保険者番号
000880                             ALTERNATE RECORD KEY     IS  ＩＤ管−施術ＩＤ番号
000890                                                          ＩＤ管−ＩＤ区分
000900                                                          ＩＤ管−施術所番号
000910                                                          ＩＤ管−保険種別
000920                                                          ＩＤ管−保険者番号
000930                             FILE STATUS              IS  状態キー
000940                             LOCK        MODE         IS  AUTOMATIC.
001180     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W5831L.DAT"
001190                             ORGANIZATION             IS  INDEXED
001200                             ACCESS                   IS  DYNAMIC
001210                             RECORD      KEY          IS  作１−保険者番号
001220                                                          作１−保険種別
001230                                                          作１−区分
001240                             FILE        STATUS       IS  状態キー
001250                             LOCK        MODE         IS  AUTOMATIC.
000870     SELECT  印刷ファイル    ASSIGN      TO     GS-PRTF001
000880                             SYMBOLIC    DESTINATION  IS "PRT"
000890                             FORMAT                   IS  定義体名Ｐ
000900                             GROUP                    IS  項目群名Ｐ
000910                             PROCESSING  MODE         IS  処理種別Ｐ
000920                             UNIT        CONTROL      IS  拡張制御Ｐ
000930                             FILE        STATUS       IS  通知情報Ｐ.
000940******************************************************************
000950*                      DATA DIVISION                             *
000960******************************************************************
000970 DATA                    DIVISION.
000980 FILE                    SECTION.
001050*                           ［ＲＬ＝  ６４０］
001060 FD  施術所情報マスタ    BLOCK   CONTAINS   1   RECORDS.
001070     COPY SEJOHO         OF  XFDLIB  JOINING    施情 AS  PREFIX.
001080*                           ［ＲＬ＝  ３２０］
001090 FD  保険者マスタ        BLOCK   CONTAINS   1   RECORDS.
001100     COPY HOKENS          OF  XFDLIB  JOINING   保   AS  PREFIX.
002070*                           ［ＲＬ＝  １２８］
002080 FD  ＩＤ管理マスタ          BLOCK   CONTAINS   1   RECORDS.
002090     COPY IDKANR          OF  XFDLIB  JOINING   ＩＤ管   AS  PREFIX.
001520*
001560 FD  作業ファイル１ RECORD  CONTAINS 32 CHARACTERS.
001570 01  作１−レコード.
001580   03  作１−レコードキー.
001650     05  作１−保険者番号.
001660       07 作１−法別番号               PIC X(2).
001670       07 作１−保番.
001680         09 作１−都道府県番号         PIC X(2).
001690         09 作１−保番号               PIC X(3).
001700         09 作１−検証番号             PIC X.
001710         09 FILLER                     PIC X(2).
001590     05  作１−保険種別                PIC 9(2).
001730     05  作１−区分                    PIC 9(1).
001720   03  作１−レコードデータ.
001730     05  作１−件数                    PIC 9(4).
001750     05  作１−費用額                  PIC 9(8).
001780     05  FILLER                        PIC X(7).
001410*
001420 FD  印刷ファイル.
001430     COPY YGN584P        OF  XMDLIB.
001440*
001450******************************************************************
001460*                WORKING-STORAGE SECTION                         *
001470******************************************************************
001480 WORKING-STORAGE         SECTION.
001490 01 キー入力                           PIC X    VALUE SPACE.
001500 01 状態キー                           PIC X(2) VALUE SPACE.
001510 01 終了フラグ                         PIC X(3) VALUE SPACE.
001520 01 終了フラグ２                       PIC X(3) VALUE SPACE.
001530 01 終了フラグ３                       PIC X(3) VALUE SPACE.
001540 01 確認入力Ｗ                         PIC X(1) VALUE SPACE.
001550 01 ファイル名Ｗ                       PIC N(2) VALUE SPACE.
001560 01 カレント元号Ｗ                     PIC 9(1) VALUE ZERO.
001570 01 協会コードＷ                       PIC 9(2) VALUE ZERO.
001580 01 オープンフラグ                     PIC X(3) VALUE SPACE.
002000 01 施術記録有Ｗ                       PIC X(3) VALUE SPACE.
001580 01 実行キーＷ                         PIC X(4) VALUE SPACE.
001590 01 前和暦Ｗ                           PIC 9 VALUE ZERO.
001600 01 行桁フラグ                         PIC X(3) VALUE SPACE.
001610 01 処理移動キー                       PIC X(4) VALUE SPACE.
001620 01 施術和暦年月Ｗ.
001630     03 施術和暦Ｗ                     PIC 9.
001640     03 施術年月Ｗ.
001650        05 施術年Ｗ                    PIC 9(2).
001660        05 施術月Ｗ                    PIC 9(2).
001670 01 施術日Ｗ                           PIC 9(2).
001680 01 請求和暦年月Ｗ.
001690    03 請求和暦Ｗ                      PIC 9     VALUE ZERO.
001700    03 請求年Ｗ                        PIC 9(2)  VALUE ZERO.
001710    03 請求月Ｗ                        PIC 9(2)  VALUE ZERO.
001740 01 頁Ｗ                               PIC 9(2) VALUE ZERO.
001750 01 カウンタ                           PIC 9(2) VALUE ZERO.
001780 01 印刷枚数ＣＮＴ                     PIC 9(2) VALUE ZERO.
001790 01 文字ＣＮＴ                         PIC 9(2) VALUE ZERO.
001800 01 総括表Ｗ.
001810    03 施術所住所Ｗ.
001820       05 住所１Ｗ                     PIC N(20) VALUE SPACE.
001830       05 住所２Ｗ                     PIC N(20) VALUE SPACE.
001840    03 施術所住所１Ｗ                  PIC X(50) VALUE SPACE.
001850    03 施術所住所２Ｗ                  PIC X(50) VALUE SPACE.
001860    03 接骨院名Ｗ                      PIC X(50) VALUE SPACE.
001870    03 代表者名Ｗ                      PIC X(50) VALUE SPACE.
001880    03 代表者カナＷ                    PIC X(50) VALUE SPACE.
001910    03 県施術ＩＤＷ                    PIC X(12)  VALUE SPACE.
001920    03 施術所電話番号Ｗ                PIC X(15) VALUE SPACE.
001930    03 施術所郵便番号Ｗ.
001940       05 施術所郵便番号１Ｗ           PIC X(3)   VALUE SPACE.
001950       05 施術所郵便番号２Ｗ           PIC X(4)   VALUE SPACE.
001960    03 取引先情報Ｗ.
001970       05 取引先銀行名Ｗ.
001980          07 取引先銀行名１Ｗ          PIC X(14)  VALUE SPACE.
001990          07 取引先銀行名２Ｗ          PIC X(14)  VALUE SPACE.
002000       05 取引先銀行支店名Ｗ.
002010          07 取引先銀行支店名１Ｗ      PIC X(14)  VALUE SPACE.
002020          07 取引先銀行支店名２Ｗ      PIC X(14)  VALUE SPACE.
002030       05 預金種別Ｗ                   PIC 9(1)   VALUE ZERO.
002040       05 口座番号Ｗ                   PIC X(10)  VALUE SPACE.
002050       05 口座名義人Ｗ                 PIC X(70)  VALUE SPACE.
002060       05 口座名義人ＷＰ               PIC X(40)  VALUE SPACE.
002070       05 口座名義人カナＷ             PIC X(40)  VALUE SPACE.
002080       05 銀行名支店名Ｗ               PIC X(60)  VALUE SPACE.
002090       05 預金種別コメントＷ           PIC N(2)   VALUE SPACE.
002100    03 明細行.
002160       05 合計件数Ｗ                   PIC 9(5)  VALUE ZERO.
002180       05 合計費用額Ｗ                 PIC 9(9)  VALUE ZERO.
002280 01 請求先名称Ｗ.
002290    03 印刷請求先名称Ｗ                PIC X(40) VALUE SPACE.
002310 01 登録番号コードＷ.
002320    03 登録番号ＷＰ.
002330       05 頭Ｗ                         PIC X(2)  VALUE SPACE.
002330       05 登録番号Ｗ                   PIC X(14) VALUE SPACE.
002340 01 支払いコードＷ.
002350    03 県コードＪＩＳＷ                PIC X(2) VALUE SPACE.
002360    03 県コード政管Ｗ                  PIC X(2) VALUE SPACE.
002370    03 FILLER                          PIC X(1) VALUE SPACE.
002380 01 会員番号題Ｗ                       PIC N(4)  VALUE SPACE.
002390 01 会員番号Ｗ                         PIC X(10) VALUE SPACE.
002670*
002680 01 印刷制御.
002690     03 定義体名Ｐ                     PIC X(8) VALUE SPACE.
002700     03 項目群名Ｐ                     PIC X(8) VALUE SPACE.
002710     03 処理種別Ｐ                     PIC X(2) VALUE SPACE.
002720     03 拡張制御Ｐ.
002730         05 端末制御Ｐ.
002740             07 移動方向Ｐ             PIC X(1).
002750             07 移動行数Ｐ             PIC 9(3).
002760         05 詳細制御Ｐ                 PIC X(2).
002770     03 通知情報Ｐ                     PIC X(2) VALUE SPACE.
002780     03 ユニット名Ｐ                   PIC X(8) VALUE SPACE.
002950 01 施術和暦年月ＷＲ.
002960    03 施術和暦ＷＲ                  PIC 9     VALUE ZERO.
002970    03 施術年ＷＲ                    PIC 9(2)  VALUE ZERO.
002980    03 施術月ＷＲ                    PIC 9(2)  VALUE ZERO.
002990*
003000******************************************************************
003010*                          連結項目                              *
003020******************************************************************
005420**********************
005430* メッセージ表示キー *
005440**********************
005450 01 連メ−キー IS EXTERNAL.
005460    03  連メ−メッセージ                 PIC N(20).
003030*
       01 連入−入力データＹＧＮ５８０ IS EXTERNAL.
          03 連入−施術和暦年月.
             05 連入−施術和暦               PIC 9(1).
             05 連入−施術年月.
                07 連入−施術年              PIC 9(2).
                07 連入−施術月              PIC 9(2).
003270*
003280 01 連印−印刷情報ＹＧＮ５８３ IS EXTERNAL.
003290    03 連印−合計件数                  PIC 9(5).
003310    03 連印−合計費用額                PIC 9(9).
      *
       01 連入−プレビュー IS EXTERNAL.
          03 連入−プレビュー区分            PIC 9.
003280*
000540************************************
000550* プリンタファイル作成用           *
000560************************************
000570 01 Ｈ連ＰＲＴＦ−作成データ IS EXTERNAL.
000580     03 Ｈ連ＰＲＴＦ−ファイル名           PIC X(8).
000590     03 Ｈ連ＰＲＴＦ−プレビュー区分       PIC 9.
000600     03 Ｈ連ＰＲＴＦ−帳票プログラム名     PIC X(8).
000610     03 Ｈ連ＰＲＴＦ−オーバレイ名         PIC X(8).
001580*
003290******************************************************************
003300*                      PROCEDURE  DIVISION                       *
003310******************************************************************
003320 PROCEDURE               DIVISION.
003330************
003340*           *
003350* 初期処理   *
003360*           *
003370************
002570     PERFORM プリンタファイル作成.
003380     PERFORM 初期化.
003390************
003400*           *
003410* 主処理     *
003420*           *
003430************
003440     PERFORM 連結項目待避.
003450     PERFORM 施術所情報マスタ読込.
003460     PERFORM 印刷処理.
003470************
003480*           *
003490* 終了処理   *
003500*           *
003510************
003520     PERFORM 終了処理.
003530     EXIT PROGRAM.
003540*
003550*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002860*================================================================*
002870 プリンタファイル作成 SECTION.
002880*================================================================*
002890*   / 初期化 /
002900     MOVE SPACE TO Ｈ連ＰＲＴＦ−作成データ.
002910     INITIALIZE Ｈ連ＰＲＴＦ−作成データ.
002930*
002940*--↓↓ 変更箇所 ↓↓--------------------------------------*
002970*   使用するプリンタファイル名セット
002971     MOVE "PRTF001"             TO Ｈ連ＰＲＴＦ−ファイル名.
002972*
002973*   使用する帳票プログラム名セット
002974     MOVE "YGN584"             TO Ｈ連ＰＲＴＦ−帳票プログラム名.
002975*
002976*--↑↑-----------------------------------------------------*
002980*
002990*   / プレビュー区分セット /
003000     MOVE 連入−プレビュー区分  TO Ｈ連ＰＲＴＦ−プレビュー区分.
003010*
003020     CALL   "CRTPRTF".
003030     CANCEL "CRTPRTF".
003040*
003560*================================================================*
003570 初期化 SECTION.
003580*
003650     OPEN INPUT 施術所情報マスタ.
003660         MOVE NC"施情" TO ファイル名Ｗ.
003670         PERFORM オープンチェック.
003680     OPEN INPUT 保険者マスタ.
003690         MOVE NC"保険者マスタ" TO ファイル名Ｗ.
003700         PERFORM オープンチェック.
014860     OPEN INPUT   ＩＤ管理マスタ.
014870         MOVE NC"ＩＤ" TO ファイル名Ｗ.
014880         PERFORM オープンチェック.
003950*
003960*================================================================*
003970 オープンチェック SECTION.
003980*
003990     IF 状態キー  NOT =  "00"
004000         DISPLAY ファイル名Ｗ NC"Ｆオープンエラー" UPON CONS
004010         DISPLAY NC"状態キー：" 状態キー           UPON CONS
004020         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004030                                                   UPON CONS
004380*-----------------------------------------*
004390         CALL "actcshm"  WITH C LINKAGE
004400*-----------------------------------------*
004040         ACCEPT  キー入力 FROM CONS
004050         PERFORM ファイル閉鎖
004060         EXIT PROGRAM.
004070*
004511*================================================================*
004520 施術所情報マスタ読込 SECTION.
004530*
004540     MOVE ZERO TO 施情−施術所番号.
004550     READ 施術所情報マスタ
004560     INVALID KEY
004570         MOVE NC"施情" TO ファイル名Ｗ
004580         PERFORM エラー表示Ｒ
004590         PERFORM ファイル閉鎖
004600         MOVE 99 TO PROGRAM-STATUS
004610         EXIT PROGRAM
004620     NOT INVALID KEY
004630         MOVE 施情−新柔整師番号     TO 登録番号コードＷ
004640         MOVE 施情−郵便番号１       TO 施術所郵便番号１Ｗ
004650         MOVE 施情−郵便番号２       TO 施術所郵便番号２Ｗ
004660         MOVE 施情−住所１           TO 施術所住所１Ｗ
004670         MOVE 施情−住所２           TO 施術所住所２Ｗ
004720         MOVE 施情−接骨院名         TO 接骨院名Ｗ
004730         MOVE 施情−代表者カナ       TO 代表者カナＷ
004740         MOVE 施情−代表者名         TO 代表者名Ｗ
004750         MOVE 施情−電話番号         TO 施術所電話番号Ｗ
004760*         MOVE 施情−接骨師会会員番号 TO 会員番号Ｗ
004770*         MOVE 施情−都道府県ＪＩＳ   TO 県コードＪＩＳＷ
004780*         MOVE 施情−都道府県政管     TO 県コード政管Ｗ
004790** 振込先情報
004800         MOVE 施情−取引先銀行名      TO 取引先銀行名Ｗ
004810         MOVE 施情−取引先銀行支店名  TO 取引先銀行支店名Ｗ
004820         MOVE 施情−預金種別          TO 預金種別Ｗ
004830         MOVE 施情−口座番号          TO 口座番号Ｗ
004840         MOVE 施情−口座名義人        TO 口座名義人ＷＰ
004850*
004860         PERFORM VARYING 文字ＣＮＴ FROM 40 BY -1
004870                   UNTIL (口座名義人ＷＰ(文字ＣＮＴ:1) NOT = SPACE) OR
004880                         (文字ＣＮＴ <= ZERO)
004890             CONTINUE
004900         END-PERFORM
004910*         STRING 口座名義人ＷＰ               DELIMITED BY SPACE
004920         STRING 口座名義人ＷＰ(1:文字ＣＮＴ) DELIMITED BY SIZE
004930                "　　"                       DELIMITED BY SIZE
004940                "へ振り込みを御願い致します" DELIMITED BY SIZE
004950                INTO 口座名義人Ｗ
004960         END-STRING
004970         MOVE 施情−口座名義人カナ    TO 口座名義人カナＷ
004980*         STRING 取引先銀行名Ｗ     DELIMITED BY SPACE
004990*                " "                DELIMITED BY SIZE
005000*                取引先銀行支店名Ｗ DELIMITED BY SPACE
005010*                INTO 銀行名支店名Ｗ
005020*         END-STRING
005030         EVALUATE 預金種別Ｗ
005040         WHEN 1
005050             MOVE NC"普通" TO 預金種別コメントＷ
005060         WHEN 2
005070             MOVE NC"当座" TO 預金種別コメントＷ
005080         WHEN OTHER
005090             MOVE SPACE    TO 預金種別コメントＷ
005100         END-EVALUATE
               PERFORM 県施術ＩＤ取得
005110*
005120     END-READ.
005210*
022270*================================================================*
       県施術ＩＤ取得 SECTION.
      *
026770*********************************************
026780** ＩＤ管理マスタより　県施術ＩＤを取得する。
026790*********************************************
026900**   / 県施術ID /
026910     MOVE 01                     TO ＩＤ管−ＩＤ区分.
026830     MOVE 10                     TO ＩＤ管−保険種別.
026920     MOVE ZERO                   TO ＩＤ管−施術所番号.
026940     MOVE SPACE                  TO ＩＤ管−保険者番号.
026950     READ ＩＤ管理マスタ
026960     NOT INVALID KEY
026970         MOVE ＩＤ管−施術ＩＤ番号   TO 県施術ＩＤＷ
026980     END-READ.
005211*================================================================*
005220 ファイル閉鎖 SECTION.
005230*
002990     IF ( オープンフラグ = "YES" )
002991         CLOSE 印刷ファイル
003041     END-IF.
005250     CLOSE  施術所情報マスタ 保険者マスタ ＩＤ管理マスタ.
005270*
005271*================================================================*
005280 終了処理 SECTION.
005290*
005300     PERFORM ファイル閉鎖.
005310*
005311*================================================================*
005320 連結項目待避 SECTION.
005330*
005340     MOVE 連入−施術和暦 TO 請求和暦Ｗ.
005350     MOVE 連入−施術年   TO 請求年Ｗ.
005360     MOVE 連入−施術月   TO 請求月Ｗ.
005370*
005371*================================================================*
005380 印刷処理 SECTION.
005390*
005400     OPEN INPUT 作業ファイル１.
005410         MOVE NC"作１" TO ファイル名Ｗ.
005420         PERFORM オープンチェック.
005460     MOVE SPACE   TO 終了フラグ２.
005490     MOVE SPACE   TO 作１−保険者番号.
005490     MOVE ZERO    TO 作１−保険種別.
005490     MOVE ZERO    TO 作１−区分.
005490     MOVE 1       TO カウンタ.
005500     START 作業ファイル１ KEY IS >= 作１−保険者番号
                                          作１−保険種別
005520                                    作１−区分
005530     END-START.
005540     IF 状態キー = "00"
005550         PERFORM 作業ファイル１読込
005600         PERFORM ヘッダセット
005560         PERFORM UNTIL  終了フラグ２     NOT = SPACE
                   PERFORM UNTIL (終了フラグ２ NOT = SPACE) OR
                                 (カウンタ     >     20)
005650                 PERFORM 明細セット
005680                 PERFORM 作業ファイル１読込
                   END-PERFORM
                   IF カウンタ > 20
005818                 PERFORM 印字処理
                       PERFORM 改頁処理
005490                 MOVE 1     TO カウンタ
                       PERFORM ヘッダセット
                   END-IF
005700         END-PERFORM
               PERFORM 合計セット
005818         PERFORM 印字処理
           ELSE
006000         MOVE  NC"　印刷の対象になるデータがありません。" TO 連メ−メッセージ
006010         CALL   "MSG001"
006020         CANCEL "MSG001"
005830     END-IF.
005840     CLOSE 作業ファイル１.
005850*
005851*================================================================*
005860 作業ファイル１読込 SECTION.
005870*
005880     READ 作業ファイル１ NEXT
005890     AT END
005900         MOVE "YES" TO 終了フラグ２
005910         INITIALIZE 作１−レコード
005920     END-READ.
005930*
006401*================================================================*
006410 ヘッダセット SECTION.
006420*
006430     MOVE SPACE TO YGN584P.
006440*     INITIALIZE YGN584P.
           MOVE 請求年Ｗ             TO 施術年.
           MOVE 請求月Ｗ             TO 施術月.
006540     MOVE 登録番号Ｗ           TO 施術所コード.
006540     MOVE 県施術ＩＤＷ         TO 機関コード.
005680     MOVE 施術所郵便番号１Ｗ   TO 郵便番号１.
005690     MOVE 施術所郵便番号２Ｗ   TO 郵便番号２.
           IF 施術所郵便番号１Ｗ NOT = SPACE
               MOVE "〒"             TO 郵便１
               MOVE "-"              TO 郵便２
           END-IF.
006560     MOVE 施術所住所１Ｗ       TO 住所１.
006570     MOVE 施術所住所２Ｗ       TO 住所２.
006770     MOVE 接骨院名Ｗ           TO 施術所名.
006770     MOVE 代表者カナＷ         TO フリガナ.
006770     MOVE 代表者名Ｗ           TO 氏名.
006560     MOVE 施術所電話番号Ｗ     TO 電話番号.
007140*================================================================*
007150 明細セット SECTION.
007160*
007210     PERFORM 請求先情報取得.
007260     MOVE 請求先名称Ｗ         TO 保険者名(カウンタ).
007300     MOVE 作１−保険者番号     TO 保険者番号(カウンタ).
           IF 作１−保険種別 = 8
               MOVE NC"○"           TO 退職(カウンタ)
           ELSE
               MOVE NC"○"           TO 国保(カウンタ)
           END-IF.
           EVALUATE 作１−区分
           WHEN 1
               MOVE 8                TO 区分(カウンタ)
               MOVE 9                TO 割合(カウンタ)
           WHEN 2
               MOVE 0                TO 区分(カウンタ)
               MOVE 9                TO 割合(カウンタ)
           WHEN 3
               MOVE 2                TO 区分(カウンタ)
               MOVE 7                TO 割合(カウンタ)
           WHEN 4
               MOVE 6                TO 区分(カウンタ)
               MOVE 7                TO 割合(カウンタ)
           WHEN 5
               MOVE 4                TO 区分(カウンタ)
               MOVE 8                TO 割合(カウンタ)
           END-EVALUATE.
007300     MOVE 作１−件数           TO 件数(カウンタ).
007320     MOVE 作１−費用額         TO 費用額(カウンタ).
           COMPUTE カウンタ = カウンタ + 1.
      *
007451*================================================================*
007460 合計セット SECTION.
007470*
007490     MOVE 連印−合計件数       TO 合計件数.  
007510     MOVE 連印−合計費用額     TO 合計費用額.
007670*
006241*================================================================*
006250 請求先情報取得 SECTION.
006260*
006270*********************************************************
006280* 連結データから保険者マスタより請求先を取得する。      *
006290* ● 請求先...... 請求先名称Ｗに格納                    *
006300*********************************************************
006310     MOVE 01                  TO 保−保険種別.
006320     MOVE 作１−保険者番号    TO 保−保険者番号.
006330     MOVE SPACE               TO 請求先名称Ｗ.
006340     READ 保険者マスタ
006350     INVALID KEY
006360         MOVE SPACE           TO 請求先名称Ｗ
006370     NOT INVALID KEY
006380         MOVE 保−保険者名称  TO 請求先名称Ｗ
006390     END-READ.
006400*
007671*================================================================*
007680 印字処理 SECTION.
007690*
004310     IF ( オープンフラグ NOT = "YES" )
004320        MOVE "YES" TO オープンフラグ
004330        OPEN I-O  印刷ファイル
004340        PERFORM エラー処理Ｐ
004350     END-IF.
013440*
007700     MOVE "YGN584P"  TO  定義体名Ｐ.
007720     MOVE SPACE      TO  処理種別Ｐ.
007710     MOVE "SCREEN"   TO  項目群名Ｐ.
007740     WRITE YGN584P.
007750     PERFORM エラー処理Ｐ.
007751*
007930*================================================================*
007940 エラー表示 SECTION.
007950*
007960     DISPLAY NC"ファイル書込エラー：" ファイル名Ｗ UPON CONS.
007970     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
007980     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
000080*-----------------------------------------*
000090     CALL "actcshm"  WITH C LINKAGE.
000100*-----------------------------------------*
007990     ACCEPT  キー入力 FROM CONS.
008000*
008001*================================================================*
008010 エラー処理Ｐ SECTION.
008020*
008030     IF 通知情報Ｐ NOT = "00"
008040         DISPLAY NC"帳票エラー"              UPON CONS
008050         DISPLAY NC"項目群名Ｐ：" 項目群名Ｐ UPON CONS
008060         DISPLAY NC"通知情報Ｐ：" 通知情報Ｐ UPON CONS
008070         DISPLAY NC"拡張制御Ｐ：" 拡張制御Ｐ UPON CONS
008080         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
008090                                             UPON CONS
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
008100         ACCEPT  キー入力 FROM CONS
008110         PERFORM ファイル閉鎖
008120         MOVE 99 TO PROGRAM-STATUS
008130         EXIT PROGRAM
008140     END-IF.
008150*
008151*================================================================*
008160 エラー表示Ｒ SECTION.
008170*
008180     DISPLAY NC"ファイル読込エラー" ファイル名Ｗ UPON CONS.
008190     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
000080*-----------------------------------------*
000090     CALL "actcshm"  WITH C LINKAGE.
000100*-----------------------------------------*
008200     ACCEPT  キー入力 FROM CONS.
008210     PERFORM ファイル閉鎖.
008220     EXIT PROGRAM.
008230*
008231*================================================================*
008240 エラー表示その他 SECTION.
008250*
008260     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
008270     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
008280                                                   UPON CONS.
000080*-----------------------------------------*
000090     CALL "actcshm"  WITH C LINKAGE.
000100*-----------------------------------------*
008290     ACCEPT  キー入力 FROM CONS.
008300     PERFORM ファイル閉鎖.
008310     EXIT PROGRAM.
008320*================================================================*
006660 改頁処理  SECTION.
006670
006680     MOVE "YGN584P" TO  定義体名Ｐ.
006690     MOVE "CT"      TO  処理種別Ｐ.
006700     MOVE "PAGE"    TO  拡張制御Ｐ.
006710     MOVE SPACE     TO  項目群名Ｐ.
006730     WRITE YGN584P.
006740     PERFORM エラー処理Ｐ.
006750     MOVE SPACE     TO  拡張制御Ｐ.
008320*================================================================*
008330******************************************************************
008340 END PROGRAM YGN584.
008350******************************************************************
