000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YNG6061.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*         レセプトリスト印刷【ﾃﾞｰﾀ作成】柔+ｳｨﾝﾄﾞｳｽﾞ版
000100*  請求年月バージョン 
000110*         MED = YAW610  
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2014-03-05
000140 DATE-COMPILED.          2014-03-05
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000270     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000280                             ORGANIZATION             IS  INDEXED
000290                             ACCESS MODE              IS  DYNAMIC
000300                             RECORD KEY               IS 受−施術和暦年月
000310                                                          受−患者コード
000320                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000330                                                          受−患者カナ
000340                                                          受−患者コード
000350                             ALTERNATE RECORD KEY     IS  受−患者コード
000360                                                         受−施術和暦年月
000370                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000380                                                          受−保険種別
000390                                                          受−保険者番号
000400                                                          受−患者コード
000410                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000420                                                          受−公費種別
000430                                                     受−費用負担者番号
000440                                                          受−患者コード
000450                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000460                                                          受−助成種別
000470                                                  受−費用負担者番号助成
000480                                                          受−患者コード
000490                             ALTERNATE RECORD KEY  IS 受−請求和暦年月
000500                                                      受−施術和暦年月
000510                                                      受−患者コード
000520                             FILE STATUS              IS  状態キー
000530                             LOCK        MODE         IS  AUTOMATIC.
000630     SELECT  制御情報マスタ  ASSIGN      TO        SEIGYOL
000640                             ORGANIZATION             IS  INDEXED
000650                             ACCESS MODE              IS  DYNAMIC
000660                             RECORD KEY               IS  制−制御区分
000670                             FILE STATUS              IS  状態キー
000680                             LOCK        MODE         IS  AUTOMATIC.
000241     SELECT  生保情報Ｆ      ASSIGN      TO        SEIHOJL
000242                             ORGANIZATION             IS INDEXED
000243                             ACCESS MODE              IS DYNAMIC
000244                             RECORD KEY               IS 生保−施術和暦年月
000245                                                         生保−患者コード
000255                             ALTERNATE RECORD KEY     IS 生保−患者コード
000265                                                         生保−施術和暦年月
000277                             FILE STATUS              IS 状態キー
000278                             LOCK        MODE         IS AUTOMATIC.
           SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
                                   ORGANIZATION             IS  INDEXED
                                   ACCESS MODE              IS  DYNAMIC
                                   RECORD KEY               IS  レセ−施術和暦年月
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−患者コード
                                                                レセ−施術和暦年月
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−施術和暦年月
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−レセ種別
                                                                レセ−請求保険者番号
                                                                レセ−患者コード
                                                                レセ−施術和暦年月
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−請求保険者番号
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                                                レセ−施術和暦年月
                                   FILE STATUS              IS  状態キー
                                   LOCK        MODE         IS  AUTOMATIC.
001060     SELECT  作業ファイル１  ASSIGN      TO   "C:\MAKISHISYS\YAWOBJ\TEMP\W60610L.DAT"
001070                             ORGANIZATION             IS  INDEXED
001080                             ACCESS                   IS  DYNAMIC
001090                             RECORD      KEY          IS  作１−請求和暦年月
001100                                                          作１−印刷区分
001120                                                          作１−印刷順番
001130                                                          作１−保険者番号
001140                                                          作１−本人家族区分
001150                                                          作１−患者カナ
001160                                                          作１−患者コード
001170                                                          作１−施術和暦年月
001180                             FILE        STATUS       IS  状態キー
001190                             LOCK        MODE         IS  AUTOMATIC.
000910* レセ並び順用
000920     SELECT  作業ファイル２  ASSIGN      TO   "C:\MAKISHISYS\YAWOBJ\TEMP\W6102L.DAT"
000930                             ORGANIZATION             IS  INDEXED
000940                             ACCESS                   IS  DYNAMIC
001230                             RECORD      KEY          IS  作２−保険区分
                                                                作２−順番
                                   ALTERNATE RECORD KEY     IS  作２−施術和暦年月
                                                                作２−患者コード
                                                                作２−保険区分
                                                                作２−順番
000960                             FILE        STATUS       IS  状態キー
000970                             LOCK        MODE         IS  AUTOMATIC.
001260******************************************************************
001270*                      DATA DIVISION                             *
001280******************************************************************
001290 DATA                    DIVISION.
001300 FILE                    SECTION.
001310*                           ［ＲＬ＝  ３２０］
001320 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001330     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
001340*                           ［ＲＬ＝  ２５６］
001350 FD  制御情報マスタ          BLOCK   CONTAINS   1   RECORDS.
001360     COPY SEIGYO          OF  XFDLIB  JOINING   制   AS  PREFIX.
001080* 
000280 FD  生保情報Ｆ          BLOCK   CONTAINS   1   RECORDS.
000281     COPY SEIHOJ          OF  XFDLIB  JOINING   生保   AS  PREFIX.
      *
       FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS GLOBAL.
           COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
001520* 
001530 FD  作業ファイル１ RECORD  CONTAINS 256 CHARACTERS.
001540 01  作１−レコード.
001550     03  作１−レコードキー.
001560         05  作１−請求和暦年月.
001570             07  作１−請求和暦              PIC 9.
001580             07  作１−請求年                PIC 9(2).
001590             07  作１−請求月                PIC 9(2).
001600         05  作１−印刷区分                  PIC 9.
001620         05  作１−印刷順番                  PIC 9(2).
001630         05  作１−保険者番号                PIC X(10).
001640         05  作１−本人家族区分              PIC 9.
001650         05  作１−患者カナ                  PIC X(20).
001660         05  作１−患者コード.
001670             07 作１−患者番号               PIC 9(6).
001680             07 作１−枝番                   PIC X(1).
001690         05  作１−施術和暦年月.
001700             07  作１−施術和暦              PIC 9.
001710             07  作１−施術年                PIC 9(2).
001720             07  作１−施術月                PIC 9(2).
001730     03  作１−レコードデータ.
001740         05  作１−患者氏名                  PIC X(50).
001750         05  作１−保険種別                  PIC 9(2).
001221         05  作１−レセ種別                  PIC 9(2).
001740         05  作１−被保険者氏名              PIC X(50).
001366         05  作１−費用額                    PIC 9(6).
001367         05  作１−負担額                    PIC 9(6).
001368         05  作１−請求額                    PIC 9(6).
001369         05  作１−レセ印刷区分              PIC 9.
001770         05  FILLER                          PIC X(82).
001780*
001790 FD  作業ファイル２ RECORD  CONTAINS 155 CHARACTERS.
001800 01  作２−レコード.
001810     03  作２−レコードキー.
001820         05  作２−保険区分                PIC 9(1).
001820         05  作２−順番                    PIC 9(4).
001830     03  作２−レコードデータ.
               05  作２−レセ種別                PIC 9(2).
001840         05  作２−患者コード.
001850             07 作２−患者番号             PIC 9(6).
001860             07 作２−枝番                 PIC X(1).
001870         05  作２−患者氏名                PIC X(50).
001740         05  作２−被保険者氏名            PIC X(50).
001880         05  作２−保険種別                PIC 9(2).
               05  作２−請求保険者番号          PIC X(10).
001890         05  作２−本人家族                PIC X(4).
001900         05  作２−施術和暦年月.
001910             07  作２−施術和暦            PIC 9.
001920             07  作２−施術年              PIC 9(2).
001930             07  作２−施術月              PIC 9(2).
001940         05  作２−印刷指定                PIC 9(1).
               05  作２−費用額                  PIC 9(6).
               05  作２−負担額                  PIC 9(6).
               05  作２−請求額                  PIC 9(6).
               05  作２−レセ印刷区分            PIC 9.
001960*----------------------------------------------------------------*
001970******************************************************************
001980*                WORKING-STORAGE SECTION                         *
001990******************************************************************
002000 WORKING-STORAGE         SECTION.
002010 01 キー入力                           PIC X     VALUE SPACE.
002020 01 状態キー                           PIC X(2)  VALUE SPACE.
002030 01 終了フラグ                         PIC X(3)  VALUE SPACE.
002040 01 終了フラグ２                       PIC X(3)  VALUE SPACE.
002050 01 作成フラグ                         PIC X(3)  VALUE SPACE.
002060 01 実行キーＷ                         PIC X(3)  VALUE SPACE.
002070 01 施術記録有Ｗ                       PIC X(3)  VALUE SPACE.
002080*
002090 01 順番Ｗ                             PIC 9(4)  VALUE ZERO.
002090 01 生保順番Ｗ                         PIC 9(4)  VALUE ZERO.
002100 01 保険種別ＷＲ                       PIC 9(2)  VALUE ZERO.
002110 01 レセプト種類ＷＲ                   PIC X(4)  VALUE SPACE.
002120 01 レセ並び順昇降Ｗ                   PIC 9(1) VALUE ZERO.
002130 01 保険者番号ＷＲ                     PIC X(10) VALUE SPACE.
002140 01 本人家族区分ＷＲ                   PIC 9     VALUE ZERO.
002150 01 請求和暦年月ＷＲ.
002160    03 請求和暦ＷＲ                    PIC 9     VALUE ZERO.
002170    03 請求年ＷＲ                      PIC 9(2)  VALUE ZERO.
002180    03 請求月ＷＲ                      PIC 9(2)  VALUE ZERO.
       01 金額Ｗ.
          03 費用額Ｗ                        PIC 9(6)  VALUE ZERO.
          03 負担額Ｗ                        PIC 9(6)  VALUE ZERO.
          03 請求額Ｗ                        PIC 9(6)  VALUE ZERO.
002220*
002230 01 請求年月Ｗ.
002240    03 請求和暦Ｗ                      PIC 9(1)  VALUE ZERO.
002250    03 請求年Ｗ                        PIC 9(2)  VALUE ZERO.
002260    03 請求月Ｗ                        PIC 9(2)  VALUE ZERO.
002270 01 施術年月Ｗ.
002280    03 施術和暦Ｗ                      PIC 9(1)  VALUE ZERO.
002290    03 施術年Ｗ                        PIC 9(2)  VALUE ZERO.
002300    03 施術月Ｗ                        PIC 9(2)  VALUE ZERO.
002310 01 本人家族区分Ｗ                     PIC 9(1)  VALUE ZERO.
002320 01 ファイル名                         PIC N(2)  VALUE SPACE.
002330 01 保険種別Ｗ                         PIC 9(2)  VALUE ZERO.
002340 01 元保険者番号Ｗ                     PIC X(10) VALUE SPACE.
002350 01 保険者番号Ｗ                       PIC X(10) VALUE SPACE.
002360 01 親保険種別Ｗ                       PIC 9(2)  VALUE ZERO.
002380 01 対象件数Ｗ                         PIC 9(5)  VALUE ZERO.
002390*
002391*
002392** 助成レセまとめ用
002393 01 助成レセまとめフラグ               PIC X(3)  VALUE SPACE.
002394*
002400******************************************************************
002410*                          連結項目                              *
002420******************************************************************
002430*
002440****************
002450* 画面入力情報 *
002460****************
002470**
002480 01 連入−入力データ６１０ IS EXTERNAL.
002490    03 連入−請求年月.
002500       05 連入−請求和暦                  PIC 9.
002510       05 連入−請求年                    PIC 9(2).
002520       05 連入−請求月                    PIC 9(2).
002540    03 連入−保険種別                     PIC 9(2).
002600*
002610*****
002611************************
002612* 助成レセまとめ
002613************************
002614 01 連レセまとめ−キー IS EXTERNAL.
002615    03 連レセまとめ−施術和暦年月.
002616       05 連レセまとめ−施術和暦               PIC 9.
002617       05 連レセまとめ−施術年月.
002618          07 連レセまとめ−施術年              PIC 9(2).
002619          07 連レセまとめ−施術月              PIC 9(2).
002620    03 連レセまとめ−患者コード.
002621       05 連レセまとめ−患者番号               PIC 9(6).
002622       05 連レセまとめ−枝番                   PIC X(1).
002623**-------------------------------------------------------**
002624*   1:助成レセプトなしの本体まとめの判定
002625*   2:横浜・川崎用の社保助成レセかの判定
002626    03 連レセまとめ−判定区分                  PIC 9.
002627**-------------------------------------------------------**
002628*  / OUT /　 0:対象外、1:対象
002629    03 連レセまとめ−判定結果                  PIC 9.
002630**
002631******************************************************************
002632*                      PROCEDURE  DIVISION                       *
002640******************************************************************
002650 PROCEDURE               DIVISION.
002660************
002670*           *
002680* 初期処理   *
002690*           *
002700************
002710     PERFORM ファイルオープン.
002720     PERFORM 連結項目待避.
002730************
002740*           *
002750* 主処理     *
002760*           *
002770************
002780     PERFORM 作業ファイル１作成.
002790     PERFORM 作業ファイル２作成.
002800************
002810*           *
002820* 終了処理   *
002830*           *
002840************
002850     PERFORM 終了処理.
002860     MOVE ZERO TO PROGRAM-STATUS.
002870     EXIT PROGRAM.
002880*
002890*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002900*================================================================*
002910 連結項目待避 SECTION.
002920*
002930* 連結項目の待避
002940     MOVE 連入−請求和暦      TO 請求和暦ＷＲ.
002950     MOVE 連入−請求年        TO 請求年ＷＲ.
002960     MOVE 連入−請求月        TO 請求月ＷＲ.
002960     MOVE 連入−保険種別      TO 保険種別ＷＲ.
002970* レセ並び順昇順・降順セット
002980     MOVE ZEROS TO 制−制御区分.
002990     READ 制御情報マスタ
003000     NOT INVALID KEY
003010         MOVE 制−レセ並び順昇降 TO レセ並び順昇降Ｗ
003020     END-READ.
003030*
003040*================================================================*
003050 ファイルオープン SECTION.
003060*
003070     OPEN INPUT 受診者情報Ｆ.
003080         MOVE NC"受診者情報Ｆ" TO ファイル名.
003090         PERFORM オープンチェック.
003130     OPEN INPUT 制御情報マスタ
003140         MOVE NC"制御情報" TO ファイル名.
003150         PERFORM オープンチェック.
006630     OPEN INPUT 生保情報Ｆ.
006640         MOVE NC"生保" TO ファイル名.
006650         PERFORM オープンチェック.
003250     OPEN INPUT レセプトＦ.
003260         MOVE NC"レセプトＦ" TO ファイル名.
003270         PERFORM オープンチェック.
003280*================================================================*
003290 オープンチェック SECTION.
003300*
003310     IF 状態キー  NOT =  "00"
003320         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
003330         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003340         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003350                                                 UPON CONS
003131*-----------------------------------------*
003132         CALL "actcshm"  WITH C LINKAGE
003133*-----------------------------------------*
003360         ACCEPT  キー入力 FROM CONS
003370         PERFORM ファイル閉鎖
003380         MOVE 99 TO PROGRAM-STATUS
003390         EXIT PROGRAM.
003400*================================================================*
003410 エラー表示 SECTION.
003420*
003430     DISPLAY NC"状態キー" 状態キー  UPON CONS.
003440     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
003450     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
003460     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
003321*-----------------------------------------*
003322     CALL "actcshm"  WITH C LINKAGE.
003323*-----------------------------------------*
003470     ACCEPT  キー入力 FROM CONS.
003480     PERFORM ファイル閉鎖.
003490     MOVE 99 TO PROGRAM-STATUS.
003500     EXIT PROGRAM.
003510*================================================================*
003520 作業ファイル１作成 SECTION.
003530*
003540     OPEN OUTPUT 作業ファイル１.
003550         MOVE NC"作業" TO ファイル名.
003560         PERFORM オープンチェック.
003570*
003790     MOVE "YES"         TO 実行キーＷ.
005170     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
005180     MOVE 請求年ＷＲ    TO レセ−請求年.  
005190     MOVE 請求月ＷＲ    TO レセ−請求月.  
013730     MOVE ZERO          TO レセ−レセ種別.
005200     MOVE ZERO          TO レセ−施術和暦.
005210     MOVE ZERO          TO レセ−施術年.  
005220     MOVE ZERO          TO レセ−施術月.  
005230     MOVE ZERO          TO レセ−患者番号.
005240     MOVE SPACE         TO レセ−枝番.    
013770     START レセプトＦ   KEY IS >= レセ−請求和暦年月
000230                                  レセ−施術和暦年月
000240                                  レセ−患者コード
000250                                  レセ−レセ種別
003690     END-START.
003700     IF 状態キー = "00"
003710         MOVE SPACE  TO 終了フラグ
003720         PERFORM レセプトＦ読込
003730         PERFORM UNTIL ( 終了フラグ = "YES" ) OR
005330                       ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
005340                       ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
005350                       ( レセ−請求月   NOT = 請求月ＷＲ   )
003770             PERFORM データチェック
003780** 労災・自賠責・自費除く
003790             IF  レセ−レセ種別 = 4 OR 5 OR 6 OR 8
004040                MOVE SPACE  TO 実行キーＷ
004050             END-IF
003820***
003830             IF 実行キーＷ = "YES"
003840*
003842*↓* 特別処理（助成レセまとめ）
003843                IF 受−助成種別 NOT = ZERO
003844                   PERFORM 助成レセまとめ判定
003845                ELSE
003846                   MOVE SPACE TO 助成レセまとめフラグ
003847                END-IF
003848*↑*
005610                EVALUATE TRUE
003850*                ******************
003860*                * 健保 老人含まず*
003870*                ******************
013940                WHEN レセ−レセ種別   = 1
                          IF (保険種別ＷＲ = ZERO) OR
                             ((受−保険種別 = 保険種別ＷＲ) AND ( 受−公費種別 = ZERO ))
003920                         MOVE SPACE TO 作１−レコード
003930                         INITIALIZE 作１−レコード
003950                         MOVE 受−保険者番号  TO 作１−保険者番号
003960                         MOVE 受−保険種別      TO 作１−保険種別
003940                         PERFORM 印刷順番セット
004010                         PERFORM 料金セット
004010                         PERFORM レコードセット
004020                         PERFORM ファイル書込
004030                     END-IF
004050*                ********
004060*                * 老人 *
004070*                ********
                      WHEN レセ−レセ種別   = 2 
                          IF (保険種別ＷＲ = ZERO) OR
                             (受−公費種別 = 保険種別ＷＲ)
004100                       MOVE SPACE TO 作１−レコード
004110                       INITIALIZE 作１−レコード
004130                       MOVE 受−費用負担者番号     TO 作１−保険者番号
004140                       MOVE 受−公費種別      TO 作１−保険種別
004120                       PERFORM 印刷順番セット
004010                       PERFORM 料金セット
004190                       PERFORM レコードセット
004200                       PERFORM ファイル書込
004210                    END-IF
004220*                ********
004230*                * 助成 *
004240*                ********
                      WHEN レセ−レセ種別   = 3
004241*↓* 特別処理（助成レセまとめ）**
004242                   IF ( 助成レセまとめフラグ = SPACE ) 
                            IF (保険種別ＷＲ = ZERO) OR
                               (受−助成種別 = 保険種別ＷＲ)
004270                         MOVE SPACE TO 作１−レコード
004280                         INITIALIZE 作１−レコード
004290                         MOVE 受−助成種別           TO 作１−保険種別
004300                         MOVE 受−費用負担者番号助成 TO 作１−保険者番号
004310                         PERFORM 印刷順番セット
004010                         PERFORM 助成料金セット
004360                         PERFORM レコードセット
004370                         PERFORM ファイル書込
004380                      END-IF
004381                   END-IF
004220*                ********
004230*                * 生保 *
004240*                ********
                      WHEN レセ−レセ種別   = 7
                          IF (保険種別ＷＲ = ZERO) OR
                             (受−保険種別 = 保険種別ＷＲ)
003920                        MOVE SPACE TO 作１−レコード
003930                        INITIALIZE 作１−レコード
003960                        MOVE 受−保険種別     TO 作１−保険種別
                              MOVE 受−施術和暦年月 TO 生保−施術和暦年月
                              MOVE 受−患者コード   TO 生保−患者コード
                              READ 生保情報Ｆ
                              NOT INVALID KEY
003950                           MOVE 生保−負担者番号   TO 作１−保険者番号
                              END-READ
004310                        PERFORM 印刷順番セット
004010                        PERFORM 料金セット
004370                        PERFORM レコードセット
004380                        PERFORM ファイル書込
004391                    END-IF
                      END-EVALUATE
004390             END-IF
004400             PERFORM レセプトＦ読込
004410         END-PERFORM
004420     END-IF.
004430     CLOSE 作業ファイル１.
004440*================================================================*
004450 レセプトＦ読込 SECTION.
004460*
004470     READ レセプトＦ NEXT
004480     AT END
004490         MOVE "YES" TO 終了フラグ
004500     END-READ.
004510*================================================================*
004520 データチェック SECTION.
004530*
004540     MOVE SPACE          TO 実行キーＷ.
005300     IF ( レセ−レセ印刷対象区分 NOT = 1 )
005310        MOVE "YES"  TO 実行キーＷ
005320     END-IF.
019520* *****************************************************************
019530* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
019540* *****************************************************************
005360     IF 実行キーＷ  = "YES"
005370*      (再度、実行キーＷ SPACE)
005380        MOVE SPACE  TO 実行キーＷ
019640        IF レセ−請求対象区分 NOT = ZERO
004090           MOVE レセ−施術和暦  TO 受−施術和暦
004100           MOVE レセ−施術年    TO 受−施術年
004110           MOVE レセ−施術月    TO 受−施術月
004120           MOVE レセ−患者番号  TO 受−患者番号
004130           MOVE レセ−枝番      TO 受−枝番
                 READ 受診者情報Ｆ
                 NOT INVALID KEY
019880              MOVE "YES"  TO 実行キーＷ
                 END-READ
019900        ELSE
019910           MOVE SPACE  TO 実行キーＷ
              END-IF
019950     END-IF.
005000*
005080*================================================================*
005090 レコードセット SECTION.
005100*
005110     MOVE レセ−請求和暦    TO 作１−請求和暦.
005120     MOVE レセ−請求年      TO 作１−請求年.
005130     MOVE レセ−請求月      TO 作１−請求月.
005140     MOVE レセ−施術和暦    TO 作１−施術和暦.
005150     MOVE レセ−施術年      TO 作１−施術年.
005160     MOVE レセ−施術月      TO 作１−施術月.
005170     MOVE 受−本人家族区分  TO 作１−本人家族区分.
005180     MOVE 受−患者カナ      TO 作１−患者カナ.
005190     MOVE 受−患者コード    TO 作１−患者コード.
005200     MOVE 受−患者氏名      TO 作１−患者氏名.
005200     MOVE 受−被保険者氏名  TO 作１−被保険者氏名.
           MOVE レセ−レセ種別    TO 作１−レセ種別.
           MOVE 費用額Ｗ          TO 作１−費用額.
           MOVE 請求額Ｗ          TO 作１−請求額.
           MOVE 負担額Ｗ          TO 作１−負担額.
005240*
005390*================================================================*
       料金セット SECTION.
           MOVE レセ−合計            TO 費用額Ｗ.
           MOVE レセ−請求金額        TO 請求額Ｗ.
           MOVE レセ−一部負担金      TO 負担額Ｗ.
005210     MOVE 受−レセ印刷区分      TO 作１−レセ印刷区分.
005380*
005390*================================================================*
       助成料金セット SECTION.
           MOVE レセ−合計                  TO 費用額Ｗ.
           MOVE レセ−助成請求金額          TO 請求額Ｗ.
           MOVE レセ−受給者負担額          TO 負担額Ｗ.
005210     MOVE 受−レセ印刷区分助成        TO 作１−レセ印刷区分.
005380*
005380*================================================================*
005390 ファイル書込 SECTION.
005400*
005410     WRITE 作１−レコード
005420     INVALID KEY
005430         MOVE NC"作業"  TO ファイル名
005440         PERFORM エラー表示
005450     END-WRITE.
005460*================================================================*
005470 作業ファイル２作成 SECTION.
005480*
005490     OPEN INPUT  作業ファイル１.
005500     OPEN OUTPUT 作業ファイル２.
005510*
005520     IF レセ並び順昇降Ｗ = 1
005530         PERFORM レセ並び順降順ファイル作成
005540     ELSE
005550         PERFORM 作業ファイル２作成処理
005560     END-IF.
005570*
005580     CLOSE 作業ファイル２.
005590     CLOSE 作業ファイル１.
005600*================================================================*
005610 レセ並び順降順ファイル作成 SECTION.
005620*
005630     MOVE ZERO       TO 順番Ｗ.      
005640     MOVE 9          TO 作１−請求和暦.
005650     MOVE 99         TO 作１−請求年.
005660     MOVE 99         TO 作１−請求月.
005670     MOVE 9          TO 作１−印刷区分.
005690     MOVE 9          TO 作１−印刷順番.
005700     MOVE HIGH-VALUE TO 作１−保険者番号.
005710     MOVE 9          TO 作１−本人家族区分.
005720     MOVE HIGH-VALUE TO 作１−患者カナ.
005730     MOVE 999999     TO 作１−患者番号.
005740     MOVE HIGH-VALUE TO 作１−枝番.
005750     MOVE 9          TO 作１−施術和暦.
005760     MOVE 99         TO 作１−施術年.
005770     MOVE 99         TO 作１−施術月.
005780     START 作業ファイル１ KEY IS <=  作１−請求和暦年月
005790                                     作１−印刷区分
005810                                     作１−印刷順番
005820                                     作１−保険者番号
005830                                     作１−本人家族区分
005840                                     作１−患者カナ
005850                                     作１−患者コード
005860                                     作１−施術和暦年月
005870                                     REVERSED
005880     END-START.
005890     IF 状態キー = "00"
005900*
005910         MOVE SPACE TO 終了フラグ
005920         PERFORM 作業ファイル１読込
005930         PERFORM UNTIL 終了フラグ = "YES"
005940             MOVE SPACE TO 作２−レコード
005950             INITIALIZE 作２−レコード
005960*
005970                 PERFORM 作業レコード２セット
005980                 PERFORM 作業ファイル２書込
005990*
006000             PERFORM 作業ファイル１読込
006010         END-PERFORM
006020     END-IF.
006030*================================================================*
006040 作業ファイル２作成処理 SECTION.
006050*
006060     MOVE ZERO       TO 順番Ｗ.      
006070     MOVE ZERO       TO 作１−請求和暦.
006080     MOVE ZERO       TO 作１−請求年.
006090     MOVE ZERO       TO 作１−請求月.
006100     MOVE ZERO       TO 作１−印刷区分.
006120     MOVE ZERO       TO 作１−印刷順番.
006130     MOVE LOW-VALUE  TO 作１−保険者番号.
006140     MOVE ZERO       TO 作１−本人家族区分.
006150     MOVE SPACE      TO 作１−患者カナ.
006160     MOVE ZERO       TO 作１−患者番号.
006170     MOVE LOW-VALUE  TO 作１−枝番.
006180     MOVE ZERO       TO 作１−施術和暦.
006190     MOVE ZERO       TO 作１−施術年.
006200     MOVE ZERO       TO 作１−施術月.
006210     START 作業ファイル１ KEY IS >=  作１−請求和暦年月
006220                                     作１−印刷区分
006240                                     作１−印刷順番
006250                                     作１−保険者番号
006260                                     作１−本人家族区分
006270                                     作１−患者カナ
006280                                     作１−患者コード
006290                                     作１−施術和暦年月
006300     END-START.
006310     IF 状態キー = "00"
006320*
006330         MOVE SPACE TO 終了フラグ
006340         PERFORM 作業ファイル１読込
006350         PERFORM UNTIL 終了フラグ = "YES"
006360             MOVE SPACE TO 作２−レコード
006370             INITIALIZE 作２−レコード
006380*
006390                 PERFORM 作業レコード２セット
006400                 PERFORM 作業ファイル２書込
006410*
006420             PERFORM 作業ファイル１読込
006430         END-PERFORM
006440     END-IF.
006450*================================================================*
006460 作業ファイル１読込 SECTION.
006470*
006480     READ 作業ファイル１ NEXT
006490     AT END
006500         MOVE "YES" TO 終了フラグ
006510     END-READ.
006520*===============================================================*
006530 作業レコード２セット SECTION.
006540*
           MOVE 作１−レセ種別       TO 作２−レセ種別.
006550     MOVE 作１−施術和暦       TO 作２−施術和暦.
006560     MOVE 作１−施術年         TO 作２−施術年.
006570     MOVE 作１−施術月         TO 作２−施術月.
006580     MOVE 作１−患者コード     TO 作２−患者コード.
006590     MOVE 作１−保険種別       TO 作２−保険種別
006600     MOVE 作１−患者氏名       TO 作２−患者氏名.
006610     MOVE 作１−被保険者氏名   TO 作２−被保険者氏名.
006610     IF 作１−本人家族区分 = 1
006620         MOVE "本人"           TO 作２−本人家族
006630     ELSE
006640         MOVE "家族"           TO 作２−本人家族
006650     END-IF.
           IF 作１−保険者番号(1:2) = "00"
              MOVE 作１−保険者番号(3:8)  TO 作２−請求保険者番号
           ELSE
              MOVE 作１−保険者番号  TO 作２−請求保険者番号
           END-IF.
           MOVE 作１−費用額         TO 作２−費用額.
           MOVE 作１−請求額         TO 作２−請求額.
           MOVE 作１−負担額         TO 作２−負担額.
005210     MOVE 作１−レセ印刷区分   TO 作２−レセ印刷区分.
           IF 作１−レセ種別 = 7
              MOVE 1                 TO 作２−保険区分
006670        COMPUTE 生保順番Ｗ  = 生保順番Ｗ + 1
006680        MOVE 生保順番Ｗ        TO 作２−順番
           ELSE
              MOVE ZERO              TO 作２−保険区分
006670        COMPUTE 順番Ｗ  = 順番Ｗ + 1
006680        MOVE 順番Ｗ            TO 作２−順番
           END-IF.
006680*===============================================================*
006690 作業ファイル２書込 SECTION.
006700*
006710     WRITE 作２−レコード
006720     INVALID KEY
006730**         DISPLAY NC"状態キー" 状態キー  UPON MSGBOX
006740         MOVE NC"作２" TO ファイル名
006750         PERFORM エラー表示
006760         PERFORM ファイル閉鎖
006770         MOVE 99 TO PROGRAM-STATUS
006780         EXIT PROGRAM.
006790*================================================================*
006800 ファイル閉鎖 SECTION.
006810*
006820     CLOSE 受診者情報Ｆ 制御情報マスタ
006830           レセプトＦ     生保情報Ｆ.
006840*================================================================*
006850 終了処理 SECTION.
006860*
006870     PERFORM ファイル閉鎖.
007280*================================================================*
007290 印刷順番セット SECTION.
007300*
      * 101:県内社保（本人）
      * 102:県内社保（家族）
      * 111:県外社保（本人）
      * 112:県外社保（家族）
      * 201:県内国保（70歳以上9割）
      * 202:県内国保（70歳以上7割）
      * 203:県内国保（一般）
      * 204:県内国保（未就学児）
      * 205:県内退職（本人）
      * 206:県内退職（家族）
      * 207:県内退職（未就学児）
      * 211:医師国保（本人）
      * 212:医師国保（家族）
      * 221:建設国保（本人）
      * 222:建設国保（家族）
      * 231:県内後高（9割）
      * 232:県内後高（7割）
      * 301:福祉医療
      * 401:県外国保
      * 402:県外退職（本人）
      * 403:県外退職（家族）
      * 501:県外後高
      * 601:共済（本人）
      * 602:共済（家族）
      * 701:県内組合（本人）
      * 702:県内組合（家族）
      * 711:県外組合（本人）
      * 712:県外組合（家族）
      * 801:生保
      *
007310     EVALUATE 作１−保険種別
007400     WHEN 01
               IF 作１−保険者番号(1:2) = "20"
007410            MOVE 2  TO 作１−印刷区分
                  EVALUATE 作１−保険者番号
                  WHEN "203018"
                      IF 受−本人家族区分 = 1
007420                    MOVE 11 TO 作１−印刷順番
                      ELSE
007420                    MOVE 12 TO 作１−印刷順番
                      END-IF
                  WHEN "203034"
                      IF 受−本人家族区分 = 1
007420                    MOVE 21 TO 作１−印刷順番
                      ELSE
007420                    MOVE 22 TO 作１−印刷順番
                      END-IF
                  WHEN OTHER
                      EVALUATE 受−特別区分
                      WHEN 1
                      WHEN 2
007420                    MOVE 01 TO 作１−印刷順番
                      WHEN 3
007420                    MOVE 02 TO 作１−印刷順番
                      WHEN 6
007420                    MOVE 04 TO 作１−印刷順番
                      WHEN OTHER
007420                    MOVE 03 TO 作１−印刷順番
                      END-EVALUATE
                   END-EVALUATE
               ELSE
007410            MOVE 4  TO 作１−印刷区分
007420            MOVE 01 TO 作１−印刷順番
               END-IF
007430     WHEN 08
               IF 作１−保険者番号(3:2) = "20"
007410            MOVE 2  TO 作１−印刷区分
                  EVALUATE 受−特別区分
                  WHEN 6
007420                MOVE 07 TO 作１−印刷順番
                  WHEN OTHER
                      IF 受−本人家族区分 = 1
007420                    MOVE 05 TO 作１−印刷順番
                      ELSE
007420                    MOVE 06 TO 作１−印刷順番
                      END-IF
                  END-EVALUATE
               ELSE
007410            MOVE 4  TO 作１−印刷区分
                  IF 受−本人家族区分 = 1
007420                MOVE 02 TO 作１−印刷順番
                  ELSE
007420                MOVE 03 TO 作１−印刷順番
                  END-IF
               END-IF
007430     WHEN 05
               IF 作１−保険者番号(3:2) = "20"
007410            MOVE 2  TO 作１−印刷区分
                  EVALUATE 受−特別区分
                  WHEN 1
007420                MOVE 31 TO 作１−印刷順番
                  WHEN OTHER
007420                MOVE 32 TO 作１−印刷順番
                  END-EVALUATE
               ELSE
007410            MOVE 5  TO 作１−印刷区分
007420            MOVE 01 TO 作１−印刷順番
               END-IF
007460     WHEN 02
007470     WHEN 06
007480     WHEN 07
007410         MOVE 1  TO 作１−印刷区分
               IF 作１−保険者番号(3:2) = "20"
                  IF 受−本人家族区分 = 1
007420                MOVE 01 TO 作１−印刷順番
                  ELSE
007420                MOVE 02 TO 作１−印刷順番
                  END-IF
               ELSE
                  IF 受−本人家族区分 = 1
007420                MOVE 11 TO 作１−印刷順番
                  ELSE
007420                MOVE 12 TO 作１−印刷順番
                  END-IF
               END-IF
007510     WHEN 03
007410         MOVE 7  TO 作１−印刷区分
               IF 作１−保険者番号(3:2) = "20"
                  IF 受−本人家族区分 = 1
007420                MOVE 01 TO 作１−印刷順番
                  ELSE
007420                MOVE 02 TO 作１−印刷順番
                  END-IF
               ELSE
                  IF 受−本人家族区分 = 1
007420                MOVE 11 TO 作１−印刷順番
                  ELSE
007420                MOVE 12 TO 作１−印刷順番
                  END-IF
               END-IF
007540     WHEN 04
007570     WHEN 09
007550         MOVE 6  TO 作１−印刷区分
               IF 受−本人家族区分 = 1
007420             MOVE 01 TO 作１−印刷順番
               ELSE
007420             MOVE 02 TO 作１−印刷順番
               END-IF
007540     WHEN 52
007570     WHEN 53
007570     WHEN 55
               IF 作１−保険者番号(3:2) = "20"
007550            MOVE 3  TO 作１−印刷区分
007420            MOVE 01 TO 作１−印刷順番
               END-IF
007570     WHEN 85
007550         MOVE 8  TO 作１−印刷区分
007420         MOVE 01 TO 作１−印刷順番
007600     END-EVALUATE.
007622*================================================================*
007623 助成レセまとめ判定 SECTION.
009193*---------------------------------------------------------------------------*
009194* 本体まとめ区分＝１
009195* の時は、フラグYES (金額を助成込みで印字）
009196*（例：横浜市の障害は、本体保険（国保系）のレセプト１枚で請求、助成レセはなし）
009197*---------------------------------------------------------------------------*
009198*
009199     MOVE SPACE TO 助成レセまとめフラグ.
009200*
009201     IF レセ−本体まとめ区分 = 1 
009202        MOVE "YES" TO 助成レセまとめフラグ
009203     END-IF.
007780*
007670*================================================================*
007671*================================================================*
007672******************************************************************
007673 END PROGRAM YNG6061.
007674******************************************************************
