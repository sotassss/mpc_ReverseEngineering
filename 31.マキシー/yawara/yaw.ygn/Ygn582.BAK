000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YGN582.
000060 AUTHOR.                 池田  幸子
000070*
000080*----------------------------------------------------------------*
000090*        【柔+ｳｨﾝﾄﾞｳｽﾞ版　群馬用 施術療養費請求書集計表】
000100*        【印刷】
000110*         MED = YGN580 YGN582P 
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2016-05-18
000140 DATE-COMPILED.          2016-05-18
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000270     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000280                             ORGANIZATION             IS  INDEXED
000290                             ACCESS MODE              IS  DYNAMIC
000300                             RECORD KEY               IS  元−元号区分
000310                             FILE STATUS              IS  状態キー
000320                             LOCK        MODE         IS  AUTOMATIC.
000330     SELECT  制御情報マスタ  ASSIGN      TO        SEIGYOL
000340                             ORGANIZATION             IS  INDEXED
000350                             ACCESS MODE              IS  DYNAMIC
000360                             RECORD KEY               IS  制−制御区分
000370                             FILE STATUS              IS  状態キー
000380                             LOCK        MODE         IS  AUTOMATIC.
000390     SELECT  施術所情報マスタ ASSIGN     TO        SEJOHOL
000400                             ORGANIZATION             IS  INDEXED
000410                             ACCESS MODE              IS  DYNAMIC
000420                             RECORD KEY               IS  施情−施術所番号
000430                             FILE STATUS              IS  状態キー
000440                             LOCK        MODE         IS  AUTOMATIC.
001350     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W58021L.DAT"
001360                             ORGANIZATION             IS  INDEXED
001370                             ACCESS                   IS  DYNAMIC
001380                             RECORD      KEY          IS  作１−保険種別
001410                             FILE        STATUS       IS  状態キー
001420                             LOCK        MODE         IS  AUTOMATIC.
000800     SELECT  印刷ファイル    ASSIGN      TO     GS-PRTF001
000810                             SYMBOLIC    DESTINATION  IS "PRT"
000820                             FORMAT                   IS  定義体名Ｐ
000830                             GROUP                    IS  項目群名Ｐ
000840                             PROCESSING  MODE         IS  処理種別Ｐ
000850                             UNIT        CONTROL      IS  拡張制御Ｐ
000860                             FILE        STATUS       IS  通知情報Ｐ.
000870******************************************************************
000880*                      DATA DIVISION                             *
000890******************************************************************
000900 DATA                    DIVISION.
000910 FILE                    SECTION.
000920*                           ［ＲＬ＝  １２８］
000930 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
000940     COPY GENGOU         OF  XFDLIB  JOINING    元   AS  PREFIX.
000950*                           ［ＲＬ＝  ２５６］
000960 FD  制御情報マスタ      BLOCK   CONTAINS   1   RECORDS.
000970     COPY SEIGYO         OF  XFDLIB  JOINING    制   AS  PREFIX.
000980*                           ［ＲＬ＝  ６４０］
000990 FD  施術所情報マスタ    BLOCK   CONTAINS   1   RECORDS.
001000     COPY SEJOHO         OF  XFDLIB  JOINING    施情 AS  PREFIX.
001780*
001420 FD  作業ファイル１ RECORD  CONTAINS 32 CHARACTERS.
001430 01  作１−レコード.
001440   03  作１−レコードキー.
001450     05  作１−保険種別                PIC 9(2).
001470   03  作１−レコードデータ.
001480     05  作１−件数                    PIC 9(4).
001490     05  作１−日数                    PIC 9(5).
001500     05  作１−費用額                  PIC 9(8).
001530     05  FILLER                        PIC X(13).
001100*
001290 FD  印刷ファイル.
001300     COPY YGN582P        OF  XMDLIB.
001310*
001320******************************************************************
001330*                WORKING-STORAGE SECTION                         *
001340******************************************************************
001350 WORKING-STORAGE         SECTION.
001360 01 キー入力                           PIC X    VALUE SPACE.
001370 01 状態キー                           PIC X(2) VALUE SPACE.
001380 01 終了フラグ                         PIC X(3) VALUE SPACE.
001390 01 終了フラグ２                       PIC X(3) VALUE SPACE.
001400 01 終了フラグ３                       PIC X(3) VALUE SPACE.
001410 01 確認入力Ｗ                         PIC X(1) VALUE SPACE.
001420 01 ファイル名Ｗ                       PIC N(2) VALUE SPACE.
001580 01 オープンフラグ                     PIC X(3) VALUE SPACE.
001450 01 実行キーＷ                         PIC X(4) VALUE SPACE.
001460 01 前和暦Ｗ                           PIC 9 VALUE ZERO.
001470 01 行桁フラグ                         PIC X(3) VALUE SPACE.
001480 01 処理移動キー                       PIC X(4) VALUE SPACE.
001550 01 請求和暦年月Ｗ.
001560    03 請求和暦Ｗ                      PIC 9     VALUE ZERO.
001570    03 請求年Ｗ                        PIC 9(2)  VALUE ZERO.
001580    03 請求月Ｗ                        PIC 9(2)  VALUE ZERO.
001590 01 元号名称Ｗ                         PIC N(2) VALUE SPACE.
001600 01 カウンタ                           PIC 9(2) VALUE ZERO.
001610 01 印刷枚数ＣＮＴ                     PIC 9(2) VALUE ZERO.
001620 01 総括表Ｗ.
001630    03 施術所住所Ｗ                    PIC X(50) VALUE SPACE.
001640    03 施術所住所１Ｗ                  PIC X(50) VALUE SPACE.
001650    03 施術所住所２Ｗ                  PIC X(50) VALUE SPACE.
001660    03 接骨院名Ｗ                      PIC X(50) VALUE SPACE.
001670    03 代表者名Ｗ                      PIC X(50) VALUE SPACE.
001670    03 代表者カナＷ                    PIC X(50) VALUE SPACE.
006760    03 県施術ＩＤＷ                    PIC X(12)  VALUE SPACE.
001680    03 施術所電話番号Ｗ                PIC X(15) VALUE SPACE.
001690    03 施術所郵便番号Ｗ.
001700       05 施術所郵便番号１Ｗ           PIC X(3)   VALUE SPACE.
001710       05 施術所郵便番号２Ｗ           PIC X(4)   VALUE SPACE.
001720    03 明細行.
001750       05 社保件数Ｗ                   PIC 9(4)  VALUE ZERO.
001750       05 社保日数Ｗ                   PIC 9(5)  VALUE ZERO.
001750       05 社保平均日数Ｗ               PIC 9(2)  VALUE ZERO.
001760       05 社保費用額Ｗ                 PIC 9(8)  VALUE ZERO.
001760       05 社保平均費用額Ｗ             PIC 9(6)  VALUE ZERO.
001750       05 組合件数Ｗ                   PIC 9(4)  VALUE ZERO.
001750       05 組合日数Ｗ                   PIC 9(5)  VALUE ZERO.
001750       05 組合平均日数Ｗ               PIC 9(2)  VALUE ZERO.
001760       05 組合費用額Ｗ                 PIC 9(8)  VALUE ZERO.
001760       05 組合平均費用額Ｗ             PIC 9(6)  VALUE ZERO.
001750       05 共済件数Ｗ                   PIC 9(4)  VALUE ZERO.
001750       05 共済日数Ｗ                   PIC 9(5)  VALUE ZERO.
001750       05 共済平均日数Ｗ               PIC 9(2)  VALUE ZERO.
001760       05 共済費用額Ｗ                 PIC 9(8)  VALUE ZERO.
001760       05 共済平均費用額Ｗ             PIC 9(6)  VALUE ZERO.
001730       05 国保件数Ｗ                   PIC 9(4)  VALUE ZERO.
001730       05 国保日数Ｗ                   PIC 9(5)  VALUE ZERO.
001730       05 国保平均日数Ｗ               PIC 9(2)  VALUE ZERO.
001740       05 国保費用額Ｗ                 PIC 9(8)  VALUE ZERO.
001740       05 国保平均費用額Ｗ             PIC 9(6)  VALUE ZERO.
001770       05 後高件数Ｗ                   PIC 9(4)  VALUE ZERO.
001770       05 後高日数Ｗ                   PIC 9(5)  VALUE ZERO.
001770       05 後高平均日数Ｗ               PIC 9(2)  VALUE ZERO.
001780       05 後高費用額Ｗ                 PIC 9(8)  VALUE ZERO.
001780       05 後高平均費用額Ｗ             PIC 9(6)  VALUE ZERO.
001770       05 労災件数Ｗ                   PIC 9(4)  VALUE ZERO.
001770       05 労災日数Ｗ                   PIC 9(5)  VALUE ZERO.
001770       05 労災平均日数Ｗ               PIC 9(2)  VALUE ZERO.
001780       05 労災費用額Ｗ                 PIC 9(8)  VALUE ZERO.
001780       05 労災平均費用額Ｗ             PIC 9(6)  VALUE ZERO.
001810    03 印刷データ.
001820       05 小計件数Ｗ                   PIC 9(4).
001820       05 小計日数Ｗ                   PIC 9(6).
001820       05 小計平均日数Ｗ               PIC 9(2).
001830       05 小計費用額Ｗ                 PIC 9(9).
001830       05 小計平均費用額Ｗ             PIC 9(6).
001820       05 総合計件数Ｗ                 PIC 9(4).
001820       05 総合計日数Ｗ                 PIC 9(6).
001820       05 総合計平均日数Ｗ             PIC 9(2).
001830       05 総合計費用額Ｗ               PIC 9(9).
001830       05 総合計平均費用額Ｗ           PIC 9(6).
001980 01 保険種別Ｗ                         PIC 9(2)  VALUE ZERO.
001990 01 公費種別Ｗ                         PIC 9(2)  VALUE ZERO.
002000 01 本人家族区分Ｗ                     PIC 9(1)  VALUE ZERO.
002010 01 費用負担者番号Ｗ                   PIC X(10) VALUE SPACE.
002020 01 登録番号コードＷ.
002030    03 登録番号ＷＰ.
002040       05 登録番号Ｗ                   PIC X(16) VALUE SPACE.
       01 支払いコードＷ.
001820    03 県コードＪＩＳＷ                PIC X(2) VALUE SPACE.
001830    03 県コード政管Ｗ                  PIC X(2) VALUE SPACE.
          03 FILLER                          PIC X(1) VALUE SPACE.
002050 01 会員番号題Ｗ                       PIC N(4)  VALUE SPACE.
002060 01 会員番号Ｗ                         PIC X(10) VALUE SPACE.
002260*
002270 01 印刷制御.
002280     03 定義体名Ｐ                     PIC X(8) VALUE SPACE.
002290     03 項目群名Ｐ                     PIC X(8) VALUE SPACE.
002300     03 処理種別Ｐ                     PIC X(2) VALUE SPACE.
002310     03 拡張制御Ｐ.
002320         05 端末制御Ｐ.
002330             07 移動方向Ｐ             PIC X(1).
002340             07 移動行数Ｐ             PIC 9(3).
002350         05 詳細制御Ｐ                 PIC X(2).
002360     03 通知情報Ｐ                     PIC X(2) VALUE SPACE.
002370     03 ユニット名Ｐ                   PIC X(8) VALUE SPACE.
002540 01 施術和暦年月ＷＲ.
002550    03 施術和暦ＷＲ                  PIC 9     VALUE ZERO.
002560    03 施術年ＷＲ                    PIC 9(2)  VALUE ZERO.
002570    03 施術月ＷＲ                    PIC 9(2)  VALUE ZERO.
002580*
002590******************************************************************
002600*                          連結項目                              *
002610******************************************************************
002620*
       01 連入−入力データＹＧＮ５８０ IS EXTERNAL.
          03 連入−施術和暦年月.
             05 連入−施術和暦               PIC 9(1).
             05 連入−施術年月.
                07 連入−施術年              PIC 9(2).
                07 連入−施術月              PIC 9(2).
      *
       01 連入−プレビュー IS EXTERNAL.
          03 連入−プレビュー区分            PIC 9.
      *
002940 01 連印−印刷情報ＹＧＮ５８１ IS EXTERNAL.
002950    03 連印−小計件数                  PIC 9(4).
002960    03 連印−小計日数                  PIC 9(6).
002960    03 連印−小計平均日数              PIC 9(2).
002970    03 連印−小計費用額                PIC 9(9).
002980    03 連印−小計平均費用額            PIC 9(6).
002950    03 連印−総合計件数                PIC 9(4).
002960    03 連印−総合計日数                PIC 9(6).
002960    03 連印−総合計平均日数            PIC 9(2).
002970    03 連印−総合計費用額              PIC 9(9).
002980    03 連印−総合計平均費用額          PIC 9(6).
013010*
000540************************************
000550* プリンタファイル作成用           *
000560************************************
000570 01 Ｈ連ＰＲＴＦ−作成データ IS EXTERNAL.
000580     03 Ｈ連ＰＲＴＦ−ファイル名           PIC X(8).
000590     03 Ｈ連ＰＲＴＦ−プレビュー区分       PIC 9.
000600     03 Ｈ連ＰＲＴＦ−帳票プログラム名     PIC X(8).
000610     03 Ｈ連ＰＲＴＦ−オーバレイ名         PIC X(8).
003070*
003280******************************************************************
003290*                      PROCEDURE  DIVISION                       *
003300******************************************************************
003310 PROCEDURE               DIVISION.
003320************
003330*           *
003340* 初期処理   *
003350*           *
003360************
002570     PERFORM プリンタファイル作成.
003370     PERFORM 初期化.
003380************
003390*           *
003400* 主処理     *
003410*           *
003420************
005000     MOVE SPACE TO 総括表Ｗ.
005010     INITIALIZE    総括表Ｗ.
003440     PERFORM 施術所情報マスタ読込.
003440     PERFORM 作業ファイル読込.
003480************
003490*           *
003500* 終了処理   *
003510*           *
003520************
003530     PERFORM 終了処理.
003540     EXIT PROGRAM.
003550*
003560*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002860*================================================================*
002870 プリンタファイル作成 SECTION.
002880*================================================================*
002890*   / 初期化 /
002900     MOVE SPACE TO Ｈ連ＰＲＴＦ−作成データ.
002910     INITIALIZE Ｈ連ＰＲＴＦ−作成データ.
002930*
002940*--↓↓ 変更箇所 ↓↓--------------------------------------*
002970*   使用するプリンタファイル名セット
002971     MOVE "PRTF001"             TO Ｈ連ＰＲＴＦ−ファイル名.
002972*
002973*   使用する帳票プログラム名セット
002974     MOVE "YGN582"             TO Ｈ連ＰＲＴＦ−帳票プログラム名.
002975*
002976*--↑↑-----------------------------------------------------*
002980*
002990*   / プレビュー区分セット /
003000     MOVE 連入−プレビュー区分  TO Ｈ連ＰＲＴＦ−プレビュー区分.
003010*
003020     CALL   "CRTPRTF".
003030     CANCEL "CRTPRTF".
003040*
003570*================================================================*
003580 初期化 SECTION.
003590*
003600     OPEN INPUT 元号マスタ.
003610             MOVE NC"元号" TO ファイル名Ｗ.
003620             PERFORM オープンチェック.
003630     OPEN INPUT 制御情報マスタ.
003640             MOVE NC"制御" TO ファイル名Ｗ.
003650             PERFORM オープンチェック.
003660     OPEN INPUT 施術所情報マスタ.
003670             MOVE NC"施情" TO ファイル名Ｗ.
003680             PERFORM オープンチェック.
003800*
003960* 連結項目の待避
003970     MOVE 連入−施術和暦 TO 施術和暦ＷＲ.
003980     MOVE 連入−施術年   TO 施術年ＷＲ.
003990     MOVE 連入−施術月   TO 施術月ＷＲ.
004000*
004010*================================================================*
004020 オープンチェック SECTION.
004030*
004040     IF 状態キー  NOT =  "00"
004050         DISPLAY ファイル名Ｗ NC"Ｆオープンエラー" UPON CONS
004060         DISPLAY NC"状態キー：" 状態キー           UPON CONS
004070         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004080                                                   UPON CONS
000080*-----------------------------------------*
000090        CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
004090         ACCEPT  キー入力 FROM CONS
004100         PERFORM ファイル閉鎖
004110         EXIT PROGRAM.
004550*================================================================*
004560 施術所情報マスタ読込 SECTION.
004570*
004580     MOVE ZERO TO 施情−施術所番号.
004590     READ 施術所情報マスタ
004600     INVALID KEY
004610         MOVE NC"施情" TO ファイル名Ｗ
004620         PERFORM エラー表示Ｒ
004630         PERFORM ファイル閉鎖
004640         MOVE 99 TO PROGRAM-STATUS
004650         EXIT PROGRAM
004660     NOT INVALID KEY
004670         MOVE 施情−新柔整師番号     TO 登録番号コードＷ
004680         MOVE 施情−郵便番号１       TO 施術所郵便番号１Ｗ
004690         MOVE 施情−郵便番号２       TO 施術所郵便番号２Ｗ
004700         MOVE 施情−住所１           TO 施術所住所１Ｗ
004710         MOVE 施情−住所２           TO 施術所住所２Ｗ
004720         MOVE 施情−接骨院名         TO 接骨院名Ｗ
004730         MOVE 施情−代表者名         TO 代表者名Ｗ
004730         MOVE 施情−代表者カナ       TO 代表者カナＷ
004740         MOVE 施情−電話番号         TO 施術所電話番号Ｗ
004830     END-READ.
004880*================================================================*
004890 ファイル閉鎖 SECTION.
004900*
002990     IF ( オープンフラグ = "YES" )
002991         CLOSE 印刷ファイル
003041     END-IF.
004910     CLOSE  元号マスタ   制御情報マスタ 施術所情報マスタ.
004930*================================================================*
004940 終了処理 SECTION.
004950*
004960     PERFORM ファイル閉鎖.
004970*================================================================*
004980 作業ファイル読込 SECTION.
004990*
005790     OPEN INPUT 作業ファイル１.
005800         MOVE NC"作１" TO ファイル名Ｗ.
005810         PERFORM オープンチェック.
      *
           MOVE ZERO      TO 作１−保険種別.
           START 作業ファイル１ KEY IS >= 作１−保険種別
           END-START.
006000     IF 状態キー = "00"
006010         MOVE SPACE  TO 終了フラグ
006020         PERFORM 作業ファイル１読込
               PERFORM UNTIL 終了フラグ = "YES"
                   EVALUATE 作１−保険種別
                   WHEN 02
005040                 MOVE  作１−件数         TO 社保件数Ｗ  
005040                 MOVE  作１−日数         TO 社保日数Ｗ  
005050                 MOVE  作１−費用額       TO 社保費用額Ｗ
005040                 IF 作１−件数 NOT = ZERO
                           COMPUTE 社保平均日数Ｗ   = 作１−日数 / 作１−件数
005050                     COMPUTE 社保平均費用額Ｗ = 作１−費用額 / 作１−件数
                       END-IF
                   WHEN 03
005040                 MOVE  作１−件数         TO 組合件数Ｗ  
005040                 MOVE  作１−日数         TO 組合日数Ｗ  
005050                 MOVE  作１−費用額       TO 組合費用額Ｗ
005040                 IF 作１−件数 NOT = ZERO
                           COMPUTE 組合平均日数Ｗ   = 作１−日数 / 作１−件数
005050                     COMPUTE 組合平均費用額Ｗ = 作１−費用額 / 作１−件数
                       END-IF
                   WHEN 04
005040                 MOVE  作１−件数         TO 共済件数Ｗ  
005040                 MOVE  作１−日数         TO 共済日数Ｗ  
005050                 MOVE  作１−費用額       TO 共済費用額Ｗ
005040                 IF 作１−件数 NOT = ZERO
                           COMPUTE 共済平均日数Ｗ   = 作１−日数 / 作１−件数
005050                     COMPUTE 共済平均費用額Ｗ = 作１−費用額 / 作１−件数
                       END-IF
                   WHEN 01
005040                 MOVE  作１−件数         TO 国保件数Ｗ  
005040                 MOVE  作１−日数         TO 国保日数Ｗ  
005050                 MOVE  作１−費用額       TO 国保費用額Ｗ
005040                 IF 作１−件数 NOT = ZERO
                           COMPUTE 国保平均日数Ｗ   = 作１−日数 / 作１−件数
005050                     COMPUTE 国保平均費用額Ｗ = 作１−費用額 / 作１−件数
                       END-IF
                   WHEN 05
005040                 MOVE  作１−件数         TO 後高件数Ｗ  
005040                 MOVE  作１−日数         TO 後高日数Ｗ  
005050                 MOVE  作１−費用額       TO 後高費用額Ｗ
005040                 IF 作１−件数 NOT = ZERO
                           COMPUTE 後高平均日数Ｗ   = 作１−日数 / 作１−件数
005050                     COMPUTE 後高平均費用額Ｗ = 作１−費用額 / 作１−件数
                       END-IF
                   WHEN 70
005040                 MOVE  作１−件数         TO 労災件数Ｗ  
005040                 MOVE  作１−日数         TO 労災日数Ｗ  
005050                 MOVE  作１−費用額       TO 労災費用額Ｗ
005040                 IF 作１−件数 NOT = ZERO
                           COMPUTE 労災平均日数Ｗ   = 作１−日数 / 作１−件数
005050                     COMPUTE 労災平均費用額Ｗ = 作１−費用額 / 作１−件数
                       END-IF
                   END-EVALUATE
006020             PERFORM 作業ファイル１読込
               END-PERFORM
               PERFORM 連結項目退避
003460         PERFORM 印刷レコードセット
003470         PERFORM 印刷処理
           ELSE
006000         MOVE  NC"　印刷の対象になるデータがありません。" TO 連メ−メッセージ
006010         CALL   "MSG001"
006020         CANCEL "MSG001"
           END-IF.
008840*================================================================*
008850 作業ファイル１読込 SECTION.
008860*
008870     READ 作業ファイル１ NEXT
008880     AT END
008890         MOVE "YES"  TO 終了フラグ
008900     END-READ.
005530*================================================================*
005540 連結項目退避 SECTION.
005550*
           MOVE 連印−小計件数            TO 小計件数Ｗ.
           MOVE 連印−小計日数            TO 小計日数Ｗ.
           MOVE 連印−小計平均日数        TO 小計平均日数Ｗ.
           MOVE 連印−小計費用額          TO 小計費用額Ｗ.
           MOVE 連印−小計平均費用額      TO 小計平均費用額Ｗ.
           MOVE 連印−総合計件数          TO 総合計件数Ｗ.
           MOVE 連印−総合計日数          TO 総合計日数Ｗ.
           MOVE 連印−総合計平均日数      TO 総合計平均日数Ｗ.
           MOVE 連印−総合計費用額        TO 総合計費用額Ｗ.
           MOVE 連印−総合計平均費用額    TO 総合計平均費用額Ｗ.
005530*================================================================*
005540 印刷レコードセット SECTION.
005550*
005560     MOVE SPACE TO YGN582P.
005570*     INITIALIZE YGN582P.
           MOVE 登録番号コードＷ(6:4) TO 施術所コード.
      */元号名称追加/080411
           MOVE 施術和暦ＷＲ         TO 元−元号区分
           READ 元号マスタ
           INVALID KEY
               MOVE SPACE            TO 元号名称
           NOT INVALID KEY
               MOVE 元−元号名称     TO 元号名称
           END-READ.
           MOVE NC"年"               TO 年.
005580     MOVE 施術年ＷＲ           TO 施術年.
005590     MOVE 施術月ＷＲ           TO 施術月.
005730     MOVE 代表者名Ｗ           TO 氏名.
005730     MOVE 代表者カナＷ         TO フリガナ.
005680     MOVE 施術所郵便番号１Ｗ   TO 郵便番号１.
005690     MOVE 施術所郵便番号２Ｗ   TO 郵便番号２.
           IF 施術所郵便番号１Ｗ NOT = SPACE
               MOVE "〒"             TO 郵便１
               MOVE "-"              TO 郵便２
           END-IF.
005680     MOVE 施術所住所１Ｗ       TO 住所１.
005690     MOVE 施術所住所２Ｗ       TO 住所２.
005730*     MOVE 接骨院名Ｗ           TO 施術所名.
           MOVE 施術所電話番号Ｗ     TO 電話番号.
      *
005740     MOVE 社保件数Ｗ           TO 社保件数  .
005740     MOVE 社保日数Ｗ           TO 社保日数  .
005740     MOVE 社保平均日数Ｗ       TO 社保平均日数  .
005750     MOVE 社保費用額Ｗ         TO 社保費用額.
005750     MOVE 社保平均費用額Ｗ     TO 社保平均費用額.
005740     MOVE 組合件数Ｗ           TO 組合件数  .
005740     MOVE 組合日数Ｗ           TO 組合日数  .
005740     MOVE 組合平均日数Ｗ       TO 組合平均日数  .
005750     MOVE 組合費用額Ｗ         TO 組合費用額.
005750     MOVE 組合平均費用額Ｗ     TO 組合平均費用額.
005740     MOVE 共済件数Ｗ           TO 共済件数  .
005740     MOVE 共済日数Ｗ           TO 共済日数  .
005740     MOVE 共済平均日数Ｗ       TO 共済平均日数  .
005750     MOVE 共済費用額Ｗ         TO 共済費用額.
005750     MOVE 共済平均費用額Ｗ     TO 共済平均費用額.
005740     MOVE 国保件数Ｗ           TO 国保件数  .
005740     MOVE 国保日数Ｗ           TO 国保日数  .
005740     MOVE 国保平均日数Ｗ       TO 国保平均日数  .
005750     MOVE 国保費用額Ｗ         TO 国保費用額.
005750     MOVE 国保平均費用額Ｗ     TO 国保平均費用額.
005780     MOVE 後高件数Ｗ           TO 後高件数  .
005740     MOVE 後高日数Ｗ           TO 後高日数  .
005740     MOVE 後高平均日数Ｗ       TO 後高平均日数  .
005750     MOVE 後高費用額Ｗ         TO 後高費用額.
005750     MOVE 後高平均費用額Ｗ     TO 後高平均費用額.
005820     MOVE 小計件数Ｗ           TO 小計件数.
005740     MOVE 小計日数Ｗ           TO 小計日数  .
005740     MOVE 小計平均日数Ｗ       TO 小計平均日数  .
005750     MOVE 小計費用額Ｗ         TO 小計費用額.
005750     MOVE 小計平均費用額Ｗ     TO 小計平均費用額.
005780     MOVE 労災件数Ｗ           TO 労災件数  .
005740     MOVE 労災日数Ｗ           TO 労災日数  .
005740     MOVE 労災平均日数Ｗ       TO 労災平均日数  .
005750     MOVE 労災費用額Ｗ         TO 労災費用額.
005750     MOVE 労災平均費用額Ｗ     TO 労災平均費用額.
005820     MOVE 総合計件数Ｗ         TO 総合計件数.
005740     MOVE 総合計日数Ｗ         TO 総合計日数  .
005740     MOVE 総合計平均日数Ｗ     TO 総合計平均日数  .
005750     MOVE 総合計費用額Ｗ       TO 総合計費用額.
005750     MOVE 総合計平均費用額Ｗ   TO 総合計平均費用額.
005840*================================================================*
005850 印刷処理 SECTION.
005860*
004310     IF ( オープンフラグ NOT = "YES" )
004320        MOVE "YES" TO オープンフラグ
004330        OPEN I-O  印刷ファイル
004340        PERFORM エラー処理Ｐ
004350     END-IF.
013440*
005870     MOVE "YGN582P"  TO  定義体名Ｐ.
005880     MOVE "SCREEN"   TO  項目群名Ｐ.
005890     MOVE SPACE      TO  処理種別Ｐ.
005900     MOVE SPACE      TO  拡張制御Ｐ.
005910     WRITE YGN582P.
005920     PERFORM エラー処理Ｐ.
005930*================================================================*
005940 エラー表示 SECTION.
005950*
005960     DISPLAY NC"ファイル書込エラー：" ファイル名Ｗ UPON CONS.
005970     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
005980     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
000080*-----------------------------------------*
000090     CALL "actcshm"  WITH C LINKAGE.
000100*-----------------------------------------*
005990     ACCEPT  キー入力 FROM CONS.
006000*================================================================*
006010 エラー処理Ｐ SECTION.
006020*
006030     IF 通知情報Ｐ NOT = "00"
006040         DISPLAY NC"帳票エラー"              UPON CONS
006050         DISPLAY NC"項目群名Ｐ：" 項目群名Ｐ UPON CONS
006060         DISPLAY NC"通知情報Ｐ：" 通知情報Ｐ UPON CONS
006070         DISPLAY NC"拡張制御Ｐ：" 拡張制御Ｐ UPON CONS
006080         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
006090                                             UPON CONS
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
006100         ACCEPT  キー入力 FROM CONS
006110         PERFORM ファイル閉鎖
006120         MOVE 99 TO PROGRAM-STATUS
006130         EXIT PROGRAM
006140     END-IF.
006150*================================================================*
006160 エラー表示Ｒ SECTION.
006170*
006180     DISPLAY NC"ファイル読込エラー" ファイル名Ｗ UPON CONS.
006190     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください" UPON CONS.
000080*-----------------------------------------*
000090     CALL "actcshm"  WITH C LINKAGE.
000100*-----------------------------------------*
006200     ACCEPT  キー入力 FROM CONS.
006210     PERFORM ファイル閉鎖.
006220     EXIT PROGRAM.
006230*================================================================*
006240 エラー表示その他 SECTION.
006250*
006260     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
006270     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
006280                                                   UPON CONS.
000080*-----------------------------------------*
000090     CALL "actcshm"  WITH C LINKAGE.
000100*-----------------------------------------*
006290     ACCEPT  キー入力 FROM CONS.
006300     PERFORM ファイル閉鎖.
006310     EXIT PROGRAM.
006311*================================================================*
006320******************************************************************
006330 END PROGRAM YGN582.
006340******************************************************************
