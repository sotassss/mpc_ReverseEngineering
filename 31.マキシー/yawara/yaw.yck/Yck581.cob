000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YCK581.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*      総括表【ﾃﾞｰﾀ作成】柔+ｳｨﾝﾄﾞｳｽﾞ版
000091* 中央と同じ(CALLのみ変更)
000092* 請求年月Ver.
000100*      MED = YCK580 
000110*----------------------------------------------------------------*
000120 DATE-WRITTEN.           2012-11-29
000130 DATE-COMPILED.          2012-11-29
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000480     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000490                             ORGANIZATION             IS  INDEXED
000500                             ACCESS MODE              IS  DYNAMIC
000510                             RECORD KEY               IS  受−施術和暦年月
000520                                                          受−患者コード
000530                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000540                                                          受−患者カナ
000550                                                          受−患者コード
000560                             ALTERNATE RECORD KEY     IS  受−患者コード
000570                                                          受−施術和暦年月
000580                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000590                                                          受−保険種別
000600                                                          受−保険者番号
000610                                                          受−患者コード
000620                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000630                                                          受−公費種別
000640                                                          受−費用負担者番号
000650                                                          受−患者コード
000660                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000670                                                          受−助成種別
000680                                                          受−費用負担者番号助成
000690                                                          受−患者コード
000700                             ALTERNATE RECORD KEY     IS  受−請求和暦年月
000710                                                          受−施術和暦年月
000720                                                          受−患者コード
000730                             FILE STATUS              IS  状態キー
000740                             LOCK        MODE         IS  AUTOMATIC.
           SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
                                   ORGANIZATION             IS  INDEXED
                                   ACCESS MODE              IS  DYNAMIC
                                   RECORD KEY               IS  レセ−施術和暦年月
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−患者コード
                                                                レセ−施術和暦年月
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−施術和暦年月
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−レセ種別
                                                                レセ−請求保険者番号
                                                                レセ−患者コード
                                                                レセ−施術和暦年月
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−請求保険者番号
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                                                レセ−施術和暦年月
                                   FILE STATUS              IS  状態キー
                                   LOCK        MODE         IS  AUTOMATIC.
000849     SELECT  作業ファイル１  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5801L.DAT"
000859                             ORGANIZATION             IS  SEQUENTIAL
000869                             ACCESS                   IS  SEQUENTIAL
000879                             FILE        STATUS       IS  状態キー
000889                             LOCK        MODE         IS  AUTOMATIC.
000961     SELECT  作業ファイル２  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5802L.DAT"
000962                             ORGANIZATION             IS  SEQUENTIAL
000963                             ACCESS                   IS  SEQUENTIAL
000973                             FILE        STATUS       IS  状態キー
000974                             LOCK        MODE         IS  AUTOMATIC.
000975     SELECT  作業ファイル３  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5803L.DAT"
000976                             ORGANIZATION             IS  INDEXED
000977                             ACCESS                   IS  DYNAMIC
000978                             RECORD      KEY          IS  作３−請求和暦年月
000979                                                          作３−助成区分
000980                                                          作３−保険者番号
000981                                                          作３−本人家族区分
000982                                                          作３−施術和暦年月
000983                                                          作３−被保険者カナ
000984                                                          作３−患者コード
000984                                                          作３−レセ種別
000993                             FILE        STATUS       IS  状態キー
001003                             LOCK        MODE         IS  AUTOMATIC.
001050******************************************************************
001060*                      DATA DIVISION                             *
001070******************************************************************
001080 DATA                    DIVISION.
001090 FILE                    SECTION.
001190*                           ［ＲＬ＝  ３２０］
001200 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001210     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
      *
       FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS GLOBAL.
           COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
001250*
001260*
001270 FD  作業ファイル１ RECORD  CONTAINS 250 CHARACTERS.
001280 01  作１−レコード.
001291     03  作１−レコードデータ.
001292         05  作１−請求和暦年月.
001293             07  作１−請求和暦            PIC 9.
001294             07  作１−請求年              PIC 9(2).
001295             07  作１−請求月              PIC 9(2).
001300         05  作１−施術和暦年月.
001310             07  作１−施術和暦            PIC 9.
001320             07  作１−施術年              PIC 9(2).
001330             07  作１−施術月              PIC 9(2).
001340         05  作１−助成区分                PIC 9.
001370         05  作１−保険者番号              PIC X(10).
001371         05  作１−記号                    PIC X(24).
001372         05  作１−番号                    PIC X(30).
001373         05  作１−被保険者カナ            PIC X(50).
001374         05  作１−患者カナ                PIC X(50).
001380         05  作１−本人家族区分            PIC 9.
001490         05  作１−費用額                  PIC 9(7).
001500         05  作１−負担額                  PIC 9(7).
001510         05  作１−請求額                  PIC 9(7).
001511         05  作１−患者コード.
001512             07 作１−患者番号             PIC 9(6).
001513             07 作１−枝番                 PIC X(1).
001514         05  作１−保険種別                PIC 9(2).
001515         05  作１−親保険種別              PIC 9(2).
001521         05  作１−老人レコード区分        PIC 9.
001522         05  作１−助成レコード区分        PIC 9.
001530         05  FILLER                        PIC X(40).
001540* 合計ファイル
001550 FD  作業ファイル２ RECORD  CONTAINS 32 CHARACTERS.
001560 01  作２−レコード.
001570     03  作２−レコードデータ.
001580         05  作２−件数合計                PIC 9(4).
001590         05  作２−費用額合計              PIC 9(8).
001600         05  作２−請求額合計              PIC 9(8).
001610         05  FILLER                        PIC X(12).
001611* 保険者番号順ファイル
001612 FD  作業ファイル３ RECORD  CONTAINS 80 CHARACTERS.
001613 01  作３−レコード.
001614     03  作３−レコードキー.
001615         05  作３−請求和暦年月.
001616             07  作３−請求和暦            PIC 9.
001617             07  作３−請求年              PIC 9(2).
001618             07  作３−請求月              PIC 9(2).
001623         05  作３−助成区分                PIC 9.
001624         05  作３−保険者番号              PIC 9(8).
001625         05  作３−本人家族区分            PIC 9.
001626         05  作３−施術和暦年月.
001627             07  作３−施術和暦            PIC 9.
001628             07  作３−施術年              PIC 9(2).
001629             07  作３−施術月              PIC 9(2).
001630         05  作３−被保険者カナ            PIC X(50).
001631         05  作３−患者コード.
001632             07 作３−患者番号             PIC 9(6).
001633             07 作３−枝番                 PIC X(1).
001221         05  作３−レセ種別                PIC 9(2).
001634     03  作３−レコードデータ.
001637         05  FILLER                        PIC X(1).
001647*
001690*----------------------------------------------------------------*
001700******************************************************************
001710*                WORKING-STORAGE SECTION                         *
001720******************************************************************
001730 WORKING-STORAGE         SECTION.
001740 01 キー入力                           PIC X    VALUE SPACE.
001750 01 状態キー                           PIC X(2) VALUE SPACE.
001760 01 終了フラグ                         PIC X(3) VALUE SPACE.
001770 01 終了フラグ２                       PIC X(3) VALUE SPACE.
001780 01 実行キーＷ                         PIC X(3)  VALUE SPACE.
001790 01 施術記録有Ｗ                       PIC X(3) VALUE SPACE.
001791 01 カウンタ                           PIC 9(2)  VALUE ZERO.
001800**
001810 01 保険種別ＷＲ                       PIC 9(2) VALUE ZERO.
001811 01 印刷形式ＷＲ                       PIC 9    VALUE ZERO.
001820 01 保険者番号ＷＲ                     PIC X(10) VALUE SPACE.
001830 01 レセプト種類ＷＲ                   PIC X(4) VALUE SPACE.
001840 01 本人家族区分ＷＲ                   PIC 9    VALUE ZERO.
001850 01 施術和暦年月ＷＲ.
001860    03 施術和暦ＷＲ                    PIC 9    VALUE ZERO.
001870    03 施術年ＷＲ                      PIC 9(2) VALUE ZERO.
001880    03 施術月ＷＲ                      PIC 9(2) VALUE ZERO.
001890 01 請求和暦年月ＷＲ.
001900    03 請求和暦ＷＲ                    PIC 9    VALUE ZERO.
001910    03 請求年ＷＲ                      PIC 9(2) VALUE ZERO.
001920    03 請求月ＷＲ                      PIC 9(2) VALUE ZERO.
001970**
001980 01 ファイル名                         PIC N(8) VALUE SPACE.
001990 01 保険種別Ｗ                         PIC 9(2) VALUE ZERO.
002070*
002071** 合計集計用
002072 01 合計Ｗ.
002073    03 件数合計Ｗ                      PIC 9(4) VALUE ZERO.
002074    03 費用額合計Ｗ                    PIC 9(8) VALUE ZERO.
002075    03 請求額合計Ｗ                    PIC 9(8) VALUE ZERO.
002076** エラーメッセージ用
002077 01 エラーメッセージＷ.
002078    03 エラー患者コードＷ              PIC X(7) VALUE SPACE.
002079    03 エラー区切りＷ                  PIC X(1) VALUE SPACE.
002080    03 エラー保険種別Ｗ                PIC X(2) VALUE SPACE.
002081    03 FILLER                          PIC X(10) VALUE SPACE.
002082*
002083** 保険者番号右詰め用
002084 01 保険者番号ＷＴ.
002085    03 保険者番号左詰めＷ.
002086      05 保険者番号左詰めＷ１          PIC X OCCURS 8 VALUE SPACE.
002087    03 保険者番号右詰めＷ.
002088      05 保険者番号右詰めＷ１          PIC X OCCURS 8 VALUE ZERO.
002089    03 保険者番号数字Ｗ                PIC 9(8)  VALUE ZERO.
002090    03 保険者番号Ｗ                    PIC X(8)  VALUE SPACE.
002091**
002092 01 計算機西暦年Ｗ                     PIC 9(2).
002100* 日付ＷＯＲＫ
002110 01 計算機西暦.
002120    03 計算機西暦年                    PIC 9(4).
002130    03 計算機西暦月日                  PIC 9(4).
002140 01 計算機西暦Ｒ REDEFINES 計算機西暦.
002150    03 計算機世紀                      PIC 9(2).
002160    03 計算機日付                      PIC 9(6).
002170    03 計算機日付Ｒ REDEFINES 計算機日付.
002180       05 計算機年月                   PIC 9(4).
002190       05 計算機年月Ｒ REDEFINES 計算機年月.
002200         07 計算機年                   PIC 9(2).
002210         07 計算機月                   PIC 9(2).
002220       05 計算機日                     PIC 9(2).
014774*
       01 複合プログラム名Ｗ     PIC X(8) VALUE "MOJI2".
002230*
002240******************************************************************
002250*                          連結項目                              *
002260******************************************************************
002270*
002271********************
002272* メッセージ表示キー *
002273********************
002274 01 連メ３−キー IS EXTERNAL.
002275    03  連メ３−メッセージ             PIC N(20).
002276    03  連メ３−メッセージ１           PIC X(20).
002277***
002287 01 連入−画面情報ＹＣＫ５８０ IS EXTERNAL.
002297    03 連入−印刷形式                  PIC 9.
002307    03 連入−請求和暦年月.
002317       05 連入−請求和暦               PIC 9.
002327       05 連入−請求年月.
002337         07 連入−請求年               PIC 9(2).
002347         07 連入−請求月               PIC 9(2).
          03 連入−プレビュー区分            PIC 9.
002710*
      * 暗号複合用
       01 連暗号複合−暗号情報 IS EXTERNAL.
          03 連暗号複合−入力情報.
             05 連暗号複合−記号               PIC X(24).
             05 連暗号複合−番号               PIC X(30).
             05 連暗号複合−暗号化項目.
                07 連暗号複合−暗号患者番号    PIC X(6).
                07 連暗号複合−暗号判定記号    PIC X.
                07 連暗号複合−暗号判定番号    PIC X.
                07 連暗号複合−暗号記号        PIC X(24).
                07 連暗号複合−暗号番号        PIC X(30).
          03 連暗号複合−出力情報.
             05 連暗号複合−複合した記号       PIC X(24).
             05 連暗号複合−複合した番号       PIC X(30).
003322*
002720******************************************************************
002730*                      PROCEDURE  DIVISION                       *
002740******************************************************************
002750 PROCEDURE               DIVISION.
002760************
002770*           *
002780* 初期処理   *
002790*           *
002800************
002810     PERFORM 初期化.
002820************
002830*           *
002840* 主処理     *
002850*           *
002860************
002870     PERFORM 作業ファイル作成.
002880************
002890*           *
002900* 終了処理   *
002910*           *
002920************
002930     PERFORM 終了処理.
002940     MOVE ZERO TO PROGRAM-STATUS.
002950     EXIT PROGRAM.
002960*
002970*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002980*================================================================*
002990 初期化 SECTION.
003000*
003010     PERFORM ファイルオープン.
003020* 連結項目の待避
003031     MOVE 連入−印刷形式      TO 印刷形式ＷＲ.
003040     MOVE 連入−請求和暦      TO 請求和暦ＷＲ.
003050     MOVE 連入−請求年        TO 請求年ＷＲ.
003060     MOVE 連入−請求月        TO 請求月ＷＲ.
003070*
003080*================================================================*
003090 ファイルオープン SECTION.
003100*
003200     OPEN INPUT 受診者情報Ｆ.
003210         MOVE NC"受診者情報Ｆ" TO ファイル名.
003220         PERFORM オープンチェック.
003250     OPEN INPUT レセプトＦ.
003260         MOVE NC"レセプトＦ" TO ファイル名.
003270         PERFORM オープンチェック.
003260*================================================================*
003270 オープンチェック SECTION.
003280*
003290     IF 状態キー  NOT =  "00"
003300         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
003310         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003320         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003330                                                 UPON CONS
003131*-----------------------------------------*
003132         CALL "actcshm"  WITH C LINKAGE
003133*-----------------------------------------*
003340         ACCEPT  キー入力 FROM CONS
003350         PERFORM ファイル閉鎖
003360         MOVE 99 TO PROGRAM-STATUS
003370         EXIT PROGRAM.
003380*================================================================*
003390 ファイル閉鎖 SECTION.
003400*
003410     CLOSE 受診者情報Ｆ レセプトＦ.
003430*================================================================*
003440 終了処理 SECTION.
003450*
003460     PERFORM ファイル閉鎖.
003470*================================================================*
003480 エラー表示 SECTION.
003490*
003500     DISPLAY NC"状態キー" 状態キー  UPON CONS.
003510     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
003520     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
003530     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
003321*-----------------------------------------*
003322     CALL "actcshm"  WITH C LINKAGE.
003323*-----------------------------------------*
003540     ACCEPT  キー入力 FROM CONS.
003550     PERFORM ファイル閉鎖.
003560     MOVE 99 TO PROGRAM-STATUS.
003570     EXIT PROGRAM.
003571*================================================================*
003572 エラー表示Ｒ SECTION.
003573*
003574     DISPLAY NC"ファイル読込エラー" ファイル名     UPON CONS.
003575     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
003576     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
003321*-----------------------------------------*
003322     CALL "actcshm"  WITH C LINKAGE.
003323*-----------------------------------------*
003577     ACCEPT  キー入力 FROM CONS.
003578     PERFORM ファイル閉鎖.
003579     MOVE 99 TO PROGRAM-STATUS.
003580     EXIT PROGRAM.
003581*================================================================*
003590 作業ファイル作成 SECTION.
003600*
003601     PERFORM 保険者番号順ファイル作成.
003602*
003610     OPEN OUTPUT 作業ファイル１.
003620         MOVE NC"作１" TO ファイル名.
003630         PERFORM オープンチェック.
003634     OPEN INPUT  作業ファイル３.
003635         MOVE NC"作３" TO ファイル名.
003636         PERFORM オープンチェック.
003637*
003641     PERFORM 作業ファイル１作成.
003651*
003652     CLOSE 作業ファイル３ 作業ファイル１.
003653*
003654* 合計ファイル作成
003664     OPEN OUTPUT 作業ファイル２.
003674         MOVE NC"作２" TO ファイル名.
003684         PERFORM オープンチェック.
003694*
003712     IF  印刷形式ＷＲ = 1 OR 2
003716         PERFORM 作業ファイル２作成
003724     END-IF.
003725*
003726     CLOSE 作業ファイル２.
003738*
005200*================================================================*
005210 保険者番号順ファイル作成 SECTION.
005220**********************************************************************
005221**   受診者情報Ｆから、該当請求年月のデータを抽出し、
005222**   作業ファイル３(保険者番号順)に書き出す.
005224**********************************************************************
005225*
005226     OPEN OUTPUT 作業ファイル３.
005227         MOVE NC"作３" TO ファイル名.
005228         PERFORM オープンチェック.
005229*
005170     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
005180     MOVE 請求年ＷＲ    TO レセ−請求年.  
005190     MOVE 請求月ＷＲ    TO レセ−請求月.  
013730     MOVE ZERO          TO レセ−レセ種別.
005200     MOVE ZERO          TO レセ−施術和暦.
005210     MOVE ZERO          TO レセ−施術年.  
005220     MOVE ZERO          TO レセ−施術月.  
005230     MOVE ZERO          TO レセ−患者番号.
005240     MOVE SPACE         TO レセ−枝番.    
013770     START レセプトＦ   KEY IS >= レセ−請求和暦年月
000230                                  レセ−施術和暦年月
000240                                  レセ−患者コード
000250                                  レセ−レセ種別
005241     END-START.
005242     IF 状態キー = "00"
005243         MOVE SPACE  TO 終了フラグ
005244         PERFORM レセプトＦ読込
005245         PERFORM UNTIL ( 終了フラグ = "YES" ) OR
005330                       ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
005340                       ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
005350                       ( レセ−請求月   NOT = 請求月ＷＲ   )
005265*
004010             PERFORM データチェック
003830             IF 実行キーＷ = "YES"
005266                 MOVE SPACE TO 作３−レコード
005267                 INITIALIZE    作３−レコード
005268                 MOVE レセ−レセ種別   TO  作３−レセ種別
005268                 MOVE レセ−請求和暦   TO  作３−請求和暦
005269                 MOVE レセ−請求年     TO  作３−請求年
005270                 MOVE レセ−請求月     TO  作３−請求月
005271                 MOVE 受−施術和暦     TO  作３−施術和暦
005272                 MOVE 受−施術年       TO  作３−施術年
005273                 MOVE 受−施術月       TO  作３−施術月
005274                 IF  受−助成種別   = ZERO  OR 50
005275                     IF (( 受−保険種別   NOT = ZERO ) AND
005276                         ( 受−保険者番号 NOT = SPACE )) OR
                              (受−保険種別 NOT = 85)
005277** 助成なし(生保親ありは助成なし扱い)
005278                        MOVE ZERO  TO   作３−助成区分
005279                     ELSE
005280** 生保単独
005281                        MOVE 2     TO   作３−助成区分
005282                     END-IF
005283                 ELSE
005284** 助成あり
005285                     MOVE 1        TO   作３−助成区分
005286                 END-IF
005287*
005288                 IF ( 受−公費種別       = ZERO  ) AND
005289                    ( 受−費用負担者番号 = SPACE )
005290                     MOVE 受−保険者番号     TO 保険者番号Ｗ
005291                 ELSE
005292* 老人は、市町村番号
005293                     MOVE 受−費用負担者番号 TO 保険者番号Ｗ
005294                 END-IF
005295                 PERFORM 保険者番号右詰め
005296                 MOVE 保険者番号数字Ｗ   TO 作３−保険者番号
005297*
005298                 MOVE 受−本人家族区分   TO 作３−本人家族区分
005299                 MOVE 受−被保険者カナ   TO 作３−被保険者カナ
005300                 MOVE 受−患者コード     TO 作３−患者コード
005301*
005302                 WRITE 作３−レコード
005303                 INVALID KEY
005304                     MOVE NC"作３"  TO ファイル名
005305                 PERFORM エラー表示
005306                 END-WRITE
                   END-IF
005313*
005314             PERFORM レセプトＦ読込
005315         END-PERFORM
005316     END-IF.
005317*
005318     CLOSE 作業ファイル３.
005319*
005320*================================================================*
005321 保険者番号右詰め SECTION.
005322*
005323     MOVE 保険者番号Ｗ    TO  保険者番号左詰めＷ.
005324     MOVE ZERO            TO  保険者番号右詰めＷ.
005325     MOVE ZERO            TO  保険者番号数字Ｗ.
005326*
005327     MOVE  9  TO  カウンタ.
005328*
005329     IF  保険者番号左詰めＷ１(8) NOT = SPACE
005330         COMPUTE カウンタ = カウンタ  -  1
005331         MOVE 保険者番号左詰めＷ１(8)  TO  保険者番号右詰めＷ１(カウンタ)
005332     END-IF.
005333     IF  保険者番号左詰めＷ１(7) NOT = SPACE
005334         COMPUTE カウンタ = カウンタ  -  1
005335         MOVE 保険者番号左詰めＷ１(7)  TO  保険者番号右詰めＷ１(カウンタ)
005336     END-IF.
005337     IF  保険者番号左詰めＷ１(6) NOT = SPACE
005338         COMPUTE カウンタ = カウンタ  -  1
005339         MOVE 保険者番号左詰めＷ１(6)  TO  保険者番号右詰めＷ１(カウンタ)
005340     END-IF.
005341     IF  保険者番号左詰めＷ１(5) NOT = SPACE
005342         COMPUTE カウンタ = カウンタ  -  1
005343         MOVE 保険者番号左詰めＷ１(5)  TO  保険者番号右詰めＷ１(カウンタ)
005344     END-IF.
005345     IF  保険者番号左詰めＷ１(4) NOT = SPACE
005346         COMPUTE カウンタ = カウンタ  -  1
005347         MOVE 保険者番号左詰めＷ１(4)  TO  保険者番号右詰めＷ１(カウンタ)
005348     END-IF.
005349     IF  保険者番号左詰めＷ１(3) NOT = SPACE
005350         COMPUTE カウンタ = カウンタ  -  1
005351         MOVE 保険者番号左詰めＷ１(3)  TO  保険者番号右詰めＷ１(カウンタ)
005352     END-IF.
005353     IF  保険者番号左詰めＷ１(2) NOT = SPACE
005354         COMPUTE カウンタ = カウンタ  -  1
005355         MOVE 保険者番号左詰めＷ１(2)  TO  保険者番号右詰めＷ１(カウンタ)
005356     END-IF.
005357     IF  保険者番号左詰めＷ１(1) NOT = SPACE
005358         COMPUTE カウンタ = カウンタ  -  1
005359         MOVE 保険者番号左詰めＷ１(1)  TO  保険者番号右詰めＷ１(カウンタ)
005360     END-IF.
005361*
005362     MOVE 保険者番号右詰めＷ TO 保険者番号数字Ｗ.
005363*
005364*================================================================*
005365*================================================================*
005366 作業ファイル１作成 SECTION.
005376*
005381     MOVE SPACE  TO 終了フラグ.
005382     PERFORM 作業ファイル３読込.
005390     PERFORM UNTIL  終了フラグ = "YES" 
008580         MOVE "YES"          TO 実行キーＷ
005432         EVALUATE 印刷形式ＷＲ 
005433* 全部
005434         WHEN ZERO 
005435            CONTINUE
005436* 助成以外
005437         WHEN 1 
005438            IF 作３−助成区分  NOT = ZERO
005439               MOVE SPACE  TO 実行キーＷ
005440            END-IF
005441* 助成のみ
005442         WHEN 2 
005443            IF 作３−助成区分  NOT = 1
005444               MOVE SPACE  TO 実行キーＷ
005445            END-IF
005446         WHEN OTHER
005447            CONTINUE
005448         END-EVALUATE
005449*
005450** 労災・自賠責・自由は対象外
005451         IF  受−保険種別 = 70 OR 80 OR 90
005460             MOVE SPACE  TO 実行キーＷ
005470         END-IF
005471** 生保単独は対象外
005472         IF  作３−助成区分  = 2
005473             MOVE SPACE  TO 実行キーＷ
005474         END-IF
005480**
005490         IF  実行キーＷ = "YES"
005520*
005610             EVALUATE TRUE
005530*            ********
005540*            * 健保 *
005550*            ********
013940             WHEN レセ−レセ種別   = 1
005580*                **********************
005590*                * 作業ファイル作成 *
005600*                **********************
005623                 MOVE SPACE TO 作１−レコード
005624                 INITIALIZE 作１−レコード
005625                 MOVE 受−保険種別       TO 作１−保険種別
005627                 MOVE 受−保険者番号     TO 作１−保険者番号
005629                 MOVE レセ−合計         TO 作１−費用額
005630                 MOVE レセ−一部負担金   TO 作１−負担額
005631                 MOVE レセ−請求金額     TO 作１−請求額
005632                 PERFORM 作１レコードセット
005633                 PERFORM 作１ファイル書込
005800*            ********
005810*            * 老人 *
005820*            ********
013940             WHEN レセ−レセ種別   = 2
005850*                **********************
005860*                * 作業ファイル作成 *
005870*                **********************
005880                 MOVE SPACE TO 作１−レコード
005890                 INITIALIZE 作１−レコード
005891                 MOVE 受−公費種別        TO 作１−保険種別
005892                 MOVE 受−費用負担者番号  TO 作１−保険者番号
005893                 MOVE  1                  TO 作１−老人レコード区分
005894                 MOVE レセ−合計          TO 作１−費用額
005895                 MOVE レセ−一部負担金    TO 作１−負担額
005896                 MOVE レセ−請求金額      TO 作１−請求額
005897                 PERFORM 作１レコードセット
005898                 PERFORM 作１ファイル書込
004220*            ********
004230*            * 助成 *
004240*            ********
                   WHEN レセ−レセ種別   = 3
005952*   / 助成あり 助成ﾚｺｰﾄﾞ /
005953                 MOVE SPACE TO 作１−レコード
005954                 INITIALIZE 作１−レコード
005955                 MOVE 受−助成種別        TO 作１−保険種別
005956                 IF 受−公費種別 = 5
                           MOVE 受−公費種別    TO 作１−親保険種別
005650                 ELSE
                           MOVE 受−保険種別    TO 作１−親保険種別
                       END-IF
005957                 MOVE 受−費用負担者番号助成 TO 作１−保険者番号
005958                 MOVE  1                  TO 作１−助成レコード区分
005959** 助成の費用額は、0
005960                 MOVE ZERO                TO 作１−費用額
005961                 MOVE レセ−受給者負担額  TO 作１−負担額
005962                 MOVE レセ−助成請求金額  TO 作１−請求額
005963                 PERFORM 作１レコードセット
005964                 PERFORM 作１ファイル書込
006040             END-EVALUATE
               END-IF
006500         PERFORM 作業ファイル３読込
006510     END-PERFORM.
006530*
006655*================================================================*
006658*================================================================*
006659 作業ファイル３読込 SECTION.
006660*
006661     READ 作業ファイル３ NEXT
006662     AT END
006663         MOVE "YES" TO 終了フラグ
006664     NOT AT END
006665         MOVE 作３−施術和暦    TO 受−施術和暦 レセ−施術和暦
006666         MOVE 作３−施術年      TO 受−施術年   レセ−施術年  
006667         MOVE 作３−施術月      TO 受−施術月   レセ−施術月  
006668         MOVE 作３−患者番号    TO 受−患者番号 レセ−患者番号
006669         MOVE 作３−枝番        TO 受−枝番     レセ−枝番    
006670         READ 受診者情報Ｆ
006671         INVALID KEY
006672              MOVE NC"受診者"   TO ファイル名
006673              PERFORM エラー表示Ｒ
006674         END-READ
               MOVE 作３−レセ種別    TO レセ−レセ種別
006670         READ レセプトＦ
006671         INVALID KEY
006672              MOVE NC"レセプト"   TO ファイル名
006673              PERFORM エラー表示Ｒ
007450         END-READ
006675     END-READ.
006676*
007187*================================================================*
007188 データチェック SECTION.
007189*
007190     MOVE SPACE  TO 実行キーＷ.
019520* *****************************************************************
019530* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
019540* *****************************************************************
019640     IF ( レセ−請求対象区分 NOT = ZERO ) AND
005778        ( レセ−償還払い区分 NOT = 2 OR 3 )
              IF (レセ−レセ種別 = 3) AND ( レセ−会総括表印刷対象区分 = 1 )
                  CONTINUE
              ELSE
004090           MOVE レセ−施術和暦  TO 受−施術和暦
004100           MOVE レセ−施術年    TO 受−施術年
004110           MOVE レセ−施術月    TO 受−施術月
004120           MOVE レセ−患者番号  TO 受−患者番号
004130           MOVE レセ−枝番      TO 受−枝番
                 READ 受診者情報Ｆ
                 NOT INVALID KEY
005786**      健保のみ
005787              IF 受−保険分類 = 1
019880                 MOVE "YES"  TO 実行キーＷ
005789              END-IF
019950           END-READ
              END-IF
019960     END-IF.
007236*
007249*================================================================*
007250 レセプトＦ読込 SECTION.
007251*
007252     READ レセプトＦ NEXT
007253     AT END
007254         MOVE "YES" TO 終了フラグ
007255     END-READ.
007256*
007257*================================================================*
007258 作１レコードセット SECTION.
007260*
007310     MOVE 作３−助成区分     TO 作１−助成区分.
007311     MOVE レセ−請求和暦     TO 作１−請求和暦.
007312     MOVE レセ−請求年       TO 作１−請求年.
007313     MOVE レセ−請求月       TO 作１−請求月.
007314     MOVE 受−施術和暦       TO 作１−施術和暦.
007320     MOVE 受−施術年         TO 作１−施術年.
007330     MOVE 受−施術月         TO 作１−施術月.
007340     MOVE 受−患者カナ       TO 作１−患者カナ.
007350     MOVE 受−患者コード     TO 作１−患者コード.
007360     MOVE 受−被保険者カナ   TO 作１−被保険者カナ.
007370     MOVE 受−本人家族区分   TO 作１−本人家族区分.
007371* 老人・助成の時は、受益者番号を番号に、記号は空白
007372     IF  作１−老人レコード区分 = 1
007373         MOVE SPACE               TO 作１−記号
007374         MOVE 受−受益者番号老人  TO 作１−番号
007375     ELSE
007376         IF 作１−助成レコード区分 = 1
007377            MOVE SPACE               TO 作１−記号
007378            MOVE 受−受益者番号助成  TO 作１−番号
007379         ELSE
007380*            MOVE 受−記号            TO 作１−記号
007390*            MOVE 受−番号            TO 作１−番号
      *    / 連暗号複合−入力情報セット /
                   MOVE 受−記号       TO 連暗号複合−記号
                   MOVE 受−番号       TO 連暗号複合−番号
                   MOVE 受−暗号化項目 TO 連暗号複合−暗号化項目
      *     
                   CALL   複合プログラム名Ｗ
                   CANCEL 複合プログラム名Ｗ
      *
                   MOVE 連暗号複合−複合した記号 TO 作１−記号
                   MOVE 連暗号複合−複合した番号 TO 作１−番号
007391         END-IF
007392     END-IF.
007400*
007510*================================================================*
008810 作１ファイル書込 SECTION.
008820*
008830     WRITE 作１−レコード.
008831     IF 状態キー  NOT =  "00"
008850         MOVE NC"作１"  TO ファイル名
008860         PERFORM エラー表示
008870     END-IF.
008871*
008872*================================================================*
008873 作業ファイル２作成 SECTION.
008874******************************************************************************
008875*  全印刷以外は、合計ページを印刷するため、合計のデータを作成する。
008876*  全データの合計件数、費用額合計、請求額合計を求める。
008877******************************************************************************
008878*
008882     OPEN INPUT  作業ファイル３.
008883         MOVE NC"作３" TO ファイル名.
008884         PERFORM オープンチェック.
008885*
008886     INITIALIZE 合計Ｗ.
008889     MOVE SPACE  TO 終了フラグ.
008890     PERFORM 作業ファイル３読込.
008891     PERFORM UNTIL  終了フラグ = "YES" 
008580         MOVE "YES"          TO 実行キーＷ
008893** 労災・自賠責・自由は対象外
008894         IF  受−保険種別 = 70 OR 80 OR 90
008895             MOVE SPACE  TO 実行キーＷ
008896         END-IF
008897** 生保単独は対象外
008898         IF  作３−助成区分  = 2
008899             MOVE SPACE  TO 実行キーＷ
008900         END-IF
008901**
008902         IF  実行キーＷ = "YES"
008905*
005610             EVALUATE TRUE
008906*            ********
008907*            * 健保 *
008908*            ********
013940             WHEN レセ−レセ種別   = 1
008918                 COMPUTE 件数合計Ｗ   = 件数合計Ｗ   + 1
008919                 COMPUTE 費用額合計Ｗ = 費用額合計Ｗ + レセ−合計
008920                 COMPUTE 請求額合計Ｗ = 請求額合計Ｗ + レセ−請求金額
008958*            ********
008959*            * 老人 *
008960*            ********
013940             WHEN レセ−レセ種別   = 2
008968                 COMPUTE 件数合計Ｗ   = 件数合計Ｗ   + 1
008969                 COMPUTE 費用額合計Ｗ = 費用額合計Ｗ + レセ−合計
008970                 COMPUTE 請求額合計Ｗ = 請求額合計Ｗ + レセ−請求金額
008958*            ********
008959*            * 助成 *
008960*            ********
013940             WHEN レセ−レセ種別   = 3
008992                 COMPUTE 件数合計Ｗ   = 件数合計Ｗ   + 1
008993                 COMPUTE 請求額合計Ｗ = 請求額合計Ｗ + レセ−助成請求金額
009005             END-EVALUATE
               END-IF
009006         PERFORM 作業ファイル３読込
009007     END-PERFORM.
009008*
009054*
009055* レコードセット
009056     MOVE SPACE TO 作２−レコード.
009057     INITIALIZE 作２−レコード.
009058     MOVE 件数合計Ｗ   TO  作２−件数合計.
009059     MOVE 費用額合計Ｗ TO  作２−費用額合計.
009060     MOVE 請求額合計Ｗ TO  作２−請求額合計.
009061     WRITE 作２−レコード.
009063     IF 状態キー  NOT =  "00"
009064         MOVE NC"作２"  TO ファイル名
009065         PERFORM エラー表示
009066     END-IF.
009067*
009068     CLOSE 作業ファイル３.
009069*
009070*================================================================*
009071******************************************************************
009072 END PROGRAM YCK581.
009073******************************************************************
