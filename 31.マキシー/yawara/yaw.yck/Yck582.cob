000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YCK582.
000060 AUTHOR.                 池田 幸子
000070*
000080*----------------------------------------------------------------*
000090*         総括表印刷（柔+ｳｨﾝﾄﾞｳｽﾞ版）
000091* 中央と同じ (中京- にする)
000092* 請求年月Ver.
000100*         MED = YCK580 YCK582P
000110*----------------------------------------------------------------*
000120 DATE-WRITTEN.           2012-11-29
000130 DATE-COMPILED.          2012-11-29
000140*----------------------------------------------------------------*
000150******************************************************************
000160*            ENVIRONMENT         DIVISION                        *
000170******************************************************************
000180 ENVIRONMENT             DIVISION.
000190 CONFIGURATION           SECTION.
000200 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000210 OBJECT-COMPUTER.        FMV-DESKPOWER.
000220 SPECIAL-NAMES.          CONSOLE  IS  CONS
000230                         SYSERR   IS  MSGBOX.
000240 INPUT-OUTPUT            SECTION.
000250 FILE-CONTROL.
000260     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000270                             ORGANIZATION             IS  INDEXED
000280                             ACCESS MODE              IS  DYNAMIC
000290                             RECORD KEY               IS  元−元号区分
000300                             FILE STATUS              IS  状態キー
000310                             LOCK        MODE         IS  AUTOMATIC.
000320     SELECT  制御情報マスタ  ASSIGN      TO        SEIGYOL
000330                             ORGANIZATION             IS  INDEXED
000340                             ACCESS MODE              IS  DYNAMIC
000350                             RECORD KEY               IS  制−制御区分
000360                             FILE STATUS              IS  状態キー
000370                             LOCK        MODE         IS  AUTOMATIC.
000450     SELECT  施術所情報マスタ ASSIGN      TO        SEJOHOL
000460                             ORGANIZATION             IS  INDEXED
000470                             ACCESS MODE              IS  DYNAMIC
000480                             RECORD KEY               IS 施情−施術所番号
000490                             FILE STATUS              IS  状態キー
000500                             LOCK        MODE         IS  AUTOMATIC.
000510     SELECT  作業ファイル１  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5801L.DAT"
000520                             ORGANIZATION             IS  SEQUENTIAL
000530                             ACCESS                   IS  SEQUENTIAL
000540                             FILE        STATUS       IS  状態キー
000550                             LOCK        MODE         IS  AUTOMATIC.
000560     SELECT  作業ファイル２  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5802L.DAT"
000570                             ORGANIZATION             IS  SEQUENTIAL
000580                             ACCESS                   IS  SEQUENTIAL
000590                             FILE        STATUS       IS  状態キー
000600                             LOCK        MODE         IS  AUTOMATIC.
000640     SELECT  印刷ファイル    ASSIGN      TO     GS-PRTF001
000650                             SYMBOLIC    DESTINATION  IS "PRT"
000660                             FORMAT                   IS  定義体名Ｐ
000670                             GROUP                    IS  項目群名Ｐ
000680                             PROCESSING  MODE         IS  処理種別Ｐ
000690                             UNIT        CONTROL      IS  拡張制御Ｐ
000700                             FILE        STATUS       IS  通知情報Ｐ.
000710******************************************************************
000720*                      DATA DIVISION                             *
000730******************************************************************
000740 DATA                    DIVISION.
000750 FILE                    SECTION.
000970*                           ［ＲＬ＝  １２８］
000980 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
000990     COPY GENGOU          OF  XFDLIB  JOINING   元   AS  PREFIX.
001000*                           ［ＲＬ＝  ２５６］
001010 FD  制御情報マスタ          BLOCK   CONTAINS   1   RECORDS.
001020     COPY SEIGYO          OF  XFDLIB  JOINING   制   AS  PREFIX.
000850*                           ［ＲＬ＝  ６４０］
000860 FD  施術所情報マスタ          BLOCK   CONTAINS   1   RECORDS.
000870     COPY SEJOHO         OF  XFDLIB  JOINING   施情   AS  PREFIX.
000871*
000880*
000881 FD  作業ファイル１ RECORD  CONTAINS 250 CHARACTERS.
000882 01  作１−レコード.
000883     03  作１−レコードデータ.
000884         05  作１−請求和暦年月.
000885             07  作１−請求和暦            PIC 9.
000886             07  作１−請求年              PIC 9(2).
000887             07  作１−請求月              PIC 9(2).
000888         05  作１−施術和暦年月.
000889             07  作１−施術和暦            PIC 9.
000890             07  作１−施術年              PIC 9(2).
000891             07  作１−施術月              PIC 9(2).
000892         05  作１−助成区分                PIC 9.
000893         05  作１−保険者番号              PIC X(10).
000894         05  作１−記号                    PIC X(24).
000895         05  作１−番号                    PIC X(30).
000896         05  作１−被保険者カナ            PIC X(50).
000897         05  作１−患者カナ                PIC X(50).
000898         05  作１−本人家族区分            PIC 9.
000899         05  作１−費用額                  PIC 9(7).
000900         05  作１−負担額                  PIC 9(7).
000901         05  作１−請求額                  PIC 9(7).
000902         05  作１−患者コード.
000903             07 作１−患者番号             PIC 9(6).
000904             07 作１−枝番                 PIC X(1).
000905         05  作１−保険種別                PIC 9(2).
000906         05  作１−親保険種別              PIC 9(2).
000907         05  作１−老人レコード区分        PIC 9.
000908         05  作１−助成レコード区分        PIC 9.
000909         05  FILLER                        PIC X(40).
000910* 合計ファイル
000911 FD  作業ファイル２ RECORD  CONTAINS 32 CHARACTERS.
000912 01  作２−レコード.
000913     03  作２−レコードデータ.
000914         05  作２−件数合計                PIC 9(4).
000915         05  作２−費用額合計              PIC 9(8).
000916         05  作２−請求額合計              PIC 9(8).
000917         05  FILLER                        PIC X(12).
001160*
001170 FD  印刷ファイル.
001180     COPY YCK582P        OF  XMDLIB.
001190*----------------------------------------------------------------*
001200******************************************************************
001210*                WORKING-STORAGE SECTION                         *
001220******************************************************************
001230 WORKING-STORAGE         SECTION.
001240 01 キー入力                           PIC X    VALUE SPACE.
001250 01 状態キー                           PIC X(2) VALUE SPACE.
001260 01 終了フラグ                         PIC X(3) VALUE SPACE.
001270 01 終了行フラグ                       PIC X(3) VALUE SPACE.
001280 01 ファイル名                         PIC N(10).
001281 01 対象なしフラグ                     PIC X(3) VALUE SPACE.
001210 01 オープンフラグ                     PIC X(3)   VALUE SPACE.
001282*
001331 01 助成区分ＰＷ                       PIC 9     VALUE ZERO.
001340 01 保険種別ＰＷ                       PIC 9(2)  VALUE ZERO.
001350 01 請求和暦年月ＰＷ.
001360    03 請求和暦ＰＷ                    PIC 9    VALUE ZERO.
001370    03 請求年ＰＷ                      PIC 9(2) VALUE ZERO.
001380    03 請求月ＰＷ                      PIC 9(2) VALUE ZERO.
001381*
001390 01 保険名称Ｗ                         PIC N(3) VALUE SPACE.
001460 01 備考Ｗ                             PIC X(20).
001470 01 前和暦Ｗ                           PIC 9    VALUE ZERO.
001480 01 行カウンタ                         PIC 9(2) VALUE 0.
001490 01 頁カウンタ                         PIC 9(4) VALUE 0.
001500 01 最大行数                           PIC 9(2) VALUE 0.
001510 01 ヘッダ行数                         PIC 9(2) VALUE 0.
001520 01 移動行数Ｗ                         PIC 9(2) VALUE 0.
001530 01 処理移動キー                       PIC X(4) VALUE SPACE.
001540 01 カレント元号Ｗ                     PIC 9(1) VALUE ZERO.
001541*
001542 01 会員番号Ｗ.
001543    03 中央                            PIC N(2) VALUE SPACE.
001544    03 ハイフン                        PIC X(1) VALUE "-".
001545    03 会員番号ＷＰ                    PIC X(10) VALUE SPACE.
001546*
001570 01 保険種別名称Ｗ.
001580    03 保険種別名称ＷＰ                PIC X(6) VALUE SPACE.
001590 01 親保険種別名称Ｗ.
001600    03 親保険種別名称ＷＰ              PIC X(4) VALUE SPACE.
001610    03 合計文字                        PIC X(4).
001620 01 総合計Ｗ.
001630    03 総合計ＷＰ                      PIC X(6).
001631*
001632** 氏名分離用
001640 01 被保険者カナＷ.
001650    03 被保険者カナＷＰ                PIC X    OCCURS 50 VALUE SPACE.
001660 01 名字カナＷ.
001670    03 名字カナＷＰ                    PIC X    OCCURS 50 VALUE SPACE.
001680 01 名前カナＷ.
001690    03 名前カナＷＰ                    PIC X    OCCURS 50 VALUE SPACE.
001700 01 カウンタ                           PIC 9(2) VALUE ZERO.
001710 01 Ｍカウンタ                         PIC 9(2) VALUE ZERO.
001720 01 Ｎカウンタ                         PIC 9(2) VALUE ZERO.
001730 01 名字フラグ                         PIC X(3) VALUE SPACE.
001731**
001740 01 小計Ｗ.
001750    03 小計件数Ｗ                      PIC 9(4) VALUE ZERO.
001760    03 費用額小計Ｗ                    PIC 9(7) VALUE ZERO.
001770    03 負担額小計Ｗ                    PIC 9(7) VALUE ZERO.
001780    03 請求額小計Ｗ                    PIC 9(7) VALUE ZERO.
001790 01 合計Ｗ.
001800    03 合計件数Ｗ                      PIC 9(4) VALUE ZERO.
001810    03 費用額合計Ｗ                    PIC 9(8) VALUE ZERO.
001820    03 負担額合計Ｗ                    PIC 9(8) VALUE ZERO.
001830    03 請求額合計Ｗ                    PIC 9(8) VALUE ZERO.
001831*
001840 01 施術和暦年月Ｗ.
001850     03 施術和暦Ｗ                   PIC 9.
001860     03 施術年月Ｗ.
001870        05 施術年Ｗ                  PIC 9(2).
001880        05 年Ｗ                      PIC N(1).
001890        05 施術月Ｗ                  PIC 9(2).
001900        05 月Ｗ                      PIC N(1).
001910 01 現在和暦年月Ｗ.
001920    03 現在和暦Ｗ                    PIC 9.
001930    03 現在年Ｗ                      PIC 9(2).
001940    03 現在月Ｗ                      PIC 9(2).
001941*
001942 01 保険種別ＷＲ                     PIC 9(2) VALUE ZERO.
001943 01 印刷形式ＷＲ                     PIC 9    VALUE ZERO.
001950*******************************************************************
001960 01 印刷制御.
001970     03 定義体名Ｐ                     PIC X(8) VALUE SPACE.
001980     03 項目群名Ｐ                     PIC X(8) VALUE SPACE.
001990     03 処理種別Ｐ                     PIC X(2) VALUE SPACE.
002000     03 拡張制御Ｐ.
002010         05 端末制御Ｐ.
002020             07 移動方向Ｐ             PIC X(1).
002030             07 移動行数Ｐ             PIC 9(3).
002040         05 詳細制御Ｐ                 PIC X(2).
002050     03 通知情報Ｐ                     PIC X(2) VALUE SPACE.
002060     03 ユニット名Ｐ                   PIC X(8) VALUE SPACE.
002070*
002080 01 計算機西暦年Ｗ                     PIC 9(2).
002090* 日付ＷＯＲＫ
002100 01 和暦終了年Ｗ                       PIC 9(4).
001500 01 計算機和暦年Ｗ                     PIC 9(2).
002110 01 計算機西暦.
002120    03 計算機西暦年                    PIC 9(4).
002130    03 計算機西暦月日                  PIC 9(4).
002140 01 計算機西暦Ｒ REDEFINES 計算機西暦.
002150    03 計算機世紀                      PIC 9(2).
002160    03 計算機日付                      PIC 9(6).
002170    03 計算機日付Ｒ REDEFINES 計算機日付.
002180       05 計算機年月                   PIC 9(4).
002190       05 計算機年月Ｒ REDEFINES 計算機年月.
002200         07 計算機年                   PIC 9(2).
002210         07 計算機月                   PIC 9(2).
002220       05 計算機日                     PIC 9(2).
002230******************************************************************
002240*                          連結項目                              *
002250******************************************************************
002260*
      ****************************
      * 元号日付チェック用ワーク *
      *****************************
       01 連元ＣＨＫ−キー IS EXTERNAL.
      */ in
          03 連元ＣＨＫ−和暦年月日.
             05 連元ＣＨＫ−和暦年月.
               07 連元ＣＨＫ−和暦           PIC 9.
               07 連元ＣＨＫ−年             PIC 9(2).
               07 連元ＣＨＫ−月             PIC 9(2).
             05 連元ＣＨＫ−日               PIC 9(2).
      *   / 確認メッセージを使用する場合："YES" /
          03 連元ＣＨＫ−確認フラグ          PIC X(3).
      */ out
      *   / 年月単位の年月日を作成 /
          03 連元ＣＨＫ−正式和暦年月日.
             05 連元ＣＨＫ−正式和暦年月.
               07 連元ＣＨＫ−正式和暦       PIC 9.
               07 連元ＣＨＫ−正式年         PIC 9(2).
               07 連元ＣＨＫ−正式月         PIC 9(2).
            05 連元ＣＨＫ−正式日            PIC 9(2).
      *   / 存在しない場合エラー："YES" /
         03 連元ＣＨＫ−エラーフラグ         PIC X(3).
      *
      *   / 正式日付の年月日を作成（元号が変更されていない場合は上記年月日と同じ） /
         03 連元ＣＨＫ−正式和暦年月日２.
            05 連元ＣＨＫ−正式和暦年月２.
              07 連元ＣＨＫ−正式和暦２     PIC 9.
              07 連元ＣＨＫ−正式年２       PIC 9(2).
              07 連元ＣＨＫ−正式月２       PIC 9(2).
            05 連元ＣＨＫ−正式日２          PIC 9(2).
      *   / 存在しない場合エラー："YES" /
         03 連元ＣＨＫ−エラーフラグ２       PIC X(3).
      *
002494 01 連メ−キー IS EXTERNAL.
002495    03  連メ−メッセージ                 PIC N(20).
002260*
002287 01 連入−画面情報ＹＣＫ５８０ IS EXTERNAL.
002297    03 連入−印刷形式                  PIC 9.
002307    03 連入−請求和暦年月.
002317       05 連入−請求和暦               PIC 9.
002327       05 連入−請求年月.
002337         07 連入−請求年               PIC 9(2).
002347         07 連入−請求月               PIC 9(2).
          03 連入−プレビュー区分            PIC 9.
002560*
000540************************************
000550* プリンタファイル作成用           *
000560************************************
000570 01 Ｈ連ＰＲＴＦ−作成データ IS EXTERNAL.
000580     03 Ｈ連ＰＲＴＦ−ファイル名           PIC X(8).
000590     03 Ｈ連ＰＲＴＦ−プレビュー区分       PIC 9.
000600     03 Ｈ連ＰＲＴＦ−帳票プログラム名     PIC X(8).
000610     03 Ｈ連ＰＲＴＦ−オーバレイ名         PIC X(8).
002269*
002270******************************************************************
002280*                      PROCEDURE  DIVISION                       *
002290******************************************************************
002300 PROCEDURE               DIVISION.
002310************
002320*           *
002330* 初期処理   *
002340*           *
002350************
002560     MOVE SPACE TO オープンフラグ.
002570     PERFORM プリンタファイル作成.
002360     PERFORM 初期化.
002361     MOVE SPACE  TO YCK582P.
002362***     INITIALIZE YCK582P.
002370************
002380*           *
002390* 主処理     *
002400*           *
002410************
002411*
002412     EVALUATE TRUE
002414     WHEN  印刷形式ＷＲ = ZERO 
002415          PERFORM 印刷処理
002425     WHEN  印刷形式ＷＲ = 1 OR 2 
002427          PERFORM 一部印刷処理
002429     WHEN OTHER
002430          CONTINUE
002431     END-EVALUATE.
002432*
002433************
002440*           *
002450* 終了処理   *
002460*           *
002470************
002480     PERFORM 終了処理.
002490     EXIT PROGRAM.
002500*
002510*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002860*================================================================*
002870 プリンタファイル作成 SECTION.
002880*================================================================*
002890*   / 初期化 /
002900     MOVE SPACE TO Ｈ連ＰＲＴＦ−作成データ.
002910     INITIALIZE Ｈ連ＰＲＴＦ−作成データ.
002920*
002930*
002940*--↓↓ 変更箇所 ↓↓--------------------------------------*
002970*   使用するプリンタファイル名セット
      *   印字調整有り
002971     MOVE "PRTF001"             TO Ｈ連ＰＲＴＦ−ファイル名.
002972*
002973*   使用する帳票プログラム名セット
002974     MOVE "YCK582"              TO Ｈ連ＰＲＴＦ−帳票プログラム名.
002975*
002976*--↑↑-----------------------------------------------------*
002980*
002990*   / プレビュー区分セット /
003000     MOVE 連入−プレビュー区分 TO Ｈ連ＰＲＴＦ−プレビュー区分.
003010*
003020     CALL   "CRTPRTF".
003030     CANCEL "CRTPRTF".
003040*
002520*================================================================*
002530 初期化 SECTION.
002540*
002550     PERFORM ファイルオープン.
002560*    /* 現在日付取得 */
002570     ACCEPT 計算機日付 FROM DATE.
002580*    /* 1980〜2079年の間で設定 */
002590     IF 計算機年 > 80
002600         MOVE 19 TO 計算機世紀
002610     ELSE
002620         MOVE 20 TO 計算機世紀
002630     END-IF.
002970     PERFORM カレント元号取得.
002980     PERFORM 和暦終了年取得.
003610     COMPUTE 計算機和暦年Ｗ = 計算機西暦年 - 和暦終了年Ｗ + 1.
           INITIALIZE 連元ＣＨＫ−キー.
           MOVE カレント元号Ｗ   TO 連元ＣＨＫ−和暦.
           MOVE 計算機和暦年Ｗ   TO 連元ＣＨＫ−年.
           MOVE 計算機月         TO 連元ＣＨＫ−月.
           MOVE ZERO             TO 連元ＣＨＫ−日.
           CALL "GENGOCHK".
           CANCEL "GENGOCHK".
002701*
002702* 連結項目の待避
002704     MOVE 連入−印刷形式    TO 印刷形式ＷＲ.
003070*
003000*================================================================*
003010 カレント元号取得 SECTION.
003020*
003030     MOVE ZEROS TO 制−制御区分.
003040     READ 制御情報マスタ
003050     NOT INVALID KEY
003060         MOVE 制−カレント元号 TO カレント元号Ｗ
003070     END-READ.
003080*
003090*================================================================*
003100 和暦終了年取得 SECTION.
003110*
003120*     DISPLAY NC"カレント元号Ｗ"  カレント元号Ｗ UPON MSGBOX.
003130     MOVE カレント元号Ｗ TO 元−元号区分.
003140     READ 元号マスタ
003150     INVALID KEY
003160         DISPLAY NC"指定和暦が登録されていません" UPON CONS
003170         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003180                                                  UPON CONS
002441*-----------------------------------------*
002442         CALL "actcshm"  WITH C LINKAGE
002443*-----------------------------------------*
003190         ACCEPT  キー入力 FROM CONS
003200         PERFORM 終了処理
003210         EXIT PROGRAM
003220     NOT INVALID KEY
               IF ( 元−正式開始西暦年 = ZERO OR SPACE )
005500             COMPUTE 前和暦Ｗ = カレント元号Ｗ - 1
005510             MOVE 前和暦Ｗ TO 元−元号区分
005520             READ 元号マスタ
005530             INVALID KEY
005540                 DISPLAY NC"指定和暦が登録されていません" UPON CONS
005550                 DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
005560                                                          UPON CONS
005570                 ACCEPT  キー入力 FROM CONS
005580                 PERFORM 終了処理
005590                 EXIT PROGRAM
005600             NOT INVALID KEY
                       IF ( 元−終了西暦年 = ZERO OR SPACE )
                           CONTINUE
                       ELSE
                           COMPUTE 和暦終了年Ｗ = 元−終了西暦年 + 1
                       END-IF
                   END-READ
               ELSE
                   MOVE 元−正式開始西暦年 TO 和暦終了年Ｗ
               END-IF
003360     END-READ.
003370*
003080*================================================================*
003090 ファイルオープン SECTION.
003100*
003410     OPEN INPUT   元号マスタ
003420         MOVE NC"元号" TO ファイル名.
003430         PERFORM オープンチェック.
003440     OPEN INPUT   制御情報マスタ
003450         MOVE NC"制御情報" TO ファイル名.
003460         PERFORM オープンチェック.
003200     OPEN INPUT 施術所情報マスタ
003210         MOVE NC"施術所情報" TO ファイル名.
003220         PERFORM オープンチェック.
003230     OPEN INPUT 作業ファイル１
003240         MOVE NC"作業１" TO ファイル名.
003250         PERFORM オープンチェック.
003251     OPEN INPUT 作業ファイル２
003252         MOVE NC"作業２" TO ファイル名.
003253         PERFORM オープンチェック.
003280*================================================================*
003290 オープンチェック SECTION.
003300*
003310     IF 状態キー  NOT =  "00"
003320         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
003330         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003340         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003350                                                 UPON CONS
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
003360         ACCEPT  キー入力 FROM CONS
003370         PERFORM ファイル閉鎖
003380         EXIT PROGRAM.
003390*================================================================*
003400 ファイル閉鎖 SECTION.
003410*
003570     IF ( オープンフラグ = "YES" )
003580         CLOSE 印刷ファイル
003590     ELSE
003600         MOVE  NC"　　データが０件です。確認して下さい。" TO 連メ−メッセージ
003610         CALL   "MSG001"
003620         CANCEL "MSG001"
003630     END-IF.
003640*
003420     CLOSE 元号マスタ 制御情報マスタ 施術所情報マスタ 作業ファイル１  作業ファイル２.
003440*================================================================*
003450 終了処理 SECTION.
003460*
003470     PERFORM ファイル閉鎖.
003480*================================================================*
003490 エラー表示 SECTION.
003500*
003510     DISPLAY NC"状態キー" 状態キー  UPON CONS.
003520     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
003530     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
003540     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
000080*-----------------------------------------*
000090     CALL "actcshm"  WITH C LINKAGE.
000100*-----------------------------------------*
003550     ACCEPT  キー入力 FROM CONS.
003560     PERFORM ファイル閉鎖.
003570     EXIT PROGRAM.
003580*================================================================*
003590 印刷処理 SECTION.
003600*
003602     MOVE SPACE TO 終了フラグ.
003603     INITIALIZE 小計Ｗ.
003604     INITIALIZE 合計Ｗ.
003605     MOVE 20    TO 最大行数.
003606     MOVE 1     TO 頁カウンタ.
003607     PERFORM 作業ファイル１読込.
003608     PERFORM UNTIL 終了フラグ = "YES"
003609*
003610         MOVE 作１−請求和暦     TO 請求和暦ＰＷ
003611         MOVE 作１−請求年       TO 請求年ＰＷ
003612         MOVE 作１−請求月       TO 請求月ＰＷ
003613         MOVE 作１−助成区分     TO 助成区分ＰＷ
003620         PERFORM ヘッダセット
003621*
003622         PERFORM UNTIL ( 終了フラグ = "YES" ) OR ( 行カウンタ > 最大行数 )  OR
003623                       ( 作１−請求和暦   NOT = 請求和暦ＰＷ ) OR
003624                       ( 作１−請求年     NOT = 請求年ＰＷ   ) OR
003625                       ( 作１−請求月     NOT = 請求月ＰＷ   ) OR
003626                       ( 作１−助成区分   NOT = 助成区分ＰＷ ) 
003627             PERFORM 明細部セット
003628             PERFORM 合計額累計
003629             PERFORM 作業ファイル１読込
003631         END-PERFORM
003632*
003633         IF ( 行カウンタ > 最大行数 )
003634             IF  ( 終了フラグ = "YES" ) OR 
003635                 ( 作１−請求和暦   NOT = 請求和暦ＰＷ ) OR
003636                 ( 作１−請求年     NOT = 請求年ＰＷ   ) OR
003637                 ( 作１−請求月     NOT = 請求月ＰＷ   ) OR
003638                 ( 作１−助成区分   NOT = 助成区分ＰＷ ) 
003639                 PERFORM 印字処理
003640                 PERFORM 改頁処理
003645                 IF  終了フラグ = "YES" 
003646                     PERFORM ヘッダセット
003647                     PERFORM 総合計セット
003648                     PERFORM 印字処理
003649                     PERFORM 改頁処理
003650                 END-IF
003651             ELSE
003652                 PERFORM 印字処理
003653                 PERFORM 改頁処理
003654             END-IF
003655         ELSE
003658             IF  終了フラグ = "YES" 
003660                 IF 行カウンタ > 最大行数
003661                    PERFORM 印字処理
003662                    PERFORM 改頁処理
003663                    PERFORM ヘッダセット
003664                 END-IF
003665                 PERFORM 総合計セット
003666             END-IF
003667             PERFORM 印字処理
003668             PERFORM 改頁処理
003669         END-IF
003670*
003671     END-PERFORM.
003672*
003673*================================================================*
003674 一部印刷処理 SECTION.
003675*
003676     MOVE SPACE TO 終了フラグ.
003677     INITIALIZE 小計Ｗ.
003678     INITIALIZE 合計Ｗ.
003679     MOVE 20    TO 最大行数.
003680     MOVE 1     TO 頁カウンタ.
003681     PERFORM 作業ファイル１読込.
003682     IF  終了フラグ = "YES"
003683         MOVE "YES"  TO 対象なしフラグ
003684     END-IF.
003685     PERFORM UNTIL 終了フラグ = "YES"
003686*
003687         MOVE 作１−請求和暦     TO 請求和暦ＰＷ
003688         MOVE 作１−請求年       TO 請求年ＰＷ
003689         MOVE 作１−請求月       TO 請求月ＰＷ
003690         MOVE 作１−助成区分     TO 助成区分ＰＷ
003691         PERFORM ヘッダセット
003692*
003693         PERFORM UNTIL ( 終了フラグ = "YES" ) OR ( 行カウンタ > 最大行数 )  OR
003694                       ( 作１−請求和暦   NOT = 請求和暦ＰＷ ) OR
003695                       ( 作１−請求年     NOT = 請求年ＰＷ   ) OR
003696                       ( 作１−請求月     NOT = 請求月ＰＷ   ) OR
003697                       ( 作１−助成区分   NOT = 助成区分ＰＷ ) 
003698             PERFORM 明細部セット
003699             PERFORM 合計額累計
003700             PERFORM 作業ファイル１読込
003701         END-PERFORM
003702*
003734         PERFORM 印字処理
003735         PERFORM 改頁処理
003738*
003739     END-PERFORM.
003740*
003741* 合計ページのみ印刷
003742     IF 対象なしフラグ NOT = "YES"
003743         PERFORM ヘッダセット
003744         MOVE ZERO  TO 頁
003750         PERFORM 合計ページのみセット
003751         PERFORM 印字処理
003752         PERFORM 改頁処理
003753     END-IF.
003760*
004330*================================================================*
004340 作業ファイル１読込 SECTION.
004350*
004360     READ 作業ファイル１ NEXT
004370     AT END
004380         MOVE "YES" TO 終了フラグ
004390     END-READ.
004400*
004401*================================================================*
004402 印字処理  SECTION.
004403*
006050     IF ( オープンフラグ NOT = "YES" )
006060        MOVE "YES" TO オープンフラグ
006070        OPEN I-O  印刷ファイル
006080        PERFORM エラー処理Ｐ
006090     END-IF.
004404     MOVE "YCK582P" TO  定義体名Ｐ.
004405     MOVE SPACE     TO  処理種別Ｐ.
004406     MOVE "SCREEN"  TO  項目群名Ｐ.
004408     WRITE YCK582P.
004409     PERFORM エラー処理Ｐ.
004410     MOVE SPACE TO YCK582P.
004413*================================================================*
004414 改頁処理  SECTION.
004415
004416     MOVE "YCK582P" TO  定義体名Ｐ.
004417     MOVE "CT"      TO  処理種別Ｐ.
004418     MOVE "PAGE"    TO  拡張制御Ｐ.
004419     MOVE SPACE     TO  項目群名Ｐ.
004421     WRITE YCK582P.
004422     PERFORM エラー処理Ｐ.
004423     MOVE SPACE     TO  拡張制御Ｐ.
004424*
004425     COMPUTE 頁カウンタ = 頁カウンタ + 1.
004426*
004650*================================================================*
004710 ヘッダセット SECTION.
004720*
      */元号修正/↓↓↓20190514
037370     IF 連元ＣＨＫ−正式和暦 > 4
              MOVE 連元ＣＨＫ−正式和暦 TO 元−元号区分
037380        READ 元号マスタ
037390        NOT INVALID KEY
037400            MOVE 元−元号名称   TO 作成和暦
037410        END-READ
              MOVE "===="             TO 作成和暦訂正
           END-IF.
      */元号修正/↑↑↑20190514
004760     MOVE 連元ＣＨＫ−正式年 TO 作成年.
004770     MOVE 連元ＣＨＫ−正式月 TO 作成月.
004780     MOVE 計算機日           TO 作成日.
004790     MOVE 頁カウンタ         TO 頁.
004800*
004810* 会員番号を取得
004820     MOVE 00         TO 施情−施術所番号.
004830     READ 施術所情報マスタ
004840     INVALID KEY
004850         MOVE SPACE       TO 会員番号
004860         MOVE SPACE       TO 代表者名
004870     NOT INVALID KEY
004881         MOVE NC"中京"                TO 中央
004890         MOVE 施情−接骨師会会員番号  TO 会員番号ＷＰ
004900         MOVE 会員番号Ｗ              TO 会員番号
004910         MOVE 施情−代表者名          TO 代表者名
004920     END-READ.
004930*
004940     MOVE 1    TO 行カウンタ.
004950*
005080*================================================================*
005090 明細部セット SECTION.
005100*
005140     MOVE 作１−施術年        TO 施術年Ｗ.
005150     MOVE NC"年"              TO 年Ｗ.
005160     MOVE 作１−施術月        TO 施術月Ｗ.
005170     MOVE NC"月"              TO 月Ｗ.
005180     MOVE 施術年月Ｗ          TO 施術年月(行カウンタ).
005181*
005182     MOVE 作１−保険者番号    TO 保険者番号(行カウンタ).
005190     IF   作１−助成レコード区分 = 1 
005200          IF 作１−保険者番号(1:2) = "99"
005201             MOVE SPACE       TO 保険者番号(行カウンタ)
005202          END-IF
005203     END-IF.
005231*
005232     IF 作１−記号(1:2) = "＊" 
005233        MOVE  SPACE        TO  記号(行カウンタ)
005234     ELSE
005235        MOVE 作１−記号    TO  記号(行カウンタ)
005236     END-IF.
005237     IF ( 作１−番号(1:1) = "*"  ) OR
005238        ( 作１−番号(1:2) = "＊" )
005239        MOVE  SPACE        TO  番号(行カウンタ)
005240     ELSE
005241        MOVE 作１−番号    TO  番号(行カウンタ)
005242     END-IF.
005243*
005244*** 老人と助成レコードは、患者カナで、本人家族区分 = 1 となる. ***
005245     IF ( 作１−老人レコード区分 = 1 )  OR
005246        ( 作１−助成レコード区分 = 1 ) 
005247        MOVE 作１−患者カナ      TO 被保険者カナＷ
005248        MOVE 1                   TO 本人家族(行カウンタ)
005249     ELSE
005250        MOVE 作１−被保険者カナ  TO 被保険者カナＷ
005251        MOVE 作１−本人家族区分  TO 本人家族(行カウンタ)
005252     END-IF.
005253**
005254     PERFORM 氏名分離.
005260     MOVE 名字カナＷ         TO 被保険者氏カナ(行カウンタ).
005270     MOVE 名前カナＷ         TO 被保険者名カナ(行カウンタ).
005290     MOVE 作１−費用額       TO 合計額(行カウンタ).
005300     MOVE 作１−請求額       TO 請求額(行カウンタ).
005310*
005320     COMPUTE 行カウンタ = 行カウンタ + 1.
005330*
005430*================================================================*
005610 氏名分離  SECTION.
005611*
005612**** 名字と名前を分離する。
005613*
005620     MOVE 1          TO カウンタ Ｍカウンタ Ｎカウンタ.
005630     MOVE SPACE      TO 名字カナＷ 名前カナＷ 名字フラグ.
005876*
005878     PERFORM VARYING カウンタ FROM 1 BY 1 
005879                     UNTIL ( カウンタ > 50 ) OR ( 名字フラグ = "YES" )
005880          IF 被保険者カナＷＰ(カウンタ)  = SPACE
005884             MOVE "YES" TO  名字フラグ
005885          ELSE
005886             MOVE 被保険者カナＷＰ(カウンタ) TO 名字カナＷＰ(Ｍカウンタ)
005887             COMPUTE Ｍカウンタ = Ｍカウンタ + 1
005888          END-IF
005890     END-PERFORM.
005891*
005892     PERFORM  UNTIL カウンタ > 20 
005894          IF 被保険者カナＷＰ(カウンタ)  = SPACE
005895             CONTINUE
005896          ELSE
005898             MOVE 被保険者カナＷＰ(カウンタ) TO 名前カナＷＰ(Ｎカウンタ)
005900             COMPUTE Ｎカウンタ = Ｎカウンタ + 1
005901          END-IF
005902          COMPUTE カウンタ = カウンタ + 1
005903     END-PERFORM.
005904*
005906*================================================================*
005907 合計額累計 SECTION.
005908*
005914     COMPUTE 合計件数Ｗ     = 合計件数Ｗ      + 1.
005915     COMPUTE 費用額合計Ｗ   = 費用額合計Ｗ    + 作１−費用額.
005916     COMPUTE 負担額合計Ｗ   = 負担額合計Ｗ    + 作１−負担額.
005917     COMPUTE 請求額合計Ｗ   = 請求額合計Ｗ    + 作１−請求額.
005918*================================================================*
006450*================================================================*
006460 総合計セット SECTION.
006470*
006500     MOVE "総合計"     TO  総合計ＷＰ.
006510     MOVE 総合計Ｗ     TO  被保険者氏カナ(行カウンタ).
006520     MOVE 合計件数Ｗ   TO  本人家族(行カウンタ).
006530     MOVE 費用額合計Ｗ TO  合計額(行カウンタ).
006540     MOVE 請求額合計Ｗ TO  請求額(行カウンタ).
006541*================================================================*
006542 合計ページのみセット SECTION.
006543*
006544     READ 作業ファイル２ NEXT
006545     NOT AT END
006549         MOVE "総合計"         TO  総合計ＷＰ
006550         MOVE 総合計Ｗ         TO  被保険者氏カナ(1)
006551         MOVE 作２−件数合計   TO  本人家族(1)
006552         MOVE 作２−費用額合計 TO  合計額(1)
006553         MOVE 作２−請求額合計 TO  請求額(1)
006557     END-READ.
006558*================================================================*
006660 エラー処理Ｐ SECTION.
006670*
006680     IF 通知情報Ｐ NOT = "00"
006690         DISPLAY NC"帳票エラー"              UPON CONS
006700         DISPLAY NC"項目群名Ｐ：" 項目群名Ｐ UPON CONS
006710         DISPLAY NC"通知情報Ｐ：" 通知情報Ｐ UPON CONS
006720         DISPLAY NC"拡張制御Ｐ：" 拡張制御Ｐ UPON CONS
006730         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
006740                                             UPON CONS
004380*-----------------------------------------*
004390         CALL "actcshm"  WITH C LINKAGE
004400*-----------------------------------------*
006750         ACCEPT  キー入力 FROM CONS
006760         PERFORM ファイル閉鎖
006770         EXIT PROGRAM
006780     END-IF.
006790*================================================================*
006800******************************************************************
006810 END PROGRAM YCK582.
006820******************************************************************
