000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YAS5811.
000060 AUTHOR.                 岡田　憲和
000070*
000080*----------------------------------------------------------------*
000090*      総括表【ﾃﾞｰﾀ作成】柔ｳｨﾝﾄﾞｳｽﾞ95版
000100* 請求年月Ver.
000110*
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2012-09-14
000140 DATE-COMPILED.          2012-09-14
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000490     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000500                             ORGANIZATION             IS  INDEXED
000510                             ACCESS MODE              IS  DYNAMIC
000520                             RECORD KEY               IS  受−施術和暦年月
000530                                                          受−患者コード
000540                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000550                                                          受−患者カナ
000560                                                          受−患者コード
000570                             ALTERNATE RECORD KEY     IS  受−患者コード
000580                                                          受−施術和暦年月
000590                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000600                                                          受−保険種別
000610                                                          受−保険者番号
000620                                                          受−患者コード
000630                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000640                                                          受−公費種別
000650                                                          受−費用負担者番号
000660                                                          受−患者コード
000670                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000680                                                          受−助成種別
000690                                                          受−費用負担者番号助成
000700                                                          受−患者コード
000710                             ALTERNATE RECORD KEY     IS  受−請求和暦年月
000720                                                          受−施術和暦年月
000730                                                          受−患者コード
000740                             FILE STATUS              IS  状態キー
000750                             LOCK        MODE         IS  AUTOMATIC.
000751*
000752     SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
000753                             ORGANIZATION             IS  INDEXED
000754                             ACCESS MODE              IS  DYNAMIC
000755                             RECORD KEY               IS  レセ−施術和暦年月
000756                                                          レセ−患者コード
000757                                                          レセ−レセ種別
000758                             ALTERNATE RECORD KEY     IS  レセ−患者コード
000759                                                          レセ−施術和暦年月
000760                                                          レセ−レセ種別
000761                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000762                                                          レセ−施術和暦年月
000763                                                          レセ−患者コード
000764                                                          レセ−レセ種別
000765                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000766                                                          レセ−レセ種別
000767                                                          レセ−請求保険者番号
000768                                                          レセ−患者コード
000769                                                          レセ−施術和暦年月
000770                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000771                                                          レセ−請求保険者番号
000772                                                          レセ−患者コード
000773                                                          レセ−レセ種別
000774                                                          レセ−施術和暦年月
000775                             FILE STATUS              IS  状態キー
000776                             LOCK        MODE         IS  AUTOMATIC.
000950*
000420     SELECT  市町村マスタ    ASSIGN      TO        SITYOSNL
000430                             ORGANIZATION             IS  INDEXED
000440                             ACCESS MODE              IS  DYNAMIC
000450                             RECORD KEY               IS  市−公費種別
000460                                                          市−市町村番号
000470                             ALTERNATE RECORD KEY     IS  市−公費種別
000480                                                          市−市町村名称
000490                                                          市−市町村番号
000500                             FILE STATUS              IS  状態キー
000510                             LOCK        MODE         IS  AUTOMATIC.
000951     SELECT  作業ファイル１  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5801L.DAT"
000960                             ORGANIZATION             IS  INDEXED
000970                             ACCESS                   IS  DYNAMIC
000905                             RECORD      KEY          IS  作１−請求和暦年月
000907                                                          作１−分類コード
                                                                作１−県コード
                                                                作１−保険順
000908                                                          作１−分類保険者番号
000909                                                          作１−本人家族キー
                                                                作１−負担割合
000910                                                          作１−患者カナ
000911                                                          作１−患者コード
000912                                                          作１−施術和暦年月
001050                             FILE        STATUS       IS  状態キー
001060                             LOCK        MODE         IS  AUTOMATIC.
001070*
001080* レセ並び順用
001090     SELECT  作業ファイル２  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5912L.DAT"
001100                             ORGANIZATION             IS  INDEXED
001110                             ACCESS                   IS  DYNAMIC
001120                             RECORD      KEY          IS  作２−施術和暦年月
001130                                                          作２−患者コード
001140                                                          作２−保険種別
001150                             FILE        STATUS       IS  状態キー
001160                             LOCK        MODE         IS  AUTOMATIC.
001170******************************************************************
001180*                      DATA DIVISION                             *
001190******************************************************************
001200 DATA                    DIVISION.
001210 FILE                    SECTION.
001310*                           ［ＲＬ＝  ３２０］
001320 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001330     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
001400*
001410*                          ［ＲＬ＝  １５３６］
001411 FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS.
001412     COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
000721*                           ［ＲＬ＝  ２５６］
000722 FD  市町村マスタ          BLOCK   CONTAINS   1   RECORDS.
000723     COPY SITYOSN        OF  XFDLIB  JOINING   市   AS  PREFIX.
001413*
001420 FD  作業ファイル１ RECORD  CONTAINS 256 CHARACTERS.
001430 01  作１−レコード.
001440     03  作１−レコードキー.
001450         05  作１−請求和暦年月.
001460             07  作１−請求和暦            PIC 9.
001470             07  作１−請求年              PIC 9(2).
001480             07  作１−請求月              PIC 9(2).
001261         05  作１−分類コード              PIC 9(2).
001261         05  作１−県コード                PIC X(2).
001400         05  作１−保険順                  PIC 9(2).
001271         05  作１−分類保険者番号          PIC X(8).
001280         05  作１−本人家族キー            PIC 9.
               05  作１−負担割合                PIC 9(2).
001300         05  作１−患者カナ                PIC X(50).
001310         05  作１−患者コード.
001320             07 作１−患者番号             PIC 9(6).
001330             07 作１−枝番                 PIC X(1).
001340         05  作１−施術和暦年月.
001350             07  作１−施術和暦            PIC 9.
001360             07  作１−施術年              PIC 9(2).
001370             07  作１−施術月              PIC 9(2).
001600     03  作１−レコードデータ.
001271         05  作１−保険者番号              PIC X(8).
001610         05  作１−患者氏名                PIC X(50).
001620         05  作１−被保険者氏名            PIC X(50).
001630         05  作１−本人家族区分            PIC 9.
001640         05  作１−費用額                  PIC 9(7).
001650         05  作１−負担額                  PIC 9(7).
001660         05  作１−請求額                  PIC 9(7).
001670         05  作１−保険種別                PIC 9(2).
001680         05  作１−親保険種別              PIC 9(2).
001690         05  作１−老人レコード区分        PIC 9.
001700         05  作１−助成レコード区分        PIC 9.
001710         05  FILLER                        PIC X(36).
001720*
001730 FD  作業ファイル２ RECORD  CONTAINS 32 CHARACTERS.
001740 01  作２−レコード.
001750     03  作２−レコードキー.
001760         05  作２−施術和暦年月.
001770             07  作２−施術和暦            PIC 9.
001780             07  作２−施術年              PIC 9(2).
001790             07  作２−施術月              PIC 9(2).
001800         05  作２−患者コード.
001810             07 作２−患者番号             PIC 9(6).
001820             07 作２−枝番                 PIC X(1).
001830         05  作２−保険種別                PIC 9(2).
001840     03  作２−レコードデータ.
001850         05  作２−順番                    PIC 9(4).
001860         05  FILLER                        PIC X(14).
001870*
001880*----------------------------------------------------------------*
001890******************************************************************
001900*                WORKING-STORAGE SECTION                         *
001910******************************************************************
001920 WORKING-STORAGE         SECTION.
001930 01 キー入力                           PIC X    VALUE SPACE.
001940 01 状態キー                           PIC X(2) VALUE SPACE.
001950 01 終了フラグ                         PIC X(3) VALUE SPACE.
001960 01 終了フラグ２                       PIC X(3) VALUE SPACE.
001970 01 実行キーＷ                         PIC X(3)  VALUE SPACE.
001980 01 施術記録有Ｗ                       PIC X(3) VALUE SPACE.
001990 01 カウンタ                           PIC 9(2)  VALUE ZERO.
001781 01 助成レコードフラグ                 PIC X(3) VALUE SPACE.
001770 01 ラベルフラグ                       PIC 9    VALUE ZERO.
002000**
001740 01 分類保険者番号Ｗ                   PIC X(8) VALUE SPACE.
002010 01 保険種別ＷＲ                       PIC 9(2) VALUE ZERO.
002020 01 印刷形式ＷＲ                       PIC 9    VALUE ZERO.
002030 01 保険者番号ＷＲ                     PIC X(10) VALUE SPACE.
002040 01 レセプト種類ＷＲ                   PIC X(4) VALUE SPACE.
002050 01 本人家族区分ＷＲ                   PIC 9    VALUE ZERO.
002060 01 施術和暦年月ＷＲ.
002070    03 施術和暦ＷＲ                    PIC 9    VALUE ZERO.
002080    03 施術年ＷＲ                      PIC 9(2) VALUE ZERO.
002090    03 施術月ＷＲ                      PIC 9(2) VALUE ZERO.
002100 01 請求和暦年月ＷＲ.
002110    03 請求和暦ＷＲ                    PIC 9    VALUE ZERO.
002120    03 請求年ＷＲ                      PIC 9(2) VALUE ZERO.
002130    03 請求月ＷＲ                      PIC 9(2) VALUE ZERO.
002140**
002150 01 順番Ｗ                             PIC 9(4) VALUE ZERO.
002160**
002170 01 ファイル名                         PIC N(8) VALUE SPACE.
002180 01 保険種別Ｗ                         PIC 9(2) VALUE ZERO.
002190*
002200** 合計集計用
002210 01 合計Ｗ.
002220    03 件数合計Ｗ                      PIC 9(4) VALUE ZERO.
002230    03 費用額合計Ｗ                    PIC 9(8) VALUE ZERO.
002240    03 請求額合計Ｗ                    PIC 9(8) VALUE ZERO.
002250** エラーメッセージ用
002260 01 エラーメッセージＷ.
002270    03 エラー患者コードＷ              PIC X(7) VALUE SPACE.
002280    03 エラー区切りＷ                  PIC X(1) VALUE SPACE.
002290    03 エラー保険種別Ｗ                PIC X(2) VALUE SPACE.
002300    03 FILLER                          PIC X(10) VALUE SPACE.
002310*
002320**
002321 01 複合プログラム名Ｗ                 PIC X(8) VALUE "MOJI2".
002322*
002323** 保険者番号右詰め用
002330 01 保険者番号ＷＴ.
002340    03 保険者番号左詰めＷ.
002350      05 保険者番号左詰めＷ１          PIC X OCCURS 8 VALUE SPACE.
002360    03 保険者番号右詰めＷ.
002370      05 保険者番号右詰めＷ１          PIC X OCCURS 8 VALUE ZERO.
002380    03 保険者番号数字Ｗ                PIC 9(8)  VALUE ZERO.
002390    03 保険者番号Ｗ                    PIC X(8)  VALUE SPACE.
002400**
002410 01 計算機西暦年Ｗ                     PIC 9(2).
002420* 日付ＷＯＲＫ
002430 01 計算機西暦.
002440    03 計算機西暦年                    PIC 9(4).
002450    03 計算機西暦月日                  PIC 9(4).
002460 01 計算機西暦Ｒ REDEFINES 計算機西暦.
002470    03 計算機世紀                      PIC 9(2).
002480    03 計算機日付                      PIC 9(6).
002490    03 計算機日付Ｒ REDEFINES 計算機日付.
002500       05 計算機年月                   PIC 9(4).
002510       05 計算機年月Ｒ REDEFINES 計算機年月.
002520         07 計算機年                   PIC 9(2).
002530         07 計算機月                   PIC 9(2).
002540       05 計算機日                     PIC 9(2).
002550*
002560** 助成レセまとめ用
002570 01 助成レセまとめフラグ               PIC X(3)  VALUE SPACE.
002580*
002590******************************************************************
002600*                          連結項目                              *
002610******************************************************************
002620*
002630********************
002640* メッセージ表示キー *
002650********************
002660 01 連メ３−キー IS EXTERNAL.
002670    03  連メ３−メッセージ             PIC N(20).
002680    03  連メ３−メッセージ１           PIC X(20).
002690***
002700 01 連入−画面情報ＹＡＳ５８０ IS EXTERNAL.
002710    03 連入−請求和暦年月.
002720       05 連入−請求和暦               PIC 9.
002730       05 連入−請求年月.
002740         07 連入−請求年               PIC 9(2).
002750         07 連入−請求月               PIC 9(2).
003080*
003090************************
003100* 助成レセまとめ
003110************************
003120 01 連レセまとめ−キー IS EXTERNAL.
003130    03 連レセまとめ−施術和暦年月.
003140       05 連レセまとめ−施術和暦               PIC 9.
003150       05 連レセまとめ−施術年月.
003160          07 連レセまとめ−施術年              PIC 9(2).
003170          07 連レセまとめ−施術月              PIC 9(2).
003180    03 連レセまとめ−患者コード.
003190       05 連レセまとめ−患者番号               PIC 9(6).
003200       05 連レセまとめ−枝番                   PIC X(1).
003210**-------------------------------------------------------**
003220*   1:助成レセプトなしの本体まとめの判定
003230*   2:横浜・川崎用の社保助成レセかの判定
003240    03 連レセまとめ−判定区分                  PIC 9.
003250**-------------------------------------------------------**
003260*  / OUT /　 0:対象外、1:対象
003270    03 連レセまとめ−判定結果                  PIC 9.
003280**
003290**
003291* 暗号複合用
003292 01 連暗号複合−暗号情報 IS EXTERNAL.
003293    03 連暗号複合−入力情報.
003294       05 連暗号複合−記号               PIC X(24).
003295       05 連暗号複合−番号               PIC X(30).
003296       05 連暗号複合−暗号化項目.
003297          07 連暗号複合−暗号患者番号    PIC X(6).
003298          07 連暗号複合−暗号判定記号    PIC X.
003299          07 連暗号複合−暗号判定番号    PIC X.
003300          07 連暗号複合−暗号記号        PIC X(24).
003301          07 連暗号複合−暗号番号        PIC X(30).
003302    03 連暗号複合−出力情報.
003303       05 連暗号複合−複合した記号       PIC X(24).
003304       05 連暗号複合−複合した番号       PIC X(30).
003305*
003306******************************************************************
003310*                      PROCEDURE  DIVISION                       *
003320******************************************************************
003330 PROCEDURE               DIVISION.
003340************
003350*           *
003360* 初期処理   *
003370*           *
003380************
003390     PERFORM 初期化.
003400************
003410*           *
003420* 主処理     *
003430*           *
003440************
003450     PERFORM 作業ファイル作成.
003460************
003470*           *
003480* 終了処理   *
003490*           *
003500************
003510     PERFORM 終了処理.
003520     MOVE ZERO TO PROGRAM-STATUS.
003530     EXIT PROGRAM.
003540*
003550*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003560*================================================================*
003570 初期化 SECTION.
003580*================================================================*
003590*
003600     PERFORM ファイルオープン.
003610* 連結項目の待避
003620     MOVE 連入−請求和暦      TO 請求和暦ＷＲ.
003630     MOVE 連入−請求年        TO 請求年ＷＲ.
003640     MOVE 連入−請求月        TO 請求月ＷＲ.
003650*
003660*================================================================*
003670 ファイルオープン SECTION.
003680*
003780     OPEN INPUT 受診者情報Ｆ.
003790         MOVE NC"受診者情報Ｆ" TO ファイル名.
003800         PERFORM オープンチェック.
003810     OPEN INPUT レセプトＦ.
003820         MOVE NC"レセプトＦ" TO ファイル名.
003830         PERFORM オープンチェック.
002571     OPEN INPUT 市町村マスタ
002572         MOVE NC"市町村" TO ファイル名.
002573         PERFORM オープンチェック.
003870*================================================================*
003880 オープンチェック SECTION.
003890*
003900     IF 状態キー  NOT =  "00"
003910         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
003920         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003930         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003940                                                 UPON CONS
003950         ACCEPT  キー入力 FROM CONS
003960         PERFORM ファイル閉鎖
003970         MOVE 99 TO PROGRAM-STATUS
003980         EXIT PROGRAM.
003990*================================================================*
004000 終了処理 SECTION.
004010*================================================================*
004020*
004030     PERFORM ファイル閉鎖.
004040*================================================================*
004050 ファイル閉鎖 SECTION.
004060*
004070     CLOSE 受診者情報Ｆ レセプトＦ 市町村マスタ.
004090*================================================================*
004100 作業ファイル作成 SECTION.
004110*================================================================*
004120*
004130     OPEN OUTPUT 作業ファイル１.
004140         MOVE NC"作１" TO ファイル名.
004150         PERFORM オープンチェック.
004160*
004170     PERFORM 作業ファイル１作成.
004180*
004190     CLOSE 作業ファイル１.
004200*
004210     OPEN OUTPUT 作業ファイル２.
004220         MOVE NC"作２" TO ファイル名.
004230         PERFORM オープンチェック.
004240     OPEN INPUT  作業ファイル１.
004250         MOVE NC"作１" TO ファイル名.
004260         PERFORM オープンチェック.
004270*
004280* 並び順用ファイル作成
004290     PERFORM 作業ファイル２作成.
004300*
004310     CLOSE 作業ファイル１ 作業ファイル２.
004320*
004330*================================================================*
004340*================================================================*
004350 作業ファイル１作成 SECTION.
004360**********************************************************************
004370**   受診者情報Ｆから、該当請求年月のデータを抽出し、
004380**   作業ファイル１に書き出す.
004390**********************************************************************
004400*
004620     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
004621     MOVE 請求年ＷＲ    TO レセ−請求年.
004622     MOVE 請求月ＷＲ    TO レセ−請求月.
004623     MOVE 1             TO レセ−施術和暦.
004624     MOVE ZERO          TO レセ−施術年.
004625     MOVE ZERO          TO レセ−施術月.
004626     MOVE ZERO          TO レセ−患者番号.
004627     MOVE LOW-VALUE     TO レセ−枝番.
004628     MOVE ZERO          TO レセ−レセ種別.
004629     START レセプトＦ   KEY IS >= レセ−請求和暦年月
004630                                  レセ−施術和暦年月
004631                                  レセ−患者コード
004632                                  レセ−レセ種別
004633     END-START.
004634     IF 状態キー = "00"
004635         MOVE SPACE  TO 終了フラグ
004636         PERFORM レセプトＦ読込
004637         PERFORM UNTIL ( 終了フラグ = "YES" ) OR
004638                       ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
004639                       ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
004640                       ( レセ−請求月   NOT = 請求月ＷＲ   )
004641*          データチェックで受診者Ｆも読む
004642           PERFORM データチェック
004644*
004645** 労災・自賠責・自由は対象外
004646           IF  受−保険種別 = 70 OR 80 OR 90
004650               MOVE SPACE  TO 実行キーＷ
004660           END-IF
004670*
004680*↓* 特別処理（助成レセまとめ）/101109
004690           IF 受−助成種別 NOT = ZERO
004700               PERFORM 助成レセまとめ判定
004710           ELSE
004720               MOVE SPACE TO 助成レセまとめフラグ
004730           END-IF
004740*↑*
004750           IF  実行キーＷ = "YES"
004801                MOVE SPACE TO 助成レコードフラグ
004760
004761*           ********
004762*           * 健保 *
004763*           ********
004764              IF レセ−レセ種別   = 1
004765*             **********************
004766*             * 作業ファイル１作成 *
004767*             **********************
004768*   / 助成なし・助成あり 親ﾚｺｰﾄﾞ /
004769                 MOVE SPACE TO 作１−レコード
004770                 INITIALIZE 作１−レコード
004771                 MOVE 受−保険種別       TO 作１−保険種別
004772                 MOVE 受−保険者番号     TO 作１−保険者番号
004773                 MOVE 受−本人家族区分   TO 作１−本人家族キー
004774                 MOVE レセ−合計         TO 作１−費用額
004775                 MOVE レセ−一部負担金   TO 作１−負担額
004776                 MOVE レセ−請求金額     TO 作１−請求額
004778                 PERFORM 作１レコードセット
004779                 PERFORM 作１ファイル書込
004780              END-IF
004781*           ********
004782*           * 老人 *
004783*           ********
004784              IF レセ−レセ種別   = 2
004785*             **********************
004786*             * 作業ファイル１作成 *
004787*             **********************
004788                 MOVE SPACE TO 作１−レコード
004789                 INITIALIZE 作１−レコード
004790                 MOVE 受−公費種別        TO 作１−保険種別
004791                 MOVE 受−費用負担者番号  TO 作１−保険者番号
004792                 MOVE  1                  TO 作１−本人家族キー
004793                 MOVE  1                  TO 作１−老人レコード区分
004794                 MOVE レセ−合計          TO 作１−費用額
004795                 MOVE レセ−一部負担金    TO 作１−負担額
004796                 MOVE レセ−請求金額      TO 作１−請求額
004798                 PERFORM 作１レコードセット
004799                 PERFORM 作１ファイル書込
004800              END-IF
004801*           ********
004802*           * 助成 *
004803*           ********
004804              IF レセ−レセ種別   = 3
004844                 MOVE "YES" TO 助成レコードフラグ
004805                 IF ( 助成レセまとめフラグ = SPACE )
005930*         / 助成の請求額０は対象外にする /170208
005930*         / 大阪の助成の請求額０は対象にする /170405
006880                    IF (レセ−助成請求金額 NOT = ZERO) OR
                             (受−費用負担者番号助成(3:2) = "27")
004806                       MOVE SPACE TO 作１−レコード
004807                       INITIALIZE 作１−レコード
004808                       MOVE 受−助成種別            TO 作１−保険種別
004809                       MOVE 受−公費種別            TO 作１−親保険種別
004810                       MOVE 受−費用負担者番号助成  TO 作１−保険者番号
004811                       MOVE  1                      TO 作１−本人家族キー
004812                       MOVE  1                      TO 作１−助成レコード区分
004813*                    後期高齢の場合費用額を強制的にゼロセット。
004814*                    本来はちゃんと金額をセットすべきだが会提出データに合わせるための処置
004815                       MOVE ZERO                    TO 作１−費用額
004816                       MOVE レセ−受給者負担額      TO 作１−負担額
004817                       MOVE レセ−助成請求金額      TO 作１−請求額
004819                       PERFORM 作１レコードセット
004820                       PERFORM 作１ファイル書込
004821                    END-IF
004821                 END-IF
004822              END-IF
004823**
004824           END-IF
004825           PERFORM レセプトＦ読込
004826        END-PERFORM
004827     END-IF.
005760**
006370*================================================================*
006371 データチェック SECTION.
006372*
004070     MOVE SPACE  TO 実行キーＷ.
005300     IF ( レセ−レセ印刷対象区分 NOT = 1 )
005310        MOVE "YES"  TO 実行キーＷ
005320     END-IF.
      */滋賀県は総括表対象区分を見る/191029
           IF ( レセ−請求保険者番号(3:2) = "25" ) AND
              ( レセ−会総括表印刷対象区分 NOT = 1 )
005310        MOVE "YES"  TO 実行キーＷ
005320     END-IF.
019520* *****************************************************************
019530* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
019540* *****************************************************************
005360     IF 実行キーＷ  = "YES"
005370*      (再度、実行キーＷ SPACE)
005380        MOVE SPACE  TO 実行キーＷ
019640        IF レセ−請求対象区分 NOT = ZERO
004090           MOVE レセ−施術和暦  TO 受−施術和暦
004100           MOVE レセ−施術年    TO 受−施術年
004110           MOVE レセ−施術月    TO 受−施術月
004120           MOVE レセ−患者番号  TO 受−患者番号
004130           MOVE レセ−枝番      TO 受−枝番
                 READ 受診者情報Ｆ
                 NOT INVALID KEY
019880              MOVE "YES"  TO 実行キーＷ
                 END-READ
019900        ELSE
019910           MOVE SPACE  TO 実行キーＷ
              END-IF
019950     END-IF.
006396*
006397*================================================================*
006398 レセプトＦ読込 SECTION.
006399*
006400     READ レセプトＦ NEXT
006401     AT END
006402         MOVE "YES" TO 終了フラグ
006403     END-READ.
006404*
006710*================================================================*
006720 作１レコードセット SECTION.
006730*================================================================*
006740*
006750     MOVE レセ−請求和暦     TO 作１−請求和暦.
006760     MOVE レセ−請求年       TO 作１−請求年.
006770     MOVE レセ−請求月       TO 作１−請求月.
006780     MOVE 受−施術和暦       TO 作１−施術和暦.
006790     MOVE 受−施術年         TO 作１−施術年.
006800     MOVE 受−施術月         TO 作１−施術月.
005480     MOVE レセ−負担割合     TO 作１−負担割合.
006810     MOVE 受−患者カナ       TO 作１−患者カナ.
006820     MOVE 受−患者氏名       TO 作１−患者氏名.
006830     MOVE 受−患者コード     TO 作１−患者コード.
006840     MOVE 受−被保険者氏名   TO 作１−被保険者氏名.
006850     MOVE 受−本人家族区分   TO 作１−本人家族区分.
      *
      */助成でラベル番号がある場合は国保連に入れる/170510
      */組合で北海道、青森、岩手、宮城、愛媛、鹿児島は協会健保に入れる/170510
      */和歌山の国保で助成がある時は一部負担金を入れる/170510
      */組合退職者は元の組合番号に入れる/170822
      */鹿児島銀行(06460067)、南日本銀行(06460075)、鹿児島信金(06460121)は組合に入れる/210518
           IF 助成レコードフラグ  = "YES"
               PERFORM 市町村情報取得
005542         PERFORM 保険区分取得
               MOVE 分類保険者番号Ｗ        TO 作１−分類保険者番号
005517         MOVE 受−費用負担者番号助成  TO 作１−保険者番号
005524     ELSE
005526         IF 受−公費種別 = ZERO
005531             MOVE 受−保険者番号      TO 作１−保険者番号
                   IF 受−保険種別 = 03
                     IF 受−保険者番号 = "06010672" OR "06040174" OR "06141469" OR "06160329" OR
                                         "06200364" OR "06220834" OR "06231062" OR "06271654" OR 
                                         "06281059" OR "06340319" OR "06380257" OR "06400782"
                         MOVE "06132427" TO 作１−分類保険者番号
                     ELSE
                         EVALUATE 受−保険者番号(1:4)
                         WHEN "0601"
                             MOVE "01010016"  TO 作１−分類保険者番号
                         WHEN "0602"
                             MOVE "01020015"  TO 作１−分類保険者番号
                         WHEN "0603"
                             MOVE "01030014"  TO 作１−分類保険者番号
                         WHEN "0604"
                             MOVE "01040013"  TO 作１−分類保険者番号
                         WHEN "0638"
                             MOVE "01380013"  TO 作１−分類保険者番号
                         WHEN "0646"
                             IF (受−保険者番号 NOT = "06460067" AND "06460075" AND "06460125")
                                MOVE "01460013"  TO 作１−分類保険者番号
                             ELSE
005531                          MOVE 受−保険者番号  TO 作１−分類保険者番号
                             END-IF
                         WHEN OTHER
005531                       MOVE 受−保険者番号  TO 作１−分類保険者番号
                         END-EVALUATE
                         IF 受−保険者番号(1:2) = "63"
                             MOVE "06"            TO 作１−分類保険者番号(1:2)
                         END-IF
                     END-IF
                   ELSE
005531                 MOVE 受−保険者番号  TO 作１−分類保険者番号
                   END-IF
005533         ELSE
005534             MOVE 1                   TO 作１−本人家族区分
005538             MOVE 受−費用負担者番号  TO 作１−分類保険者番号 作１−保険者番号
005540         END-IF
005541*
005542         PERFORM 保険区分取得
005543*
005545     END-IF.
005170*
005705*================================================================*
005706 市町村情報取得 SECTION.
005780*
004045     MOVE 受−助成種別            TO 市−公費種別.
004046     MOVE 受−費用負担者番号助成  TO 市−市町村番号.
004048     READ 市町村マスタ
004049     INVALID KEY
               MOVE ZERO                TO ラベルフラグ
004050         MOVE SPACE               TO 分類保険者番号Ｗ
004051     NOT INVALID KEY
               IF 市−ラベル番号 = SPACE
                   MOVE ZERO                    TO ラベルフラグ
004050             MOVE 受−費用負担者番号助成  TO 分類保険者番号Ｗ
               ELSE
                   MOVE 1               TO ラベルフラグ
004050             MOVE 市−保険者番号  TO 分類保険者番号Ｗ
               END-IF
004064     END-READ.
      *
006870*================================================================*
006880 保険区分取得 SECTION.
006890*================================================================*
005707***************************************************
005708*/ 船員を分ける/170925
      *
005708* 分類コード
005709*  10:社保・日雇
      *  10:船員
005710*  30:組合
      *  40:共済・自衛官
005711*  50:国保・国保組合・退職・後高
005712*  60:全国歯科医師、兵庫県医師
005712*  70:原爆
005712*  80:原爆以外の助成
005712*  90:生保
005716*
005718***************************************************
      */助成で親保険が国保でラベル番号がある場合は国保連に入れる/170510
      */組合で北海道、青森、岩手、宮城、愛媛、鹿児島は協会健保に入れる/170510
      */全国歯科医師、兵庫県医師は国保連の後に入れる/170510
      */原爆は別の括りにする/170510
      */大阪は後高の次に後高助成を入れる/180927
      */大阪の助成は国保連に入れる/200317
      */全国土木建設、全国建設工事の神奈川の助成（直送）は国保連の後に入れる/200317
      */鹿児島銀行(06460067)、南日本銀行(06460075)、鹿児島信金(06460121)は組合に入れる/210518
005719*
005720     IF 助成レコードフラグ  = "YES"
005726         MOVE  80 TO  作１−分類コード
      *         IF (受−保険種別 = 01 OR 08 OR 05) AND (ラベルフラグ = 1)
               IF (ラベルフラグ = 1)
005726             MOVE  50 TO  作１−分類コード
005743             MOVE  4  TO  作１−保険順
005742             MOVE 受−費用負担者番号助成(3:2) TO 作１−県コード
               END-IF
               IF 受−助成種別 = 54
005726             MOVE  70 TO  作１−分類コード
               END-IF
               IF 受−費用負担者番号助成(3:2) = "27"
005726             MOVE  50 TO  作１−分類コード
005731             IF 受−保険種別 = 05
                       EVALUATE 受−助成種別
010760                 WHEN 54
010770                     MOVE 2     TO 作１−保険順
010780                 WHEN 51
010790                     MOVE 3     TO 作１−保険順
010800                 WHEN 53
010810                     MOVE 4     TO 作１−保険順
010820                 WHEN 52
010830                     MOVE 5     TO 作１−保険順
010840                 WHEN 55
010850                     MOVE 6     TO 作１−保険順
010840                 WHEN OTHER
010850                     MOVE 7     TO 作１−保険順
                       END-EVALUATE
                   ELSE
                       EVALUATE 受−助成種別
010760                 WHEN 54
010770                     MOVE 10     TO 作１−保険順
010780                 WHEN 51
010790                     MOVE 11     TO 作１−保険順
010800                 WHEN 53
010810                     MOVE 12     TO 作１−保険順
010820                 WHEN 52
010830                     MOVE 13     TO 作１−保険順
010840                 WHEN 55
010850                     MOVE 14     TO 作１−保険順
010840                 WHEN OTHER
010850                     MOVE 15     TO 作１−保険順
                       END-EVALUATE
                   END-IF
               END-IF
               IF (受−保険者番号 = "133033" OR "133298") AND
                   (受−費用負担者番号助成(3:2) = "14")
005743             MOVE 80       TO 作１−分類コード
010850             MOVE ZERO     TO 作１−保険順
               END-IF
005723*         EVALUATE 受−助成種別
      *         WHEN 51
005724*             MOVE  9 TO  作１−分類コード
005725*         WHEN 52
005726*             MOVE 10 TO  作１−分類コード
005725*         WHEN 53
005726*             MOVE 12 TO  作１−分類コード
005725*         WHEN 54
005726*             MOVE  8 TO  作１−分類コード
005725*         WHEN 55
005726*             MOVE 11 TO  作１−分類コード
005727*         END-EVALUATE
005728     ELSE
005729         IF 受−公費種別 = ZERO
005730*       / 健保 /
005731             EVALUATE 受−保険種別
005741             WHEN 01
                       IF 受−保険者番号 = "093013" OR "283077"
005743                     MOVE 60 TO  作１−分類コード
                       ELSE
005743                     MOVE 50 TO  作１−分類コード
005743                     MOVE 1  TO  作１−保険順
                           IF 受−保険者番号(1:2) = "27"
                               MOVE 8 TO 作１−保険順
                           END-IF
005742                     MOVE 受−保険者番号(1:2) TO 作１−県コード
                       END-IF
005732             WHEN 02
005733             WHEN 06
005736                 MOVE 10 TO  作１−分類コード
005743                 MOVE 1  TO  作１−保険順
005742                 MOVE 受−保険者番号(3:2) TO 作１−県コード
005734             WHEN 07
005736                 MOVE 10 TO  作１−分類コード
005743                 MOVE 1  TO  作１−保険順
005742                 MOVE 受−保険者番号(3:2) TO 作１−県コード
005737             WHEN 03
005740                 MOVE 30 TO  作１−分類コード
                       IF (受−保険者番号(1:4) = "0601" OR "0602" OR "0603" OR "0604" OR
                                                 "0638" OR "0646" ) AND
                          (受−保険者番号 NOT = "06460067" AND "06460075" AND "06460125")
005736                     MOVE 10 TO  作１−分類コード
005743                     MOVE 2  TO  作１−保険順
005742                     MOVE 受−保険者番号(3:2) TO 作１−県コード
                       END-IF
005738             WHEN 04
005739             WHEN 09
005740                 MOVE 40 TO  作１−分類コード
005753             WHEN 08
005755                 MOVE 50 TO  作１−分類コード
005743                 MOVE 2  TO  作１−保険順
                       IF 受−保険者番号(3:2) = "27"
                           MOVE 9 TO 作１−保険順
                       END-IF
005742                 MOVE 受−保険者番号(3:2) TO 作１−県コード
005500* 生保
005510             WHEN 85
005755                 MOVE 90 TO  作１−分類コード
005765             END-EVALUATE
005766*
005767         ELSE
005768*       / 老人 /
005770             MOVE 50 TO  作１−分類コード
005743             MOVE 3  TO  作１−保険順
                   IF 受−費用負担者番号(3:2) = "27"
                       MOVE 1 TO 作１−保険順
                   END-IF
005742             MOVE 受−費用負担者番号(3:2) TO 作１−県コード
005778         END-IF
005779     END-IF.
      *
007660*================================================================*
007670 作１ファイル書込 SECTION.
007680*================================================================*
007690*
007700     WRITE 作１−レコード.
007710     IF 状態キー  NOT =  "00"
007720         MOVE NC"作１"  TO ファイル名
007730         PERFORM エラー表示
007740     END-IF.
007750*
007760*================================================================*
007770*================================================================*
007780 作業ファイル２作成 SECTION.
007790*
007800* 並び順作成
007810     MOVE SPACE TO 終了フラグ.
007820     MOVE ZERO  TO 順番Ｗ.
007830     PERFORM 作業ファイル１読込.
007840     PERFORM UNTIL 終了フラグ = "YES"
007850             PERFORM 作２レコードセット
007860             PERFORM 作２ファイル書込
007870             PERFORM 作業ファイル１読込
007880     END-PERFORM.
007890
007900*================================================================*
007910 作２レコードセット SECTION.
007920*
007930     MOVE 作１−施術和暦       TO 作２−施術和暦.
007940     MOVE 作１−施術年         TO 作２−施術年.
007950     MOVE 作１−施術月         TO 作２−施術月.
007960     MOVE 作１−患者コード     TO 作２−患者コード.
007970     MOVE 作１−保険種別       TO 作２−保険種別.
007980     COMPUTE 順番Ｗ  = 順番Ｗ + 1.
007990     MOVE 順番Ｗ               TO 作２−順番.
008000*
008010*================================================================*
008020 作２ファイル書込 SECTION.
008030*
008040     WRITE 作２−レコード
008050     INVALID KEY
008060         MOVE NC"作２"  TO ファイル名
008070         PERFORM エラー表示
008080     END-WRITE.
008090*================================================================*
008100 作業ファイル１読込 SECTION.
008110*
008120     READ 作業ファイル１ NEXT
008130     AT END
008140         MOVE "YES" TO 終了フラグ
008150     END-READ.
008160*
008170*================================================================*
008180*================================================================*
008190 エラー表示 SECTION.
008200*
008210     DISPLAY NC"状態キー" 状態キー  UPON CONS.
008220     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
008230     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
008240     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
008250     ACCEPT  キー入力 FROM CONS.
008260     PERFORM ファイル閉鎖.
008270     MOVE 99 TO PROGRAM-STATUS.
008280     EXIT PROGRAM.
008290*================================================================*
008300 エラー表示Ｒ SECTION.
008310*
008320     DISPLAY NC"ファイル読込エラー" ファイル名     UPON CONS.
008330     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
008340     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
008350     ACCEPT  キー入力 FROM CONS.
008360     PERFORM ファイル閉鎖.
008370     MOVE 99 TO PROGRAM-STATUS.
008380     EXIT PROGRAM.
008790*================================================================*
008791 助成レセまとめ判定 SECTION.
008792*---------------------------------------------------------------------------*
008793* 市町村マスタを読み、レセまとめ区分＝１でかつ、本体保険が国保・退職
008794* の時は、フラグYES (金額を助成込みで印字）
008795*（例：横浜市の障害は、本体保険（国保系）のレセプト１枚で請求、助成レセはなし）
008796*---------------------------------------------------------------------------*
008797*
008798     MOVE SPACE TO 助成レセまとめフラグ.
008799*
008800     IF レセ−本体まとめ区分 = 1
008801        MOVE "YES" TO 助成レセまとめフラグ
008802     END-IF.
008803*
009060******************************************************************
009070 END PROGRAM YAS5811.
009080******************************************************************
