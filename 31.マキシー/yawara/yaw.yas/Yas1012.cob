000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YAS1012.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090* ＩＦ請求サポートセンター 会提出データ作成【ﾃﾞｰﾀ作成】柔+ｳｨﾝﾄﾞｳｽﾞ版
000100* 請求年月Ver.のみ
000110*      MED = YAS580
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2021-09-08
000140 DATE-COMPILED.          2021-09-08
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000420     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000430                             ORGANIZATION             IS  INDEXED
000440                             ACCESS MODE              IS  DYNAMIC
000450                             RECORD KEY               IS 受−施術和暦年月
000460                                                          受−患者コード
000470                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000480                                                          受−患者カナ
000490                                                          受−患者コード
000500                             ALTERNATE RECORD KEY     IS  受−患者コード
000510                                                         受−施術和暦年月
000520                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000530                                                          受−保険種別
000540                                                          受−保険者番号
000550                                                          受−患者コード
000560                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000570                                                          受−公費種別
000580                                                     受−費用負担者番号
000590                                                          受−患者コード
000600                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000610                                                          受−助成種別
000620                                                  受−費用負担者番号助成
000630                                                          受−患者コード
000640                             ALTERNATE RECORD KEY  IS 受−請求和暦年月
000650                                                      受−施術和暦年月
000660                                                      受−患者コード
000670                             FILE STATUS              IS  状態キー
000680                             LOCK        MODE         IS  AUTOMATIC.
           SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
                                   ORGANIZATION             IS  INDEXED
                                   ACCESS MODE              IS  DYNAMIC
                                   RECORD KEY               IS  レセ−施術和暦年月
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−患者コード
                                                                レセ−施術和暦年月
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−施術和暦年月
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−レセ種別
                                                                レセ−請求保険者番号
                                                                レセ−患者コード
                                                                レセ−施術和暦年月
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−請求保険者番号
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                                                レセ−施術和暦年月
                                   FILE STATUS              IS  状態キー
                                   LOCK        MODE         IS  AUTOMATIC.
000420     SELECT  市町村マスタ    ASSIGN      TO        SITYOSNL
000430                             ORGANIZATION             IS  INDEXED
000440                             ACCESS MODE              IS  DYNAMIC
000450                             RECORD KEY               IS  市−公費種別
000460                                                          市−市町村番号
000470                             ALTERNATE RECORD KEY     IS  市−公費種別
000480                                                          市−市町村名称
000490                                                          市−市町村番号
000500                             FILE STATUS              IS  状態キー
000510                             LOCK        MODE         IS  AUTOMATIC.
000771*
000902     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W1021L.DAT"
000903                             ORGANIZATION             IS  INDEXED
000904                             ACCESS                   IS  DYNAMIC
000905                             RECORD      KEY          IS  作１−請求和暦年月
000907                                                          作１−分類コード
                                                                作１−県コード
                                                                作１−保険順
000908                                                          作１−分類保険者番号
000909                                                          作１−本人家族区分
                                                                作１−負担割合
000910                                                          作１−患者カナ
000911                                                          作１−患者コード
000912                                                          作１−施術和暦年月
000913                             FILE        STATUS       IS  状態キー
000914                             LOCK        MODE         IS  AUTOMATIC.
000930* 並び順用
000931     SELECT  作業ファイル２  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W1022L.DAT"
000932                             ORGANIZATION             IS  INDEXED
000940                             ACCESS                   IS  DYNAMIC
000950                             RECORD      KEY          IS  作２−施術和暦年月
000960                                                          作２−患者コード
000970                                                          作２−保険種別
000971                             ALTERNATE RECORD KEY     IS  作２−順番
000980                             FILE        STATUS       IS  状態キー
000990                             LOCK        MODE         IS  AUTOMATIC.
001000*
001010******************************************************************
001020*                      DATA DIVISION                             *
001030******************************************************************
001040 DATA                    DIVISION.
001050 FILE                    SECTION.
001120*                           ［ＲＬ＝  ３２０］
001130 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001140     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
      *
       FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS GLOBAL.
           COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
000721*                           ［ＲＬ＝  ２５６］
000722 FD  市町村マスタ          BLOCK   CONTAINS   1   RECORDS.
000723     COPY SITYOSN        OF  XFDLIB  JOINING   市   AS  PREFIX.
001180*
001181****
001190 FD  作業ファイル１ RECORD  CONTAINS 128 CHARACTERS.
001200 01  作１−レコード.
001210     03  作１−レコードキー.
001220         05  作１−請求和暦年月.
001230             07  作１−請求和暦            PIC 9.
001240             07  作１−請求年              PIC 9(2).
001250             07  作１−請求月              PIC 9(2).
001261         05  作１−分類コード              PIC 9(2).
001261         05  作１−県コード                PIC X(2).
001400         05  作１−保険順                  PIC 9(2).
001271         05  作１−分類保険者番号          PIC X(8).
001280         05  作１−本人家族区分            PIC 9.
               05  作１−負担割合                PIC 9(2).
001300         05  作１−患者カナ                PIC X(50).
001310         05  作１−患者コード.
001320             07 作１−患者番号             PIC 9(6).
001330             07 作１−枝番                 PIC X(1).
001340         05  作１−施術和暦年月.
001350             07  作１−施術和暦            PIC 9.
001360             07  作１−施術年              PIC 9(2).
001370             07  作１−施術月              PIC 9(2).
001380     03  作１−レコードデータ.
001400         05  作１−保険種別                PIC 9(2).
001271         05  作１−保険者番号              PIC X(8).
               05  作１−費用額                  PIC 9(6).
               05  作１−負担額                  PIC 9(6).
               05  作１−請求額                  PIC 9(6).
001260         05  作１−返戻区分                PIC 9.
001400         05  作１−分類保険種別            PIC 9(2).
001260         05  作１−併区分                  PIC 9.
001595         05  作１−表示フラグ              PIC 9(1).
001598         05  FILLER                        PIC X(11).
001459*
001581 FD  作業ファイル２ RECORD  CONTAINS 80 CHARACTERS.
001582 01  作２−レコード.
001583     03  作２−レコードキー.
001584         05  作２−施術和暦年月.
001585             07  作２−施術和暦            PIC 9.
001586             07  作２−施術年              PIC 9(2).
001587             07  作２−施術月              PIC 9(2).
001588         05  作２−患者コード.
001589             07 作２−患者番号             PIC 9(6).
001590             07 作２−枝番                 PIC X(1).
001591         05  作２−保険種別                PIC 9(2).
001592     03  作２−レコードデータ.
001593         05  作２−返戻区分                PIC 9(1).
001594         05  作２−分類コード              PIC 9(2).
001261         05  作２−県コード                PIC X(2).
001595         05  作２−順番                    PIC 9(4).
001595         05  作２−保険種別順番            PIC 9(1).
001595         05  作２−ソート番号              PIC 9(4).
001595         05  作２−データ表示順            PIC 9(4).
001596         05  作２−本人家族区分            PIC 9(1).
001597         05  作２−保険者番号              PIC X(8).
001591         05  作２−負担割合                PIC 9(2).
               05  作２−費用額                  PIC 9(6).
               05  作２−負担額                  PIC 9(6).
               05  作２−請求額                  PIC 9(6).
001591         05  作２−分類保険種別            PIC 9(2).
001597         05  作２−分類保険者番号          PIC X(8).
001260         05  作２−併区分                  PIC 9(1).
001595         05  作２−表示フラグ              PIC 9(1).
001598         05  FILLER                        PIC X(7).
001599*
001600*----------------------------------------------------------------*
001601******************************************************************
001610*                WORKING-STORAGE SECTION                         *
001620******************************************************************
001630 WORKING-STORAGE         SECTION.
001640 01 キー入力                           PIC X    VALUE SPACE.
001650 01 状態キー                           PIC X(2) VALUE SPACE.
001660 01 終了フラグ                         PIC X(3) VALUE SPACE.
001670 01 終了フラグ２                       PIC X(3) VALUE SPACE.
001680 01 実行キーＷ                         PIC X(3) VALUE SPACE.
001690 01 施術記録有Ｗ                       PIC X(3) VALUE SPACE.
001700 01 ファイル名                         PIC N(8) VALUE SPACE.
001710 01 カウンタ                           PIC 9(2) VALUE ZERO.
001720 01 カウンタ２                         PIC 9(2) VALUE ZERO.
001730 01 対象外フラグ                       PIC X(3) VALUE SPACE.
001740 01 分類フラグ                         PIC X(3) VALUE SPACE.
001750 01 作成フラグ                         PIC X(3) VALUE SPACE.
001760 01 脱出フラグ                         PIC X(3) VALUE SPACE.
001770 01 負担割合Ｗ                         PIC 9    VALUE ZERO.
001780 01 順番Ｗ                             PIC 9(4) VALUE ZERO.
001780 01 ソート番号Ｗ                       PIC 9(4) VALUE ZERO.
001780 01 データ表示順Ｗ                     PIC 9(4) VALUE ZERO.
001780 01 総括番号Ｗ                         PIC 9(4) VALUE ZERO.
001781 01 助成レコードフラグ                 PIC X(3) VALUE SPACE.
001782 01 保険種別Ｗ                         PIC 9(2) VALUE ZERO.
001740 01 分類保険者番号Ｗ                   PIC X(8) VALUE SPACE.
001770 01 ラベルフラグ                       PIC 9    VALUE ZERO.
001790*
001800** 退避用
001801 01 返戻区分ＷＲ                       PIC 9 VALUE ZERO.
001802 01 分類コードＷＲ                     PIC 9(2) VALUE ZERO.
001220 01 県コードＷＲ                       PIC X(2) VALUE SPACE.
001597 01 保険者番号ＷＲ                     PIC X(8) VALUE SPACE.
001830 01 請求和暦年月ＷＲ.
001840    03 請求和暦ＷＲ                    PIC 9    VALUE ZERO.
001850    03 請求年ＷＲ                      PIC 9(2) VALUE ZERO.
001860    03 請求月ＷＲ                      PIC 9(2) VALUE ZERO.
001790*
001870** エラーメッセージ用
001880 01 エラーメッセージＷ.
001890    03 エラー患者コードＷ              PIC X(7) VALUE SPACE.
001900    03 エラー区切りＷ                  PIC X(1) VALUE SPACE.
001910    03 エラー保険種別Ｗ                PIC X(2) VALUE SPACE.
001920    03 FILLER                          PIC X(10) VALUE SPACE.
001930*
001940*****************************************************************
001950 01 計算機西暦年Ｗ                     PIC 9(2).
001960* 日付ＷＯＲＫ
001970 01 計算機西暦.
001980    03 計算機西暦年                    PIC 9(4).
001990    03 計算機西暦月日                  PIC 9(4).
002000 01 計算機西暦Ｒ REDEFINES 計算機西暦.
002010    03 計算機世紀                      PIC 9(2).
002020    03 計算機日付                      PIC 9(6).
002030    03 計算機日付Ｒ REDEFINES 計算機日付.
002040       05 計算機年月                   PIC 9(4).
002050       05 計算機年月Ｒ REDEFINES 計算機年月.
002060         07 計算機年                   PIC 9(2).
002070         07 計算機月                   PIC 9(2).
002080       05 計算機日                     PIC 9(2).
002090*
002100******************************************************************
002110*                          連結項目                              *
002120******************************************************************
002130*
002140********************
002150* メッセージ表示キー *
002160********************
002170 01 連メ−キー IS EXTERNAL.
002180    03  連メ−メッセージ               PIC N(20).
002190*
002200 01 連メ３−キー IS EXTERNAL.
002210    03  連メ３−メッセージ             PIC N(20).
002220    03  連メ３−メッセージ１           PIC X(20).
002230****************
002240* 画面入力情報 *
002250****************
       01 連入−画面情報ＹＡＳ５８０ IS EXTERNAL.
          03 連入−請求和暦年月.
             05 連入−請求和暦               PIC 9.
             05 連入−請求年月.
               07 連入−請求年               PIC 9(2).
               07 連入−請求月               PIC 9(2).
002320*
002660******************************************************************
002670*                      PROCEDURE  DIVISION                       *
002680******************************************************************
002690 PROCEDURE               DIVISION.
002700************
002710*           *
002720* 初期処理   *
002730*           *
002740************
002750     PERFORM 初期化.
002760************
002770*           *
002780* 主処理     *
002790*           *
002800************
002810     PERFORM 作業ファイル作成.
002820************
002830*           *
002840* 終了処理   *
002850*           *
002860************
002870     PERFORM 終了処理.
002880     MOVE ZERO TO PROGRAM-STATUS.
002890     EXIT PROGRAM.
002900*
002910*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002920*================================================================*
002930 初期化 SECTION.
002940*
002950     PERFORM ファイルオープン.
002960* 連結項目の待避
002970     MOVE 連入−請求和暦      TO 請求和暦ＷＲ.
002980     MOVE 連入−請求年        TO 請求年ＷＲ.
002990     MOVE 連入−請求月        TO 請求月ＷＲ.
003000*
003010*================================================================*
003020 ファイルオープン SECTION.
003030*
003070     OPEN INPUT 受診者情報Ｆ.
003080         MOVE NC"受診者情報Ｆ" TO ファイル名.
003090         PERFORM オープンチェック.
003250     OPEN INPUT レセプトＦ.
003260         MOVE NC"レセプトＦ" TO ファイル名.
003270         PERFORM オープンチェック.
002571     OPEN INPUT 市町村マスタ
002572         MOVE NC"市町村" TO ファイル名.
002573         PERFORM オープンチェック.
003121*
003130*================================================================*
003140 オープンチェック SECTION.
003150*
003160     IF 状態キー  NOT =  "00"
003170         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
003180         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003190         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003200                                                 UPON CONS
003131*-----------------------------------------*
003132         CALL "actcshm"  WITH C LINKAGE
003133*-----------------------------------------*
003210         ACCEPT  キー入力 FROM CONS
003220         PERFORM ファイル閉鎖
003230         MOVE 99 TO PROGRAM-STATUS
003240         EXIT PROGRAM.
003250*================================================================*
003260 ファイル閉鎖 SECTION.
003270*
003280     CLOSE 受診者情報Ｆ レセプトＦ 市町村マスタ.
003290*================================================================*
003300 終了処理 SECTION.
003310*
003320     PERFORM ファイル閉鎖.
003330*================================================================*
003340 エラー表示 SECTION.
003350*
003360     DISPLAY NC"状態キー" 状態キー  UPON CONS.
003370     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
003380     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
003390     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
003321*-----------------------------------------*
003322     CALL "actcshm"  WITH C LINKAGE.
003323*-----------------------------------------*
003400     ACCEPT  キー入力 FROM CONS.
003410     PERFORM ファイル閉鎖.
003420     MOVE 99 TO PROGRAM-STATUS.
003430     EXIT PROGRAM.
003440*================================================================*
003450 作業ファイル作成 SECTION.
003460*
003470     OPEN OUTPUT 作業ファイル１.
003480         MOVE NC"作１" TO ファイル名.
003490         PERFORM オープンチェック.
003500*
005170     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
005180     MOVE 請求年ＷＲ    TO レセ−請求年.  
005190     MOVE 請求月ＷＲ    TO レセ−請求月.  
013730     MOVE ZERO          TO レセ−レセ種別.
005200     MOVE ZERO          TO レセ−施術和暦.
005210     MOVE ZERO          TO レセ−施術年.  
005220     MOVE ZERO          TO レセ−施術月.  
005230     MOVE ZERO          TO レセ−患者番号.
005240     MOVE SPACE         TO レセ−枝番.    
013770     START レセプトＦ   KEY IS >= レセ−請求和暦年月
000230                                  レセ−施術和暦年月
000240                                  レセ−患者コード
000250                                  レセ−レセ種別
003620     END-START.
003630     IF 状態キー = "00"
003640         MOVE SPACE  TO 終了フラグ
003650         PERFORM レセプトＦ読込
003660         PERFORM UNTIL ( 終了フラグ = "YES" ) OR
005330                       ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
005340                       ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
005350                       ( レセ−請求月   NOT = 請求月ＷＲ   )
003730*
004010             PERFORM データチェック
004470** 労災・自賠責・自由・生保単独は対象外
003790             IF  レセ−レセ種別 = 4 OR 5 OR 6 OR 7 OR 8
004040                MOVE SPACE  TO 実行キーＷ
004050             END-IF
004470** 助成の請求額０円は対象外
003790             IF  (レセ−レセ種別 = 3) AND ( レセ−助成請求金額 = ZERO)
004040                MOVE SPACE  TO 実行キーＷ
004050             END-IF
004470** 大阪助成の請求額０円は対象
003790             IF  (レセ−レセ種別 = 3) AND ( レセ−助成請求金額 = ZERO) AND
                       (受−費用負担者番号助成(3:2) = "27") 
      */請求対象でない場合は対象外/20190822
                   AND (レセ−請求対象区分 NOT = ZERO)
004040                MOVE "YES"  TO 実行キーＷ
004050             END-IF
003737             IF  実行キーＷ = "YES"
003738                 PERFORM レコードセット
003739             END-IF
003850             PERFORM レセプトＦ読込
003860         END-PERFORM
003870     END-IF.
003880*
003890     CLOSE 作業ファイル１.
003900*
003910******
003920     OPEN OUTPUT 作業ファイル２.
003930         MOVE NC"作２" TO ファイル名.
003940         PERFORM オープンチェック.
003950     OPEN INPUT 作業ファイル１.
003960         MOVE NC"作１" TO ファイル名.
003970         PERFORM オープンチェック.
003980*
003990* 並び順用ファイル作成
004000     PERFORM 作業ファイル作成２.
004010*
004020     CLOSE 作業ファイル１ 作業ファイル２.
004030*
004031*****
004032*  ０件かチェック
004033     OPEN INPUT 作業ファイル２.
004034         MOVE NC"作２" TO ファイル名.
004035         PERFORM オープンチェック.
004036     READ 作業ファイル２ NEXT
004037     AT END
004039         MOVE  NC"　データがありません。確認して下さい。" TO 連メ−メッセージ
004040         CALL   "MSG001"
004041         CANCEL "MSG001"
004042     END-READ.
004043     CLOSE 作業ファイル２.
004044*
004046*================================================================*
004050 データチェック SECTION.
004060*
004070     MOVE SPACE  TO 実行キーＷ.
005300     IF ( レセ−レセ印刷対象区分 NOT = 1 )
005310        MOVE "YES"  TO 実行キーＷ
005320     END-IF.
      */滋賀県は総括表対象区分を見る/191029
           IF ( レセ−請求保険者番号(3:2) = "25" ) AND
              ( レセ−会総括表印刷対象区分 NOT = 1 )
005310        MOVE "YES"  TO 実行キーＷ
005320     END-IF.
019520* *****************************************************************
019530* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
019540* *****************************************************************
005360     IF 実行キーＷ  = "YES"
005370*      (再度、実行キーＷ SPACE)
005380        MOVE SPACE  TO 実行キーＷ
019640        IF レセ−請求対象区分 NOT = ZERO
004090           MOVE レセ−施術和暦  TO 受−施術和暦
004100           MOVE レセ−施術年    TO 受−施術年
004110           MOVE レセ−施術月    TO 受−施術月
004120           MOVE レセ−患者番号  TO 受−患者番号
004130           MOVE レセ−枝番      TO 受−枝番
                 READ 受診者情報Ｆ
                 NOT INVALID KEY
019880              MOVE "YES"  TO 実行キーＷ
                 END-READ
019900        ELSE
019910           MOVE SPACE  TO 実行キーＷ
              END-IF
019950     END-IF.
004530*
004540*================================================================*
004630*================================================================*
004640 レセプトＦ読込 SECTION.
004650*
004660     READ レセプトＦ NEXT
004670     AT END
004680         MOVE "YES" TO 終了フラグ
004690     END-READ.
004700*
004780*================================================================*
004790 レコードセット SECTION.
004800*
004801     MOVE SPACE TO 助成レコードフラグ.
004803*
           IF レセ−レセ種別 = 3
004844         MOVE "YES" TO 助成レコードフラグ
           END-IF
004811     PERFORM レコード明細セット.
004827*
005442*================================================================*
005443 レコード明細セット SECTION.
005456*
005473     MOVE SPACE TO 作１−レコード.
005474     INITIALIZE 作１−レコード.
005475     MOVE レセ−請求和暦     TO 作１−請求和暦.
005476     MOVE レセ−請求年       TO 作１−請求年.
005477     MOVE レセ−請求月       TO 作１−請求月.
005478     MOVE レセ−施術和暦     TO 作１−施術和暦.
005479     MOVE レセ−施術年       TO 作１−施術年.
005480     MOVE レセ−施術月       TO 作１−施術月.
005480     MOVE レセ−負担割合     TO 作１−負担割合.
005650     MOVE レセ−請求区分     TO 作１−返戻区分.
005481     MOVE 受−患者コード     TO 作１−患者コード.
005485     MOVE 受−患者カナ       TO 作１−患者カナ.
005527     MOVE 受−本人家族区分   TO 作１−本人家族区分.
      *
      */助成でラベル番号がある場合は国保連に入れる/170510
      */組合で北海道、青森、岩手、宮城、愛媛、鹿児島は協会健保に入れる/170510
      */和歌山の国保で助成がある時は一部負担金を入れる/170510
      */組合退職者は元の組合番号に入れる/170822
      */鹿児島銀行(06460067)、南日本銀行(06460075)、鹿児島信金(06460121)は組合に入れる/210518
           IF 助成レコードフラグ  = "YES"
               PERFORM 市町村情報取得
005542         PERFORM 分類取得
               MOVE 分類保険者番号Ｗ        TO 作１−分類保険者番号
005517         MOVE 受−費用負担者番号助成  TO 作１−保険者番号
005518         MOVE 受−助成種別            TO 作１−分類保険種別 作１−保険種別
               MOVE レセ−一部負担金        TO 作１−費用額
               MOVE レセ−受給者負担額      TO 作１−負担額
               MOVE レセ−助成請求金額      TO 作１−請求額
      */助成で本体が国保ではないまたは合算請求ではない場合
019640         IF ((受−費用負担者番号助成(3:2) = "25" OR "04") AND
                   ((受−保険種別 NOT = 01 AND 08 AND 05) OR ( レセ−本体まとめ区分 NOT = 1 ))) OR
                  ((受−費用負担者番号助成(3:2) = "01" OR "21") AND
                   ( 受−助成種別 NOT = 54 ))
                   MOVE 2                   TO 作１−表示フラグ
               END-IF
005524     ELSE
005526         IF 受−公費種別 = ZERO
005531             MOVE 受−保険者番号      TO 作１−保険者番号
005532             MOVE 受−保険種別        TO 作１−保険種別
                   IF 受−保険種別 = 03
                     IF 受−保険者番号 = "06010672" OR "06040174" OR "06141469" OR "06160329" OR
                                         "06200364" OR "06220834" OR "06231062" OR "06271654" OR 
                                         "06281059" OR "06340319" OR "06380257" OR "06400782"
                         MOVE "06132427" TO 作１−分類保険者番号
                         MOVE 02          TO 作１−分類保険種別
                     ELSE
                         EVALUATE 受−保険者番号(1:4)
                         WHEN "0601"
                             MOVE "01010016"  TO 作１−分類保険者番号
005532                       MOVE 02          TO 作１−分類保険種別
                         WHEN "0602"
                             MOVE "01020015"  TO 作１−分類保険者番号
005532                       MOVE 02          TO 作１−分類保険種別
                         WHEN "0603"
                             MOVE "01030014"  TO 作１−分類保険者番号
005532                       MOVE 02          TO 作１−分類保険種別
                         WHEN "0604"
                             MOVE "01040013"  TO 作１−分類保険者番号
005532                       MOVE 02          TO 作１−分類保険種別
                         WHEN "0638"
                             MOVE "01380013"  TO 作１−分類保険者番号
005532                       MOVE 02          TO 作１−分類保険種別
                         WHEN "0646"
                             IF (受−保険者番号 NOT = "06460067" AND "06460075" AND "06460125")
                                MOVE "01460013"  TO 作１−分類保険者番号
005532                          MOVE 02          TO 作１−分類保険種別
                             ELSE
005531                          MOVE 受−保険者番号  TO 作１−分類保険者番号
005532                          MOVE 受−保険種別    TO 作１−分類保険種別
                             END-IF
                         WHEN OTHER
005531                       MOVE 受−保険者番号  TO 作１−分類保険者番号
005532                       MOVE 受−保険種別    TO 作１−分類保険種別
                         END-EVALUATE
                         IF 受−保険者番号(1:2) = "63"
                             MOVE "06"            TO 作１−分類保険者番号(1:2)
                         END-IF
                     END-IF
                   ELSE
005531                 MOVE 受−保険者番号  TO 作１−分類保険者番号
005532                 MOVE 受−保険種別    TO 作１−分類保険種別
                   END-IF
                   MOVE レセ−合計          TO 作１−費用額
                   MOVE レセ−一部負担金    TO 作１−負担額
                   MOVE レセ−請求金額      TO 作１−請求額
                   IF (受−保険種別 = 01 OR 08) AND
                      (受−助成種別 NOT = ZERO) AND
                      (レセ−本体まとめ区分 = 1)
                       MOVE 1                    TO 作１−併区分
                   END-IF
005533         ELSE
005534             MOVE 1                   TO 作１−本人家族区分
005538             MOVE 受−費用負担者番号  TO 作１−分類保険者番号 作１−保険者番号
005539             MOVE 受−公費種別        TO 作１−分類保険種別 作１−保険種別
                   MOVE レセ−合計          TO 作１−費用額
                   MOVE レセ−一部負担金    TO 作１−負担額
                   MOVE レセ−請求金額      TO 作１−請求額
                   IF (受−助成種別 NOT = ZERO) AND
                      (レセ−本体まとめ区分 = 1)
                       MOVE 1                    TO 作１−併区分
                   END-IF
005540         END-IF
005541*
005542         PERFORM 分類取得
005543*
005545     END-IF.
005564*
005656     WRITE 作１−レコード.
005657     IF 状態キー  NOT =  "00"
005658         MOVE NC"作１"  TO ファイル名
005659         PERFORM エラー表示
005660     END-IF.
005661*
005705*================================================================*
005706 分類取得 SECTION.
005707***************************************************
005708*/ 船員を分ける/170925
      *
005708* 分類コード
005709*  10:社保・日雇
      *  10:船員
005710*  30:組合
      *  40:共済・自衛官
005711*  50:国保・国保組合・退職・後高
005712*  60:全国歯科医師、兵庫県医師
005712*  70:原爆
005712*  80:原爆以外の助成
005716*
005718***************************************************
      */助成で親保険が国保でラベル番号がある場合は国保連に入れる/170510
      */組合で北海道、青森、岩手、宮城、愛媛、鹿児島は協会健保に入れる/170510
      */全国歯科医師、兵庫県医師は国保連の後に入れる/170510
      */原爆は別の括りにする/170510
      */大阪は後高の次に後高助成を入れる/180927
      */大阪の助成は国保連に入れる/200317
      */全国土木建設、全国建設工事の神奈川の助成（直送）は国保連の後に入れる/200317
      */鹿児島銀行(06460067)、南日本銀行(06460075)、鹿児島信金(06460121)は組合に入れる/210518
005719*
005720     IF 助成レコードフラグ  = "YES"
005726         MOVE  80 TO  作１−分類コード
      *         IF (受−保険種別 = 01 OR 08 OR 05) AND (ラベルフラグ = 1)
               IF (ラベルフラグ = 1)
005726             MOVE  50 TO  作１−分類コード
005743             MOVE  4  TO  作１−保険順
005742             MOVE 受−費用負担者番号助成(3:2) TO 作１−県コード
               END-IF
               IF 受−助成種別 = 54
005726             MOVE  70 TO  作１−分類コード
               END-IF
               IF 受−費用負担者番号助成(3:2) = "27"
005726             MOVE  50 TO  作１−分類コード
005731             IF 受−保険種別 = 05
                       EVALUATE 受−助成種別
010760                 WHEN 54
010770                     MOVE 2     TO 作１−保険順
010780                 WHEN 51
010790                     MOVE 3     TO 作１−保険順
010800                 WHEN 53
010810                     MOVE 4     TO 作１−保険順
010820                 WHEN 52
010830                     MOVE 5     TO 作１−保険順
010840                 WHEN 55
010850                     MOVE 6     TO 作１−保険順
010840                 WHEN OTHER
010850                     MOVE 7     TO 作１−保険順
                       END-EVALUATE
                   ELSE
                       EVALUATE 受−助成種別
010760                 WHEN 54
010770                     MOVE 10     TO 作１−保険順
010780                 WHEN 51
010790                     MOVE 11     TO 作１−保険順
010800                 WHEN 53
010810                     MOVE 12     TO 作１−保険順
010820                 WHEN 52
010830                     MOVE 13     TO 作１−保険順
010840                 WHEN 55
010850                     MOVE 14     TO 作１−保険順
010840                 WHEN OTHER
010850                     MOVE 15     TO 作１−保険順
                       END-EVALUATE
                   END-IF
               END-IF
               IF (受−保険者番号 = "133033" OR "133298") AND
                   (受−費用負担者番号助成(3:2) = "14")
005743             MOVE 80       TO 作１−分類コード
010850             MOVE ZERO     TO 作１−保険順
               END-IF
005723*         EVALUATE 受−助成種別
      *         WHEN 51
005724*             MOVE  9 TO  作１−分類コード
005725*         WHEN 52
005726*             MOVE 10 TO  作１−分類コード
005725*         WHEN 53
005726*             MOVE 12 TO  作１−分類コード
005725*         WHEN 54
005726*             MOVE  8 TO  作１−分類コード
005725*         WHEN 55
005726*             MOVE 11 TO  作１−分類コード
005727*         END-EVALUATE
005728     ELSE
005729         IF 受−公費種別 = ZERO
005730*       / 健保 /
005731             EVALUATE 受−保険種別
005741             WHEN 01
                       IF 受−保険者番号 = "093013" OR "283077"
005743                     MOVE 60 TO  作１−分類コード
                       ELSE
005743                     MOVE 50 TO  作１−分類コード
005743                     MOVE 1  TO  作１−保険順
                           IF 受−保険者番号(1:2) = "27"
                               MOVE 8 TO 作１−保険順
                           END-IF
005742                     MOVE 受−保険者番号(1:2) TO 作１−県コード
                       END-IF
005732             WHEN 02
005733             WHEN 06
005736                 MOVE 10 TO  作１−分類コード
005743                 MOVE 1  TO  作１−保険順
005742                 MOVE 受−保険者番号(3:2) TO 作１−県コード
005734             WHEN 07
005736                 MOVE 10 TO  作１−分類コード
005743                 MOVE 1  TO  作１−保険順
005742                 MOVE 受−保険者番号(3:2) TO 作１−県コード
005737             WHEN 03
005740                 MOVE 30 TO  作１−分類コード
                       IF (受−保険者番号(1:4) = "0601" OR "0602" OR "0603" OR "0604" OR
                                                 "0638" OR "0646" ) AND
                          (受−保険者番号 NOT = "06460067" AND "06460075" AND "06460125")
005736                     MOVE 10 TO  作１−分類コード
005743                     MOVE 2  TO  作１−保険順
005742                     MOVE 受−保険者番号(3:2) TO 作１−県コード
                       END-IF
005738             WHEN 04
005739             WHEN 09
005740                 MOVE 40 TO  作１−分類コード
005753             WHEN 08
005755                 MOVE 50 TO  作１−分類コード
005743                 MOVE 2  TO  作１−保険順
                       IF 受−保険者番号(3:2) = "27"
                           MOVE 9 TO 作１−保険順
                       END-IF
005742                 MOVE 受−保険者番号(3:2) TO 作１−県コード
005765             END-EVALUATE
005766*
005767         ELSE
005768*       / 老人 /
005770             MOVE 50 TO  作１−分類コード
005743             MOVE 3  TO  作１−保険順
                   IF 受−費用負担者番号(3:2) = "27"
                       MOVE 1 TO 作１−保険順
                   END-IF
005742             MOVE 受−費用負担者番号(3:2) TO 作１−県コード
005778         END-IF
005779     END-IF.
      *
005705*================================================================*
005706 市町村情報取得 SECTION.
005780*
004045     MOVE 受−助成種別            TO 市−公費種別.
004046     MOVE 受−費用負担者番号助成  TO 市−市町村番号.
004048     READ 市町村マスタ
004049     INVALID KEY
               MOVE ZERO                TO ラベルフラグ
004050         MOVE SPACE               TO 分類保険者番号Ｗ
004051     NOT INVALID KEY
               IF 市−ラベル番号 = SPACE
                   MOVE ZERO                    TO ラベルフラグ
004050             MOVE 受−費用負担者番号助成  TO 分類保険者番号Ｗ
               ELSE
                   MOVE 1               TO ラベルフラグ
004050             MOVE 市−保険者番号  TO 分類保険者番号Ｗ
               END-IF
004064     END-READ.
      *
005809*================================================================*
005810 作業ファイル作成２ SECTION.
005811*
005813     MOVE SPACE TO 終了フラグ.
005814     MOVE ZERO  TO 順番Ｗ.      
005814     MOVE 1     TO ソート番号Ｗ.      
005814     MOVE ZERO  TO データ表示順Ｗ.      
005815     PERFORM 作業ファイル１読込.
003140     MOVE 作１−分類コード      TO 分類コードＷＲ.
003140     MOVE 作１−分類保険者番号  TO 保険者番号ＷＲ.
003200     MOVE 作１−県コード        TO 県コードＷＲ.
005818     PERFORM UNTIL 終了フラグ = "YES"
               IF ( 作１−分類コード(1:1) NOT = 分類コードＷＲ(1:1) )
                   MOVE ZERO         TO ソート番号Ｗ
               END-IF
               IF ((作１−県コード = SPACE) AND (作１−分類保険者番号 NOT = 保険者番号ＷＲ)) OR
                  ((作１−県コード NOT = SPACE) AND (作１−県コード NOT = 県コードＷＲ ))
                   MOVE ZERO      TO データ表示順Ｗ
005851             COMPUTE ソート番号Ｗ    = ソート番号Ｗ + 1
               END-IF
  5819         PERFORM 作２レコードセット
  5820         PERFORM 作２ファイル書込
005821*
003140         MOVE 作１−分類コード      TO 分類コードＷＲ
003140         MOVE 作１−分類保険者番号  TO 保険者番号ＷＲ
003200         MOVE 作１−県コード        TO 県コードＷＲ
005821*
005824         PERFORM 作業ファイル１読込
005825     END-PERFORM.
005826*
005843*================================================================*
005844 作２レコードセット SECTION.
005845*
005851     COMPUTE 順番Ｗ          = 順番Ｗ + 1.
005852     MOVE 順番Ｗ          TO 作２−順番.
005852     MOVE ソート番号Ｗ    TO 作２−ソート番号.
005851     COMPUTE データ表示順Ｗ  = データ表示順Ｗ + 1.
005852     MOVE データ表示順Ｗ  TO 作２−データ表示順.
           EVALUATE 作１−分類コード(1:1)
           WHEN 1
               MOVE 1           TO 作２−保険種別順番
           WHEN 3
               MOVE 2           TO 作２−保険種別順番
           WHEN 4
               MOVE 3           TO 作２−保険種別順番
           WHEN 5
               MOVE 4           TO 作２−保険種別順番
           WHEN OTHER
               MOVE 5           TO 作２−保険種別順番
           END-EVALUATE.
           MOVE 作１−表示フラグ  TO 作２−表示フラグ.
005853*
005854     MOVE 作１−施術和暦       TO 作２−施術和暦.
005855     MOVE 作１−施術年         TO 作２−施術年.
005856     MOVE 作１−施術月         TO 作２−施術月.
005857     MOVE 作１−患者コード     TO 作２−患者コード.
005858     MOVE 作１−保険種別       TO 作２−保険種別.
005858     MOVE 作１−分類保険種別   TO 作２−分類保険種別.
005859     MOVE 作１−返戻区分       TO 作２−返戻区分.
005859     MOVE 作１−併区分         TO 作２−併区分.
005860     MOVE 作１−分類コード     TO 作２−分類コード.
005860     MOVE 作１−県コード       TO 作２−県コード.
005861     MOVE 作１−本人家族区分   TO 作２−本人家族区分.
005862     MOVE 作１−分類保険者番号 TO 作２−分類保険者番号.
005862     MOVE 作１−保険者番号     TO 作２−保険者番号.
005859     MOVE 作１−負担割合       TO 作２−負担割合.
005859     MOVE 作１−費用額         TO 作２−費用額.
005859     MOVE 作１−負担額         TO 作２−負担額.
005859     MOVE 作１−請求額         TO 作２−請求額.
005863*
005886*================================================================*
005887 作２ファイル書込 SECTION.
005888*
005889     WRITE 作２−レコード
005890     INVALID KEY
HILO           DISPLAY 作２−レコードキー
005891         MOVE NC"作２"  TO ファイル名
005892         PERFORM エラー表示
005893     END-WRITE.
005894*================================================================*
005895 作業ファイル１読込 SECTION.
005896*
005897     READ 作業ファイル１ NEXT
005898     AT END
005899         MOVE "YES" TO 終了フラグ
005900     END-READ.
005901*
005910*================================================================*
005911******************************************************************
005912 END PROGRAM YAS1012.
005913******************************************************************
