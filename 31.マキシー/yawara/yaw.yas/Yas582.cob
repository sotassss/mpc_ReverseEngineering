000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YAS582.
000060 AUTHOR.                 岡田 憲和
000070*
000080*----------------------------------------------------------------*
000090*         総括表印刷（柔ｳｨﾝﾄﾞｳｽﾞ95版）
000100* 請求年月Ver.
000110*         MED =  YAS582P
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2012-09-13
000140 DATE-COMPILED.          2012-09-13
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000270     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000280                             ORGANIZATION             IS  INDEXED
000290                             ACCESS MODE              IS  DYNAMIC
000300                             RECORD KEY               IS  元−元号区分
000310                             FILE STATUS              IS  状態キー
000320                             LOCK        MODE         IS  AUTOMATIC.
000400     SELECT  制御情報マスタ  ASSIGN      TO        SEIGYOL
000410                             ORGANIZATION             IS  INDEXED
000420                             ACCESS MODE              IS  DYNAMIC
000430                             RECORD KEY               IS  制−制御区分
000440                             FILE STATUS              IS  状態キー
000450                             LOCK        MODE         IS  AUTOMATIC.
000460     SELECT  施術所情報マスタ ASSIGN      TO        SEJOHOL
000470                             ORGANIZATION             IS  INDEXED
000480                             ACCESS MODE              IS  DYNAMIC
000490                             RECORD KEY               IS 施情−施術所番号
000500                             FILE STATUS              IS  状態キー
000510                             LOCK        MODE         IS  AUTOMATIC.
000520     SELECT  作業ファイル１  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5801L.DAT"
000530                             ORGANIZATION             IS  INDEXED
000540                             ACCESS                   IS  DYNAMIC
000905                             RECORD      KEY          IS  作１−請求和暦年月
000907                                                          作１−分類コード
                                                                作１−県コード
                                                                作１−保険順
000908                                                          作１−分類保険者番号
000909                                                          作１−本人家族キー
                                                                作１−負担割合
000910                                                          作１−患者カナ
000911                                                          作１−患者コード
000912                                                          作１−施術和暦年月
000620                             FILE        STATUS       IS  状態キー
000630                             LOCK        MODE         IS  AUTOMATIC.
000640     SELECT  印刷ファイル    ASSIGN      TO     GS-PRTF001
000650                             SYMBOLIC    DESTINATION  IS "PRT"
000660                             FORMAT                   IS  定義体名Ｐ
000670                             GROUP                    IS  項目群名Ｐ
000680                             PROCESSING  MODE         IS  処理種別Ｐ
000690                             UNIT        CONTROL      IS  拡張制御Ｐ
000700                             FILE        STATUS       IS  通知情報Ｐ.
000710******************************************************************
000720*                      DATA DIVISION                             *
000730******************************************************************
000740 DATA                    DIVISION.
000750 FILE                    SECTION.
000760*                           ［ＲＬ＝  １２８］
000770 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
000780     COPY GENGOU          OF  XFDLIB  JOINING   元   AS  PREFIX.
000820*                           ［ＲＬ＝  ２５６］
000830 FD  制御情報マスタ          BLOCK   CONTAINS   1   RECORDS.
000840     COPY SEIGYO          OF  XFDLIB  JOINING   制   AS  PREFIX.
000850*                           ［ＲＬ＝  ６４０］
000860 FD  施術所情報マスタ          BLOCK   CONTAINS   1   RECORDS.
000870     COPY SEJOHO         OF  XFDLIB  JOINING   施情   AS  PREFIX.
000880*
000890*
000900*
000910 FD  作業ファイル１ RECORD  CONTAINS 256 CHARACTERS.
000920 01  作１−レコード.
000930     03  作１−レコードキー.
001450         05  作１−請求和暦年月.
001460             07  作１−請求和暦            PIC 9.
001470             07  作１−請求年              PIC 9(2).
001480             07  作１−請求月              PIC 9(2).
001261         05  作１−分類コード              PIC 9(2).
001261         05  作１−県コード                PIC X(2).
001400         05  作１−保険順                  PIC 9(2).
001271         05  作１−分類保険者番号          PIC X(8).
001280         05  作１−本人家族キー            PIC 9.
               05  作１−負担割合                PIC 9(2).
001300         05  作１−患者カナ                PIC X(50).
001310         05  作１−患者コード.
001320             07 作１−患者番号             PIC 9(6).
001330             07 作１−枝番                 PIC X(1).
001340         05  作１−施術和暦年月.
001350             07  作１−施術和暦            PIC 9.
001360             07  作１−施術年              PIC 9(2).
001370             07  作１−施術月              PIC 9(2).
001090     03  作１−レコードデータ.
001271         05  作１−保険者番号              PIC X(8).
001100         05  作１−記号                    PIC X(24).
001110         05  作１−番号                    PIC X(30).
001120         05  作１−被保険者カナ            PIC X(50).
001130         05  作１−本人家族区分            PIC 9.
001140         05  作１−費用額                  PIC 9(7).
001150         05  作１−負担額                  PIC 9(7).
001160         05  作１−請求額                  PIC 9(7).
001170         05  作１−保険種別                PIC 9(2).
001180         05  作１−親保険種別              PIC 9(2).
001190         05  作１−老人レコード区分        PIC 9.
001200         05  作１−助成レコード区分        PIC 9.
001210         05  FILLER                        PIC X(32).
001220*
001230 FD  印刷ファイル.
001240     COPY YAS582P        OF  XMDLIB.
001250*----------------------------------------------------------------*
001260******************************************************************
001270*                WORKING-STORAGE SECTION                         *
001280******************************************************************
001290 WORKING-STORAGE         SECTION.
001300 01 キー入力                           PIC X    VALUE SPACE.
001310 01 状態キー                           PIC X(2) VALUE SPACE.
001320 01 終了フラグ                         PIC X(3) VALUE SPACE.
001330 01 終了行フラグ                       PIC X(3) VALUE SPACE.
001340 01 ファイル名                         PIC N(10).
001350 01 対象なしフラグ                     PIC X(3) VALUE SPACE.
001360 01 オープンフラグ                     PIC X(3)  VALUE SPACE.
001361*
001370 01 保険種別ＰＷ                       PIC 9(2)  VALUE ZERO.
001380 01 印刷順序Ｗ                         PIC 9(2)  VALUE ZERO.
001390 01 請求和暦年月ＰＷ.
001400    03 請求和暦ＰＷ                    PIC 9    VALUE ZERO.
001410    03 請求年ＰＷ                      PIC 9(2) VALUE ZERO.
001420    03 請求月ＰＷ                      PIC 9(2) VALUE ZERO.
001430*
001440 01 保険名称Ｗ                         PIC N(3) VALUE SPACE.
001450 01 備考Ｗ                             PIC X(20).
001460 01 前和暦Ｗ                           PIC 9    VALUE ZERO.
001470 01 行カウンタ                         PIC 9(2) VALUE 0.
001480 01 頁カウンタ                         PIC 9(4) VALUE 0.
001490 01 最大行数                           PIC 9(2) VALUE 0.
001500 01 ヘッダ行数                         PIC 9(2) VALUE 0.
001510 01 移動行数Ｗ                         PIC 9(2) VALUE 0.
001520 01 処理移動キー                       PIC X(4) VALUE SPACE.
001530 01 カレント元号Ｗ                     PIC 9(1) VALUE ZERO.
001540*
001550 01 会員番号Ｗ.
001560    03 会名                            PIC N(2) VALUE SPACE.
001570    03 ハイフン                        PIC X(1) VALUE "-".
001580    03 会員番号ＷＰ                    PIC X(10) VALUE SPACE.
001590*
001600 01 保険種別名称Ｗ.
001610    03 保険種別名称ＷＰ                PIC X(6) VALUE SPACE.
001620 01 親保険種別名称Ｗ.
001630    03 親保険種別名称ＷＰ              PIC X(4) VALUE SPACE.
001640    03 合計文字                        PIC X(4).
001650 01 総合計Ｗ.
001660    03 総合計ＷＰ                      PIC X(6).
001670*
001680** 氏名分離用
001690 01 被保険者カナＷ.
001700    03 被保険者カナＷＰ                PIC X    OCCURS 50 VALUE SPACE.
001710 01 名字カナＷ.
001720    03 名字カナＷＰ                    PIC X    OCCURS 50 VALUE SPACE.
001730 01 名前カナＷ.
001740    03 名前カナＷＰ                    PIC X    OCCURS 50 VALUE SPACE.
001750 01 カウンタ                           PIC 9(2) VALUE ZERO.
001760 01 Ｍカウンタ                         PIC 9(2) VALUE ZERO.
001770 01 Ｎカウンタ                         PIC 9(2) VALUE ZERO.
001780 01 名字フラグ                         PIC X(3) VALUE SPACE.
001790**
001800 01 小計Ｗ.
001810    03 小計件数Ｗ                      PIC 9(4) VALUE ZERO.
001820    03 費用額小計Ｗ                    PIC 9(7) VALUE ZERO.
001830    03 負担額小計Ｗ                    PIC 9(7) VALUE ZERO.
001840    03 請求額小計Ｗ                    PIC 9(7) VALUE ZERO.
001850 01 合計Ｗ.
001860    03 合計件数Ｗ                      PIC 9(4) VALUE ZERO.
001870    03 費用額合計Ｗ                    PIC 9(8) VALUE ZERO.
001880    03 負担額合計Ｗ                    PIC 9(8) VALUE ZERO.
001890    03 請求額合計Ｗ                    PIC 9(8) VALUE ZERO.
001900*
001910 01 施術和暦年月Ｗ.
001920     03 施術和暦Ｗ                   PIC 9.
001930     03 施術年月Ｗ.
001940        05 施術年Ｗ                  PIC 9(2).
001950        05 年Ｗ                      PIC N(1).
001960        05 施術月Ｗ                  PIC 9(2).
001970        05 月Ｗ                      PIC N(1).
001980 01 現在和暦年月Ｗ.
001990    03 現在和暦Ｗ                    PIC 9.
002000    03 現在年Ｗ                      PIC 9(2).
002010    03 現在月Ｗ                      PIC 9(2).
002020*
002030 01 保険種別ＷＲ                     PIC 9(2) VALUE ZERO.
002040 01 印刷形式ＷＲ                     PIC 9    VALUE ZERO.
002050*
002060** 月末日用
002070 01 施術西暦年Ｗ                     PIC 9(4)  VALUE ZERO.
002080 01 商Ｗ                             PIC 9(3)  VALUE ZERO.
002090 01 余Ｗ                             PIC 9(3)  VALUE ZERO.
002100*
002110 01 提出和暦年月Ｗ.
002120     03 提出和暦Ｗ                   PIC 9    VALUE ZERO.
002130     03 提出年月日Ｗ.
002140        05 提出年Ｗ                  PIC 9(2) VALUE ZERO.
002150        05 提出月Ｗ                  PIC 9(2) VALUE ZERO.
002160        05 提出日Ｗ                  PIC 9(2) VALUE ZERO.
002170*
002180*******************************************************************
002190 01 印刷制御.
002200     03 定義体名Ｐ                     PIC X(8) VALUE SPACE.
002210     03 項目群名Ｐ                     PIC X(8) VALUE SPACE.
002220     03 処理種別Ｐ                     PIC X(2) VALUE SPACE.
002230     03 拡張制御Ｐ.
002240         05 端末制御Ｐ.
002250             07 移動方向Ｐ             PIC X(1).
002260             07 移動行数Ｐ             PIC 9(3).
002270         05 詳細制御Ｐ                 PIC X(2).
002280     03 通知情報Ｐ                     PIC X(2) VALUE SPACE.
002290     03 ユニット名Ｐ                   PIC X(8) VALUE SPACE.
002300*
002310 01 計算機西暦年Ｗ                     PIC 9(2).
002320* 日付ＷＯＲＫ
002330 01 和暦終了年Ｗ                       PIC 9(4).
002340 01 計算機西暦.
002350    03 計算機西暦年                    PIC 9(4).
002360    03 計算機西暦月日                  PIC 9(4).
002370 01 計算機西暦Ｒ REDEFINES 計算機西暦.
002380    03 計算機世紀                      PIC 9(2).
002390    03 計算機日付                      PIC 9(6).
002400    03 計算機日付Ｒ REDEFINES 計算機日付.
002410       05 計算機年月                   PIC 9(4).
002420       05 計算機年月Ｒ REDEFINES 計算機年月.
002430         07 計算機年                   PIC 9(2).
002440         07 計算機月                   PIC 9(2).
002450       05 計算機日                     PIC 9(2).
002460*
002470******************************************************************
002480*                          連結項目                              *
002490******************************************************************
002500*
002510********************
002520* メッセージ表示キー *
002530********************
002540 01 連メ−キー IS EXTERNAL.
002550    03  連メ−メッセージ                 PIC N(20).
002560*
002570 01 連入−画面情報ＹＡＳ５８０ IS EXTERNAL.
002580    03 連入−請求和暦年月.
002590       05 連入−請求和暦               PIC 9.
002600       05 連入−請求年月.
002610         07 連入−請求年               PIC 9(2).
002620         07 連入−請求月               PIC 9(2).
002630*
002640 01 連印−画面情報ＹＡＳ５８０ IS EXTERNAL GLOBAL.
002643    03 連印−プレビュー区分            PIC 9.
002644*
002645************************************
002646* プリンタファイル作成用           *
002647************************************
002648 01 Ｈ連ＰＲＴＦ−作成データ IS EXTERNAL.
002649     03 Ｈ連ＰＲＴＦ−ファイル名           PIC X(8).
002650     03 Ｈ連ＰＲＴＦ−プレビュー区分       PIC 9.
002651     03 Ｈ連ＰＲＴＦ−帳票プログラム名     PIC X(8).
002652     03 Ｈ連ＰＲＴＦ−オーバレイ名         PIC X(8).
002653*
002654******************************************************************
002655*                      PROCEDURE  DIVISION                       *
002660******************************************************************
002670 PROCEDURE               DIVISION.
002680************
002690*           *
002700* 初期処理   *
002710*           *
002720************
002730     MOVE SPACE TO オープンフラグ.
002731     PERFORM プリンタファイル作成.
002732     PERFORM 初期化.
002740     PERFORM 月末日取得.
002750     MOVE SPACE  TO YAS582P.
002760************
002770*           *
002780* 主処理     *
002790*           *
002800************
002810*
002820     PERFORM 印刷処理.
002830*
002840************
002850*           *
002860* 終了処理   *
002870*           *
002880************
002890     PERFORM 終了処理.
002900     EXIT PROGRAM.
002910*
002920*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002930*================================================================*
002931 プリンタファイル作成 SECTION.
002932*================================================================*
002933*   / 初期化 /
002934     MOVE SPACE TO Ｈ連ＰＲＴＦ−作成データ.
002935     INITIALIZE Ｈ連ＰＲＴＦ−作成データ.
002936*
002937*
002938*--↓↓ 変更箇所 ↓↓--------------------------------------*
002939*   使用するプリンタファイル名セット
002940     MOVE "PRTF001"             TO Ｈ連ＰＲＴＦ−ファイル名.
002941*
002942*   使用する帳票プログラム名セット
002943     MOVE "YAS582"              TO Ｈ連ＰＲＴＦ−帳票プログラム名.
002944*
002945*--↑↑-----------------------------------------------------*
002946*
002947*   / プレビュー区分セット /
002948     MOVE 連印−プレビュー区分 TO Ｈ連ＰＲＴＦ−プレビュー区分.
002949*
002950     CALL   "CRTPRTF".
002951     CANCEL "CRTPRTF".
002952*
002953*================================================================*
002954 初期化 SECTION.
002955*
002960     PERFORM ファイルオープン.
002970*    /* 現在日付取得 */
002980     ACCEPT 計算機日付 FROM DATE.
002990*    /* 1980〜2079年の間で設定 */
003000     IF 計算機年 > 80
003010         MOVE 19 TO 計算機世紀
003020     ELSE
003030         MOVE 20 TO 計算機世紀
003040     END-IF.
003050     PERFORM カレント元号取得.
003060     PERFORM 和暦終了年取得.
003070**     COMPUTE 計算機西暦年Ｗ = 計算機西暦年 - 1988.
003080*
003090*================================================================*
003100 カレント元号取得 SECTION.
003110*
003120     MOVE ZEROS TO 制−制御区分.
003130     READ 制御情報マスタ
003140     NOT INVALID KEY
003150         MOVE 制−カレント元号 TO カレント元号Ｗ
003160     END-READ.
003170*
003180*================================================================*
003190 和暦終了年取得 SECTION.
003200*
003210     MOVE カレント元号Ｗ TO 元−元号区分.
003220     READ 元号マスタ
003230     INVALID KEY
003240         DISPLAY NC"指定和暦が登録されていません" UPON CONS
003250         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003260                                                  UPON CONS
003270         ACCEPT  キー入力 FROM CONS
003280         PERFORM 終了処理
003290         EXIT PROGRAM
003300     NOT INVALID KEY
003310         COMPUTE 前和暦Ｗ = カレント元号Ｗ - 1
003320         MOVE 前和暦Ｗ TO 元−元号区分
003330         READ 元号マスタ
003340         INVALID KEY
003350             DISPLAY NC"指定和暦が登録されていません" UPON CONS
003360             DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003370                                                      UPON CONS
003380             ACCEPT  キー入力 FROM CONS
003390             PERFORM 終了処理
003400             EXIT PROGRAM
003410         NOT INVALID KEY
003420             MOVE 元−終了西暦年 TO 和暦終了年Ｗ
003430         END-READ
003440     END-READ.
003450*
003460*================================================================*
003470 ファイルオープン SECTION.
003480*
003490     OPEN INPUT 元号マスタ
003500         MOVE NC"元号" TO ファイル名.
003510         PERFORM オープンチェック.
003550     OPEN INPUT 制御情報マスタ
003560         MOVE NC"制御情報" TO ファイル名.
003570         PERFORM オープンチェック.
003580     OPEN INPUT 施術所情報マスタ
003590         MOVE NC"施術所情報" TO ファイル名.
003600         PERFORM オープンチェック.
003610     OPEN INPUT 作業ファイル１
003620         MOVE NC"作業１" TO ファイル名.
003630         PERFORM オープンチェック.
003660*================================================================*
003670 オープンチェック SECTION.
003680*
003690     IF 状態キー  NOT =  "00"
003700         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
003710         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003720         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003730                                                 UPON CONS
003740         ACCEPT  キー入力 FROM CONS
003750         PERFORM ファイル閉鎖
003760         EXIT PROGRAM.
003770*================================================================*
003780 ファイル閉鎖 SECTION.
003790*
003800     IF ( オープンフラグ = "YES" )
003801         CLOSE 印刷ファイル
003802     ELSE
003803         MOVE  NC"　　データが０件です。確認して下さい。" TO 連メ−メッセージ
003804         CALL   "MSG001"
003805         CANCEL "MSG001"
003806     END-IF.
003807*
003808     CLOSE 印刷ファイル  元号マスタ 制御情報マスタ 施術所情報マスタ
003810           作業ファイル１.
003820*================================================================*
003830 終了処理 SECTION.
003840*
003850     PERFORM ファイル閉鎖.
003860*================================================================*
003870 エラー表示 SECTION.
003880*
003890     DISPLAY NC"状態キー" 状態キー  UPON CONS.
003900     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
003910     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
003920     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
003930     ACCEPT  キー入力 FROM CONS.
003940     PERFORM ファイル閉鎖.
003950     EXIT PROGRAM.
003960*================================================================*
003970 印刷処理 SECTION.
003980*
003990     MOVE SPACE TO 終了フラグ.
004000     INITIALIZE 小計Ｗ.
004010     INITIALIZE 合計Ｗ.
004020     MOVE 15    TO 最大行数.
004030     MOVE 1     TO 頁カウンタ.
004040     PERFORM 作業ファイル１読込.
004050     IF 終了フラグ = "YES"
004060        MOVE  NC"　　　該当データがありません。" TO 連メ−メッセージ
004070        CALL   "MSG001"
004080        CANCEL "MSG001"
004090        PERFORM 終了処理
004100        EXIT PROGRAM
004110     END-IF.
004120     PERFORM UNTIL 終了フラグ = "YES"
004130*
004140         MOVE 作１−請求和暦     TO 請求和暦ＰＷ
004150         MOVE 作１−請求年       TO 請求年ＰＷ
004160         MOVE 作１−請求月       TO 請求月ＰＷ
004170         PERFORM ヘッダセット
004180*
004190         PERFORM UNTIL ( 終了フラグ = "YES" ) OR ( 行カウンタ > 最大行数 )  OR
004200                       ( 作１−請求和暦   NOT = 請求和暦ＰＷ ) OR
004210                       ( 作１−請求年     NOT = 請求年ＰＷ   ) OR
004220                       ( 作１−請求月     NOT = 請求月ＰＷ   ) 
004230             PERFORM 明細部セット
004240             PERFORM 合計額累計
004250             PERFORM 作業ファイル１読込
004260         END-PERFORM
004270*
004280         IF ( 行カウンタ > 最大行数 )
004290             IF  ( 終了フラグ = "YES" ) OR 
004300                 ( 作１−請求和暦   NOT = 請求和暦ＰＷ ) OR
004310                 ( 作１−請求年     NOT = 請求年ＰＷ   ) OR
004320                 ( 作１−請求月     NOT = 請求月ＰＷ   ) 
004330                 PERFORM 印字処理
004340                 PERFORM 改頁処理
004350                 IF  終了フラグ = "YES" 
004360                     PERFORM ヘッダセット
004370                     PERFORM 総合計セット
004380                     PERFORM 印字処理
004390                     PERFORM 改頁処理
004400                 END-IF
004410             ELSE
004420                 PERFORM 印字処理
004430                 PERFORM 改頁処理
004440             END-IF
004450         ELSE
004460             IF  終了フラグ = "YES" 
004470                 IF 行カウンタ > 最大行数
004480                    PERFORM 印字処理
004490                    PERFORM 改頁処理
004500                    PERFORM ヘッダセット
004510                 END-IF
004520                 PERFORM 総合計セット
004530             END-IF
004540             PERFORM 印字処理
004550             PERFORM 改頁処理
004560         END-IF
004570*
004580     END-PERFORM.
004590*
004600*================================================================*
004610*================================================================*
004620 作業ファイル１読込 SECTION.
004630*
004640     READ 作業ファイル１ NEXT
004650     AT END
004660         MOVE "YES" TO 終了フラグ
004670     END-READ.
004680*
004690*================================================================*
004700 印字処理  SECTION.
004710*
004720     IF ( オープンフラグ NOT = "YES" )
004721        MOVE "YES" TO オープンフラグ
004722        OPEN I-O  印刷ファイル
004723        PERFORM エラー処理Ｐ
004724     END-IF.
004725*
004726     MOVE "YAS582P" TO  定義体名Ｐ.
004730     MOVE SPACE     TO  処理種別Ｐ.
004740     MOVE "SCREEN"  TO  項目群名Ｐ.
004750     WRITE YAS582P.
004760     PERFORM エラー処理Ｐ.
004770     MOVE SPACE TO YAS582P.
004780*================================================================*
004790 改頁処理  SECTION.
004800
004810     MOVE "YAS582P" TO  定義体名Ｐ.
004820     MOVE "CT"      TO  処理種別Ｐ.
004830     MOVE "PAGE"    TO  拡張制御Ｐ.
004840     MOVE SPACE     TO  項目群名Ｐ.
004850     WRITE YAS582P.
004860     PERFORM エラー処理Ｐ.
004870     MOVE SPACE     TO  拡張制御Ｐ.
004880*
004890     COMPUTE 頁カウンタ = 頁カウンタ + 1.
004900*
004910*================================================================*
004920 ヘッダセット SECTION.
004930*
004940     MOVE 提出年Ｗ     TO 提出年.
004950     MOVE 提出月Ｗ     TO 提出月.
004960     MOVE 提出日Ｗ     TO 提出日.
004970     MOVE 頁カウンタ   TO 頁.
004980*
004990* 会員番号を取得
005000     MOVE 00         TO 施情−施術所番号.
005010     READ 施術所情報マスタ
005020     INVALID KEY
005030         MOVE SPACE       TO 会員番号
005040         MOVE SPACE       TO 代表者名
005050     NOT INVALID KEY
005060         MOVE 施情−接骨師会会員番号  TO 会員番号
005070         MOVE 施情−代表者名          TO 代表者名
005080     END-READ.
005090*
005100     MOVE 1    TO 行カウンタ.
005110*
005120*================================================================*
005130 明細部セット SECTION.
005140*
005150****     MOVE 作１−印刷順序      TO 保険種別(行カウンタ).
005160     MOVE 作１−保険者番号    TO 保険者番号(行カウンタ).
005170     IF   作１−助成レコード区分 = 1 
005180          IF 作１−保険者番号(1:2) = "99"
005190             MOVE SPACE       TO 保険者番号(行カウンタ)
005200          END-IF
005210     END-IF.
005220*
005230     IF 作１−記号(1:1) = "＊" 
005240        MOVE  SPACE        TO  記号(行カウンタ)
005250     ELSE
005260        MOVE 作１−記号    TO  記号(行カウンタ)
005270     END-IF.
005280     IF ( 作１−番号(1:1) = "*"  ) OR
005290        ( 作１−番号(1:2) = "＊" )
005300        MOVE  SPACE        TO  番号(行カウンタ)
005310     ELSE
005320        MOVE 作１−番号    TO  番号(行カウンタ)
005330     END-IF.
005340*
005350*** 老人と助成レコードは、患者カナで、本人家族区分 = 1 となる. ***
005360     IF ( 作１−老人レコード区分 = 1 )  OR
005370        ( 作１−助成レコード区分 = 1 ) 
005380        MOVE 作１−患者カナ      TO 被保険者カナＷ
005390        MOVE 1                   TO 本人家族(行カウンタ)
005400     ELSE
005410        MOVE 作１−被保険者カナ  TO 被保険者カナＷ
005420        MOVE 作１−本人家族区分  TO 本人家族(行カウンタ)
005430     END-IF.
005440**
005450     PERFORM 氏名分離.
005460     MOVE 名字カナＷ         TO 被保険者氏カナ(行カウンタ).
005470     MOVE 名前カナＷ         TO 被保険者名カナ(行カウンタ).
005480     MOVE 作１−費用額       TO 合計額(行カウンタ).
005490     MOVE 作１−請求額       TO 請求額(行カウンタ).
005500*
005510     COMPUTE 行カウンタ = 行カウンタ + 1.
005520*
005530*================================================================*
005540 氏名分離  SECTION.
005550*
005560**** 名字と名前を分離する。
005570*
005580     MOVE 1          TO カウンタ Ｍカウンタ Ｎカウンタ.
005590     MOVE SPACE      TO 名字カナＷ 名前カナＷ 名字フラグ.
005600*
005610     PERFORM VARYING カウンタ FROM 1 BY 1 
005620                     UNTIL ( カウンタ > 50 ) OR ( 名字フラグ = "YES" )
005630          IF 被保険者カナＷＰ(カウンタ)  = SPACE
005640             MOVE "YES" TO  名字フラグ
005650          ELSE
005660             MOVE 被保険者カナＷＰ(カウンタ) TO 名字カナＷＰ(Ｍカウンタ)
005670             COMPUTE Ｍカウンタ = Ｍカウンタ + 1
005680          END-IF
005690     END-PERFORM.
005700*
005710     PERFORM  UNTIL カウンタ > 50 
005720          IF 被保険者カナＷＰ(カウンタ)  = SPACE
005730             CONTINUE
005740          ELSE
005750             MOVE 被保険者カナＷＰ(カウンタ) TO 名前カナＷＰ(Ｎカウンタ)
005760             COMPUTE Ｎカウンタ = Ｎカウンタ + 1
005770          END-IF
005780          COMPUTE カウンタ = カウンタ + 1
005790     END-PERFORM.
005800*
005810*================================================================*
005820 合計額累計 SECTION.
005830*
005840*     COMPUTE 小計件数Ｗ     = 小計件数Ｗ      + 1.
005850*     COMPUTE 費用額小計Ｗ   = 費用額小計Ｗ    + 作１−費用額.
005860*     COMPUTE 負担額小計Ｗ   = 負担額小計Ｗ    + 作１−負担額.
005870*     COMPUTE 請求額小計Ｗ   = 請求額小計Ｗ    + 作１−請求額.
005880*
005890     COMPUTE 合計件数Ｗ     = 合計件数Ｗ      + 1.
005900     COMPUTE 費用額合計Ｗ   = 費用額合計Ｗ    + 作１−費用額.
005910     COMPUTE 負担額合計Ｗ   = 負担額合計Ｗ    + 作１−負担額.
005920     COMPUTE 請求額合計Ｗ   = 請求額合計Ｗ    + 作１−請求額.
005930*================================================================*
005940 総合計セット SECTION.
005950*
005960     MOVE "総合計"     TO  総合計ＷＰ.
005970     MOVE 総合計Ｗ     TO  被保険者氏カナ(行カウンタ).
005980     MOVE 合計件数Ｗ   TO  本人家族(行カウンタ).
005990     MOVE 費用額合計Ｗ TO  合計額(行カウンタ).
006000     MOVE 請求額合計Ｗ TO  請求額(行カウンタ).
006010*================================================================*
006020*================================================================*
006030 月末日取得 SECTION.
006040*
006050     MOVE 連入−請求年   TO 提出年Ｗ.
006060     MOVE 連入−請求月   TO 提出月Ｗ.
006070     MOVE 連入−請求和暦 TO 元−元号区分.
006080     READ 元号マスタ
006090     NOT INVALID KEY
006100         MOVE 元−開始西暦年 TO 施術西暦年Ｗ
006110     END-READ.
006120     IF 施術西暦年Ｗ NOT = ZERO
006130        COMPUTE 施術西暦年Ｗ = 施術西暦年Ｗ + 連入−請求年 - 1
006140     END-IF.
006150*
006160     EVALUATE 連入−請求月
006170     WHEN 4
006180     WHEN 6
006190     WHEN 9
006200     WHEN 11
006210         MOVE 30 TO 提出日Ｗ
006220     WHEN 2
006230         DIVIDE 4 INTO 施術西暦年Ｗ GIVING    商Ｗ
006240                                    REMAINDER 余Ｗ
006250         END-DIVIDE
006260         IF 余Ｗ = ZERO
006270             MOVE 29 TO 提出日Ｗ
006280         ELSE
006290             MOVE 28 TO 提出日Ｗ
006300         END-IF
006310     WHEN 1
006320     WHEN 3
006330     WHEN 5
006340     WHEN 7
006350     WHEN 8
006360     WHEN 10
006370     WHEN 12
006380         MOVE 31 TO 提出日Ｗ
006390     WHEN OTHER
006400          CONTINUE
006410     END-EVALUATE.
006420*
006430*================================================================*
006440 エラー処理Ｐ SECTION.
006450*
006460     IF 通知情報Ｐ NOT = "00"
006470         DISPLAY NC"帳票エラー"              UPON CONS
006480         DISPLAY NC"項目群名Ｐ：" 項目群名Ｐ UPON CONS
006490         DISPLAY NC"通知情報Ｐ：" 通知情報Ｐ UPON CONS
006500         DISPLAY NC"拡張制御Ｐ：" 拡張制御Ｐ UPON CONS
006510         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
006520                                             UPON CONS
006530         ACCEPT  キー入力 FROM CONS
006540         PERFORM ファイル閉鎖
006550         EXIT PROGRAM
006560     END-IF.
006570*================================================================*
006580******************************************************************
006590 END PROGRAM YAS582.
006600******************************************************************
