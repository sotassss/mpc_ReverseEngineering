000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YAS5821.
000060 AUTHOR.                 岡田 憲和
000070*
000080*----------------------------------------------------------------*
000090*         総括表印刷（柔ｳｨﾝﾄﾞｳｽﾞ95版）
000100* 請求年月Ver.
000110*         MED =  YAS5821P
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2012-09-14
000140 DATE-COMPILED.          2012-09-14
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000270     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000280                             ORGANIZATION             IS  INDEXED
000290                             ACCESS MODE              IS  DYNAMIC
000300                             RECORD KEY               IS  元−元号区分
000310                             FILE STATUS              IS  状態キー
000320                             LOCK        MODE         IS  AUTOMATIC.
000330     SELECT  名称マスタ      ASSIGN      TO        MEISYOL
000340                             ORGANIZATION             IS  INDEXED
000350                             ACCESS MODE              IS  DYNAMIC
000360                             RECORD KEY               IS  名−区分コード
000370                                                          名−名称コード
000380                             FILE STATUS              IS  状態キー
000390                             LOCK        MODE         IS  AUTOMATIC.
000400     SELECT  制御情報マスタ  ASSIGN      TO        SEIGYOL
000410                             ORGANIZATION             IS  INDEXED
000420                             ACCESS MODE              IS  DYNAMIC
000430                             RECORD KEY               IS  制−制御区分
000440                             FILE STATUS              IS  状態キー
000450                             LOCK        MODE         IS  AUTOMATIC.
000460     SELECT  施術所情報マスタ ASSIGN      TO        SEJOHOL
000470                             ORGANIZATION             IS  INDEXED
000480                             ACCESS MODE              IS  DYNAMIC
000490                             RECORD KEY               IS 施情−施術所番号
000500                             FILE STATUS              IS  状態キー
000510                             LOCK        MODE         IS  AUTOMATIC.
000520     SELECT  市町村マスタ    ASSIGN      TO        SITYOSNL
000530                             ORGANIZATION             IS  INDEXED
000540                             ACCESS MODE              IS  DYNAMIC
000550                             RECORD KEY               IS  市−公費種別
000560                                                          市−市町村番号
000570                             ALTERNATE RECORD KEY     IS  市−公費種別
000580                                                          市−市町村名称
000590                                                          市−市町村番号
000600                             FILE STATUS              IS  状態キー
000610                             LOCK        MODE         IS  AUTOMATIC.
000620     SELECT  保険者マスタ    ASSIGN      TO        HOKENSL
000630                             ORGANIZATION             IS  INDEXED
000640                             ACCESS MODE              IS  DYNAMIC
000650                             RECORD KEY               IS  保−保険種別
000660                                                          保−保険者番号
000670                             ALTERNATE RECORD KEY     IS  保−保険種別
000680                                                          保−保険者名称
000690                                                          保−保険者番号
000700                             FILE STATUS              IS  状態キー
000710                             LOCK        MODE         IS  AUTOMATIC.
000720     SELECT  作業ファイル１  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5801L.DAT"
000730                             ORGANIZATION             IS  INDEXED
000740                             ACCESS                   IS  DYNAMIC
000905                             RECORD      KEY          IS  作１−請求和暦年月
000907                                                          作１−分類コード
                                                                作１−県コード
                                                                作１−保険順
000908                                                          作１−分類保険者番号
000909                                                          作１−本人家族キー
                                                                作１−負担割合
000910                                                          作１−患者カナ
000911                                                          作１−患者コード
000912                                                          作１−施術和暦年月
000820                             FILE        STATUS       IS  状態キー
000830                             LOCK        MODE         IS  AUTOMATIC.
000840     SELECT  印刷ファイル    ASSIGN      TO     GS-PRTF001
000850                             SYMBOLIC    DESTINATION  IS "PRT"
000860                             FORMAT                   IS  定義体名Ｐ
000870                             GROUP                    IS  項目群名Ｐ
000880                             PROCESSING  MODE         IS  処理種別Ｐ
000890                             UNIT        CONTROL      IS  拡張制御Ｐ
000900                             FILE        STATUS       IS  通知情報Ｐ.
000910******************************************************************
000920*                      DATA DIVISION                             *
000930******************************************************************
000940 DATA                    DIVISION.
000950 FILE                    SECTION.
000960*                           ［ＲＬ＝  １２８］
000970 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
000980     COPY GENGOU          OF  XFDLIB  JOINING   元   AS  PREFIX.
000990*                           ［ＲＬ＝  １２８］
001000 FD  名称マスタ          BLOCK   CONTAINS   1   RECORDS.
001010     COPY MEISYO          OF  XFDLIB  JOINING   名   AS  PREFIX.
001020*                           ［ＲＬ＝  ２５６］
001030 FD  制御情報マスタ          BLOCK   CONTAINS   1   RECORDS.
001040     COPY SEIGYO          OF  XFDLIB  JOINING   制   AS  PREFIX.
001050*                           ［ＲＬ＝  ６４０］
001060 FD  施術所情報マスタ          BLOCK   CONTAINS   1   RECORDS.
001070     COPY SEJOHO         OF  XFDLIB  JOINING   施情   AS  PREFIX.
001080*                           ［ＲＬ＝  ２５６］
001090 FD  市町村マスタ        BLOCK   CONTAINS   1   RECORDS.
001100     COPY SITYOSN         OF  XFDLIB  JOINING   市   AS  PREFIX.
001110*                           ［ＲＬ＝  ２５６］
001120 FD  保険者マスタ        BLOCK   CONTAINS   1   RECORDS.
001130     COPY HOKENS          OF  XFDLIB  JOINING   保   AS  PREFIX.
001140*
001150 FD  作業ファイル１ RECORD  CONTAINS 256 CHARACTERS.
001160 01  作１−レコード.
001170     03  作１−レコードキー.
001450         05  作１−請求和暦年月.
001460             07  作１−請求和暦            PIC 9.
001470             07  作１−請求年              PIC 9(2).
001480             07  作１−請求月              PIC 9(2).
001261         05  作１−分類コード              PIC 9(2).
001261         05  作１−県コード                PIC X(2).
001400         05  作１−保険順                  PIC 9(2).
001271         05  作１−分類保険者番号          PIC X(8).
001280         05  作１−本人家族キー            PIC 9.
               05  作１−負担割合                PIC 9(2).
001300         05  作１−患者カナ                PIC X(50).
001310         05  作１−患者コード.
001320             07 作１−患者番号             PIC 9(6).
001330             07 作１−枝番                 PIC X(1).
001340         05  作１−施術和暦年月.
001350             07  作１−施術和暦            PIC 9.
001360             07  作１−施術年              PIC 9(2).
001370             07  作１−施術月              PIC 9(2).
001330     03  作１−レコードデータ.
001271         05  作１−保険者番号              PIC X(8).
001340         05  作１−患者氏名                PIC X(50).
001350         05  作１−被保険者氏名            PIC X(50).
001360         05  作１−本人家族区分            PIC 9.
001370         05  作１−費用額                  PIC 9(7).
001380         05  作１−負担額                  PIC 9(7).
001390         05  作１−請求額                  PIC 9(7).
001400         05  作１−保険種別                PIC 9(2).
001410         05  作１−親保険種別              PIC 9(2).
001420         05  作１−老人レコード区分        PIC 9.
001430         05  作１−助成レコード区分        PIC 9.
001440         05  FILLER                        PIC X(36).
001450*
001460 FD  印刷ファイル.
001470     COPY YAS5821P        OF  XMDLIB.
001480*----------------------------------------------------------------*
001490******************************************************************
001500*                WORKING-STORAGE SECTION                         *
001510******************************************************************
001520 WORKING-STORAGE         SECTION.
001530 01 キー入力                           PIC X    VALUE SPACE.
001540 01 状態キー                           PIC X(2) VALUE SPACE.
001550 01 終了フラグ                         PIC X(3) VALUE SPACE.
001560 01 終了行フラグ                       PIC X(3) VALUE SPACE.
001570 01 ファイル名                         PIC N(10).
001580 01 対象なしフラグ                     PIC X(3) VALUE SPACE.
001581 01 オープンフラグ                     PIC X(3)  VALUE SPACE.
001590*
001600 01 保険種別ＰＷ                       PIC 9(2)  VALUE ZERO.
001610 01 印刷順序Ｗ                         PIC 9(2)  VALUE ZERO.
001620 01 請求和暦年月ＰＷ.
001630    03 請求和暦ＰＷ                    PIC 9    VALUE ZERO.
001640    03 請求年ＰＷ                      PIC 9(2) VALUE ZERO.
001650    03 請求月ＰＷ                      PIC 9(2) VALUE ZERO.
001660*
001670 01 保険名称Ｗ                         PIC N(3) VALUE SPACE.
001680 01 備考Ｗ                             PIC X(20).
001690 01 前和暦Ｗ                           PIC 9    VALUE ZERO.
001700 01 行カウンタ                         PIC 9(2) VALUE 0.
001710 01 頁カウンタ                         PIC 9(4) VALUE 0.
001720 01 番号カウンタ                       PIC 9(4) VALUE 0.
001730 01 最大行数                           PIC 9(2) VALUE 0.
001740 01 ヘッダ行数                         PIC 9(2) VALUE 0.
001750 01 移動行数Ｗ                         PIC 9(2) VALUE 0.
001760 01 処理移動キー                       PIC X(4) VALUE SPACE.
001770 01 カレント元号Ｗ                     PIC 9(1) VALUE ZERO.
001780*
001790 01 会員番号Ｗ.
001800    03 会名                            PIC N(2) VALUE SPACE.
001810    03 ハイフン                        PIC X(1) VALUE "-".
001820    03 会員番号ＷＰ                    PIC X(10) VALUE SPACE.
001830*
001840 01 保険種別名称Ｗ.
001850    03 保険種別名称ＷＰ                PIC X(6) VALUE SPACE.
001860 01 親保険種別名称Ｗ.
001870    03 親保険種別名称ＷＰ              PIC X(4) VALUE SPACE.
001880    03 合計文字                        PIC X(4).
001890 01 総合計Ｗ.
001900    03 総合計ＷＰ                      PIC X(6).
001910*
001920** 氏名分離用
001930 01 被保険者カナＷ.
001940    03 被保険者カナＷＰ                PIC X    OCCURS 20 VALUE SPACE.
001950 01 名字カナＷ.
001960    03 名字カナＷＰ                    PIC X    OCCURS 20 VALUE SPACE.
001970 01 名前カナＷ.
001980    03 名前カナＷＰ                    PIC X    OCCURS 20 VALUE SPACE.
001990 01 カウンタ                           PIC 9(2) VALUE ZERO.
002000 01 Ｍカウンタ                         PIC 9(2) VALUE ZERO.
002010 01 Ｎカウンタ                         PIC 9(2) VALUE ZERO.
002020 01 名字フラグ                         PIC X(3) VALUE SPACE.
002030**
002040 01 小計Ｗ.
002050    03 小計件数Ｗ                      PIC 9(4) VALUE ZERO.
002060    03 費用額小計Ｗ                    PIC 9(7) VALUE ZERO.
002070    03 負担額小計Ｗ                    PIC 9(7) VALUE ZERO.
002080    03 請求額小計Ｗ                    PIC 9(7) VALUE ZERO.
002090 01 合計Ｗ.
002100    03 合計件数Ｗ                      PIC 9(4) VALUE ZERO.
002110    03 費用額合計Ｗ                    PIC 9(8) VALUE ZERO.
002120    03 負担額合計Ｗ                    PIC 9(8) VALUE ZERO.
002130    03 請求額合計Ｗ                    PIC 9(8) VALUE ZERO.
002140 01 保険者名称Ｗ.
002150    03 保険者名称１Ｗ                  PIC X(20) VALUE SPACE.
002160    03 保険者名称２Ｗ                  PIC X(20) VALUE SPACE.
002170*
002180 01 施術和暦年月Ｗ.
002190     03 施術和暦Ｗ                   PIC 9.
002200     03 施術年月Ｗ.
002210        05 施術年Ｗ                  PIC 9(2).
002220        05 年Ｗ                      PIC N(1).
002230        05 施術月Ｗ                  PIC 9(2).
002240        05 月Ｗ                      PIC N(1).
002250 01 現在和暦年月Ｗ.
002260    03 現在和暦Ｗ                    PIC 9.
002270    03 現在年Ｗ                      PIC 9(2).
002280    03 現在月Ｗ                      PIC 9(2).
002290*
002300 01 保険種別ＷＲ                     PIC 9(2) VALUE ZERO.
002310 01 印刷形式ＷＲ                     PIC 9    VALUE ZERO.
002320*
002330** 月末日用
002340 01 施術西暦年Ｗ                     PIC 9(4)  VALUE ZERO.
002350 01 商Ｗ                             PIC 9(3)  VALUE ZERO.
002360 01 余Ｗ                             PIC 9(3)  VALUE ZERO.
002370*
002380 01 提出和暦年月Ｗ.
002390     03 提出和暦Ｗ                   PIC 9    VALUE ZERO.
002400     03 提出年月日Ｗ.
002410        05 提出年Ｗ                  PIC 9(2) VALUE ZERO.
002420        05 提出月Ｗ                  PIC 9(2) VALUE ZERO.
002430        05 提出日Ｗ                  PIC 9(2) VALUE ZERO.
002440*
002450*******************************************************************
002460 01 印刷制御.
002470     03 定義体名Ｐ                     PIC X(8) VALUE SPACE.
002480     03 項目群名Ｐ                     PIC X(8) VALUE SPACE.
002490     03 処理種別Ｐ                     PIC X(2) VALUE SPACE.
002500     03 拡張制御Ｐ.
002510         05 端末制御Ｐ.
002520             07 移動方向Ｐ             PIC X(1).
002530             07 移動行数Ｐ             PIC 9(3).
002540         05 詳細制御Ｐ                 PIC X(2).
002550     03 通知情報Ｐ                     PIC X(2) VALUE SPACE.
002560     03 ユニット名Ｐ                   PIC X(8) VALUE SPACE.
002570*
002580 01 計算機西暦年Ｗ                     PIC 9(2).
002590* 日付ＷＯＲＫ
002600 01 和暦終了年Ｗ                       PIC 9(4).
002610 01 計算機西暦.
002620    03 計算機西暦年                    PIC 9(4).
002630    03 計算機西暦月日                  PIC 9(4).
002640 01 計算機西暦Ｒ REDEFINES 計算機西暦.
002650    03 計算機世紀                      PIC 9(2).
002660    03 計算機日付                      PIC 9(6).
002670    03 計算機日付Ｒ REDEFINES 計算機日付.
002680       05 計算機年月                   PIC 9(4).
002690       05 計算機年月Ｒ REDEFINES 計算機年月.
002700         07 計算機年                   PIC 9(2).
002710         07 計算機月                   PIC 9(2).
002720       05 計算機日                     PIC 9(2).
002730*
002740******************************************************************
002750*                          連結項目                              *
002760******************************************************************
002770*
002780********************
002790* メッセージ表示キー *
002800********************
002810 01 連メ−キー IS EXTERNAL.
002820    03  連メ−メッセージ                 PIC N(20).
002830*
002850 01 連入−画面情報ＹＡＳ５８０ IS EXTERNAL.
002851    03 連入−請求和暦年月.
002860       05 連入−請求和暦               PIC 9.
002870       05 連入−請求年月.
002880         07 連入−請求年               PIC 9(2).
002890         07 連入−請求月               PIC 9(2).
002900*
002910*
002911 01 連印−画面情報ＹＡＳ５８０ IS EXTERNAL GLOBAL.
002912    03 連印−プレビュー区分            PIC 9.
002913*
002914************************************
002915* プリンタファイル作成用           *
002916************************************
002917 01 Ｈ連ＰＲＴＦ−作成データ IS EXTERNAL.
002918     03 Ｈ連ＰＲＴＦ−ファイル名           PIC X(8).
002919     03 Ｈ連ＰＲＴＦ−プレビュー区分       PIC 9.
002920     03 Ｈ連ＰＲＴＦ−帳票プログラム名     PIC X(8).
002921     03 Ｈ連ＰＲＴＦ−オーバレイ名         PIC X(8).
002922******************************************************************
002923*                      PROCEDURE  DIVISION                       *
002930******************************************************************
002940 PROCEDURE               DIVISION.
002950************
002960*           *
002970* 初期処理   *
002980*           *
002990************
003000     MOVE SPACE TO オープンフラグ.
003001     PERFORM プリンタファイル作成.
003002     PERFORM 初期化.
003010     MOVE SPACE  TO YAS5821P.
003020************
003030*           *
003040* 主処理     *
003050*           *
003060************
003070*
003080     PERFORM 印刷処理.
003090*
003100************
003110*           *
003120* 終了処理   *
003130*           *
003140************
003150     PERFORM 終了処理.
003160     EXIT PROGRAM.
003170*
003180*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003190*================================================================*
003191 プリンタファイル作成 SECTION.
003192*================================================================*
003193*   / 初期化 /
003194     MOVE SPACE TO Ｈ連ＰＲＴＦ−作成データ.
003195     INITIALIZE Ｈ連ＰＲＴＦ−作成データ.
003196*
003197*
003198*--↓↓ 変更箇所 ↓↓--------------------------------------*
003199*   使用するプリンタファイル名セット
003200     MOVE "PRTF001"             TO Ｈ連ＰＲＴＦ−ファイル名.
003201*
003202*   使用する帳票プログラム名セット
003203     MOVE "YAS5821"             TO Ｈ連ＰＲＴＦ−帳票プログラム名.
003204*
003205*--↑↑-----------------------------------------------------*
003206*
003207*   / プレビュー区分セット /
003208     MOVE 連印−プレビュー区分 TO Ｈ連ＰＲＴＦ−プレビュー区分.
003209*
003210     CALL   "CRTPRTF".
003211     CANCEL "CRTPRTF".
003212*
003213*================================================================*
003214 初期化 SECTION.
003215*
003220     PERFORM ファイルオープン.
003230*    /* 現在日付取得 */
003240     ACCEPT 計算機日付 FROM DATE.
003250*    /* 1980〜2079年の間で設定 */
003260     IF 計算機年 > 80
003270         MOVE 19 TO 計算機世紀
003280     ELSE
003290         MOVE 20 TO 計算機世紀
003300     END-IF.
003310     PERFORM カレント元号取得.
003320     PERFORM 和暦終了年取得.
003330**     COMPUTE 計算機西暦年Ｗ = 計算機西暦年 - 1988.
003340*
003350*================================================================*
003360 カレント元号取得 SECTION.
003370*
003380     MOVE ZEROS TO 制−制御区分.
003390     READ 制御情報マスタ
003400     NOT INVALID KEY
003410         MOVE 制−カレント元号 TO カレント元号Ｗ
003420     END-READ.
003430*
003440*================================================================*
003450 和暦終了年取得 SECTION.
003460*
003470     MOVE カレント元号Ｗ TO 元−元号区分.
003480     READ 元号マスタ
003490     INVALID KEY
003500         DISPLAY NC"指定和暦が登録されていません" UPON CONS
003510         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003520                                                  UPON CONS
003530         ACCEPT  キー入力 FROM CONS
003540         PERFORM 終了処理
003550         EXIT PROGRAM
003560     NOT INVALID KEY
003570         COMPUTE 前和暦Ｗ = カレント元号Ｗ - 1
003580         MOVE 前和暦Ｗ TO 元−元号区分
003590         READ 元号マスタ
003600         INVALID KEY
003610             DISPLAY NC"指定和暦が登録されていません" UPON CONS
003620             DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003630                                                      UPON CONS
003640             ACCEPT  キー入力 FROM CONS
003650             PERFORM 終了処理
003660             EXIT PROGRAM
003670         NOT INVALID KEY
003680             MOVE 元−終了西暦年 TO 和暦終了年Ｗ
003690         END-READ
003700     END-READ.
003710*
003720*================================================================*
003730 ファイルオープン SECTION.
003740*
003750     OPEN INPUT 元号マスタ
003760         MOVE NC"元号" TO ファイル名.
003770         PERFORM オープンチェック.
003780     OPEN INPUT 名称マスタ
003790         MOVE NC"名称" TO ファイル名.
003800         PERFORM オープンチェック.
003810     OPEN INPUT 制御情報マスタ
003820         MOVE NC"制御情報" TO ファイル名.
003830         PERFORM オープンチェック.
003840     OPEN INPUT 施術所情報マスタ
003850         MOVE NC"施術所情報" TO ファイル名.
003860         PERFORM オープンチェック.
003870     OPEN INPUT 市町村マスタ.
003880         MOVE NC"市町" TO ファイル名.
003890         PERFORM オープンチェック.
003900     OPEN INPUT 保険者マスタ.
003910         MOVE NC"保険" TO ファイル名.
003920         PERFORM オープンチェック.
003930     OPEN INPUT 作業ファイル１
003940         MOVE NC"作業１" TO ファイル名.
003950         PERFORM オープンチェック.
003980*================================================================*
003990 オープンチェック SECTION.
004000*
004010     IF 状態キー  NOT =  "00"
004020         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
004030         DISPLAY NC"状態キー：" 状態キー         UPON CONS
004040         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004050                                                 UPON CONS
004060         ACCEPT  キー入力 FROM CONS
004070         PERFORM ファイル閉鎖
004080         EXIT PROGRAM.
004090*================================================================*
004100 ファイル閉鎖 SECTION.
004110*
004111     IF ( オープンフラグ = "YES" )
004112         CLOSE 印刷ファイル
004113     ELSE
004114         MOVE  NC"　　データが０件です。確認して下さい。" TO 連メ−メッセージ
004115         CALL   "MSG001"
004116         CANCEL "MSG001"
004117     END-IF.
004118*
004120     CLOSE 元号マスタ 名称マスタ 制御情報マスタ 施術所情報マスタ
004130           市町村マスタ  保険者マスタ 作業ファイル１.
004140*================================================================*
004150 終了処理 SECTION.
004160*
004170     PERFORM ファイル閉鎖.
004180*================================================================*
004190 エラー表示 SECTION.
004200*
004210     DISPLAY NC"状態キー" 状態キー  UPON CONS.
004220     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
004230     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
004240     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
004250     ACCEPT  キー入力 FROM CONS.
004260     PERFORM ファイル閉鎖.
004270     EXIT PROGRAM.
004280*================================================================*
004290 印刷処理 SECTION.
004300*
004310     MOVE SPACE TO 終了フラグ.
004320     INITIALIZE 小計Ｗ.
004330     INITIALIZE 合計Ｗ.
004340     MOVE 27    TO 最大行数.
004350     MOVE 1     TO 頁カウンタ.
004360     MOVE 1     TO 番号カウンタ.
004370     PERFORM 作業ファイル１読込.
004380     IF 終了フラグ = "YES"
004390        MOVE  NC"　　　該当データがありません。" TO 連メ−メッセージ
004400        CALL   "MSG001"
004410        CANCEL "MSG001"
004420        PERFORM 終了処理
004430        EXIT PROGRAM
004440     END-IF.
004450     PERFORM ヘッダセット.
004460     PERFORM ヘッダ印刷処理.
004470     PERFORM UNTIL 終了フラグ = "YES"
004480         IF ( 行カウンタ        >= 最大行数   )
004490             PERFORM 改頁処理
004500             COMPUTE 頁カウンタ = 頁カウンタ + 1
004510             PERFORM ヘッダセット
004520             PERFORM ヘッダ印刷処理
004530*/カウンタはヘッダセットで１をセット済み/120604
004540*             MOVE ZERO TO 行カウンタ
004550         END-IF
004560         PERFORM 明細部セット
004570         PERFORM 明細印刷処理
004580         PERFORM 合計額累計
004590         PERFORM 作業ファイル１読込
004600     END-PERFORM.
004610     PERFORM 総合計セット.
004620     PERFORM 明細印刷処理.
004630*
004640*================================================================*
004650 作業ファイル１読込 SECTION.
004660*
004670     READ 作業ファイル１ NEXT
004680     AT END
004690         MOVE "YES" TO 終了フラグ
004700     END-READ.
004710*
004720*================================================================*
004730 ヘッダ印刷処理 SECTION.
004740*
004741     IF ( オープンフラグ NOT = "YES" )
004742        MOVE "YES" TO オープンフラグ
004743        OPEN I-O  印刷ファイル
004744        PERFORM エラー処理Ｐ
004745     END-IF.
004746*
004750     MOVE "YAS5821P"  TO  定義体名Ｐ.
004760     MOVE "HEAD01"   TO  項目群名Ｐ.
004770     MOVE SPACE      TO  処理種別Ｐ.
004780     MOVE SPACE      TO  拡張制御Ｐ.
004790     WRITE YAS5821P.
004800     PERFORM エラー処理Ｐ.
004810*================================================================*
004820 明細印刷処理 SECTION.
004830*
004840     MOVE "YAS5821P"  TO  定義体名Ｐ.
004850     MOVE "GRP001"   TO  項目群名Ｐ.
004860     MOVE SPACE      TO  処理種別Ｐ.
004870     MOVE SPACE      TO  拡張制御Ｐ.
004880     WRITE YAS5821P.
004890     PERFORM エラー処理Ｐ.
004900     MOVE SPACE      TO YAS5821P.
004910     COMPUTE 行カウンタ = 行カウンタ + 1.
004920*================================================================*
004930 改頁処理  SECTION.
004940
004950     MOVE "YAS5821P" TO  定義体名Ｐ.
004960     MOVE "CT"      TO  処理種別Ｐ.
004970     MOVE "PAGE"    TO  拡張制御Ｐ.
004980     MOVE SPACE     TO  項目群名Ｐ.
004990     WRITE YAS5821P.
005000     PERFORM エラー処理Ｐ.
005010     MOVE SPACE     TO  拡張制御Ｐ.
005020*
005030*================================================================*
005040 ヘッダセット SECTION.
005050*
005060     MOVE 作１−請求年 TO 施術年.
005070     MOVE 作１−請求月 TO 施術月.
005080     MOVE 頁カウンタ   TO ページ.
005090*
005100     MOVE 1    TO 行カウンタ.
005110*
005120*================================================================*
005130 明細部セット SECTION.
005140*
005150     MOVE 番号カウンタ     TO 番号.
005160     COMPUTE 番号カウンタ = 番号カウンタ + 1.
005170     MOVE 02               TO 名−区分コード.
005180     MOVE 作１−保険種別   TO 名−名称コード.
005190     READ 名称マスタ
005200     INVALID KEY
005210         MOVE SPACE     TO 保険種別
005220     NOT INVALID KEY
005230         MOVE 名−略称  TO 保険種別
005240     END-READ.
005250     MOVE 作１−保険者番号    TO 保険者番号.
005260     IF   作１−助成レコード区分 = 1 
005270          IF 作１−保険者番号(1:2) = "99"
005280             MOVE SPACE       TO 保険者番号
005290          END-IF
005300     END-IF.
005310*
005320     IF ( 作１−保険種別 = 5 ) OR ( 作１−保険種別 > 50 )
005330         MOVE 作１−保険種別     TO 市−公費種別
005340         MOVE 作１−保険者番号   TO 市−市町村番号
005350         READ 市町村マスタ
005360         INVALID KEY
005370             MOVE SPACE              TO 保険者名称Ｗ
005380         NOT INVALID KEY
005390             MOVE 市−市町村名称     TO 保険者名称Ｗ
005400         END-READ
005410         MOVE 保険者名称１Ｗ         TO 保険者名称１
005420         MOVE 保険者名称２Ｗ         TO 保険者名称２
005430     ELSE
005440         MOVE 作１−保険種別     TO 保−保険種別
005450         MOVE 作１−保険者番号   TO 保−保険者番号
005460         READ 保険者マスタ
005470         INVALID KEY
005480             MOVE SPACE              TO 保険者名称Ｗ
005490         NOT INVALID KEY
005500             MOVE 保−保険者名称     TO 保険者名称Ｗ
005510         END-READ
005520         MOVE 保険者名称１Ｗ         TO 保険者名称１
005530         MOVE 保険者名称２Ｗ         TO 保険者名称２
005540     END-IF.
005550*** 老人と助成レコードは、患者カナで、本人家族区分 = 1 となる. ***
005560     IF ( 作１−老人レコード区分 = 1 )  OR
005570        ( 作１−助成レコード区分 = 1 ) 
005580        MOVE SPACE               TO 被保険者名
005590        MOVE NC"本人"            TO 本人家族
005600     ELSE
005610        IF 作１−本人家族区分 = 1
005620            MOVE SPACE               TO 被保険者名
005630            MOVE NC"本人"            TO 本人家族
005640        ELSE
005650            MOVE 作１−被保険者氏名  TO 被保険者名
005660            MOVE NC"家族"            TO 本人家族
005670        END-IF
005680     END-IF.
005690**
005700     MOVE 作１−患者氏名     TO 受診者名.
005710     MOVE 作１−費用額       TO 費用額.
005720     MOVE 作１−請求額       TO 請求額.
005730*
005740*================================================================*
005750 合計額累計 SECTION.
005760*
005770     COMPUTE 合計件数Ｗ     = 合計件数Ｗ      + 1.
005780     COMPUTE 費用額合計Ｗ   = 費用額合計Ｗ    + 作１−費用額.
005790     COMPUTE 負担額合計Ｗ   = 負担額合計Ｗ    + 作１−負担額.
005800     COMPUTE 請求額合計Ｗ   = 請求額合計Ｗ    + 作１−請求額.
005810*================================================================*
005820 総合計セット SECTION.
005830*
005840     MOVE NC"合計"     TO  本人家族.
005850     MOVE 費用額合計Ｗ TO  費用額.
005860     MOVE 請求額合計Ｗ TO  請求額.
005870*================================================================*
005880 エラー処理Ｐ SECTION.
005890*
005900     IF 通知情報Ｐ NOT = "00"
005910         DISPLAY NC"帳票エラー"              UPON CONS
005920         DISPLAY NC"項目群名Ｐ：" 項目群名Ｐ UPON CONS
005930         DISPLAY NC"通知情報Ｐ：" 通知情報Ｐ UPON CONS
005940         DISPLAY NC"拡張制御Ｐ：" 拡張制御Ｐ UPON CONS
005950         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
005960                                             UPON CONS
005970         ACCEPT  キー入力 FROM CONS
005980         PERFORM ファイル閉鎖
005990         EXIT PROGRAM
006000     END-IF.
006010*================================================================*
006020******************************************************************
006030 END PROGRAM YAS5821.
006040******************************************************************
