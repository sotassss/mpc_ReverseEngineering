000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YJB582.
000060 AUTHOR.                 池田 幸子
000070*
000080*----------------------------------------------------------------*
000090*         総括表印刷（柔ｳｨﾝﾄﾞｳｽﾞ95版）
000100* 請求年月Ver.
000110*         MED = YJB580G YJB582P
      * 画面をＵＳＢ作成と一緒にした
000120*----------------------------------------------------------------*
000120 DATE-WRITTEN.           2010-04-08
000130 DATE-COMPILED.          2010-04-08
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000270     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000280                             ORGANIZATION             IS  INDEXED
000290                             ACCESS MODE              IS  DYNAMIC
000300                             RECORD KEY               IS  元−元号区分
000310                             FILE STATUS              IS  状態キー
000320                             LOCK        MODE         IS  AUTOMATIC.
000460     SELECT  施術所情報マスタ ASSIGN      TO        SEJOHOL
000470                             ORGANIZATION             IS  INDEXED
000480                             ACCESS MODE              IS  DYNAMIC
000490                             RECORD KEY               IS 施情−施術所番号
000500                             FILE STATUS              IS  状態キー
000510                             LOCK        MODE         IS  AUTOMATIC.
000511     SELECT  作業ファイル１  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5801L.DAT"
000512                             ORGANIZATION             IS  SEQUENTIAL
000513                             ACCESS                   IS  SEQUENTIAL
000514                             FILE        STATUS       IS  状態キー
000515                             LOCK        MODE         IS  AUTOMATIC.
000690     SELECT  印刷ファイル    ASSIGN      TO     GS-PRTF001
000700                             SYMBOLIC    DESTINATION  IS "PRT"
000710                             FORMAT                   IS  定義体名Ｐ
000720                             GROUP                    IS  項目群名Ｐ
000730                             PROCESSING  MODE         IS  処理種別Ｐ
000740                             UNIT        CONTROL      IS  拡張制御Ｐ
000750                             FILE        STATUS       IS  通知情報Ｐ.
000760******************************************************************
000770*                      DATA DIVISION                             *
000780******************************************************************
000790 DATA                    DIVISION.
000800 FILE                    SECTION.
000810*                           ［ＲＬ＝  １２８］
000820 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
000830     COPY GENGOU          OF  XFDLIB  JOINING   元   AS  PREFIX.
000900*                           ［ＲＬ＝  ６４０］
000910 FD  施術所情報マスタ          BLOCK   CONTAINS   1   RECORDS.
000920     COPY SEJOHO         OF  XFDLIB  JOINING   施情   AS  PREFIX.
000941*
000942 FD  作業ファイル１ RECORD  CONTAINS 200 CHARACTERS.
000943 01  作１−レコード.
000944     03  作１−レコードデータ.
000945         05  作１−請求和暦年月.
000946             07  作１−請求和暦            PIC 9.
000947             07  作１−請求年              PIC 9(2).
000948             07  作１−請求月              PIC 9(2).
000949         05  作１−施術和暦年月.
000950             07  作１−施術和暦            PIC 9.
000951             07  作１−施術年              PIC 9(2).
000952             07  作１−施術月              PIC 9(2).
000953         05  作１−助成区分                PIC 9.
000954         05  作１−保険者番号              PIC X(10).
000955         05  作１−記号                    PIC X(24).
000956         05  作１−番号                    PIC X(30).
000957         05  作１−被保険者カナ            PIC X(20).
000958         05  作１−患者カナ                PIC X(20).
000959         05  作１−本人家族区分            PIC 9.
000960         05  作１−費用額                  PIC 9(7).
000961         05  作１−負担額                  PIC 9(7).
000962         05  作１−請求額                  PIC 9(7).
000963         05  作１−患者コード.
000964             07 作１−患者番号             PIC 9(6).
000965             07 作１−枝番                 PIC X(1).
000966         05  作１−保険種別                PIC 9(2).
000967         05  作１−親保険種別              PIC 9(2).
000968         05  作１−老人レコード区分        PIC 9.
000969         05  作１−助成レコード区分        PIC 9.
000970         05  FILLER                        PIC X(50).
000971*
001340*
001350 FD  印刷ファイル.
001360     COPY YJB582P        OF  XMDLIB.
001370*----------------------------------------------------------------*
001380******************************************************************
001390*                WORKING-STORAGE SECTION                         *
001400******************************************************************
001410 WORKING-STORAGE         SECTION.
001420 01 キー入力                           PIC X    VALUE SPACE.
001430 01 状態キー                           PIC X(2) VALUE SPACE.
001440 01 終了フラグ                         PIC X(3) VALUE SPACE.
001450 01 終了行フラグ                       PIC X(3) VALUE SPACE.
001460 01 ファイル名                         PIC N(10).
001470 01 対象なしフラグ                     PIC X(3) VALUE SPACE.
001210 01 オープンフラグ                     PIC X(3)   VALUE SPACE.
001480*
001490 01 助成区分ＰＷ                       PIC 9     VALUE ZERO.
001500 01 保険種別ＰＷ                       PIC 9(2)  VALUE ZERO.
001510 01 印刷順序Ｗ                         PIC 9(2)  VALUE ZERO.
001520 01 請求和暦年月ＰＷ.
001530    03 請求和暦ＰＷ                    PIC 9    VALUE ZERO.
001540    03 請求年ＰＷ                      PIC 9(2) VALUE ZERO.
001550    03 請求月ＰＷ                      PIC 9(2) VALUE ZERO.
001560*
001570 01 保険名称Ｗ                         PIC N(3) VALUE SPACE.
001580 01 備考Ｗ                             PIC X(20).
001590 01 前和暦Ｗ                           PIC 9    VALUE ZERO.
001600 01 行カウンタ                         PIC 9(2) VALUE 0.
001610 01 頁カウンタ                         PIC 9(4) VALUE 0.
001620 01 最大行数                           PIC 9(2) VALUE 0.
001630 01 ヘッダ行数                         PIC 9(2) VALUE 0.
001640 01 移動行数Ｗ                         PIC 9(2) VALUE 0.
001650 01 処理移動キー                       PIC X(4) VALUE SPACE.
001660 01 カレント元号Ｗ                     PIC 9(1) VALUE ZERO.
001670*
001680 01 会員番号Ｗ.
001690    03 会名                            PIC N(2) VALUE SPACE.
001700    03 ハイフン                        PIC X(1) VALUE "-".
001710    03 会員番号ＷＰ                    PIC X(10) VALUE SPACE.
001720*
001730 01 保険種別名称Ｗ.
001740    03 保険種別名称ＷＰ                PIC X(6) VALUE SPACE.
001750 01 親保険種別名称Ｗ.
001760    03 親保険種別名称ＷＰ              PIC X(4) VALUE SPACE.
001770    03 合計文字                        PIC X(4).
001780 01 総合計Ｗ.
001790    03 総合計ＷＰ                      PIC X(6).
001800*
001810** 氏名分離用
001820 01 被保険者カナＷ.
001830    03 被保険者カナＷＰ                PIC X    OCCURS 20 VALUE SPACE.
001840 01 名字カナＷ.
001850    03 名字カナＷＰ                    PIC X    OCCURS 20 VALUE SPACE.
001860 01 名前カナＷ.
001870    03 名前カナＷＰ                    PIC X    OCCURS 20 VALUE SPACE.
001880 01 カウンタ                           PIC 9(2) VALUE ZERO.
001890 01 Ｍカウンタ                         PIC 9(2) VALUE ZERO.
001900 01 Ｎカウンタ                         PIC 9(2) VALUE ZERO.
001910 01 名字フラグ                         PIC X(3) VALUE SPACE.
001920**
001930 01 小計Ｗ.
001940    03 小計件数Ｗ                      PIC 9(4) VALUE ZERO.
001950    03 費用額小計Ｗ                    PIC 9(7) VALUE ZERO.
001960    03 負担額小計Ｗ                    PIC 9(7) VALUE ZERO.
001970    03 請求額小計Ｗ                    PIC 9(7) VALUE ZERO.
001980 01 合計Ｗ.
001990    03 合計件数Ｗ                      PIC 9(4) VALUE ZERO.
002000    03 費用額合計Ｗ                    PIC 9(8) VALUE ZERO.
002010    03 負担額合計Ｗ                    PIC 9(8) VALUE ZERO.
002020    03 請求額合計Ｗ                    PIC 9(8) VALUE ZERO.
002030*
002040 01 施術和暦年月Ｗ.
002050     03 施術和暦Ｗ                   PIC 9.
002060     03 施術年月Ｗ.
002070        05 施術年Ｗ                  PIC 9(2).
002080        05 年Ｗ                      PIC N(1).
002090        05 施術月Ｗ                  PIC 9(2).
002100        05 月Ｗ                      PIC N(1).
002110 01 現在和暦年月Ｗ.
002120    03 現在和暦Ｗ                    PIC 9.
002130    03 現在年Ｗ                      PIC 9(2).
002140    03 現在月Ｗ                      PIC 9(2).
002150*
002160 01 保険種別ＷＲ                     PIC 9(2) VALUE ZERO.
002170 01 印刷形式ＷＲ                     PIC 9    VALUE ZERO.
002171*
002172** 月末日用
002173 01 施術西暦年Ｗ                     PIC 9(4)  VALUE ZERO.
002174 01 商Ｗ                             PIC 9(3)  VALUE ZERO.
002175 01 余Ｗ                             PIC 9(3)  VALUE ZERO.
002176*
002177 01 提出和暦年月Ｗ.
002178     03 提出和暦Ｗ                   PIC 9    VALUE ZERO.
002179     03 提出年月日Ｗ.
002180        05 提出年Ｗ                  PIC 9(2) VALUE ZERO.
002181        05 提出月Ｗ                  PIC 9(2) VALUE ZERO.
002182        05 提出日Ｗ                  PIC 9(2) VALUE ZERO.
002183*
002184*******************************************************************
002190 01 印刷制御.
002200     03 定義体名Ｐ                     PIC X(8) VALUE SPACE.
002210     03 項目群名Ｐ                     PIC X(8) VALUE SPACE.
002220     03 処理種別Ｐ                     PIC X(2) VALUE SPACE.
002230     03 拡張制御Ｐ.
002240         05 端末制御Ｐ.
002250             07 移動方向Ｐ             PIC X(1).
002260             07 移動行数Ｐ             PIC 9(3).
002270         05 詳細制御Ｐ                 PIC X(2).
002280     03 通知情報Ｐ                     PIC X(2) VALUE SPACE.
002290     03 ユニット名Ｐ                   PIC X(8) VALUE SPACE.
002300*
002460******************************************************************
002470*                          連結項目                              *
002480******************************************************************
002490*
002491********************
002492* メッセージ表示キー *
002493********************
002494 01 連メ−キー IS EXTERNAL.
002495    03  連メ−メッセージ                 PIC N(20).
002497*
002500 01 連入−画面情報ＹＪＢ５８０ IS EXTERNAL.
002510    03 連入−請求和暦年月.
002520       05 連入−請求和暦               PIC 9.
002530       05 連入−請求年月.
002540         07 連入−請求年               PIC 9(2).
002550         07 連入−請求月               PIC 9(2).
      *
       01 連印−画面情報ＹＪＢ５８０ IS EXTERNAL.
          03 連印−開始頁                    PIC 9(3).
          03 連印−終了頁                    PIC 9(3).
          03 連印−プレビュー区分            PIC 9.
002560*
000540************************************
000550* プリンタファイル作成用           *
000560************************************
000570 01 Ｈ連ＰＲＴＦ−作成データ IS EXTERNAL.
000580     03 Ｈ連ＰＲＴＦ−ファイル名           PIC X(8).
000590     03 Ｈ連ＰＲＴＦ−プレビュー区分       PIC 9.
000600     03 Ｈ連ＰＲＴＦ−帳票プログラム名     PIC X(8).
000610     03 Ｈ連ＰＲＴＦ−オーバレイ名         PIC X(8).
002980*
002570******************************************************************
002580*                      PROCEDURE  DIVISION                       *
002590******************************************************************
002600 PROCEDURE               DIVISION.
002610************
002620*           *
002630* 初期処理   *
002640*           *
002650************
002560     MOVE SPACE TO オープンフラグ.
002570     PERFORM プリンタファイル作成.
002660     PERFORM 初期化.
002661     PERFORM 月末日取得.
002670     MOVE SPACE  TO YJB582P.
002680************
002690*           *
002700* 主処理     *
002710*           *
002720************
002730*
002740     PERFORM 印刷処理.
002750*
002760************
002770*           *
002780* 終了処理   *
002790*           *
002800************
002810     PERFORM 終了処理.
002820     EXIT PROGRAM.
002830*
002840*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002860*================================================================*
002870 プリンタファイル作成 SECTION.
002880*================================================================*
002890*   / 初期化 /
002900     MOVE SPACE TO Ｈ連ＰＲＴＦ−作成データ.
002910     INITIALIZE Ｈ連ＰＲＴＦ−作成データ.
002920*
002930*
002940*--↓↓ 変更箇所 ↓↓--------------------------------------*
002970*   使用するプリンタファイル名セット
002971     MOVE "PRTF001"             TO Ｈ連ＰＲＴＦ−ファイル名.
002972*
002973*   使用する帳票プログラム名セット
002974     MOVE "YJB582"              TO Ｈ連ＰＲＴＦ−帳票プログラム名.
002975*
002976*--↑↑-----------------------------------------------------*
002980*
002990*   / プレビュー区分セット /
003000     MOVE 連印−プレビュー区分 TO Ｈ連ＰＲＴＦ−プレビュー区分.
003010*
003020     CALL   "CRTPRTF".
003030     CANCEL "CRTPRTF".
003040*
002850*================================================================*
002860 初期化 SECTION.
002870*
002880     PERFORM ファイルオープン.
003040*
003430*================================================================*
003440 ファイルオープン SECTION.
003450*
003460     OPEN INPUT 元号マスタ
003470         MOVE NC"元号" TO ファイル名.
003480         PERFORM オープンチェック.
003550     OPEN INPUT 施術所情報マスタ
003560         MOVE NC"施術所情報" TO ファイル名.
003570         PERFORM オープンチェック.
003580     OPEN INPUT 作業ファイル１
003590         MOVE NC"作業１" TO ファイル名.
003600         PERFORM オープンチェック.
003660*================================================================*
003670 オープンチェック SECTION.
003680*
003690     IF 状態キー  NOT =  "00"
003700         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
003710         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003720         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003730                                                 UPON CONS
000080*-----------------------------------------*
000090         CALL "actcshm"  WITH C LINKAGE
000100*-----------------------------------------*
003740         ACCEPT  キー入力 FROM CONS
003750         PERFORM ファイル閉鎖
003760         EXIT PROGRAM.
003770*================================================================*
003780 ファイル閉鎖 SECTION.
003790*
003570     IF ( オープンフラグ = "YES" )
003580         CLOSE 印刷ファイル
003590     ELSE
003600         MOVE  NC"　　データが０件です。確認して下さい。" TO 連メ−メッセージ
003610         CALL   "MSG001"
003620         CANCEL "MSG001"
003630     END-IF.
003640*
003800     CLOSE 元号マスタ  施術所情報マスタ  作業ファイル１.
003820*================================================================*
003830 終了処理 SECTION.
003840*
003850     PERFORM ファイル閉鎖.
003860*================================================================*
003870 エラー表示 SECTION.
003880*
003890     DISPLAY NC"状態キー" 状態キー  UPON CONS.
003900     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
003910     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
003920     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
004380*-----------------------------------------*
004390     CALL "actcshm"  WITH C LINKAGE.
004400*-----------------------------------------*
003930     ACCEPT  キー入力 FROM CONS.
003940     PERFORM ファイル閉鎖.
003950     EXIT PROGRAM.
003960*================================================================*
003970 印刷処理 SECTION.
003980*
003990     MOVE SPACE TO 終了フラグ.
004000     INITIALIZE 小計Ｗ.
004010     INITIALIZE 合計Ｗ.
004020     MOVE 15    TO 最大行数.
004030     MOVE 1     TO 頁カウンタ.
004040     PERFORM 作業ファイル１読込.
004050     PERFORM UNTIL 終了フラグ = "YES"
004060*
004070         MOVE 作１−請求和暦     TO 請求和暦ＰＷ
004080         MOVE 作１−請求年       TO 請求年ＰＷ
004090         MOVE 作１−請求月       TO 請求月ＰＷ
004100         MOVE 作１−助成区分     TO 助成区分ＰＷ
004110         PERFORM ヘッダセット
004120*
004130         PERFORM UNTIL ( 終了フラグ = "YES" ) OR ( 行カウンタ > 最大行数 )  OR
004140                       ( 作１−請求和暦   NOT = 請求和暦ＰＷ ) OR
004150                       ( 作１−請求年     NOT = 請求年ＰＷ   ) OR
004160                       ( 作１−請求月     NOT = 請求月ＰＷ   ) 
004170             PERFORM 明細部セット
004180             PERFORM 合計額累計
004190             PERFORM 作業ファイル１読込
004200         END-PERFORM
004210*
004220         IF ( 行カウンタ > 最大行数 )
004230             IF  ( 終了フラグ = "YES" ) OR 
004240                 ( 作１−請求和暦   NOT = 請求和暦ＰＷ ) OR
004250                 ( 作１−請求年     NOT = 請求年ＰＷ   ) OR
004260                 ( 作１−請求月     NOT = 請求月ＰＷ   ) 
004270                 PERFORM 印字処理
004280                 PERFORM 改頁処理
004290                 IF  終了フラグ = "YES" 
004300                     PERFORM ヘッダセット
004310                     PERFORM 総合計セット
004320                     PERFORM 印字処理
004330                     PERFORM 改頁処理
004340                 END-IF
004350             ELSE
004360                 PERFORM 印字処理
004370                 PERFORM 改頁処理
004380             END-IF
004390         ELSE
004400             IF  終了フラグ = "YES" 
004410                 IF 行カウンタ > 最大行数
004420                    PERFORM 印字処理
004430                    PERFORM 改頁処理
004440                    PERFORM ヘッダセット
004450                 END-IF
004460                 PERFORM 総合計セット
004470             END-IF
004480             PERFORM 印字処理
004490             PERFORM 改頁処理
004500         END-IF
004510*
004520     END-PERFORM.
004530*
004540*================================================================*
004970*================================================================*
004980 作業ファイル１読込 SECTION.
004990*
005000     READ 作業ファイル１ NEXT
005010     AT END
005020         MOVE "YES" TO 終了フラグ
005030     END-READ.
005040*
005050*================================================================*
005060 印字処理  SECTION.
005070*
006050     IF ( オープンフラグ NOT = "YES" )
006060        MOVE "YES" TO オープンフラグ
006070        OPEN I-O  印刷ファイル
006080        PERFORM エラー処理Ｐ
006090     END-IF.
      */頁指定/0603
           IF (連印−開始頁 <= 頁カウンタ  ) AND
              (頁カウンタ   <= 連印−終了頁)
005080         MOVE "YJB582P" TO  定義体名Ｐ
005090         MOVE SPACE     TO  処理種別Ｐ
005100         MOVE "SCREEN"  TO  項目群名Ｐ
005110         WRITE YJB582P
005120         PERFORM エラー処理Ｐ
           END-IF.
005130     MOVE SPACE TO YJB582P.
005140*================================================================*
005150 改頁処理  SECTION.
005160
      */頁指定/0603
           IF (連印−開始頁 <= 頁カウンタ  ) AND
              (頁カウンタ   <= 連印−終了頁)
005170         MOVE "YJB582P" TO  定義体名Ｐ
005180         MOVE "CT"      TO  処理種別Ｐ
005190         MOVE "PAGE"    TO  拡張制御Ｐ
005200         MOVE SPACE     TO  項目群名Ｐ
005210         WRITE YJB582P
005220         PERFORM エラー処理Ｐ
005230         MOVE SPACE     TO  拡張制御Ｐ
           END-IF.
005240*
005250     COMPUTE 頁カウンタ = 頁カウンタ + 1.
005260*
005270*================================================================*
005280 ヘッダセット SECTION.
005290*
005291     MOVE 提出年Ｗ     TO 提出年.
005292     MOVE 提出月Ｗ     TO 提出月.
005293     MOVE 提出日Ｗ     TO 提出日.
005330     MOVE 頁カウンタ   TO 頁.
005340*
005350* 会員番号を取得
005360     MOVE 00         TO 施情−施術所番号.
005370     READ 施術所情報マスタ
005380     INVALID KEY
005390         MOVE SPACE       TO 会員番号
005400         MOVE SPACE       TO 代表者名
005410     NOT INVALID KEY
005420         MOVE NC"ＪＢ"                TO 会名
005430         MOVE 施情−接骨師会会員番号  TO 会員番号ＷＰ
005440         MOVE 会員番号Ｗ              TO 会員番号
005450         MOVE 施情−代表者名          TO 代表者名
005460     END-READ.
005470*
005480     MOVE 1    TO 行カウンタ.
005490*
005500*================================================================*
005510 明細部セット SECTION.
005520*
005530****     MOVE 作１−印刷順序      TO 保険種別(行カウンタ).
005540     MOVE 作１−保険者番号    TO 保険者番号(行カウンタ).
005550     IF   作１−助成レコード区分 = 1 
005560          IF 作１−保険者番号(1:2) = "99"
005570             MOVE SPACE       TO 保険者番号(行カウンタ)
005580          END-IF
005590     END-IF.
005600*
005610     IF 作１−記号(1:2) = "＊" 
005620        MOVE  SPACE        TO  記号(行カウンタ)
005630     ELSE
005640        MOVE 作１−記号    TO  記号(行カウンタ)
005650     END-IF.
005660     IF ( 作１−番号(1:1) = "*"  ) OR
005670        ( 作１−番号(1:2) = "＊" )
005680        MOVE  SPACE        TO  番号(行カウンタ)
005690     ELSE
005700        MOVE 作１−番号    TO  番号(行カウンタ)
005710     END-IF.
005720*
005730*** 老人と助成レコードは、患者カナで、本人家族区分 = 1 となる. ***
005731*** 13/09 全て患者カナに変更 ***
005740     IF ( 作１−老人レコード区分 = 1 )  OR
005750        ( 作１−助成レコード区分 = 1 ) 
005760        MOVE 作１−患者カナ      TO 被保険者カナＷ
005770        MOVE 1                   TO 本人家族(行カウンタ)
005780     ELSE
005781***        MOVE 作１−被保険者カナ  TO 被保険者カナＷ
005782        MOVE 作１−患者カナ      TO 被保険者カナＷ
005800        MOVE 作１−本人家族区分  TO 本人家族(行カウンタ)
005810     END-IF.
005820**
005830     PERFORM 氏名分離.
005840     MOVE 名字カナＷ         TO 被保険者氏カナ(行カウンタ).
005850     MOVE 名前カナＷ         TO 被保険者名カナ(行カウンタ).
005860     MOVE 作１−費用額       TO 合計額(行カウンタ).
005870     MOVE 作１−請求額       TO 請求額(行カウンタ).
005880*
005890     COMPUTE 行カウンタ = 行カウンタ + 1.
005900*
005910*================================================================*
005920 氏名分離  SECTION.
005930*
005940**** 名字と名前を分離する。
005950*
005960     MOVE 1          TO カウンタ Ｍカウンタ Ｎカウンタ.
005970     MOVE SPACE      TO 名字カナＷ 名前カナＷ 名字フラグ.
005980*
005990     PERFORM VARYING カウンタ FROM 1 BY 1 
006000                     UNTIL ( カウンタ > 20 ) OR ( 名字フラグ = "YES" )
006010          IF 被保険者カナＷＰ(カウンタ)  = SPACE
006020             MOVE "YES" TO  名字フラグ
006030          ELSE
006040             MOVE 被保険者カナＷＰ(カウンタ) TO 名字カナＷＰ(Ｍカウンタ)
006050             COMPUTE Ｍカウンタ = Ｍカウンタ + 1
006060          END-IF
006070     END-PERFORM.
006080*
006090     PERFORM  UNTIL カウンタ > 20 
006100          IF 被保険者カナＷＰ(カウンタ)  = SPACE
006110             CONTINUE
006120          ELSE
006130             MOVE 被保険者カナＷＰ(カウンタ) TO 名前カナＷＰ(Ｎカウンタ)
006140             COMPUTE Ｎカウンタ = Ｎカウンタ + 1
006150          END-IF
006160          COMPUTE カウンタ = カウンタ + 1
006170     END-PERFORM.
006180*
006190*================================================================*
006200 合計額累計 SECTION.
006210*
006220*     COMPUTE 小計件数Ｗ     = 小計件数Ｗ      + 1.
006230*     COMPUTE 費用額小計Ｗ   = 費用額小計Ｗ    + 作１−費用額.
006240*     COMPUTE 負担額小計Ｗ   = 負担額小計Ｗ    + 作１−負担額.
006250*     COMPUTE 請求額小計Ｗ   = 請求額小計Ｗ    + 作１−請求額.
006260*
006270     COMPUTE 合計件数Ｗ     = 合計件数Ｗ      + 1.
006280     COMPUTE 費用額合計Ｗ   = 費用額合計Ｗ    + 作１−費用額.
006290     COMPUTE 負担額合計Ｗ   = 負担額合計Ｗ    + 作１−負担額.
006300     COMPUTE 請求額合計Ｗ   = 請求額合計Ｗ    + 作１−請求額.
006310*================================================================*
006320 総合計セット SECTION.
006330*
006340     MOVE "総合計"     TO  総合計ＷＰ.
006350     MOVE 総合計Ｗ     TO  被保険者氏カナ(行カウンタ).
006360     MOVE 合計件数Ｗ   TO  本人家族(行カウンタ).
006370     MOVE 費用額合計Ｗ TO  合計額(行カウンタ).
006380     MOVE 請求額合計Ｗ TO  請求額(行カウンタ).
006390*================================================================*
006391*================================================================*
006392 月末日取得 SECTION.
006393*
006394     MOVE 連入−請求年   TO 提出年Ｗ.
006395     MOVE 連入−請求月   TO 提出月Ｗ.
006396     MOVE 連入−請求和暦 TO 元−元号区分.
006397     READ 元号マスタ
006398     NOT INVALID KEY
006399         MOVE 元−開始西暦年 TO 施術西暦年Ｗ
006400     END-READ.
006401     IF 施術西暦年Ｗ NOT = ZERO
006402        COMPUTE 施術西暦年Ｗ = 施術西暦年Ｗ + 連入−請求年 - 1
006403     END-IF.
006404*
006405     EVALUATE 連入−請求月
006406     WHEN 4
006407     WHEN 6
006408     WHEN 9
006409     WHEN 11
006410         MOVE 30 TO 提出日Ｗ
006411     WHEN 2
006412         DIVIDE 4 INTO 施術西暦年Ｗ GIVING    商Ｗ
006413                                    REMAINDER 余Ｗ
006414         END-DIVIDE
006415         IF 余Ｗ = ZERO
006416             MOVE 29 TO 提出日Ｗ
006417         ELSE
006418             MOVE 28 TO 提出日Ｗ
006419         END-IF
006420     WHEN 1
006421     WHEN 3
006422     WHEN 5
006423     WHEN 7
006424     WHEN 8
006425     WHEN 10
006426     WHEN 12
006427         MOVE 31 TO 提出日Ｗ
006428     WHEN OTHER
006429          CONTINUE
006430     END-EVALUATE.
006431*
006500*================================================================*
006510 エラー処理Ｐ SECTION.
006520*
006530     IF 通知情報Ｐ NOT = "00"
006540         DISPLAY NC"帳票エラー"              UPON CONS
006550         DISPLAY NC"項目群名Ｐ：" 項目群名Ｐ UPON CONS
006560         DISPLAY NC"通知情報Ｐ：" 通知情報Ｐ UPON CONS
006570         DISPLAY NC"拡張制御Ｐ：" 拡張制御Ｐ UPON CONS
006580         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
006590                                             UPON CONS
004380*-----------------------------------------*
004390         CALL "actcshm"  WITH C LINKAGE
004400*-----------------------------------------*
006600         ACCEPT  キー入力 FROM CONS
006610         PERFORM ファイル閉鎖
006620         EXIT PROGRAM
006630     END-IF.
006640*================================================================*
006650******************************************************************
006660 END PROGRAM YJB582.
006670******************************************************************
