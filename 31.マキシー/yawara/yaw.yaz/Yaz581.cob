000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YAZ581.
000060 AUTHOR.                 池田 幸子
000070*
000080*----------------------------------------------------------------*
000090*      提出用並べ順リスト【ﾃﾞｰﾀ作成】柔+ｳｨﾝﾄﾞｳｽﾞ版
000100* 請求年月バージョン 
000110*      MED = YAZ580 YAZ582P
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2012-05-09
000140 DATE-COMPILED.          2012-05-09
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000590     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000600                             ORGANIZATION             IS  INDEXED
000610                             ACCESS MODE              IS  DYNAMIC
000620                             RECORD KEY               IS 受−施術和暦年月
000630                                                          受−患者コード
000640                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000650                                                          受−患者カナ
000660                                                          受−患者コード
000670                             ALTERNATE RECORD KEY     IS  受−患者コード
000680                                                         受−施術和暦年月
000690                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000700                                                          受−保険種別
000710                                                          受−保険者番号
000720                                                          受−患者コード
000730                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000740                                                          受−公費種別
000750                                                     受−費用負担者番号
000760                                                          受−患者コード
000770                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000780                                                          受−助成種別
000790                                                  受−費用負担者番号助成
000800                                                          受−患者コード
000810                             ALTERNATE RECORD KEY  IS 受−請求和暦年月
000820                                                      受−施術和暦年月
000830                                                      受−患者コード
000840                             FILE STATUS              IS  状態キー
000850                             LOCK        MODE         IS  AUTOMATIC.
000130     SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
000140                             ORGANIZATION             IS  INDEXED
000150                             ACCESS MODE              IS  DYNAMIC
000160                             RECORD KEY               IS  レセ−施術和暦年月
000170                                                          レセ−患者コード
000180                                                          レセ−レセ種別
000190                             ALTERNATE RECORD KEY     IS  レセ−患者コード
000200                                                          レセ−施術和暦年月
000210                                                          レセ−レセ種別
000220                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000230                                                          レセ−施術和暦年月
000240                                                          レセ−患者コード
000250                                                          レセ−レセ種別
000260                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000270                                                          レセ−レセ種別
000280                                                          レセ−請求保険者番号
000290                                                          レセ−患者コード
000300                                                          レセ−施術和暦年月
000310                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
000320                                                          レセ−請求保険者番号
000330                                                          レセ−患者コード
000340                                                          レセ−レセ種別
000350                                                          レセ−施術和暦年月
000360                             FILE STATUS              IS  状態キー
000370                             LOCK        MODE         IS  AUTOMATIC.
000241     SELECT  生保情報Ｆ      ASSIGN      TO        SEIHOJL
000242                             ORGANIZATION           IS INDEXED
000243                             ACCESS MODE            IS DYNAMIC
000244                             RECORD KEY             IS 生保−施術和暦年月
000245                                                       生保−患者コード
000255                             ALTERNATE RECORD KEY   IS 生保−患者コード
000265                                                       生保−施術和暦年月
000277                             FILE STATUS            IS 状態キー
000278                             LOCK        MODE       IS AUTOMATIC.
000241     SELECT  労災情報Ｆ      ASSIGN      TO        ROUSAIJL
000242                             ORGANIZATION             IS INDEXED
000243                             ACCESS MODE              IS DYNAMIC
000244                             RECORD KEY               IS 労災−施術和暦年月
000245                                                         労災−患者コード
000255                             ALTERNATE RECORD KEY     IS 労災−患者コード
000265                                                         労災−施術和暦年月
000277                             FILE STATUS              IS 状態キー
000278                             LOCK        MODE         IS AUTOMATIC.
001130     SELECT  作業ファイル１  ASSIGN      TO        "C:\MAKISHISYS\YAWOBJ\TEMP\W5811L.DAT"
001140                             ORGANIZATION             IS  INDEXED
001150                             ACCESS                   IS  DYNAMIC
001160                             RECORD      KEY          IS  作１−印刷順序
001180                                                          作１−保番
001170                                                          作１−種別
001190                                                          作１−保険者番号
001170                                                          作１−本人家族区分
001200                                                          作１−患者コード
001210                                                          作１−施術和暦年月
001220                             FILE        STATUS       IS  状態キー
001230                             LOCK        MODE         IS  AUTOMATIC.
001240******************************************************************
001250*                      DATA DIVISION                             *
001260******************************************************************
001270 DATA                    DIVISION.
001280 FILE                    SECTION.
001410*                           ［ＲＬ＝  ３２０］
001420 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001430     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
      *                          ［ＲＬ＝  １５３６］
       FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS.
           COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
001080* 
000280 FD  生保情報Ｆ          BLOCK   CONTAINS   1   RECORDS.
000281     COPY SEIHOJ          OF  XFDLIB  JOINING   生保   AS  PREFIX.
001510*
000280 FD  労災情報Ｆ          BLOCK   CONTAINS   1   RECORDS.
000281     COPY ROUSAIJ         OF  XFDLIB  JOINING   労災   AS  PREFIX.
000282*
001520 FD  作業ファイル１ RECORD  CONTAINS 176 CHARACTERS.
001530 01  作１−レコード.
001540     03  作１−レコードキー.
001590         05  作１−印刷順序.
                   07  作１−順番                PIC 9(2).
001570             07  作１−県                  PIC 9(2).
001580             07  作１−保種                PIC 9(1).
001720         05  作１−保番                    PIC X(6).
001600         05  作１−種別                    PIC X(2).
001710         05  作１−保険者番号              PIC X(10).
001700         05  作１−本人家族区分            PIC 9(1).
001620         05  作１−患者コード.
001630             07  作１−患者番号            PIC 9(6).
001640             07  作１−枝番                PIC X(1).
001650         05  作１−施術和暦年月.
001660             07  作１−施術和暦            PIC 9(1).
001670             07  作１−施術年              PIC 9(2).
001680             07  作１−施術月              PIC 9(2).
001690     03  作２−レコードデータ.
001600         05  作１−保険種別                PIC 9(2).
001600         05  作１−助成種別                PIC 9(2).
001740         05  作１−患者氏名                PIC X(50).
001750         05  作１−被保険者氏名            PIC X(50).
001880         05  作１−実日数                  PIC 9(2).
001790         05  作１−費用額                  PIC 9(7).
001800         05  作１−負担額                  PIC 9(7).
001810         05  作１−請求額                  PIC 9(7).
002360         05  作１−前期区分                PIC 9(1).
002370         05  FILLER                        PIC X(12).
002380*
002390*----------------------------------------------------------------*
002400******************************************************************
002410*                WORKING-STORAGE SECTION                         *
002420******************************************************************
002430 WORKING-STORAGE         SECTION.
002440 01 キー入力                           PIC X    VALUE SPACE.
002450 01 状態キー                           PIC X(2) VALUE SPACE.
002460 01 終了フラグ                         PIC X(3) VALUE SPACE.
002470 01 終了フラグ２                       PIC X(3) VALUE SPACE.
002480 01 実行キーＷ                         PIC X(3)  VALUE SPACE.
002490 01 通院フラグ                         PIC X(3)  VALUE SPACE.
002500 01 作成フラグ                         PIC X(3)  VALUE SPACE.
002510 01 初日再検フラグ                     PIC X(3)  VALUE SPACE.
002520 01 前月フラグ                         PIC X(3)  VALUE SPACE.
001750 01 カウンタ                           PIC 9(2)  VALUE ZERO.
002540**
002550 01 保険種別ＷＲ                       PIC 9(2) VALUE ZERO.
002560 01 保険者番号ＷＲ                     PIC X(10) VALUE SPACE.
002570 01 レセプト種類ＷＲ                   PIC X(4) VALUE SPACE.
002580 01 本人家族区分ＷＲ                   PIC 9     VALUE ZERO.
002590 01 請求和暦年月ＷＲ.
002600    03 請求和暦ＷＲ                    PIC 9    VALUE ZERO.
002610    03 請求年ＷＲ                      PIC 9(2) VALUE ZERO.
002620    03 請求月ＷＲ                      PIC 9(2) VALUE ZERO.
002630*
002640** 助成レセまとめ用
002650 01 助成レセまとめフラグ               PIC X(3)  VALUE SPACE.
002660**
002720 01 レセ種別Ｗ                         PIC 9(2) VALUE ZERO.
002690 01 処理移動フラグ                     PIC X(4) VALUE SPACE.
002700 01 作業移動キー                       PIC X(4) VALUE SPACE.
002710 01 ファイル名                         PIC N(2) VALUE SPACE.
002720 01 保険種別Ｗ                         PIC 9(2) VALUE ZERO.
002730 01 印刷保険種別Ｗ                     PIC 9(2) VALUE ZERO.
002740 01 印刷順序Ｗ.
          03 順番Ｗ                          PIC 9(2) VALUE ZERO.
          03 県番Ｗ                          PIC X(2) VALUE SPACE.
          03 保種Ｗ                          PIC 9(1) VALUE ZERO.
002750 01 備考Ｗ                             PIC X(20) VALUE SPACE.
002760 01 部位ＣＮＴ                         PIC 9(1) VALUE ZERO.
002770 01 日数ＣＮＴ                         PIC 9(2) VALUE ZERO.
003920 01 合計Ｗ.
003930    03 国保件数Ｗ                      PIC 9(4).
003940    03 国保請求額Ｗ                    PIC 9(8).
003930    03 退職件数Ｗ                      PIC 9(4).
003940    03 退職請求額Ｗ                    PIC 9(8).
003930    03 老人件数Ｗ                      PIC 9(4).
003940    03 老人請求額Ｗ                    PIC 9(8).
003930    03 後高件数Ｗ                      PIC 9(4).
003940    03 後高請求額Ｗ                    PIC 9(8).
003930    03 国組件数Ｗ                      PIC 9(4).
003940    03 国組請求額Ｗ                    PIC 9(8).
003930    03 国保合計件数Ｗ                  PIC 9(4).
003940    03 国保合計請求額Ｗ                PIC 9(8).
003930    03 社保件数Ｗ                      PIC 9(4).
003940    03 社保請求額Ｗ                    PIC 9(8).
003930    03 協会件数Ｗ                      PIC 9(4).
003940    03 協会請求額Ｗ                    PIC 9(8).
003930    03 船員件数Ｗ                      PIC 9(4).
003940    03 船員請求額Ｗ                    PIC 9(8).
003930    03 組合件数Ｗ                      PIC 9(4).
003940    03 組合請求額Ｗ                    PIC 9(8).
003930    03 自衛件数Ｗ                      PIC 9(4).
003940    03 自衛請求額Ｗ                    PIC 9(8).
003930    03 共済件数Ｗ                      PIC 9(4).
003940    03 共済請求額Ｗ                    PIC 9(8).
003930    03 被爆件数Ｗ                      PIC 9(4).
003940    03 被爆請求額Ｗ                    PIC 9(8).
003930    03 障害件数Ｗ                      PIC 9(4).
003940    03 障害請求額Ｗ                    PIC 9(8).
003930    03 母子件数Ｗ                      PIC 9(4).
003940    03 母子請求額Ｗ                    PIC 9(8).
003930    03 乳児件数Ｗ                      PIC 9(4).
003940    03 乳児請求額Ｗ                    PIC 9(8).
003930    03 福祉件数Ｗ                      PIC 9(4).
003940    03 福祉請求額Ｗ                    PIC 9(8).
003930    03 生保件数Ｗ                      PIC 9(4).
003940    03 生保請求額Ｗ                    PIC 9(8).
003930    03 労災件数Ｗ                      PIC 9(4).
003940    03 労災請求額Ｗ                    PIC 9(8).
003930    03 自賠件数Ｗ                      PIC 9(4).
003940    03 自賠請求額Ｗ                    PIC 9(8).
003930    03 自費件数Ｗ                      PIC 9(4).
003940    03 自費請求額Ｗ                    PIC 9(8).
003930    03 合計件数Ｗ                      PIC 9(5).
003940    03 合計請求額Ｗ                    PIC 9(9).
002780* 退避用
002790 01 終了年月日ＷＴ.
002800    03 終了年ＷＴ                      PIC 9(2)  VALUE ZERO.
002810    03 終了月ＷＴ                      PIC 9(2)  VALUE ZERO.
002820    03 終了日ＷＴ                      PIC 9(2)  VALUE ZERO.
002830*
002840** 3カ月比較用
002850 01 対象フラグ                       PIC X(3) VALUE SPACE.
002860 01 比較なしフラグ                   PIC X(3) VALUE SPACE.
002870 01 結果フラグ                       PIC X(3) VALUE SPACE.
002880 01 番号カウンタ                     PIC 9(2) VALUE ZERO.
002890 01 期間月Ｗ.
002900    03 期間Ｗ                        PIC 9(2) OCCURS 9 VALUE ZERO.
002910*
002920 01 計算年月日Ｗ.
002930    03 計算和暦Ｗ                    PIC 9(1)  VALUE ZERO.
002940    03 計算年Ｗ                      PIC S9(2)  VALUE ZERO.
002950    03 計算月Ｗ                      PIC S9(2)  VALUE ZERO.
002960    03 計算日Ｗ                      PIC S9(2)  VALUE ZERO.
002970 01 開始年月日Ｗ.
002980    03 開始和暦Ｗ                    PIC 9(1)  VALUE ZERO.
002990    03 開始年Ｗ                      PIC 9(2)  VALUE ZERO.
003000    03 開始月Ｗ                      PIC 9(2)  VALUE ZERO.
003010    03 開始日Ｗ                      PIC 9(2)  VALUE ZERO.
003020    03 開始西暦年Ｗ                  PIC S9(4)  VALUE ZERO.
003030 01 終了年月日Ｗ.
003040    03 終了和暦Ｗ                    PIC 9(1)  VALUE ZERO.
003050    03 終了年Ｗ                      PIC 9(2)  VALUE ZERO.
003060    03 終了月Ｗ                      PIC 9(2)  VALUE ZERO.
003070    03 終了日Ｗ                      PIC 9(2)  VALUE ZERO.
003080    03 終了西暦年Ｗ                  PIC S9(4)  VALUE ZERO.
003090**
003100 01 保険者番号Ｗ                       PIC X(10) VALUE SPACE.
003110*
003120* エラーメッセージ用
003130 01 エラーメッセージＷ.
003140    03 エラー患者コードＷ              PIC X(7) VALUE SPACE.
003150    03 エラー区切りＷ                  PIC X(1) VALUE SPACE.
003160    03 エラー保険種別Ｗ                PIC X(2) VALUE SPACE.
003170    03 FILLER                          PIC X(10) VALUE SPACE.
003260*
003270 01 名称区分Ｗ                         PIC 9(2).
003280 01 名称コードＷ                       PIC 9(1).
003290 01 名称分解Ｔ.
003300     03 名称分解Ｗ                     PIC X(1)  OCCURS 20 .
003310 01 桁数Ｗ                             PIC 9(2).
003320*
003330 01 カウンタ１                         PIC 9(3).
003340 01 カウンタ２                         PIC 9(2).
003350*------------------------------------------------------------------------*
003370*
003380 01 計算機西暦年Ｗ                     PIC 9(2).
003390* 日付ＷＯＲＫ
003400 01 計算機西暦.
003410    03 計算機西暦年                    PIC 9(4).
003420    03 計算機西暦月日                  PIC 9(4).
003430 01 計算機西暦Ｒ REDEFINES 計算機西暦.
003440    03 計算機世紀                      PIC 9(2).
003450    03 計算機日付                      PIC 9(6).
003460    03 計算機日付Ｒ REDEFINES 計算機日付.
003470       05 計算機年月                   PIC 9(4).
003480       05 計算機年月Ｒ REDEFINES 計算機年月.
003490         07 計算機年                   PIC 9(2).
003500         07 計算機月                   PIC 9(2).
003510       05 計算機日                     PIC 9(2).
003520*
003530******************************************************************
003540*                          連結項目                              *
003550******************************************************************
003580********************
003590* メッセージ表示キー *
003600********************
003610 01 連メ３−キー IS EXTERNAL.
003620    03  連メ３−メッセージ             PIC N(20).
003630    03  連メ３−メッセージ１           PIC X(20).
003640*
003650* キー項目
003910 01 連入−画面情報ＹＡＺ５８０   IS EXTERNAL.
003920    03 連入−請求和暦年月.
003930       05 連入−請求和暦               PIC 9.
003940       05 連入−請求年                 PIC 9(2).
003950       05 連入−請求月                 PIC 9(2).
          03 連入−プレビュー区分            PIC 9(1).
004320*
003910 01 連印−印刷情報ＹＡＺ５８１   IS EXTERNAL.
003930    03 連印−国保件数               PIC 9(4).
003940    03 連印−国保請求額             PIC 9(8).
003930    03 連印−退職件数               PIC 9(4).
003940    03 連印−退職請求額             PIC 9(8).
003930    03 連印−老人件数               PIC 9(4).
003940    03 連印−老人請求額             PIC 9(8).
003930    03 連印−後高件数               PIC 9(4).
003940    03 連印−後高請求額             PIC 9(8).
003930    03 連印−国組件数               PIC 9(4).
003940    03 連印−国組請求額             PIC 9(8).
003930    03 連印−国保合計件数           PIC 9(4).
003940    03 連印−国保合計請求額         PIC 9(8).
003930    03 連印−社保件数               PIC 9(4).
003940    03 連印−社保請求額             PIC 9(8).
003930    03 連印−協会件数               PIC 9(4).
003940    03 連印−協会請求額             PIC 9(8).
003930    03 連印−船員件数               PIC 9(4).
003940    03 連印−船員請求額             PIC 9(8).
003930    03 連印−組合件数               PIC 9(4).
003940    03 連印−組合請求額             PIC 9(8).
003930    03 連印−自衛件数               PIC 9(4).
003940    03 連印−自衛請求額             PIC 9(8).
003930    03 連印−共済件数               PIC 9(4).
003940    03 連印−共済請求額             PIC 9(8).
003930    03 連印−被爆件数               PIC 9(4).
003940    03 連印−被爆請求額             PIC 9(8).
003930    03 連印−障害件数               PIC 9(4).
003940    03 連印−障害請求額             PIC 9(8).
003930    03 連印−母子件数               PIC 9(4).
003940    03 連印−母子請求額             PIC 9(8).
003930    03 連印−乳児件数               PIC 9(4).
003940    03 連印−乳児請求額             PIC 9(8).
003930    03 連印−福祉件数               PIC 9(4).
003940    03 連印−福祉請求額             PIC 9(8).
003930    03 連印−生保件数               PIC 9(4).
003940    03 連印−生保請求額             PIC 9(8).
003930    03 連印−労災件数               PIC 9(4).
003940    03 連印−労災請求額             PIC 9(8).
003930    03 連印−自賠件数               PIC 9(4).
003940    03 連印−自賠請求額             PIC 9(8).
003930    03 連印−自費件数               PIC 9(4).
003940    03 連印−自費請求額             PIC 9(8).
003930    03 連印−合計件数               PIC 9(5).
003940    03 連印−合計請求額             PIC 9(9).
004320*
004590******************************************************************
004600*                      PROCEDURE  DIVISION                       *
004610******************************************************************
004620 PROCEDURE               DIVISION.
004630************
004640*           *
004650* 初期処理   *
004660*           *
004670************
004680     PERFORM 初期化.
004690************
004700*           *
004710* 主処理     *
004720*           *
004730************
           MOVE ZERO TO 合計Ｗ.
004750     PERFORM 作業ファイル作成.
004760************
004770*           *
004780* 終了処理   *
004790*           *
004800************
004810     PERFORM 連結項目設定.
004820     PERFORM 終了処理.
004830     MOVE ZERO TO PROGRAM-STATUS.
004840     EXIT PROGRAM.
004850*
004860*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
004870*================================================================*
004880 初期化 SECTION.
004890*
004900     PERFORM ファイルオープン.
004910* 連結項目の待避
004920     MOVE 連入−請求和暦      TO 請求和暦ＷＲ.
004930     MOVE 連入−請求年        TO 請求年ＷＲ.
004940     MOVE 連入−請求月        TO 請求月ＷＲ.
004990*
005000*================================================================*
005010 ファイルオープン SECTION.
005020*
005150     OPEN INPUT 受診者情報Ｆ.
005160         MOVE NC"受診者情報Ｆ" TO ファイル名.
005170         PERFORM オープンチェック.
006630     OPEN INPUT レセプトＦ.
006640         MOVE NC"レセ" TO ファイル名.
006650         PERFORM オープンチェック.
006630     OPEN INPUT 生保情報Ｆ.
006640         MOVE NC"生保" TO ファイル名.
006650         PERFORM オープンチェック.
006630     OPEN INPUT 労災情報Ｆ.
006640         MOVE NC"労災" TO ファイル名.
006650         PERFORM オープンチェック.
005210*================================================================*
005220 オープンチェック SECTION.
005230*
005240     IF 状態キー  NOT =  "00"
005250         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
005260         DISPLAY NC"状態キー：" 状態キー         UPON CONS
005270         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
005280                                                 UPON CONS
004380*-----------------------------------------*
004390         CALL "actcshm"  WITH C LINKAGE
004400*-----------------------------------------*
005290         ACCEPT  キー入力 FROM CONS
005300         PERFORM ファイル閉鎖
005310         MOVE 99 TO PROGRAM-STATUS
005320         EXIT PROGRAM.
005330*
005340*================================================================*
005350 連結項目設定 SECTION.
005360*
           INITIALIZE 連印−印刷情報ＹＡＺ５８１.
005370*/ 合計を連結項目へセット
           MOVE 国保件数Ｗ       TO 連印−国保件数      .
           MOVE 国保請求額Ｗ     TO 連印−国保請求額    .
           MOVE 退職件数Ｗ       TO 連印−退職件数      .
           MOVE 退職請求額Ｗ     TO 連印−退職請求額    .
           MOVE 老人件数Ｗ       TO 連印−老人件数      .
           MOVE 老人請求額Ｗ     TO 連印−老人請求額    .
           MOVE 後高件数Ｗ       TO 連印−後高件数      .
           MOVE 後高請求額Ｗ     TO 連印−後高請求額    .
           MOVE 国組件数Ｗ       TO 連印−国組件数      .
           MOVE 国組請求額Ｗ     TO 連印−国組請求額    .
           MOVE 国保合計件数Ｗ   TO 連印−国保合計件数  .
           MOVE 国保合計請求額Ｗ TO 連印−国保合計請求額.
           MOVE 社保件数Ｗ       TO 連印−社保件数      .
           MOVE 社保請求額Ｗ     TO 連印−社保請求額    .
           MOVE 協会件数Ｗ       TO 連印−協会件数      .
           MOVE 協会請求額Ｗ     TO 連印−協会請求額    .
           MOVE 船員件数Ｗ       TO 連印−船員件数      .
           MOVE 船員請求額Ｗ     TO 連印−船員請求額    .
           MOVE 組合件数Ｗ       TO 連印−組合件数      .
           MOVE 組合請求額Ｗ     TO 連印−組合請求額    .
           MOVE 自衛件数Ｗ       TO 連印−自衛件数      .
           MOVE 自衛請求額Ｗ     TO 連印−自衛請求額    .
           MOVE 共済件数Ｗ       TO 連印−共済件数      .
           MOVE 共済請求額Ｗ     TO 連印−共済請求額    .
           MOVE 被爆件数Ｗ       TO 連印−被爆件数      .
           MOVE 被爆請求額Ｗ     TO 連印−被爆請求額    .
           MOVE 障害件数Ｗ       TO 連印−障害件数      .
           MOVE 障害請求額Ｗ     TO 連印−障害請求額    .
           MOVE 母子件数Ｗ       TO 連印−母子件数      .
           MOVE 母子請求額Ｗ     TO 連印−母子請求額    .
           MOVE 乳児件数Ｗ       TO 連印−乳児件数      .
           MOVE 乳児請求額Ｗ     TO 連印−乳児請求額    .
           MOVE 福祉件数Ｗ       TO 連印−福祉件数      .
           MOVE 福祉請求額Ｗ     TO 連印−福祉請求額    .
           MOVE 生保件数Ｗ       TO 連印−生保件数      .
           MOVE 生保請求額Ｗ     TO 連印−生保請求額    .
           MOVE 労災件数Ｗ       TO 連印−労災件数      .
           MOVE 労災請求額Ｗ     TO 連印−労災請求額    .
           MOVE 自賠件数Ｗ       TO 連印−自賠件数      .
           MOVE 自賠請求額Ｗ     TO 連印−自賠請求額    .
           MOVE 自費件数Ｗ       TO 連印−自費件数      .
           MOVE 自費請求額Ｗ     TO 連印−自費請求額    .
           MOVE 合計件数Ｗ       TO 連印−合計件数      .
           MOVE 合計請求額Ｗ     TO 連印−合計請求額    .
005390*================================================================*
005400 ファイル閉鎖 SECTION.
005410*
005420     CLOSE 受診者情報Ｆ  レセプトＦ 生保情報Ｆ 労災情報Ｆ.
005440*================================================================*
005450 終了処理 SECTION.
005460*
005470     PERFORM ファイル閉鎖.
005480*================================================================*
005490 エラー表示 SECTION.
005500*
005510     DISPLAY NC"状態キー" 状態キー  UPON CONS.
005520     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
005530     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
005540     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
005550                                                   UPON CONS.
004380*-----------------------------------------*
004390     CALL "actcshm"  WITH C LINKAGE.
004400*-----------------------------------------*
005560     ACCEPT  キー入力 FROM CONS.
005570     PERFORM ファイル閉鎖.
005580     MOVE 99 TO PROGRAM-STATUS.
005590     EXIT PROGRAM.
005600*================================================================*
005610 エラー表示Ｒ SECTION.
005620*
005630     DISPLAY NC"ファイル読込エラー" ファイル名     UPON CONS.
005640     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
005650     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
005660                                                   UPON CONS.
004380*-----------------------------------------*
004390     CALL "actcshm"  WITH C LINKAGE.
004400*-----------------------------------------*
005670     ACCEPT  キー入力 FROM CONS.
005680     PERFORM ファイル閉鎖.
005690     MOVE 99 TO PROGRAM-STATUS.
005700     EXIT PROGRAM.
005710*
006640*================================================================*
006650 作業ファイル作成 SECTION.
006660*
006690     OPEN OUTPUT 作業ファイル１.
006700         MOVE NC"作１" TO ファイル名.
006710         PERFORM オープンチェック.
006750*
006890     PERFORM 作業ファイル作成１.
007120*
007130     CLOSE 作業ファイル１.
007140*
019480*================================================================*
019490 データチェック SECTION.
019500*
019510     MOVE SPACE          TO 実行キーＷ.
019520* *****************************************************************
019530* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
019540* *****************************************************************
019640     IF レセ−請求対象区分 NOT = ZERO
004090         MOVE レセ−施術和暦  TO 受−施術和暦
004100         MOVE レセ−施術年    TO 受−施術年
004110         MOVE レセ−施術月    TO 受−施術月
004120         MOVE レセ−患者番号  TO 受−患者番号
004130         MOVE レセ−枝番      TO 受−枝番
               READ 受診者情報Ｆ
               NOT INVALID KEY
019880            MOVE "YES"  TO 実行キーＷ
               END-READ
019900     ELSE
019910         MOVE SPACE  TO 実行キーＷ
019950     END-IF.
019970*
011520*================================================================*
011530 健保料金計算 SECTION.
011540*
011560     MOVE レセ−レセ実日数  TO 作１−実日数.
011570*
011640     MOVE レセ−合計        TO 作１−費用額.
011650     MOVE レセ−一部負担金  TO 作１−負担額.
011660     MOVE レセ−請求金額    TO 作１−請求額.
011720***
011910*================================================================*
011920 助成料金計算 SECTION.
011930*
011950     MOVE レセ−レセ実日数     TO 作１−実日数.
011960     MOVE レセ−合計           TO 作１−費用額.
011970     MOVE レセ−受給者負担額   TO 作１−負担額.
011980     MOVE レセ−助成請求金額   TO 作１−請求額.
011910*================================================================*
011920 合計料金計算 SECTION.
      *
           EVALUATE 作１−順番
           WHEN 1
               EVALUATE 作１−保種
               WHEN 1
                  EVALUATE 作１−保険種別
                  WHEN 01
                     COMPUTE 国保件数Ｗ    = 国保件数Ｗ + 1
                     COMPUTE 国保請求額Ｗ  = 国保請求額Ｗ + レセ−請求金額
                  WHEN 08
                     COMPUTE 退職件数Ｗ    = 退職件数Ｗ + 1
                     COMPUTE 退職請求額Ｗ  = 退職請求額Ｗ + レセ−請求金額
                  END-EVALUATE
               WHEN 2
                  COMPUTE 国組件数Ｗ    = 国組件数Ｗ + 1
                  COMPUTE 国組請求額Ｗ  = 国組請求額Ｗ + レセ−請求金額
               WHEN 3
                  IF 作１−保険者番号(1:2) = "27"
                     COMPUTE 老人件数Ｗ    = 老人件数Ｗ + 1
                     COMPUTE 老人請求額Ｗ  = 老人請求額Ｗ + レセ−請求金額
                  ELSE
                     COMPUTE 後高件数Ｗ    = 後高件数Ｗ + 1
                     COMPUTE 後高請求額Ｗ  = 後高請求額Ｗ + レセ−請求金額
                  END-IF
               END-EVALUATE
               COMPUTE 国保合計件数Ｗ   = 国保合計件数Ｗ + 1
               COMPUTE 国保合計請求額Ｗ = 国保合計請求額Ｗ + レセ−請求金額
           WHEN 2
               COMPUTE 社保件数Ｗ       = 社保件数Ｗ + 1
               COMPUTE 社保請求額Ｗ     = 社保請求額Ｗ + レセ−請求金額
           WHEN 3
               COMPUTE 協会件数Ｗ       = 協会件数Ｗ + 1
               COMPUTE 協会請求額Ｗ     = 協会請求額Ｗ + レセ−請求金額
           WHEN 4
               COMPUTE 船員件数Ｗ       = 船員件数Ｗ + 1
               COMPUTE 船員請求額Ｗ     = 船員請求額Ｗ + レセ−請求金額
           WHEN 5
               COMPUTE 組合件数Ｗ       = 組合件数Ｗ + 1
               COMPUTE 組合請求額Ｗ     = 組合請求額Ｗ + レセ−請求金額
           WHEN 6
               COMPUTE 自衛件数Ｗ       = 自衛件数Ｗ + 1
               COMPUTE 自衛請求額Ｗ     = 自衛請求額Ｗ + レセ−請求金額
           WHEN 7
               COMPUTE 共済件数Ｗ       = 共済件数Ｗ + 1
               COMPUTE 共済請求額Ｗ     = 共済請求額Ｗ + レセ−請求金額
           WHEN 8
               COMPUTE 被爆件数Ｗ       = 被爆件数Ｗ + 1
               COMPUTE 被爆請求額Ｗ     = 被爆請求額Ｗ + レセ−助成請求金額
           WHEN 9
               COMPUTE 障害件数Ｗ       = 障害件数Ｗ + 1
               COMPUTE 障害請求額Ｗ     = 障害請求額Ｗ + レセ−助成請求金額
           WHEN 10
               COMPUTE 母子件数Ｗ       = 母子件数Ｗ + 1
               COMPUTE 母子請求額Ｗ     = 母子請求額Ｗ + レセ−助成請求金額
           WHEN 11
               COMPUTE 乳児件数Ｗ       = 乳児件数Ｗ + 1
               COMPUTE 乳児請求額Ｗ     = 乳児請求額Ｗ + レセ−助成請求金額
           WHEN 12
               COMPUTE 福祉件数Ｗ       = 福祉件数Ｗ + 1
               COMPUTE 福祉請求額Ｗ     = 福祉請求額Ｗ + レセ−請求金額
           WHEN 13
               COMPUTE 生保件数Ｗ       = 生保件数Ｗ + 1
               COMPUTE 生保請求額Ｗ     = 生保請求額Ｗ + レセ−請求金額
      *     WHEN 14
      *         COMPUTE 労災件数Ｗ       = 労災件数Ｗ + 1
      *         COMPUTE 労災請求額Ｗ     = 労災請求額Ｗ + レセ−請求金額
      *     WHEN 15
      *         COMPUTE 自賠件数Ｗ       = 自賠件数Ｗ + 1
      *         COMPUTE 自賠請求額Ｗ     = 自賠請求額Ｗ + レセ−請求金額
      *     WHEN 16
      *         COMPUTE 自費件数Ｗ       = 自費件数Ｗ + 1
      *         COMPUTE 自費請求額Ｗ     = 自費請求額Ｗ + レセ−請求金額
           END-EVALUATE.
           COMPUTE 合計件数Ｗ           = 合計件数Ｗ + 1.
           IF (作１−順番 < 8) OR (作１−順番 > 11)
              COMPUTE 合計請求額Ｗ      = 合計請求額Ｗ + レセ−請求金額
           ELSE
              COMPUTE 合計請求額Ｗ      = 合計請求額Ｗ + レセ−助成請求金額
           END-IF.
011930*
011990*================================================================*
012000 作業ファイル作成１ SECTION.
012010*
012020*********************
012030* 全印刷 *
012040*********************
005170     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
005180     MOVE 請求年ＷＲ    TO レセ−請求年.  
005190     MOVE 請求月ＷＲ    TO レセ−請求月.  
013730     MOVE ZERO          TO レセ−レセ種別.
005200     MOVE ZERO          TO レセ−施術和暦.
005210     MOVE ZERO          TO レセ−施術年.  
005220     MOVE ZERO          TO レセ−施術月.  
005230     MOVE ZERO          TO レセ−患者番号.
005240     MOVE SPACE         TO レセ−枝番.    
013770     START レセプトＦ   KEY IS >= レセ−請求和暦年月
000230                                  レセ−施術和暦年月
000240                                  レセ−患者コード
000250                                  レセ−レセ種別
005280     END-START.
005290     IF 状態キー = "00"
005300         MOVE SPACE  TO 終了フラグ
013820         PERFORM レセプトＦ読込
005320         PERFORM UNTIL ( 終了フラグ = "YES" ) OR
005330                       ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
005340                       ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
005350                       ( レセ−請求月   NOT = 請求月ＷＲ   )
012240           PERFORM データチェック
012250**
012260           IF 実行キーＷ = "YES"
012270*
012280*↓* 特別処理（助成レセまとめ）
012290              IF 受−助成種別 NOT = ZERO
012300                 PERFORM 助成レセまとめ判定
012310              ELSE
012320                 MOVE SPACE TO 助成レセまとめフラグ
012330              END-IF
012340*↑*
012350*           ********
012360*           * 健保 *
012370*           ********
013940              IF レセ−レセ種別   = 1
012390*             **********************
012400*             * 作業ファイル１作成 *
012410*             **********************
012440                 MOVE SPACE TO 作１−レコード
012450                 INITIALIZE 作１−レコード
012460                 MOVE 受−保険種別    TO 作１−保険種別
012470                 MOVE 受−保険者番号  TO 作１−保険者番号
012490                 PERFORM 作１レコードセット
012510                 PERFORM 健保料金計算
012510                 PERFORM 合計料金計算
012520                 PERFORM 作１ファイル書込
012550              END-IF
012560*           ********
012570*           * 老人 *
012580*           ********
013940              IF レセ−レセ種別   = 2 
012610*             **********************
012620*             * 作業ファイル１作成 *
012630*             **********************
012640                 MOVE SPACE TO 作１−レコード
012650                 INITIALIZE 作１−レコード
012660                 MOVE 受−公費種別        TO 作１−保険種別
012670                 MOVE 受−費用負担者番号  TO 作１−保険者番号
012700                 PERFORM 作１レコードセット
012510                 PERFORM 健保料金計算
012510                 PERFORM 合計料金計算
012730                 PERFORM 作１ファイル書込
012750              END-IF
014020*           ********
014030*           * 助成 *
014040*           ********
013940              IF レセ−レセ種別   = 3 
010480*↓* 特別処理（助成レセまとめ）
010540                 IF ( 助成レセまとめフラグ = SPACE ) 
010550*↑*
005930*/ 請求額０円は対象外にする /170411
006880                    IF (レセ−助成請求金額 NOT = ZERO)
010560                       MOVE SPACE  TO  作成フラグ
010570                       IF 保険者番号ＷＲ NOT = SPACE
010580                          IF 受−費用負担者番号助成  = 保険者番号ＷＲ 
010590                             MOVE "YES"  TO  作成フラグ
010600                          END-IF
010610                       ELSE
010620                          MOVE "YES"  TO  作成フラグ
010630                       END-IF 
010640                       IF 作成フラグ  = "YES"
010650                          MOVE SPACE TO 作１−レコード
010660                          INITIALIZE 作１−レコード
010670                          MOVE 受−助成種別    TO 作１−保険種別
010680                          MOVE 受−費用負担者番号助成  TO 作１−保険者番号
010710                          PERFORM 作１レコードセット
010730                          PERFORM 助成料金計算
012510                          PERFORM 合計料金計算
010740*
010850                          PERFORM 作１ファイル書込
010860                       END-IF
                          END-IF
010890                 END-IF
010910              END-IF
012350*           ********
012360*           * 生保 *
012370*           ********
013940              IF レセ−レセ種別   = 7
012390*             **********************
012400*             * 作業ファイル１作成 *
012410*             **********************
012440                 MOVE SPACE TO 作１−レコード
012450                 INITIALIZE 作１−レコード
012460                 MOVE 受−保険種別     TO 作１−保険種別
                       MOVE 受−施術和暦年月 TO 生保−施術和暦年月
                       MOVE 受−患者コード   TO 生保−患者コード
                       READ 生保情報Ｆ
                       NOT INVALID KEY
012470                    MOVE 生保−負担者番号  TO 作１−保険者番号
                       END-READ
012490                 PERFORM 作１レコードセット
012510                 PERFORM 健保料金計算
012510                 PERFORM 合計料金計算
012520                 PERFORM 作１ファイル書込
012550              END-IF
012350*           ********
012360*           * 労災 *
012370*           ********
013940*              IF レセ−レセ種別   = 4
012390*             **********************
012400*             * 作業ファイル１作成 *
012410*             **********************
012440*                 MOVE SPACE TO 作１−レコード
012450*                 INITIALIZE 作１−レコード
012460*                 MOVE 受−保険種別     TO 作１−保険種別
      *                 MOVE 受−施術和暦年月 TO 労災−施術和暦年月
      *                 MOVE 受−患者コード   TO 労災−患者コード
      *                 READ 労災情報Ｆ
      *                 NOT INVALID KEY
012470*                    MOVE 労災−労働保険番号  TO 作１−保険者番号
      *                 END-READ
012490*                 PERFORM 作１レコードセット
012510*                 PERFORM 健保料金計算
012510*                 PERFORM 合計料金計算
012520*                 PERFORM 作１ファイル書込
012550*              END-IF
013130           END-IF
004300           PERFORM レセプトＦ読込
013150        END-PERFORM
013160     END-IF.
013170*
013370*================================================================*
013380 レセプトＦ読込 SECTION.
013390*
013400     READ レセプトＦ NEXT
013410     AT END
013420         MOVE "YES" TO 終了フラグ
013430     END-READ.
013440*
013450*================================================================*
013460 印刷順序セット SECTION.
013470*
013480**///  印刷順序の変更はこのSECTIONで   ////**
013490*
013500**********************************************
013510*  汎用印刷順序:保険名称(保険種別コード)     *
013520*    1:国保(1)、退職(8)、後高(5)             *
013530*    2:社保(2)                               *
013540*    3:協会(2)                               *
013550*    4:船員(7)                               *
013560*    5:組合(3)                               *
013570*    6:自衛官(9)                             *
013580*    7:共済(4)                               *
013590*    8:被爆(54)                              *
013590*    9:障害(53)                              *
013590*   10:母子(52)                              *
013590*   11:子供(55,60)                           *
013590*   12:４１老(51)                            *
013590*   13:生保(85)                              *
013590*   14:労災(70)                              *
013590*   15:自賠(80)                              *
013630*                                            *
013640*  ※保険種別 06:日雇は 02:協会へ含める。    *
      *                                            *
      *  ＋　県番号                                *
      *  国保系のみ 1:国退 2:国組 3:後高           *
013650**********************************************
013660*
            INITIALIZE 印刷順序Ｗ.
013840      EVALUATE  印刷保険種別Ｗ
013850* 国保
013860      WHEN  01
013870         MOVE 01               TO 順番Ｗ
               MOVE 受−法別番号     TO 県番Ｗ
               IF 受−保険者番号(3:1) = 3
                  MOVE 2             TO 保種Ｗ
               ELSE
                  MOVE 1             TO 保種Ｗ
               END-IF
013880* 社保
013890      WHEN  02
               IF 受−保険者番号(5:1) = SPACE
013910             MOVE 02           TO 順番Ｗ
                   MOVE 受−法別番号 TO 県番Ｗ
013920         ELSE
013930             MOVE 03           TO 順番Ｗ
                   MOVE 受−都道府県番号 TO 県番Ｗ
013940         END-IF
               MOVE ZERO             TO 保種Ｗ
013950* 組合
013960      WHEN  03
013910         MOVE 05               TO 順番Ｗ
               MOVE 受−都道府県番号 TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014020* 共済
014030      WHEN  04
013910         MOVE 07               TO 順番Ｗ
               MOVE SPACE            TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014090* 老人
014100      WHEN  05
013870         MOVE 01               TO 順番Ｗ
               MOVE 受−都道府県番号 TO 県番Ｗ
               MOVE 3                TO 保種Ｗ
014120* 日雇
014130      WHEN  06
013930         MOVE 03               TO 順番Ｗ
               MOVE 受−都道府県番号 TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014120* 船員
014130      WHEN  06 THRU 07
013930         MOVE 04               TO 順番Ｗ
               MOVE 受−都道府県番号 TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014190* 退職国保
014200      WHEN  08
013870         MOVE 01               TO 順番Ｗ
               MOVE 受−都道府県番号 TO 県番Ｗ
               MOVE 1                TO 保種Ｗ
014220* 自衛官
014230      WHEN  09
013910         MOVE 06               TO 順番Ｗ
               MOVE SPACE            TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014250* 被爆
014260      WHEN 54
013910         MOVE 08               TO 順番Ｗ
               MOVE SPACE            TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014250* 障害
014260      WHEN 53
013910         MOVE 09               TO 順番Ｗ
               MOVE SPACE            TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014250* 母子
014260      WHEN 52
013910         MOVE 10               TO 順番Ｗ
               MOVE SPACE            TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014250* 子供
014260      WHEN 55
            WHEN 60
013910         MOVE 11               TO 順番Ｗ
               MOVE SPACE            TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014250* ４１老
014260      WHEN 51
013910         MOVE 12               TO 順番Ｗ
               MOVE SPACE            TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014250* 生保
014260      WHEN 85
013910         MOVE 13               TO 順番Ｗ
               MOVE SPACE            TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014250* 労災
014260*      WHEN 70
013910*         MOVE 14               TO 順番Ｗ
      *         MOVE SPACE            TO 県番Ｗ
      *         MOVE ZERO             TO 保種Ｗ
014250* 自賠
014260*      WHEN 80
013910*         MOVE 15               TO 順番Ｗ
      *         MOVE SPACE            TO 県番Ｗ
      *         MOVE ZERO             TO 保種Ｗ
014370*
014380      WHEN OTHER
               CONTINUE
014470      END-EVALUATE.
014480*
014490*================================================================*
014500 作１レコードセット SECTION.
014510*
014520     MOVE  SPACE  TO  通院フラグ.
014530     MOVE  SPACE  TO  初日再検フラグ.
014540*
014550     MOVE 作１−保険種別     TO  印刷保険種別Ｗ.
014560     PERFORM 印刷順序セット.
014570     MOVE 印刷順序Ｗ         TO 作１−印刷順序.
      *
           EVALUATE 作１−保険種別
           WHEN 01
               MOVE SPACE             TO 作１−種別
               IF 受−保険者番号(3:1) = 3
                  MOVE SPACE          TO 作１−保番
               ELSE
                  MOVE 受−保険者番号 TO 作１−保番
               END-IF
           WHEN 03
           WHEN 08
               MOVE 受−保番       TO 作１−保番
               MOVE 受−法別番号   TO 作１−種別
           WHEN OTHER
               MOVE SPACE          TO 作１−保番
               MOVE SPACE          TO 作１−種別
           END-EVALUATE.
014580*
      *     IF 連入−請求和暦年月 NOT = 受−施術和暦年月
014620        MOVE 受−施術和暦    TO 作１−施術和暦.
014630        MOVE 受−施術年      TO 作１−施術年.
014640        MOVE 受−施術月      TO 作１−施術月.
      *     END-IF.
014650     MOVE 受−本人家族区分   TO 作１−本人家族区分.
014670     MOVE 受−患者コード     TO 作１−患者コード.
014680     MOVE 受−患者氏名       TO 作１−患者氏名.
014690     MOVE 受−被保険者氏名   TO 作１−被保険者氏名.
014730*
014840     MOVE 受−助成種別       TO 作１−助成種別.
014860*
014870*--- 特別区分 --------------------------*
014910     IF ( 受−特別区分 <= 3 )
014920        MOVE 受−特別区分    TO 作１−前期区分
015000     END-IF.
015010*
019880*================================================================*
019890 作１ファイル書込 SECTION.
019900*
019910     WRITE 作１−レコード
019920     INVALID KEY
019930         MOVE NC"作１"  TO ファイル名
019940         PERFORM エラー表示
019950     END-WRITE.
019960*
019970*================================================================*
019980*================================================================*
019990 助成レセまとめ判定 SECTION.
020000*---------------------------------------------------------------------------*
020010* 本体まとめ区分＝１
020020* の時は、フラグYES (金額を助成込みで印字）
020030*（例：横浜市の障害は、本体保険（国保系）のレセプト１枚で請求、助成レセはなし）
020040*---------------------------------------------------------------------------*
020050*
020060     MOVE SPACE TO 助成レセまとめフラグ.
020070*
020120     IF レセ−本体まとめ区分 = 1 
020150        MOVE "YES" TO 助成レセまとめフラグ
020170     END-IF.
020190*
020390*================================================================*
020400******************************************************************
020410 END PROGRAM YAZ581.
020420******************************************************************
