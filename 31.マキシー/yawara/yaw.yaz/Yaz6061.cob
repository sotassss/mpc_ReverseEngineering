000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YAZ6061.
000060 AUTHOR.                 池田　幸子
000070*
000080*----------------------------------------------------------------*
000090*      レセプト印刷表示【ﾃﾞｰﾀ作成】柔+ｳｨﾝﾄﾞｳｽﾞ版
000100* 請求年月Ver.
000110*      MED = YAZ610
000111* 中央と同じ
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2012-05-22
000140 DATE-COMPILED.          2012-05-22
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000261     SELECT  市町村マスタ    ASSIGN      TO        SITYOSNL
000262                             ORGANIZATION             IS  INDEXED
000263                             ACCESS MODE              IS  DYNAMIC
000264                             RECORD KEY               IS  市−公費種別
000265                                                          市−市町村番号
000266                             ALTERNATE RECORD KEY     IS  市−公費種別
000267                                                          市−市町村名称
000268                                                          市−市町村番号
000269                             FILE STATUS              IS  状態キー
000270                             LOCK        MODE         IS  AUTOMATIC.
000241     SELECT  生保情報Ｆ      ASSIGN      TO        SEIHOJL
000242                             ORGANIZATION             IS INDEXED
000243                             ACCESS MODE              IS DYNAMIC
000244                             RECORD KEY               IS 生保−施術和暦年月
000245                                                         生保−患者コード
000255                             ALTERNATE RECORD KEY     IS 生保−患者コード
000265                                                         生保−施術和暦年月
000277                             FILE STATUS              IS 状態キー
000278                             LOCK        MODE         IS AUTOMATIC.
000450     SELECT  制御情報マスタ  ASSIGN      TO        SEIGYOL
000460                             ORGANIZATION             IS  INDEXED
000470                             ACCESS MODE              IS  DYNAMIC
000480                             RECORD KEY               IS  制−制御区分
000490                             FILE STATUS              IS  状態キー
000500                             LOCK        MODE         IS  AUTOMATIC.
000510     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000520                             ORGANIZATION             IS  INDEXED
000530                             ACCESS MODE              IS  DYNAMIC
000540                             RECORD KEY               IS  受−施術和暦年月
000550                                                          受−患者コード
000560                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000570                                                          受−患者カナ
000580                                                          受−患者コード
000590                             ALTERNATE RECORD KEY     IS  受−患者コード
000600                                                          受−施術和暦年月
000610                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000620                                                          受−保険種別
000630                                                          受−保険者番号
000640                                                          受−患者コード
000650                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000660                                                          受−公費種別
000670                                                          受−費用負担者番号
000680                                                          受−患者コード
000690                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
000700                                                          受−助成種別
000710                                                          受−費用負担者番号助成
000720                                                          受−患者コード
000730                             ALTERNATE RECORD KEY     IS  受−請求和暦年月
000740                                                          受−施術和暦年月
000750                                                          受−患者コード
000760                             FILE STATUS              IS  状態キー
000770                             LOCK        MODE         IS  AUTOMATIC.
           SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
                                   ORGANIZATION             IS  INDEXED
                                   ACCESS MODE              IS  DYNAMIC
                                   RECORD KEY               IS  レセ−施術和暦年月
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−患者コード
                                                                レセ−施術和暦年月
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−施術和暦年月
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−レセ種別
                                                                レセ−請求保険者番号
                                                                レセ−患者コード
                                                                レセ−施術和暦年月
                                   ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
                                                                レセ−請求保険者番号
                                                                レセ−患者コード
                                                                レセ−レセ種別
                                                                レセ−施術和暦年月
                                   FILE STATUS              IS  状態キー
                                   LOCK        MODE         IS  AUTOMATIC.
000780     SELECT  作業ファイル１  ASSIGN      TO     "C:\MAKISHISYS\YAWOBJ\TEMP\W60610L.DAT"
000790                             ORGANIZATION             IS  INDEXED
000800                             ACCESS                   IS  DYNAMIC
000810                             RECORD      KEY          IS  作１−印刷順序
001180                                                          作１−保番
001170                                                          作１−種別
001190                                                          作１−保険者番号
001170                                                          作１−本人家族区分
001200                                                          作１−患者コード
001210                                                          作１−施術和暦年月
000890                             FILE        STATUS       IS  状態キー
000900                             LOCK        MODE         IS  AUTOMATIC.
000910* レセ並び順用
000920     SELECT  作業ファイル２  ASSIGN      TO   "C:\MAKISHISYS\YAWOBJ\TEMP\W6102L.DAT"
000930                             ORGANIZATION             IS  INDEXED
000940                             ACCESS                   IS  DYNAMIC
001230                             RECORD      KEY          IS  作２−保険区分
                                                                作２−順番
                                   ALTERNATE RECORD KEY     IS  作２−施術和暦年月
                                                                作２−患者コード
                                                                作２−保険区分
                                                                作２−順番
000960                             FILE        STATUS       IS  状態キー
000970                             LOCK        MODE         IS  AUTOMATIC.
000980******************************************************************
000990*                      DATA DIVISION                             *
001000******************************************************************
001010 DATA                    DIVISION.
001020 FILE                    SECTION.
001021*                           ［ＲＬ＝  ２５６］
001022 FD  市町村マスタ          BLOCK   CONTAINS   1   RECORDS.
001023     COPY SITYOSN        OF  XFDLIB  JOINING   市   AS  PREFIX.
001080* 
000280 FD  生保情報Ｆ          BLOCK   CONTAINS   1   RECORDS.
000281     COPY SEIHOJ          OF  XFDLIB  JOINING   生保   AS  PREFIX.
001090*                           ［ＲＬ＝  ２５６］
001100 FD  制御情報マスタ          BLOCK   CONTAINS   1   RECORDS.
001110     COPY SEIGYO          OF  XFDLIB  JOINING   制   AS  PREFIX.
      *
       FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS GLOBAL.
           COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
001120*                           ［ＲＬ＝  ３２０］
001130 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001140     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
001150* 保険者番号順ファイル
001160 FD  作業ファイル１ RECORD  CONTAINS 160 CHARACTERS.
001170 01  作１−レコード.
001180     03  作１−レコードキー.
001590         05  作１−印刷順序.
                   07  作１−順番                PIC 9(2).
001570             07  作１−県                  PIC 9(2).
001580             07  作１−保種                PIC 9(1).
001720         05  作１−保番                    PIC X(6).
001600         05  作１−種別                    PIC X(2).
001710         05  作１−保険者番号              PIC 9(8).
001700         05  作１−本人家族区分            PIC 9(1).
001620         05  作１−患者コード.
001630             07  作１−患者番号            PIC 9(6).
001640             07  作１−枝番                PIC X(1).
001650         05  作１−施術和暦年月.
001660             07  作１−施術和暦            PIC 9(1).
001670             07  作１−施術年              PIC 9(2).
001680             07  作１−施術月              PIC 9(2).
001350     03  作１−レコードデータ.
001360         05  作１−保険種別                PIC 9(2).
001221         05  作１−レセ種別                PIC 9(2).
001740         05  作１−患者氏名                PIC X(50).
001740         05  作１−被保険者氏名            PIC X(50).
001366         05  作１−費用額                  PIC 9(6).
001367         05  作１−負担額                  PIC 9(6).
001368         05  作１−請求額                  PIC 9(6).
001369         05  作１−レセ印刷区分            PIC 9.
001370         05  FILLER                        PIC X(2).
001380*
001790 FD  作業ファイル２ RECORD  CONTAINS 155 CHARACTERS.
001800 01  作２−レコード.
001810     03  作２−レコードキー.
001820         05  作２−保険区分                PIC 9(1).
001820         05  作２−順番                    PIC 9(4).
001830     03  作２−レコードデータ.
               05  作２−レセ種別                PIC 9(2).
001840         05  作２−患者コード.
001850             07 作２−患者番号             PIC 9(6).
001860             07 作２−枝番                 PIC X(1).
001870         05  作２−患者氏名                PIC X(50).
001740         05  作２−被保険者氏名            PIC X(50).
001880         05  作２−保険種別                PIC 9(2).
               05  作２−請求保険者番号          PIC X(10).
001890         05  作２−本人家族                PIC X(4).
001900         05  作２−施術和暦年月.
001910             07  作２−施術和暦            PIC 9.
001920             07  作２−施術年              PIC 9(2).
001930             07  作２−施術月              PIC 9(2).
001940         05  作２−印刷指定                PIC 9(1).
               05  作２−費用額                  PIC 9(6).
               05  作２−負担額                  PIC 9(6).
               05  作２−請求額                  PIC 9(6).
               05  作２−レセ印刷区分            PIC 9.
001560*
001570*----------------------------------------------------------------*
001580******************************************************************
001590*                WORKING-STORAGE SECTION                         *
001600******************************************************************
001610 WORKING-STORAGE         SECTION.
001620 01 キー入力                           PIC X    VALUE SPACE.
001630 01 状態キー                           PIC X(2) VALUE SPACE.
001640 01 終了フラグ                         PIC X(3) VALUE SPACE.
001650 01 終了フラグ２                       PIC X(3) VALUE SPACE.
001660 01 実行キーＷ                         PIC X(3)  VALUE SPACE.
001670 01 施術記録有Ｗ                       PIC X(3) VALUE SPACE.
001680 01 カウンタ                           PIC 9(2)  VALUE ZERO.
001690**
001700 01 助成区分Ｗ                         PIC 9    VALUE ZERO.
001710 01 保険種別ＷＲ                       PIC 9(2) VALUE ZERO.
002730 01 印刷保険種別Ｗ                     PIC 9(2) VALUE ZERO.
001720 01 印刷形式ＷＲ                       PIC 9    VALUE ZERO.
001730 01 保険者番号ＷＲ                     PIC X(10) VALUE SPACE.
001740 01 レセプト種類ＷＲ                   PIC X(4) VALUE SPACE.
001750 01 本人家族区分ＷＲ                   PIC 9    VALUE ZERO.
001760 01 施術和暦年月ＷＲ.
001770    03 施術和暦ＷＲ                    PIC 9    VALUE ZERO.
001780    03 施術年ＷＲ                      PIC 9(2) VALUE ZERO.
001790    03 施術月ＷＲ                      PIC 9(2) VALUE ZERO.
001800 01 請求和暦年月ＷＲ.
001810    03 請求和暦ＷＲ                    PIC 9    VALUE ZERO.
001820    03 請求年ＷＲ                      PIC 9(2) VALUE ZERO.
001830    03 請求月ＷＲ                      PIC 9(2) VALUE ZERO.
002740 01 印刷順序Ｗ.
          03 順番Ｗ                          PIC 9(2) VALUE ZERO.
          03 県番Ｗ                          PIC X(2) VALUE SPACE.
          03 保種Ｗ                          PIC 9(1) VALUE ZERO.
001840**
001850 01 ファイル名                         PIC N(8) VALUE SPACE.
001860 01 保険種別Ｗ                         PIC 9(2) VALUE ZERO.
001870 01 順番ＷＲ                           PIC 9(4) VALUE ZERO.
002090 01 生保順番Ｗ                         PIC 9(4)  VALUE ZERO.
001880 01 レセ並び順昇降Ｗ                   PIC 9(1) VALUE ZERO.
001890*
001891*
001892** 助成レセまとめ用
001893 01 助成レセまとめフラグ               PIC X(3)  VALUE SPACE.
001894*
       01 金額Ｗ.
          03 費用額Ｗ                        PIC 9(6)  VALUE ZERO.
          03 負担額Ｗ                        PIC 9(6)  VALUE ZERO.
          03 請求額Ｗ                        PIC 9(6)  VALUE ZERO.
001900** 合計集計用
001910 01 合計Ｗ.
001920    03 件数合計Ｗ                      PIC 9(4) VALUE ZERO.
001930    03 費用額合計Ｗ                    PIC 9(8) VALUE ZERO.
001940    03 請求額合計Ｗ                    PIC 9(8) VALUE ZERO.
001950** エラーメッセージ用
001960 01 エラーメッセージＷ.
001970    03 エラー患者コードＷ              PIC X(7) VALUE SPACE.
001980    03 エラー区切りＷ                  PIC X(1) VALUE SPACE.
001990    03 エラー保険種別Ｗ                PIC X(2) VALUE SPACE.
002000    03 FILLER                          PIC X(10) VALUE SPACE.
002010*
002020** 保険者番号右詰め用
002030 01 保険者番号ＷＴ.
002040    03 保険者番号左詰めＷ.
002050      05 保険者番号左詰めＷ１          PIC X OCCURS 8 VALUE SPACE.
002060    03 保険者番号右詰めＷ.
002070      05 保険者番号右詰めＷ１          PIC X OCCURS 8 VALUE ZERO.
002080    03 保険者番号数字Ｗ                PIC 9(8)  VALUE ZERO.
002090    03 保険者番号Ｗ                    PIC X(8)  VALUE SPACE.
002100**
002110 01 計算機西暦年Ｗ                     PIC 9(2).
002120* 日付ＷＯＲＫ
002130 01 計算機西暦.
002140    03 計算機西暦年                    PIC 9(4).
002150    03 計算機西暦月日                  PIC 9(4).
002160 01 計算機西暦Ｒ REDEFINES 計算機西暦.
002170    03 計算機世紀                      PIC 9(2).
002180    03 計算機日付                      PIC 9(6).
002190    03 計算機日付Ｒ REDEFINES 計算機日付.
002200       05 計算機年月                   PIC 9(4).
002210       05 計算機年月Ｒ REDEFINES 計算機年月.
002220         07 計算機年                   PIC 9(2).
002230         07 計算機月                   PIC 9(2).
002240       05 計算機日                     PIC 9(2).
002250*
002260******************************************************************
002270*                          連結項目                              *
002280******************************************************************
002290*
002300********************
002310* メッセージ表示キー *
002320********************
002330 01 連メ−キー IS EXTERNAL.
002340    03  連メ−メッセージ                 PIC N(20).
002350*
003610 01 連メ３−キー IS EXTERNAL.
003620    03  連メ３−メッセージ             PIC N(20).
003630    03  連メ３−メッセージ１           PIC X(20).
002360***
002480 01 連入−入力データ６１０ IS EXTERNAL.
002490    03 連入−請求年月.
002500       05 連入−請求和暦                  PIC 9.
002510       05 連入−請求年                    PIC 9(2).
002520       05 連入−請求月                    PIC 9(2).
002540    03 連入−保険種別                     PIC 9(2).
002490*
002491************************
002492* 助成レセまとめ
002493************************
002494 01 連レセまとめ−キー IS EXTERNAL.
002495    03 連レセまとめ−施術和暦年月.
002496       05 連レセまとめ−施術和暦               PIC 9.
002497       05 連レセまとめ−施術年月.
002498          07 連レセまとめ−施術年              PIC 9(2).
002499          07 連レセまとめ−施術月              PIC 9(2).
002500    03 連レセまとめ−患者コード.
002501       05 連レセまとめ−患者番号               PIC 9(6).
002502       05 連レセまとめ−枝番                   PIC X(1).
002503**-------------------------------------------------------**
002504*   1:助成レセプトなしの本体まとめの判定
002505*   2:横浜・川崎用の社保助成レセかの判定
002506    03 連レセまとめ−判定区分                  PIC 9.
002507**-------------------------------------------------------**
002508*  / OUT /　 0:対象外、1:対象
002509    03 連レセまとめ−判定結果                  PIC 9.
002510**
002511******************************************************************
002512*                      PROCEDURE  DIVISION                       *
002520******************************************************************
002530 PROCEDURE               DIVISION.
002540************
002550*           *
002560* 初期処理   *
002570*           *
002580************
002590     PERFORM 初期化.
002600************
002610*           *
002620* 主処理     *
002630*           *
002640************
002650     PERFORM 作業ファイル作成.
002660************
002670*           *
002680* 終了処理   *
002690*           *
002700************
002710     PERFORM 終了処理.
002720     MOVE ZERO TO PROGRAM-STATUS.
002730     EXIT PROGRAM.
002740*
002750*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
002760*================================================================*
002770 初期化 SECTION.
002780*
002790     PERFORM ファイルオープン.
002800* 連結項目の待避
002810     MOVE 連入−請求和暦      TO 請求和暦ＷＲ.
002820     MOVE 連入−請求年        TO 請求年ＷＲ.
002830     MOVE 連入−請求月        TO 請求月ＷＲ.
002960     MOVE 連入−保険種別      TO 保険種別ＷＲ.
002840* レセ並び順昇順・降順セット
002850     MOVE ZEROS TO 制−制御区分.
002860     READ 制御情報マスタ
002870     NOT INVALID KEY
002880         MOVE 制−レセ並び順昇降 TO レセ並び順昇降Ｗ
002890     END-READ.
002900*
002910*================================================================*
002920 ファイルオープン SECTION.
002930*
003000     OPEN INPUT 制御情報マスタ
003010         MOVE NC"制御情報" TO ファイル名.
003020         PERFORM オープンチェック.
003030     OPEN INPUT 受診者情報Ｆ.
003040         MOVE NC"受診者情報Ｆ" TO ファイル名.
003050         PERFORM オープンチェック.
003051     OPEN INPUT 市町村マスタ.
003052         MOVE NC"市町村" TO ファイル名.
003053         PERFORM オープンチェック.
006630     OPEN INPUT 生保情報Ｆ.
006640         MOVE NC"生保" TO ファイル名.
006650         PERFORM オープンチェック.
003250     OPEN INPUT レセプトＦ.
003260         MOVE NC"レセプトＦ" TO ファイル名.
003270         PERFORM オープンチェック.
003060*================================================================*
003070 オープンチェック SECTION.
003080*
003090     IF 状態キー  NOT =  "00"
003100         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
003110         DISPLAY NC"状態キー：" 状態キー         UPON CONS
003120         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
003130                                                 UPON CONS
003131*-----------------------------------------*
003132         CALL "actcshm"  WITH C LINKAGE
003133*-----------------------------------------*
003140         ACCEPT  キー入力 FROM CONS
003150         PERFORM ファイル閉鎖
003160         MOVE 99 TO PROGRAM-STATUS
003170         EXIT PROGRAM.
003180*================================================================*
003190 ファイル閉鎖 SECTION.
003200*
003210     CLOSE 制御情報マスタ 受診者情報Ｆ
                 市町村マスタ 生保情報Ｆ レセプトＦ.
003220*================================================================*
003230 終了処理 SECTION.
003240*
003250     PERFORM ファイル閉鎖.
003260*================================================================*
003270 エラー表示 SECTION.
003280*
003290     DISPLAY NC"状態キー" 状態キー  UPON CONS.
003300     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
003310     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
003320     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
003321*-----------------------------------------*
003322     CALL "actcshm"  WITH C LINKAGE.
003323*-----------------------------------------*
003330     ACCEPT  キー入力 FROM CONS.
003340     PERFORM ファイル閉鎖.
003350     MOVE 99 TO PROGRAM-STATUS.
003360     EXIT PROGRAM.
003370*================================================================*
003380 エラー表示Ｒ SECTION.
003390*
003400     DISPLAY NC"ファイル読込エラー" ファイル名     UPON CONS.
003410     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
003420     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
003321*-----------------------------------------*
003322     CALL "actcshm"  WITH C LINKAGE.
003323*-----------------------------------------*
003430     ACCEPT  キー入力 FROM CONS.
003440     PERFORM ファイル閉鎖.
003450     MOVE 99 TO PROGRAM-STATUS.
003460     EXIT PROGRAM.
003470*================================================================*
003480 作業ファイル作成 SECTION.
003490*
003500     PERFORM 保険者番号順ファイル作成.
003510*
003520* レセ並び順ファイル作成
003530     OPEN OUTPUT 作業ファイル２.
003540         MOVE NC"作２" TO ファイル名.
003550         PERFORM オープンチェック.
003560*
003570     IF レセ並び順昇降Ｗ = 1
003580         PERFORM レセ並び順降順ファイル作成
003590     ELSE
003600         PERFORM レセ並び順ファイル作成
003610     END-IF.
003620*
003630     CLOSE 作業ファイル２.
003640*
003650*================================================================*
003660 保険者番号順ファイル作成 SECTION.
003670**********************************************************************
003680**   受診者情報Ｆから、該当請求年月のデータを抽出し、
003690**   作業ファイル１(保険者番号順)に書き出す.
003700**********************************************************************
003710*
003720     OPEN OUTPUT 作業ファイル１.
003730         MOVE NC"作１" TO ファイル名.
003740         PERFORM オープンチェック.
003750*
003760     MOVE "YES"         TO 実行キーＷ.
005170     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
005180     MOVE 請求年ＷＲ    TO レセ−請求年.  
005190     MOVE 請求月ＷＲ    TO レセ−請求月.  
013730     MOVE ZERO          TO レセ−レセ種別.
005200     MOVE ZERO          TO レセ−施術和暦.
005210     MOVE ZERO          TO レセ−施術年.  
005220     MOVE ZERO          TO レセ−施術月.  
005230     MOVE ZERO          TO レセ−患者番号.
005240     MOVE SPACE         TO レセ−枝番.    
013770     START レセプトＦ   KEY IS >= レセ−請求和暦年月
000230                                  レセ−施術和暦年月
000240                                  レセ−患者コード
000250                                  レセ−レセ種別
003880     END-START.
003900     IF 状態キー = "00"
003910        MOVE SPACE  TO 終了フラグ
003920        PERFORM レセプトＦ読込
003930        PERFORM UNTIL ( 終了フラグ = "YES" ) OR
005330                      ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
005340                      ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
005350                      ( レセ−請求月   NOT = 請求月ＷＲ   )
003970*
003980           PERFORM データチェック
003990** 労災・自賠責・自由は対象外
003790           IF レセ−レセ種別 = 4 OR 5 OR 6 OR 8
004010              MOVE SPACE  TO 実行キーＷ
004020           END-IF
003830           IF 実行キーＷ = "YES"
      *
012280*↓* 特別処理（助成レセまとめ）
012290              IF 受−助成種別 NOT = ZERO
012300                 PERFORM 助成レセまとめ判定
012310              ELSE
012320                 MOVE SPACE TO 助成レセまとめフラグ
012330              END-IF
004160*
005610              EVALUATE TRUE
004190*            ********
004191*            * 健保 *
004200*            ********
013940                WHEN レセ−レセ種別   = 1
                          IF (保険種別ＷＲ = ZERO) OR
                             ((受−保険種別 = 保険種別ＷＲ) AND ( 受−公費種別 = ZERO ))
004230*                **********************
004240*                * 作業ファイル作成 *
004250*                **********************
004300                       MOVE SPACE TO 作１−レコード
004310                       INITIALIZE 作１−レコード
004330                       MOVE 受−保険種別       TO 作１−保険種別
004340                       MOVE 受−保険者番号     TO 保険者番号Ｗ
004350                       PERFORM 保険者番号右詰め
004360                       MOVE 保険者番号数字Ｗ   TO 作１−保険者番号
004010                       PERFORM 料金セット
004370                       PERFORM 作１レコードセット
004380                       PERFORM 作１ファイル書込
004630                    END-IF
004650*            ********
004660*            * 老人 *
004670*            ********
                      WHEN レセ−レセ種別   = 2 
                          IF (保険種別ＷＲ = ZERO) OR
                             (受−公費種別 = 保険種別ＷＲ)
004700*                    **********************
004710*                    * 作業ファイル作成 *
004720*                    **********************
004750                        MOVE SPACE TO 作１−レコード
004760                        INITIALIZE 作１−レコード
004780                        MOVE 受−公費種別       TO 作１−保険種別
004790                        MOVE 受−費用負担者番号  TO 保険者番号Ｗ
004800                        PERFORM 保険者番号右詰め
004810                        MOVE 保険者番号数字Ｗ   TO 作１−保険者番号
004010                        PERFORM 料金セット
004820                        PERFORM 作１レコードセット
004830                        PERFORM 作１ファイル書込
005070                    END-IF
004220*            ********
004230*            * 助成 *
004240*            ********
004241*↓* 特別処理（助成レセまとめ）**
                      WHEN レセ−レセ種別   = 3
010540                    IF ( 助成レセまとめフラグ = SPACE ) 
                             IF (保険種別ＷＲ = ZERO) OR
                                (受−助成種別 = 保険種別ＷＲ)
005930*/ 請求額０円は対象外にする /170411
006880                          IF (レセ−助成請求金額 NOT = ZERO)
004540                             MOVE SPACE TO 作１−レコード
004550                             INITIALIZE 作１−レコード
004570                             MOVE 受−助成種別       TO 作１−保険種別
004580                             MOVE 受−費用負担者番号助成 TO 保険者番号Ｗ
004590                             PERFORM 保険者番号右詰め
004600                             MOVE 保険者番号数字Ｗ   TO 作１−保険者番号
004010                             PERFORM 助成料金セット
004620                             PERFORM 作１レコードセット
004630                             PERFORM 作１ファイル書込
                                END-IF
                             END-IF
                          END-IF
004220*                ********
004230*                * 生保 *
004240*                ********
                      WHEN レセ−レセ種別   = 7
                          IF (保険種別ＷＲ = ZERO) OR
                             (受−保険種別 = 保険種別ＷＲ)
003920                        MOVE SPACE TO 作１−レコード
003930                        INITIALIZE 作１−レコード
003960                        MOVE 受−保険種別     TO 作１−保険種別
                              MOVE 受−施術和暦年月 TO 生保−施術和暦年月
                              MOVE 受−患者コード   TO 生保−患者コード
                              READ 生保情報Ｆ
                              NOT INVALID KEY
003950                           MOVE 生保−負担者番号   TO 保険者番号Ｗ
                              END-READ
004380                        PERFORM 保険者番号右詰め
004390                        MOVE 保険者番号数字Ｗ   TO 作１−保険者番号
004010                        PERFORM 料金セット
004370                        PERFORM 作１レコードセット
004380                        PERFORM 作１ファイル書込
004391                    END-IF
                      END-EVALUATE
                   END-IF
005120*
004410             PERFORM レセプトＦ読込
005110         END-PERFORM
005120     END-IF.
005130*
005140     CLOSE 作業ファイル１.
005150*
005160*================================================================*
005170 データ確認 SECTION.
005180*
005190     IF 状態キー  NOT =  "00"
005200         MOVE  NC"　　　該当年月のデータがありません。" TO 連メ−メッセージ
005210         CALL   "MSG001"
005220         CANCEL "MSG001"
005230         PERFORM ファイル閉鎖
005240         MOVE 99 TO PROGRAM-STATUS
005250         EXIT PROGRAM
005260     END-IF.
005270*
005280*================================================================*
005290 データチェック SECTION.
005300*
005310     MOVE SPACE  TO 実行キーＷ.
005300     IF ( レセ−レセ印刷対象区分 NOT = 1 )
005310        MOVE "YES"  TO 実行キーＷ
005320     END-IF.
019520* *****************************************************************
019530* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
019540* *****************************************************************
005360     IF 実行キーＷ  = "YES"
005370*      (再度、実行キーＷ SPACE)
005380        MOVE SPACE  TO 実行キーＷ
019640        IF レセ−請求対象区分 NOT = ZERO
004090           MOVE レセ−施術和暦  TO 受−施術和暦
004100           MOVE レセ−施術年    TO 受−施術年
004110           MOVE レセ−施術月    TO 受−施術月
004120           MOVE レセ−患者番号  TO 受−患者番号
004130           MOVE レセ−枝番      TO 受−枝番
                 READ 受診者情報Ｆ
                 NOT INVALID KEY
019880              MOVE "YES"  TO 実行キーＷ
                 END-READ
019900        ELSE
019910           MOVE SPACE  TO 実行キーＷ
              END-IF
019950     END-IF.
005770*
005860*================================================================*
005870 作１レコードセット SECTION.
005880*
014550     MOVE 作１−保険種別     TO  印刷保険種別Ｗ.
014560     PERFORM 印刷順序セット.
014570     MOVE 印刷順序Ｗ         TO 作１−印刷順序.
      *
           EVALUATE 作１−保険種別
           WHEN 01
               MOVE SPACE             TO 作１−種別
               IF 受−保険者番号(3:1) = 3
                  MOVE SPACE          TO 作１−保番
               ELSE
                  MOVE 受−保険者番号 TO 作１−保番
               END-IF
           WHEN 03
           WHEN 08
               MOVE 受−保番       TO 作１−保番
               MOVE 受−法別番号   TO 作１−種別
           WHEN OTHER
               MOVE SPACE          TO 作１−保番
               MOVE SPACE          TO 作１−種別
           END-EVALUATE.
014580*
005150     MOVE レセ−施術和暦    TO 作１−施術和暦.
005160     MOVE レセ−施術年      TO 作１−施術年.
005170     MOVE レセ−施術月      TO 作１−施術月.
           MOVE レセ−レセ種別    TO 作１−レセ種別.
005950     MOVE 受−本人家族区分   TO 作１−本人家族区分.
005970     MOVE 受−患者コード     TO 作１−患者コード.
005210     MOVE 受−患者氏名       TO 作１−患者氏名.
005210     MOVE 受−被保険者氏名   TO 作１−被保険者氏名.
           MOVE 費用額Ｗ           TO 作１−費用額.
           MOVE 請求額Ｗ           TO 作１−請求額.
           MOVE 負担額Ｗ           TO 作１−負担額.
005980*
005390*================================================================*
       料金セット SECTION.
           MOVE レセ−合計            TO 費用額Ｗ.
           MOVE レセ−請求金額        TO 請求額Ｗ.
           MOVE レセ−一部負担金      TO 負担額Ｗ.
005210     MOVE 受−レセ印刷区分      TO 作１−レセ印刷区分.
005380*
005390*================================================================*
       助成料金セット SECTION.
           MOVE レセ−合計                  TO 費用額Ｗ.
           MOVE レセ−助成請求金額          TO 請求額Ｗ.
           MOVE レセ−受給者負担額          TO 負担額Ｗ.
005210     MOVE 受−レセ印刷区分助成        TO 作１−レセ印刷区分.
005380*
005990*================================================================*
006000 作１ファイル書込 SECTION.
006010*
006020     WRITE 作１−レコード
006030     INVALID KEY
006040         MOVE NC"作１"  TO ファイル名
006050         PERFORM エラー表示
006060     END-WRITE.
006070*
006080*================================================================*
006090 保険者番号右詰め SECTION.
006100*
006110     MOVE 保険者番号Ｗ    TO  保険者番号左詰めＷ.
006120     MOVE ZERO            TO  保険者番号右詰めＷ.
006130     MOVE ZERO            TO  保険者番号数字Ｗ.
006140*
006150     MOVE  9  TO  カウンタ.
006160*
006170     IF  保険者番号左詰めＷ１(8) NOT = SPACE
006180         COMPUTE カウンタ = カウンタ  -  1
006190         MOVE 保険者番号左詰めＷ１(8)  TO  保険者番号右詰めＷ１(カウンタ)
006200     END-IF.
006210     IF  保険者番号左詰めＷ１(7) NOT = SPACE
006220         COMPUTE カウンタ = カウンタ  -  1
006230         MOVE 保険者番号左詰めＷ１(7)  TO  保険者番号右詰めＷ１(カウンタ)
006240     END-IF.
006250     IF  保険者番号左詰めＷ１(6) NOT = SPACE
006260         COMPUTE カウンタ = カウンタ  -  1
006270         MOVE 保険者番号左詰めＷ１(6)  TO  保険者番号右詰めＷ１(カウンタ)
006280     END-IF.
006290     IF  保険者番号左詰めＷ１(5) NOT = SPACE
006300         COMPUTE カウンタ = カウンタ  -  1
006310         MOVE 保険者番号左詰めＷ１(5)  TO  保険者番号右詰めＷ１(カウンタ)
006320     END-IF.
006330     IF  保険者番号左詰めＷ１(4) NOT = SPACE
006340         COMPUTE カウンタ = カウンタ  -  1
006350         MOVE 保険者番号左詰めＷ１(4)  TO  保険者番号右詰めＷ１(カウンタ)
006360     END-IF.
006370     IF  保険者番号左詰めＷ１(3) NOT = SPACE
006380         COMPUTE カウンタ = カウンタ  -  1
006390         MOVE 保険者番号左詰めＷ１(3)  TO  保険者番号右詰めＷ１(カウンタ)
006400     END-IF.
006410     IF  保険者番号左詰めＷ１(2) NOT = SPACE
006420         COMPUTE カウンタ = カウンタ  -  1
006430         MOVE 保険者番号左詰めＷ１(2)  TO  保険者番号右詰めＷ１(カウンタ)
006440     END-IF.
006450     IF  保険者番号左詰めＷ１(1) NOT = SPACE
006460         COMPUTE カウンタ = カウンタ  -  1
006470         MOVE 保険者番号左詰めＷ１(1)  TO  保険者番号右詰めＷ１(カウンタ)
006480     END-IF.
006490*
006500     MOVE 保険者番号右詰めＷ TO 保険者番号数字Ｗ.
006510*
006520*================================================================*
006530 作業ファイル１読込 SECTION.
006540*
006550     READ 作業ファイル１ NEXT
006560     AT END
006570         MOVE "YES" TO 終了フラグ
006580     NOT AT END
006590         MOVE 作１−施術和暦    TO 受−施術和暦
006600         MOVE 作１−施術年      TO 受−施術年
006610         MOVE 作１−施術月      TO 受−施術月
006620         MOVE 作１−患者番号    TO 受−患者番号
006630         MOVE 作１−枝番        TO 受−枝番
006640         READ 受診者情報Ｆ
006650         INVALID KEY
006660              MOVE NC"受診者"   TO ファイル名
006670              PERFORM エラー表示Ｒ
006680         END-READ
006690     END-READ.
006700*
006710*================================================================*
006930 レセプトＦ読込 SECTION.
006940*
006950     READ レセプトＦ NEXT
006750     AT END
006760         MOVE "YES" TO 終了フラグ
006770     END-READ.
006780*
006790*================================================================*
006800 レセ並び順降順ファイル作成 SECTION.
006810******************************************************************************
006820*  全印刷の場合、
006830*  レセプトを総括表の印字順番と逆に並べるため、順番を出力する。
006840******************************************************************************
006850*
006860     OPEN INPUT 作業ファイル１.
006870         MOVE NC"作１" TO ファイル名.
006880         PERFORM オープンチェック.
006890*
006900     MOVE SPACE TO 終了フラグ.
006910     MOVE ZERO  TO 順番ＷＲ.
006950     MOVE 99999      TO 作１−印刷順序.
007010     MOVE HIGH-VALUE TO 作１−保番.
007010     MOVE HIGH-VALUE TO 作１−種別.
006960     MOVE 99999999   TO 作１−保険者番号.
006980     MOVE 9          TO 作１−本人家族区分.
007000     MOVE 999999     TO 作１−患者番号.
007010     MOVE HIGH-VALUE TO 作１−枝番.
007020     MOVE 9          TO 作１−施術和暦.
007030     MOVE 99         TO 作１−施術年.
007040     MOVE 99         TO 作１−施術月.
007050     START 作業ファイル１ KEY IS <=  作１−印刷順序
007060                                     作１−保番
                                           作１−種別
007070                                     作１−保険者番号
007080                                     作１−本人家族区分
007110                                     作１−患者コード
007090                                     作１−施術和暦年月
007130                                     REVERSED
007140     END-START.
007150     IF 状態キー = "00"
007160*
007170         MOVE SPACE TO 終了フラグ
007180         PERFORM 作業ファイル１読込
007190         PERFORM UNTIL 終了フラグ = "YES"
007200             PERFORM 作２レコードセット
007210             PERFORM 作２ファイル書込
007220             PERFORM 作業ファイル１読込
007230         END-PERFORM
007240     END-IF.
007250*
007260     CLOSE 作業ファイル１.
007270*
007280*================================================================*
007290 レセ並び順ファイル作成 SECTION.
007300******************************************************************************
007310*  全印刷の場合、
007320*  レセプトを総括表の印字順番と同じ様に並べるため、順番を出力する。
007330******************************************************************************
007340*
007350     OPEN INPUT 作業ファイル１.
007360         MOVE NC"作１" TO ファイル名.
007370         PERFORM オープンチェック.
007380*
007390     MOVE SPACE TO 終了フラグ.
007400     MOVE ZERO  TO 順番ＷＲ.      
007410     PERFORM 作業ファイル１読込.
007420     PERFORM UNTIL 終了フラグ = "YES"
007430             PERFORM 作２レコードセット
007440             PERFORM 作２ファイル書込
007450             PERFORM 作業ファイル１読込
007460     END-PERFORM.
007470*
007480     CLOSE 作業ファイル１.
007490*
007500*================================================================*
007510 作２レコードセット SECTION.
007520*
           MOVE 作１−レセ種別       TO 作２−レセ種別.
007530     MOVE 作１−施術和暦       TO 作２−施術和暦.
007540     MOVE 作１−施術年         TO 作２−施術年.
007550     MOVE 作１−施術月         TO 作２−施術月.
007560     MOVE 作１−患者コード     TO 作２−患者コード.
007570     MOVE 作１−保険種別       TO 作２−保険種別
006610     MOVE 作１−患者氏名       TO 作２−患者氏名.
006610     MOVE 作１−被保険者氏名   TO 作２−被保険者氏名.
007630     IF 作１−本人家族区分 = 1
007600         MOVE "本人"           TO 作２−本人家族
007610     ELSE
007620         MOVE "家族"           TO 作２−本人家族
007630     END-IF.
007592     MOVE 作１−保険者番号     TO 保険者番号ＷＲ.
           IF 保険者番号ＷＲ(1:2) = "00"
              MOVE 保険者番号ＷＲ(3:8)  TO 作２−請求保険者番号
           ELSE
              MOVE 保険者番号ＷＲ    TO 作２−請求保険者番号
           END-IF.
           MOVE 作１−費用額         TO 作２−費用額.
           MOVE 作１−請求額         TO 作２−請求額.
           MOVE 作１−負担額         TO 作２−負担額.
005210     MOVE 作１−レセ印刷区分   TO 作２−レセ印刷区分.
           IF 作１−レセ種別 = 7
              MOVE 1                 TO 作２−保険区分
006670        COMPUTE 生保順番Ｗ  = 生保順番Ｗ + 1
006680        MOVE 生保順番Ｗ            TO 作２−順番
           ELSE
              MOVE ZERO              TO 作２−保険区分
006670        COMPUTE 順番ＷＲ  = 順番ＷＲ + 1
006680        MOVE 順番ＷＲ          TO 作２−順番
           END-IF.
007660*
007670*================================================================*
007680 作２ファイル書込 SECTION.
007690*
007700     WRITE 作２−レコード
007710     INVALID KEY
007720         MOVE NC"作２"  TO ファイル名
007730         PERFORM エラー表示
007740     END-WRITE.
007741*================================================================*
009192 助成レセまとめ判定 SECTION.
009193*---------------------------------------------------------------------------*
009194* 本体まとめ区分＝１
009195* の時は、フラグYES (金額を助成込みで印字）
009196*（例：横浜市の障害は、本体保険（国保系）のレセプト１枚で請求、助成レセはなし）
009197*---------------------------------------------------------------------------*
009198*
009199     MOVE SPACE TO 助成レセまとめフラグ.
009200*
009201     IF レセ−本体まとめ区分 = 1 
009202        MOVE "YES" TO 助成レセまとめフラグ
009203     END-IF.
007780*
013450*================================================================*
013460 印刷順序セット SECTION.
013470*
013480**///  印刷順序の変更はこのSECTIONで   ////**
013490*
013500**********************************************
013510*  汎用印刷順序:保険名称(保険種別コード)     *
013520*    1:国保(1)、退職(8)、後高(5)             *
013530*    2:社保(2)                               *
013540*    3:協会(2)                               *
013550*    4:船員(7)                               *
013560*    5:組合(3)                               *
013570*    6:自衛官(9)                             *
013580*    7:共済(4)                               *
013590*    8:被爆(54)                              *
013590*    9:障害(53)                              *
013590*   10:母子(52)                              *
013590*   11:子供(55,60)                           *
013590*   12:生保(85)                              *
013630*                                            *
013640*  ※保険種別 06:日雇は 02:協会へ含める。    *
      *                                            *
      *  ＋　県番号                                *
      *  国保系のみ 1:国退 2:国組 3:後高           *
013650**********************************************
013660*
            INITIALIZE 印刷順序Ｗ.
013840      EVALUATE  印刷保険種別Ｗ
013850* 国保
013860      WHEN  01
013870         MOVE 01               TO 順番Ｗ
               MOVE 受−法別番号     TO 県番Ｗ
               IF 受−保険者番号(3:1) = 3
                  MOVE 2             TO 保種Ｗ
               ELSE
                  MOVE 1             TO 保種Ｗ
               END-IF
013880* 社保
013890      WHEN  02
               IF 受−保険者番号(5:1) = SPACE
013910             MOVE 02           TO 順番Ｗ
                   MOVE 受−法別番号 TO 県番Ｗ
013920         ELSE
013930             MOVE 03           TO 順番Ｗ
                   MOVE 受−都道府県番号 TO 県番Ｗ
013940         END-IF
               MOVE ZERO             TO 保種Ｗ
013950* 組合
013960      WHEN  03
013910         MOVE 05               TO 順番Ｗ
               MOVE 受−都道府県番号 TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014020* 共済
014030      WHEN  04
013910         MOVE 07               TO 順番Ｗ
               MOVE SPACE            TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014090* 老人
014100      WHEN  05
013870         MOVE 01               TO 順番Ｗ
               MOVE 受−都道府県番号 TO 県番Ｗ
               MOVE 3                TO 保種Ｗ
014120* 日雇
014130      WHEN  06
013930         MOVE 03               TO 順番Ｗ
               MOVE 受−都道府県番号 TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014120* 船員
014130      WHEN  06 THRU 07
013930         MOVE 04               TO 順番Ｗ
               MOVE 受−都道府県番号 TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014190* 退職国保
014200      WHEN  08
013870         MOVE 01               TO 順番Ｗ
               MOVE 受−都道府県番号 TO 県番Ｗ
               MOVE 1                TO 保種Ｗ
014220* 自衛官
014230      WHEN  09
013910         MOVE 06               TO 順番Ｗ
               MOVE SPACE            TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014250* 被爆
014260      WHEN 54
013910         MOVE 08               TO 順番Ｗ
               MOVE SPACE            TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014250* 障害
014260      WHEN 53
013910         MOVE 09               TO 順番Ｗ
               MOVE SPACE            TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014250* 母子
014260      WHEN 52
013910         MOVE 10               TO 順番Ｗ
               MOVE SPACE            TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014250* 子供
014260      WHEN 55
            WHEN 60
013910         MOVE 11               TO 順番Ｗ
               MOVE SPACE            TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014250* 生保
014260      WHEN 85
013910         MOVE 12               TO 順番Ｗ
               MOVE SPACE            TO 県番Ｗ
               MOVE ZERO             TO 保種Ｗ
014370*
014380      WHEN OTHER
014390         MOVE SPACE                TO エラーメッセージＷ
014400         MOVE 受−患者コード       TO エラー患者コードＷ
014410         MOVE 印刷保険種別Ｗ       TO エラー保険種別Ｗ
014420         MOVE "-"                  TO エラー区切りＷ
014430         MOVE  NC"保険種別が異常です。患者−保険種別" TO 連メ３−メッセージ
014440         MOVE  エラーメッセージＷ  TO 連メ３−メッセージ１
014450         CALL   "MSG003"
014460         CANCEL "MSG003"
014470      END-EVALUATE.
014480*
007781*================================================================*
007782*================================================================*
007783******************************************************************
007784 END PROGRAM YAZ6061.
007785******************************************************************
