002190******************************************************************
002191*            IDENTIFICATION      DIVISION                        *
002192******************************************************************
002193 IDENTIFICATION          DIVISION.
002194 PROGRAM-ID.             YJK581.
002195 AUTHOR.                 岡田　憲和
002196*
002197*----------------------------------------------------------------*
002198*      請求台帳【ﾃﾞｰﾀ作成】柔ｳｨﾝﾄﾞｳｽﾞ95版
002199* 請求年月Ver.
002200*      MED = YJK580G
002201*----------------------------------------------------------------*
002202 DATE-WRITTEN.           2012-08-03
002203 DATE-COMPILED.          2012-08-03
002204*----------------------------------------------------------------*
002205******************************************************************
002206*            ENVIRONMENT         DIVISION                        *
002207******************************************************************
002208 ENVIRONMENT             DIVISION.
002209 CONFIGURATION           SECTION.
002210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
002211 OBJECT-COMPUTER.        FMV-DESKPOWER.
002212 SPECIAL-NAMES.          CONSOLE  IS  CONS
002213                         SYSERR   IS  MSGBOX.
002214 INPUT-OUTPUT            SECTION.
002215 FILE-CONTROL.
002225     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
002226                             ORGANIZATION             IS  INDEXED
002227                             ACCESS MODE              IS  DYNAMIC
002228                             RECORD KEY               IS  受−施術和暦年月
002229                                                          受−患者コード
002230                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
002231                                                          受−患者カナ
002232                                                          受−患者コード
002233                             ALTERNATE RECORD KEY     IS  受−患者コード
002234                                                          受−施術和暦年月
002235                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
002236                                                          受−保険種別
002237                                                          受−保険者番号
002238                                                          受−患者コード
002239                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
002240                                                          受−公費種別
002241                                                          受−費用負担者番号
002242                                                          受−患者コード
002243                             ALTERNATE RECORD KEY     IS  受−施術和暦年月
002244                                                          受−助成種別
002245                                                          受−費用負担者番号助成
002246                                                          受−患者コード
002247                             ALTERNATE RECORD KEY     IS  受−請求和暦年月
002248                                                          受−施術和暦年月
002249                                                          受−患者コード
002250                             FILE STATUS              IS  状態キー
002251                             LOCK        MODE         IS  AUTOMATIC.
002261     SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
002262                             ORGANIZATION             IS  INDEXED
002263                             ACCESS MODE              IS  DYNAMIC
002264                             RECORD KEY               IS  レセ−施術和暦年月
002265                                                          レセ−患者コード
002266                                                          レセ−レセ種別
002267                             ALTERNATE RECORD KEY     IS  レセ−患者コード
002268                                                          レセ−施術和暦年月
002269                                                          レセ−レセ種別
002270                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
002271                                                          レセ−施術和暦年月
002272                                                          レセ−患者コード
002273                                                          レセ−レセ種別
002274                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
002275                                                          レセ−レセ種別
002276                                                          レセ−請求保険者番号
002277                                                          レセ−患者コード
002278                                                          レセ−施術和暦年月
002279                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
002280                                                          レセ−請求保険者番号
002281                                                          レセ−患者コード
002282                                                          レセ−レセ種別
002283                                                          レセ−施術和暦年月
002284                             FILE STATUS              IS  状態キー
002285                             LOCK        MODE         IS  AUTOMATIC.
002286     SELECT  作業ファイル１  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5801L.DAT"
002287                             ORGANIZATION             IS  SEQUENTIAL
002288                             ACCESS                   IS  SEQUENTIAL
002289                             FILE        STATUS       IS  状態キー
002290                             LOCK        MODE         IS  AUTOMATIC.
002291     SELECT  作業ファイル３  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5803L.DAT"
002292                             ORGANIZATION             IS  INDEXED
002293                             ACCESS                   IS  DYNAMIC
002294                             RECORD      KEY          IS  作３−請求和暦年月
002295                                                          作３−印刷区分
002296                                                          作３−保険区分
002297                                                          作３−保険者番号
002298                                                          作３−本人家族区分
002299                                                          作３−被保険者カナ
002300                                                          作３−患者カナ
002301                                                          作３−当月区分
002302                                                          作３−施術和暦年月
002303                                                          作３−患者コード
002304                                                          作３−親子区分
002305                                                          作３−助成区分
002307                             FILE        STATUS       IS  状態キー
002308                             LOCK        MODE         IS  AUTOMATIC.
002309* レセ並び順用
002310     SELECT  作業ファイル４  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5912L.DAT"
002311                             ORGANIZATION             IS  INDEXED
002312                             ACCESS                   IS  DYNAMIC
002313                             RECORD      KEY          IS  作４−施術和暦年月
002314                                                          作４−患者コード
002315                                                          作４−保険種別
002316                             FILE        STATUS       IS  状態キー
002317                             LOCK        MODE         IS  AUTOMATIC.
002318******************************************************************
002319*                      DATA DIVISION                             *
002320******************************************************************
002321 DATA                    DIVISION.
002322 FILE                    SECTION.
002326*                           ［ＲＬ＝  ３２０］
002327 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
002328     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
002332*                          ［ＲＬ＝  １５３６］
002333 FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS.
002334     COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
002335*
002336*
002337 FD  作業ファイル１ RECORD  CONTAINS 288 CHARACTERS.
002338 01  作１−レコード.
002339     03  作１−レコードデータ.
002340         05  作１−請求和暦年月.
002341             07  作１−請求和暦            PIC 9.
002342             07  作１−請求年              PIC 9(2).
002343             07  作１−請求月              PIC 9(2).
002344         05  作１−施術和暦年月.
002345             07  作１−施術和暦            PIC 9.
002346             07  作１−施術年              PIC 9(2).
002347             07  作１−施術月              PIC 9(2).
002348         05  作１−印刷区分                PIC 9(2).
002349         05  作１−保険者番号              PIC X(10).
002350         05  作１−記号                    PIC X(24).
002351         05  作１−番号                    PIC X(30).
002352         05  作１−被保険者カナ            PIC X(50).
002353         05  作１−被保険者氏名            PIC X(50).
002354         05  作１−患者カナ                PIC X(50).
002355         05  作１−本人家族区分            PIC 9.
002356         05  作１−費用額                  PIC 9(7).
002357         05  作１−負担額                  PIC 9(7).
002358         05  作１−請求額                  PIC 9(7).
002359         05  作１−患者コード.
002360             07 作１−患者番号             PIC 9(6).
002361             07 作１−枝番                 PIC X(1).
002362         05  作１−保険種別                PIC 9(2).
002363         05  作１−親保険種別              PIC 9(2).
002364         05  作１−老人レコード区分        PIC 9.
002365         05  作１−助成レコード区分        PIC 9.
002366         05  FILLER                        PIC X(27).
002367* 保険者番号順ファイル
002368 FD  作業ファイル３ RECORD  CONTAINS 192 CHARACTERS.
002369 01  作３−レコード.
002370     03  作３−レコードキー.
002371         05  作３−請求和暦年月.
002372             07  作３−請求和暦            PIC 9.
002373             07  作３−請求年              PIC 9(2).
002374             07  作３−請求月              PIC 9(2).
002375         05  作３−印刷区分                PIC 9(2).
002376         05  作３−保険区分                PIC 9.
002377         05  作３−保険者番号              PIC 9(8).
002378         05  作３−本人家族区分            PIC 9.
002379         05  作３−被保険者カナ            PIC X(50).
002380         05  作３−患者カナ                PIC X(50).
002381         05  作３−当月区分                PIC 9.
002382         05  作３−施術和暦年月.
002383             07  作３−施術和暦            PIC 9.
002384             07  作３−施術年              PIC 9(2).
002385             07  作３−施術月              PIC 9(2).
002386         05  作３−患者コード.
002387             07 作３−患者番号             PIC 9(6).
002388             07 作３−枝番                 PIC X(1).
002389         05  作３−親子区分                PIC 9.
002390         05  作３−助成区分                PIC 9.
002393     03  作３−レコードデータ.
002394         05  FILLER                        PIC X(60).
002395*
002396 FD  作業ファイル４ RECORD  CONTAINS 32 CHARACTERS.
002397 01  作４−レコード.
002398     03  作４−レコードキー.
002399         05  作４−施術和暦年月.
002400             07  作４−施術和暦            PIC 9.
002401             07  作４−施術年              PIC 9(2).
002402             07  作４−施術月              PIC 9(2).
002403         05  作４−患者コード.
002404             07 作４−患者番号             PIC 9(6).
002405             07 作４−枝番                 PIC X(1).
002406         05  作４−保険種別                PIC 9(2).
002407     03  作４−レコードデータ.
002408         05  作４−順番                    PIC 9(4).
002409         05  FILLER                        PIC X(14).
002410*
002411*----------------------------------------------------------------*
002412******************************************************************
002413*                WORKING-STORAGE SECTION                         *
002414******************************************************************
002415 WORKING-STORAGE         SECTION.
002416 01 キー入力                           PIC X    VALUE SPACE.
002417 01 状態キー                           PIC X(2) VALUE SPACE.
002418 01 終了フラグ                         PIC X(3) VALUE SPACE.
002419 01 終了フラグ２                       PIC X(3) VALUE SPACE.
002420 01 終了フラグ３                       PIC X(3) VALUE SPACE.
002421 01 実行キーＷ                         PIC X(3)  VALUE SPACE.
002422 01 施術記録有Ｗ                       PIC X(3) VALUE SPACE.
002423 01 カウンタ                           PIC 9(2)  VALUE ZERO.
002424**
002425 01 保険種別ＷＲ                       PIC 9(2) VALUE ZERO.
002426 01 印刷形式ＷＲ                       PIC 9    VALUE ZERO.
002427 01 保険者番号ＷＲ                     PIC X(10) VALUE SPACE.
002428 01 レセプト種類ＷＲ                   PIC X(4) VALUE SPACE.
002429 01 本人家族区分ＷＲ                   PIC 9    VALUE ZERO.
002430 01 施術和暦年月ＷＲ.
002431    03 施術和暦ＷＲ                    PIC 9    VALUE ZERO.
002432    03 施術年ＷＲ                      PIC 9(2) VALUE ZERO.
002433    03 施術月ＷＲ                      PIC 9(2) VALUE ZERO.
002434 01 請求和暦年月ＷＲ.
002435    03 請求和暦ＷＲ                    PIC 9    VALUE ZERO.
002440    03 請求年ＷＲ                      PIC 9(2) VALUE ZERO.
002450    03 請求月ＷＲ                      PIC 9(2) VALUE ZERO.
002460**
002470 01 ファイル名                         PIC N(8) VALUE SPACE.
002480 01 保険種別Ｗ                         PIC 9(2) VALUE ZERO.
002490 01 順番Ｗ                             PIC 9(4) VALUE ZERO.
002500 01 印刷区分Ｗ                         PIC 9(2) VALUE ZERO.
002510*
002520** 助成レセまとめ用
002530 01 助成レセまとめフラグ               PIC X(3)  VALUE SPACE.
002540*
002550** 合計集計用
002560 01 合計Ｗ.
002570    03 件数合計Ｗ                      PIC 9(4) VALUE ZERO.
002580    03 費用額合計Ｗ                    PIC 9(8) VALUE ZERO.
002590    03 請求額合計Ｗ                    PIC 9(8) VALUE ZERO.
002600** エラーメッセージ用
002610 01 エラーメッセージＷ.
002620    03 エラー患者コードＷ              PIC X(7) VALUE SPACE.
002630    03 エラー区切りＷ                  PIC X(1) VALUE SPACE.
002640    03 エラー保険種別Ｗ                PIC X(2) VALUE SPACE.
002650    03 FILLER                          PIC X(10) VALUE SPACE.
002660**
002661 01 複合プログラム名Ｗ                 PIC X(8) VALUE "MOJI2".
002662*
002670** 保険者番号右詰め用
002680 01 保険者番号ＷＴ.
002690    03 保険者番号左詰めＷ.
002700      05 保険者番号左詰めＷ１          PIC X OCCURS 8 VALUE SPACE.
002710    03 保険者番号右詰めＷ.
002720      05 保険者番号右詰めＷ１          PIC X OCCURS 8 VALUE ZERO.
002730    03 保険者番号数字Ｗ                PIC 9(8)  VALUE ZERO.
002740    03 保険者番号Ｗ                    PIC X(8)  VALUE SPACE.
002750**
002760 01 計算機西暦年Ｗ                     PIC 9(2).
002770* 日付ＷＯＲＫ
002780 01 計算機西暦.
002790    03 計算機西暦年                    PIC 9(4).
002800    03 計算機西暦月日                  PIC 9(4).
002810 01 計算機西暦Ｒ REDEFINES 計算機西暦.
002820    03 計算機世紀                      PIC 9(2).
002830    03 計算機日付                      PIC 9(6).
002840    03 計算機日付Ｒ REDEFINES 計算機日付.
002850       05 計算機年月                   PIC 9(4).
002860       05 計算機年月Ｒ REDEFINES 計算機年月.
002870         07 計算機年                   PIC 9(2).
002880         07 計算機月                   PIC 9(2).
002890       05 計算機日                     PIC 9(2).
002900*
002910******************************************************************
002920*                          連結項目                              *
002930******************************************************************
002940*
002950********************
002960* メッセージ表示キー *
002970********************
002980 01 連メ３−キー IS EXTERNAL.
002990    03  連メ３−メッセージ             PIC N(20).
003000    03  連メ３−メッセージ１           PIC X(20).
003010***
003020 01 連入−画面情報ＹＪＫ５８０ IS EXTERNAL.
003030    03 連入−印刷形式                  PIC 9.
003040    03 連入−請求和暦年月.
003050       05 連入−請求和暦               PIC 9.
003060       05 連入−請求年月.
003070         07 連入−請求年               PIC 9(2).
003080         07 連入−請求月               PIC 9(2).
003090    03 連入−保険種別                  PIC 9(2).
003640**
003650* 暗号複合用
003651 01 連暗号複合−暗号情報 IS EXTERNAL.
003652    03 連暗号複合−入力情報.
003653       05 連暗号複合−記号               PIC X(24).
003654       05 連暗号複合−番号               PIC X(30).
003655       05 連暗号複合−暗号化項目.
003656          07 連暗号複合−暗号患者番号    PIC X(6).
003657          07 連暗号複合−暗号判定記号    PIC X.
003658          07 連暗号複合−暗号判定番号    PIC X.
003659          07 連暗号複合−暗号記号        PIC X(24).
003660          07 連暗号複合−暗号番号        PIC X(30).
003661    03 連暗号複合−出力情報.
003662       05 連暗号複合−複合した記号       PIC X(24).
003663       05 連暗号複合−複合した番号       PIC X(30).
003666*
003667******************************************************************
003670*                      PROCEDURE  DIVISION                       *
003680******************************************************************
003690 PROCEDURE               DIVISION.
003700************
003710*           *
003720* 初期処理   *
003730*           *
003740************
003751     PERFORM 初期化.
003760************
003770*           *
003780* 主処理     *
003790*           *
003800************
003810     PERFORM 作業ファイル作成.
003820************
003830*           *
003840* 終了処理   *
003850*           *
003860************
003870     PERFORM 終了処理.
003880     MOVE ZERO TO PROGRAM-STATUS.
003890     EXIT PROGRAM.
003900*
003910*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
003920*================================================================*
003930 初期化 SECTION.
003940*
003950     PERFORM ファイルオープン.
003960* 連結項目の待避
003971     MOVE 連入−印刷形式      TO 印刷形式ＷＲ.
003980     MOVE 連入−請求和暦      TO 請求和暦ＷＲ.
003990     MOVE 連入−請求年        TO 請求年ＷＲ.
004000     MOVE 連入−請求月        TO 請求月ＷＲ.
004010     MOVE 連入−保険種別      TO 保険種別ＷＲ.
004020*
004030*================================================================*
004040 ファイルオープン SECTION.
004050*
004180     OPEN INPUT 受診者情報Ｆ.
004190         MOVE NC"受診者情報Ｆ" TO ファイル名.
004200         PERFORM オープンチェック.
004240     OPEN INPUT レセプトＦ.
004241         MOVE NC"レセプトＦ" TO ファイル名.
004242         PERFORM オープンチェック.
004243*================================================================*
004250 オープンチェック SECTION.
004260*
004270     IF 状態キー  NOT =  "00"
004280         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
004290         DISPLAY NC"状態キー：" 状態キー         UPON CONS
004300         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004310                                                 UPON CONS
004320         ACCEPT  キー入力 FROM CONS
004330         PERFORM ファイル閉鎖
004340         MOVE 99 TO PROGRAM-STATUS
004350         EXIT PROGRAM.
004360*================================================================*
004370 ファイル閉鎖 SECTION.
004380*
004390     CLOSE 受診者情報Ｆ レセプトＦ.
004410*================================================================*
004420 終了処理 SECTION.
004430*
004440     PERFORM ファイル閉鎖.
004450*================================================================*
004460 エラー表示 SECTION.
004470*
004480     DISPLAY NC"状態キー" 状態キー  UPON CONS.
004490     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
004500     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
004510     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
004520     ACCEPT  キー入力 FROM CONS.
004521
004530     PERFORM ファイル閉鎖.
004540     MOVE 99 TO PROGRAM-STATUS.
004550     EXIT PROGRAM.
004560*================================================================*
004570 エラー表示Ｒ SECTION.
004580*
004590     DISPLAY NC"ファイル読込エラー" ファイル名     UPON CONS.
004600     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
004610     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
004620     ACCEPT  キー入力 FROM CONS.
004630     PERFORM ファイル閉鎖.
004640     MOVE 99 TO PROGRAM-STATUS.
004650     EXIT PROGRAM.
004660*================================================================*
004670 作業ファイル作成 SECTION.
004680*
004690     PERFORM 保険者番号順ファイル作成.
004700*
004710     OPEN OUTPUT 作業ファイル１.
004720         MOVE NC"作１" TO ファイル名.
004730         PERFORM オープンチェック.
004740     OPEN INPUT  作業ファイル３.
004750         MOVE NC"作３" TO ファイル名.
004760         PERFORM オープンチェック.
004770*
004780     PERFORM 作業ファイル１作成.
004790*
004800     CLOSE 作業ファイル３ 作業ファイル１.
004810*
004820* レセ並び順ファイル作成
004830     OPEN OUTPUT 作業ファイル４.
004840         MOVE NC"作４" TO ファイル名.
004850         PERFORM オープンチェック.
004860*
004870     IF  印刷形式ＷＲ = ZERO
004880         PERFORM レセ並び順ファイル作成
004890     END-IF.
004900*
004910     CLOSE 作業ファイル４.
004920*
004930*================================================================*
004940 保険者番号順ファイル作成 SECTION.
004950**********************************************************************
004960**   受診者情報Ｆから、該当請求年月のデータを抽出し、
004970**   作業ファイル３(保険者番号順)に書き出す.
004980**********************************************************************
004990*
005000     OPEN OUTPUT 作業ファイル３.
005010         MOVE NC"作３" TO ファイル名.
005020         PERFORM オープンチェック.
005030*
005040     MOVE 請求和暦ＷＲ  TO レセ−請求和暦.
005050     MOVE 請求年ＷＲ    TO レセ−請求年.
005060     MOVE 請求月ＷＲ    TO レセ−請求月.
005070     MOVE 1             TO レセ−施術和暦.
005080     MOVE ZERO          TO レセ−施術年.
005090     MOVE ZERO          TO レセ−施術月.
005100     MOVE ZERO          TO レセ−患者番号.
005110     MOVE LOW-VALUE     TO レセ−枝番.
005120     MOVE ZERO          TO レセ−レセ種別.
005122     START レセプトＦ   KEY IS >= レセ−請求和暦年月
005130                                  レセ−施術和暦年月
005140                                  レセ−患者コード
005150                                  レセ−レセ種別
005151     END-START.
005161     IF 状態キー = "00"
005170         MOVE SPACE  TO 終了フラグ
005180         PERFORM レセプトＦ読込
005190         PERFORM UNTIL ( 終了フラグ = "YES" ) OR
005200                       ( レセ−請求和暦 NOT = 請求和暦ＷＲ ) OR
005210                       ( レセ−請求年   NOT = 請求年ＷＲ   ) OR
005220                       ( レセ−請求月   NOT = 請求月ＷＲ   )
005230*            データチェックで受診者Ｆも読む
005231             PERFORM データチェック
005233*
005234             IF  実行キーＷ = "YES"
005240                 MOVE SPACE TO 作３−レコード
005250                 INITIALIZE    作３−レコード
005260                 MOVE レセ−請求和暦   TO  作３−請求和暦
005270                 MOVE レセ−請求年     TO  作３−請求年
005280                 MOVE レセ−請求月     TO  作３−請求月
005290                 MOVE レセ−施術和暦   TO  作３−施術和暦
005300                 MOVE レセ−施術年     TO  作３−施術年
005310                 MOVE レセ−施術月     TO  作３−施術月
005320
005321                 IF レセ−請求和暦年月 = 受−施術和暦年月
005330                     MOVE ZERO       TO  作３−当月区分
005340                 ELSE
005350                     MOVE 1          TO  作３−当月区分
005360                 END-IF
005370                 PERFORM 印刷区分セット
005380*
005386                 IF  受−助成種別   = ZERO  OR 50
005387                     IF ( 受−保険種別   NOT = ZERO ) AND
005388                        ( 受−保険者番号 NOT = SPACE )
005389** 助成なし(生保親ありは助成なし扱い)
005390                        MOVE ZERO  TO   作３−助成区分
005391                     ELSE
005392** 生保単独
005393                        MOVE 2     TO   作３−助成区分
005394                     END-IF
005395                 ELSE
005396** 助成あり
005397                     MOVE 1        TO   作３−助成区分
005398**
005399*                  （助成まとめは、助成なし扱いにする）
005400                     PERFORM 助成レセまとめ判定
005401                     IF ( 助成レセまとめフラグ = "YES" )
005402                        MOVE ZERO  TO  作３−助成区分
005403                     END-IF
005406*
005407                 END-IF
005408*
005409                 IF ( 受−公費種別       = ZERO  ) AND
005410                    ( 受−費用負担者番号 = SPACE )
005411                     MOVE 受−保険者番号     TO 保険者番号Ｗ
005421                 ELSE
005430* 老人は、市町村番号
005440                     MOVE 受−費用負担者番号 TO 保険者番号Ｗ
005450                 END-IF
005460                 PERFORM 保険者番号右詰め
005470                 MOVE 保険者番号数字Ｗ   TO 作３−保険者番号
005480*
005490                 MOVE 受−本人家族区分   TO 作３−本人家族区分
005500                 MOVE 受−被保険者カナ   TO 作３−被保険者カナ
005510                 MOVE 受−患者カナ       TO 作３−患者カナ
005520                 MOVE 受−患者コード     TO 作３−患者コード
005530*
005721                 EVALUATE レセ−レセ種別
005722                 WHEN 1
005723                 WHEN 2
005724                     MOVE ZERO           TO 作３−親子区分
005725                 WHEN 3
005726                     MOVE 1              TO 作３−親子区分
005728                     PERFORM 印刷区分セット２
005729                     MOVE 受−費用負担者番号助成  TO 作３−保険者番号
005730                 END-EVALUATE
005731*
005732                 IF (レセ−レセ種別 = 3) AND (作３−助成区分 = ZERO)
005733                    CONTINUE
005734                 ELSE
005740                    WRITE 作３−レコード
005741                    INVALID KEY
005742                       MOVE NC"作３"  TO ファイル名
005743                    PERFORM エラー表示
005744                    END-WRITE
005745                 END-IF
005746*
005747             END-IF
005750
005751             PERFORM レセプトＦ読込
005752        END-PERFORM
005760     END-IF.
005761*
005770     CLOSE 作業ファイル３.
005780*
005790*================================================================*
005800 作業ファイル１作成 SECTION.
005810*
005820     MOVE SPACE  TO 終了フラグ.
005830*    作業ファイル３と受診者ＦとレセＦ読込.
005831     PERFORM 作業ファイル３読込.
005840     PERFORM UNTIL  終了フラグ３ = "YES"
005860*
005870         MOVE "YES"          TO 実行キーＷ
005871         EVALUATE 印刷形式ＷＲ
005872* 全部
005873         WHEN ZERO
005874             CONTINUE
005875* 助成以外
005876         WHEN 1
005877             IF 作３−助成区分  NOT = ZERO
005878                 MOVE SPACE  TO 実行キーＷ
005879             END-IF
005880* 助成のみ
005881         WHEN 2
005882             IF 作３−助成区分  NOT = 1
005883                 MOVE SPACE  TO 実行キーＷ
005884             END-IF
005885         WHEN OTHER
005886             CONTINUE
005887         END-EVALUATE
005888*
005891** 労災・自賠責・自由は対象外
005892         IF  受−保険種別 = 70 OR 80 OR 90
005893             MOVE SPACE  TO 実行キーＷ
005900         END-IF
005910**
005920         IF  実行キーＷ = "YES"
005954*
005960*↓* 特別処理（助成レセまとめ）
005970             IF 受−助成種別 NOT = ZERO
005980                PERFORM 助成レセまとめ判定
005990             ELSE
006000                MOVE SPACE TO 助成レセまとめフラグ
006010             END-IF
006020*↑*
006030*
006040*            ********
006050*            * 健保 *
006060*            ********
006074             IF 作３−親子区分 = ZERO
006075                 IF ( 受−保険種別   NOT = ZERO ) AND
006080                    ( 受−保険者番号 NOT = SPACE )
006090*                **********************
006100*                * 作業ファイル作成 *
006110*                **********************
006123                     IF ( 受−公費種別       = ZERO  ) AND
006130                        ( 受−費用負担者番号 = SPACE )
006143                         IF 作３−印刷区分  = 1 OR 2 OR 3 OR 4 OR 12 OR 13
006150                            MOVE SPACE TO 作１−レコード
006160                            INITIALIZE 作１−レコード
006170                            MOVE 受−保険種別       TO 作１−保険種別
006180                            MOVE 受−保険者番号     TO 作１−保険者番号
006190*
006200*↓* 特別処理（助成レセまとめ）
006210                            IF 助成レセまとめフラグ = "YES"
006220                               MOVE レセ−合計           TO 作１−費用額
006230                               MOVE レセ−受給者負担額   TO 作１−負担額
006240                               COMPUTE 作１−請求額 = レセ−合計 - レセ−受給者負担額
006250                            ELSE
006260                               MOVE レセ−合計           TO 作１−費用額
006270                               MOVE レセ−一部負担金     TO 作１−負担額
006280                               MOVE レセ−請求金額       TO 作１−請求額
006290                            END-IF
006300*↑*
006310*
006326                            PERFORM 作１レコードセット
006330                            PERFORM 作１ファイル書込
006340                         END-IF
006350                     END-IF
006360                 END-IF
006370*            ********
006380*            * 老人 *
006390*            ********
006400                 IF ( 受−公費種別       NOT = ZERO ) AND
006410                    ( 受−費用負担者番号 NOT = SPACE )
006420*                    **********************
006430*                    * 作業ファイル作成 *
006440*                    **********************
006450                     IF 作３−印刷区分  = 5
006460                         MOVE SPACE TO 作１−レコード
006470                         INITIALIZE 作１−レコード
006480                         MOVE 受−公費種別        TO 作１−保険種別
006490                         MOVE 受−費用負担者番号  TO 作１−保険者番号
006500                         MOVE  1                  TO 作１−老人レコード区分
006510*
006520*↓* 特別処理（助成レセまとめ）
006530                         IF 助成レセまとめフラグ = "YES"
006540                            MOVE レセ−合計            TO 作１−費用額
006550                            MOVE レセ−受給者負担額    TO 作１−負担額
006560                            COMPUTE 作１−請求額 = レセ−合計 - レセ−受給者負担額
006570                         ELSE
006580                            MOVE レセ−合計            TO 作１−費用額
006590                            MOVE レセ−一部負担金      TO 作１−負担額
006600                            MOVE レセ−請求金額        TO 作１−請求額
006610                         END-IF
006620*↑*
006630*
006640                         PERFORM 作１レコードセット
006650                         PERFORM 作１ファイル書込
006660                     END-IF
006670                 END-IF
006680             END-IF
006681*            ********
006690*            * 助成 *
006700*            ********
006711             IF 作３−親子区分 = 1
006712                IF ( 受−助成種別       NOT = ZERO ) AND
006720                   ( 受−費用負担者番号助成 NOT = SPACE )
005930*         / 横浜・川崎の小児医療の請求額０円は対象外にする /170411
006880                   IF (レセ−助成請求金額 = ZERO) AND
                            (受−助成種別 = "55") AND
                            (受−費用負担者番号助成(3:3) = "144" OR "145")
                            CONTINUE
                         ELSE
006730*                    **********************
006740*                    * 作業ファイル作成 *
006751*                    **********************
006768                      IF 作３−印刷区分  = 6 OR 7 OR 8 OR 9 OR 10 OR 11
006770                         MOVE SPACE TO 作１−レコード
006780                         INITIALIZE 作１−レコード
006790                         MOVE 受−助成種別       TO 作１−保険種別
006810                         IF 受−公費種別 NOT = ZERO
006811                             MOVE 受−公費種別        TO 作１−親保険種別
006812                         ELSE
006813                             MOVE 受−保険種別        TO 作１−親保険種別
006814                         END-IF
006815                         MOVE 受−費用負担者番号助成  TO 作１−保険者番号
006820                         MOVE  1                      TO 作１−助成レコード区分
006850                         MOVE レセ−一部負担金        TO 作１−費用額
006881                         MOVE レセ−受給者負担額      TO 作１−負担額
006890                         MOVE レセ−助成請求金額      TO 作１−請求額
006904                         PERFORM 作１レコードセット
006910                         PERFORM 作１ファイル書込
006920*
006930                      END-IF
006940                   END-IF
                      END-IF
006950             END-IF
006970          END-IF
006971          PERFORM 作業ファイル３読込
006972      END-PERFORM.
006980*
006990*================================================================*
007000*================================================================*
007010 保険者番号右詰め SECTION.
007020*
007030     MOVE 保険者番号Ｗ    TO  保険者番号左詰めＷ.
007040     MOVE ZERO            TO  保険者番号右詰めＷ.
007050     MOVE ZERO            TO  保険者番号数字Ｗ.
007060*
007070     MOVE  9  TO  カウンタ.
007080*
007090     IF  保険者番号左詰めＷ１(8) NOT = SPACE
007100         COMPUTE カウンタ = カウンタ  -  1
007110         MOVE 保険者番号左詰めＷ１(8)  TO  保険者番号右詰めＷ１(カウンタ)
007120     END-IF.
007130     IF  保険者番号左詰めＷ１(7) NOT = SPACE
007140         COMPUTE カウンタ = カウンタ  -  1
007150         MOVE 保険者番号左詰めＷ１(7)  TO  保険者番号右詰めＷ１(カウンタ)
007160     END-IF.
007170     IF  保険者番号左詰めＷ１(6) NOT = SPACE
007180         COMPUTE カウンタ = カウンタ  -  1
007190         MOVE 保険者番号左詰めＷ１(6)  TO  保険者番号右詰めＷ１(カウンタ)
007200     END-IF.
007210     IF  保険者番号左詰めＷ１(5) NOT = SPACE
007220         COMPUTE カウンタ = カウンタ  -  1
007230         MOVE 保険者番号左詰めＷ１(5)  TO  保険者番号右詰めＷ１(カウンタ)
007240     END-IF.
007250     IF  保険者番号左詰めＷ１(4) NOT = SPACE
007260         COMPUTE カウンタ = カウンタ  -  1
007270         MOVE 保険者番号左詰めＷ１(4)  TO  保険者番号右詰めＷ１(カウンタ)
007280     END-IF.
007290     IF  保険者番号左詰めＷ１(3) NOT = SPACE
007300         COMPUTE カウンタ = カウンタ  -  1
007310         MOVE 保険者番号左詰めＷ１(3)  TO  保険者番号右詰めＷ１(カウンタ)
007320     END-IF.
007330     IF  保険者番号左詰めＷ１(2) NOT = SPACE
007340         COMPUTE カウンタ = カウンタ  -  1
007350         MOVE 保険者番号左詰めＷ１(2)  TO  保険者番号右詰めＷ１(カウンタ)
007360     END-IF.
007370     IF  保険者番号左詰めＷ１(1) NOT = SPACE
007380         COMPUTE カウンタ = カウンタ  -  1
007390         MOVE 保険者番号左詰めＷ１(1)  TO  保険者番号右詰めＷ１(カウンタ)
007400     END-IF.
007410*
007420     MOVE 保険者番号右詰めＷ TO 保険者番号数字Ｗ.
007430*
007440*================================================================*
007450 作業ファイル３読込 SECTION.
007460*
007470     READ 作業ファイル３ NEXT
007480     AT END
007490         MOVE "YES" TO 終了フラグ３
007500     NOT AT END
007560         MOVE 作３−施術和暦    TO 受−施術和暦 レセ−施術和暦
007561         MOVE 作３−施術年      TO 受−施術年   レセ−施術年
007562         MOVE 作３−施術月      TO 受−施術月   レセ−施術月
007563         MOVE 作３−患者番号    TO 受−患者番号 レセ−患者番号
007564         MOVE 作３−枝番        TO 受−枝番     レセ−枝番
007565         READ 受診者情報Ｆ
007570         INVALID KEY
007580              MOVE NC"受診者"   TO ファイル名
007590              PERFORM エラー表示Ｒ
007600         END-READ
007610         IF 作３−親子区分 = 1
007611             MOVE 3             TO レセ−レセ種別
007612         ELSE
007613            IF 受−公費種別 = 5
007614                MOVE 2          TO レセ−レセ種別
007615            ELSE
007616                MOVE 1          TO レセ−レセ種別
007617            END-IF
007618         END-IF
007619         READ レセプトＦ
007620         INVALID KEY
007621              MOVE NC"レセプト"   TO ファイル名
007622              PERFORM エラー表示Ｒ
007623         END-READ
007624     END-READ.
007625*
008300*================================================================*
008301 データチェック SECTION.
008302*
008303     MOVE SPACE          TO 実行キーＷ.
008304* *****************************************************************
008305* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
008306* *****************************************************************
008307     IF ( レセ−請求対象区分 NOT = ZERO ) AND
008308        ( レセ−償還払い区分 NOT = 1 )
008309        IF (レセ−レセ種別 = 3) AND ( レセ−会総括表印刷対象区分 = 1 )
008310            CONTINUE
008311        ELSE
008312           MOVE レセ−施術和暦  TO 受−施術和暦
008313           MOVE レセ−施術年    TO 受−施術年
008314           MOVE レセ−施術月    TO 受−施術月
008315           MOVE レセ−患者番号  TO 受−患者番号
008316           MOVE レセ−枝番      TO 受−枝番
008317           READ 受診者情報Ｆ
008318           NOT INVALID KEY
008319**      健保のみ
008320              IF 受−保険分類 = 1
008321                 MOVE "YES"  TO 実行キーＷ
008322              END-IF
008323           END-READ
008324        END-IF
008325     END-IF.
008326*
008362*================================================================*
008363 レセプトＦ読込 SECTION.
008364*
008366     READ レセプトＦ NEXT
008367     AT END
008368         MOVE "YES" TO 終了フラグ
008369     END-READ.
008370*
008371*================================================================*
008372 作１レコードセット SECTION.
008373*
008374     MOVE 作３−印刷区分     TO 作１−印刷区分.
008375     MOVE レセ−請求和暦     TO 作１−請求和暦.
008376     MOVE レセ−請求年       TO 作１−請求年.
008377     MOVE レセ−請求月       TO 作１−請求月.
008378     MOVE 受−施術和暦       TO 作１−施術和暦.
008380     MOVE 受−施術年         TO 作１−施術年.
008390     MOVE 受−施術月         TO 作１−施術月.
008400     MOVE 受−患者カナ       TO 作１−患者カナ.
008410     MOVE 受−患者コード     TO 作１−患者コード.
008420     MOVE 受−被保険者カナ   TO 作１−被保険者カナ.
008430     MOVE 受−被保険者氏名   TO 作１−被保険者氏名.
008440     MOVE 受−本人家族区分   TO 作１−本人家族区分.
008450* 老人・助成の時は、受益者番号を番号に、記号は空白
008460     IF  作１−老人レコード区分 = 1
008470         MOVE SPACE               TO 作１−記号
008480         MOVE 受−受益者番号老人  TO 作１−番号
008490     ELSE
008500         IF 作１−助成レコード区分 = 1
008510            MOVE SPACE               TO 作１−記号
008520            MOVE 受−受益者番号助成  TO 作１−番号
008530         ELSE
008570*            MOVE 受−記号            TO 作１−記号
008571*            MOVE 受−番号            TO 作１−番号
008572*    / 連暗号複合−入力情報セット /
008573             MOVE 受−記号       TO 連暗号複合−記号
008574             MOVE 受−番号       TO 連暗号複合−番号
008575             MOVE 受−暗号化項目 TO 連暗号複合−暗号化項目
008576*
008577             CALL   複合プログラム名Ｗ
008578             CANCEL 複合プログラム名Ｗ
008579*
008580             MOVE 連暗号複合−複合した記号 TO 作１−記号
008581             MOVE 連暗号複合−複合した番号 TO 作１−番号
008582         END-IF
008583     END-IF.
008584*
008590*================================================================*
008850 作１ファイル書込 SECTION.
008860*
008874     WRITE 作１−レコード.
008880     IF 状態キー  NOT =  "00"
008890         MOVE NC"作１"  TO ファイル名
008900         PERFORM エラー表示
008910     END-IF.
008920*
008930*================================================================*
008940 レセ並び順ファイル作成 SECTION.
008950******************************************************************************
008960*  全印刷の場合、
008970*  レセプトを総括表の印字順番と同じ様に並べるため、順番を出力する。
008980******************************************************************************
008990*
009000     OPEN INPUT 作業ファイル１.
009010         MOVE NC"作１" TO ファイル名.
009020         PERFORM オープンチェック.
009030*
009040     MOVE SPACE TO 終了フラグ.
009050     MOVE ZERO  TO 順番Ｗ.
009060     PERFORM 作業ファイル１読込.
009070     PERFORM UNTIL 終了フラグ = "YES"
009080         MOVE 作１−印刷区分  TO 印刷区分Ｗ
009090         PERFORM UNTIL ( 終了フラグ = "YES" ) OR
009100                       ( 作１−印刷区分 NOT = 印刷区分Ｗ )
009110             MOVE 作１−印刷区分  TO 印刷区分Ｗ
009120             PERFORM 作４レコードセット
009130             PERFORM 作４ファイル書込
009140             PERFORM 作業ファイル１読込
009150         END-PERFORM
009160         MOVE ZERO  TO 順番Ｗ
009170     END-PERFORM.
009180*
009190     CLOSE 作業ファイル１.
009200*
009210*================================================================*
009220 作４レコードセット SECTION.
009230*
009240     MOVE 作１−施術和暦       TO 作４−施術和暦.
009250     MOVE 作１−施術年         TO 作４−施術年.
009260     MOVE 作１−施術月         TO 作４−施術月.
009270     MOVE 作１−患者コード     TO 作４−患者コード.
009280     MOVE 作１−保険種別       TO 作４−保険種別.
009290     COMPUTE 順番Ｗ  = 順番Ｗ + 1.
009300     MOVE 順番Ｗ               TO 作４−順番.
009310*
009320*================================================================*
009330 作４ファイル書込 SECTION.
009340*
009354     WRITE 作４−レコード
009360     INVALID KEY
009370         MOVE NC"作４"  TO ファイル名
009380         PERFORM エラー表示
009390     END-WRITE.
009400*================================================================*
009410 作業ファイル１読込 SECTION.
009420*
009430     READ 作業ファイル１ NEXT
009440     AT END
009450         MOVE "YES" TO 終了フラグ
009460     END-READ.
009470*
009480*================================================================*
009490 印刷区分セット SECTION.
009500*
009510*　印刷順を設定
009520*  　１：社保・船員・日雇い　２：国保　　　　３：国保組合
009530*　　４：退職　　　　　　　　５：２７老人　　６：４１老人
009540*　　７：母子　　　　　　　　８：乳幼児　　　９：障害者
009550*　１０：被爆者　　        １１：その他　  １２：組合
009560*　１３：共済
009570*
009580     IF 受−助成種別 NOT = 50
009590         IF 受−公費種別 = 05
009600             MOVE 5      TO 作３−印刷区分
009610         ELSE
009620             EVALUATE 受−保険種別
009630             WHEN 01
009640                 IF 受−保険者番号(3:1) = "3"
009650                     MOVE 3      TO 作３−印刷区分
009660                 ELSE
009670                     MOVE 2      TO 作３−印刷区分
009680                 END-IF
009690             WHEN 02
009700             WHEN 06
009710             WHEN 07
009720                 MOVE 1       TO 作３−印刷区分
009730             WHEN 03
009740                 MOVE 12      TO 作３−印刷区分
009750             WHEN 04
009760                 MOVE 13      TO 作３−印刷区分
009770                 MOVE 1       TO 作３−保険区分
009780             WHEN 09
009790                 MOVE 13      TO 作３−印刷区分
009800                 MOVE 2       TO 作３−保険区分
009810             WHEN 08
009820                 MOVE 4       TO 作３−印刷区分
009830             END-EVALUATE
009840         END-IF
009850     END-IF.
009860*
009870*================================================================*
009880 印刷区分セット２ SECTION.
009890*
009900*　印刷順を設定
009910*  　１：社保・船員・日雇い　２：国保　　　　３：国保組合
009920*　　４：退職　　　　　　　　５：２７老人　　６：４１老人
009930*　　７：母子　　　　　　　　８：乳幼児　　　９：障害者
009940*　１０：被爆者　　        １１：その他　  １２：組合
009950*　１３：共済
009960*
009970     MOVE ZERO       TO 作３−保険区分
009980*
009990     EVALUATE 受−助成種別
010000     WHEN 51
010010         MOVE 6      TO 作３−印刷区分
010020     WHEN 52
010030         MOVE 7      TO 作３−印刷区分
010040     WHEN 53
010050         MOVE 9      TO 作３−印刷区分
010060     WHEN 54
010070         MOVE 10     TO 作３−印刷区分
010080     WHEN 55
010090         MOVE 8      TO 作３−印刷区分
010100     WHEN 60
010110         MOVE 11     TO 作３−印刷区分
010120     END-EVALUATE.
010130*
010140*================================================================*
010150 助成レセまとめ判定 SECTION.
010160*---------------------------------------------------------------------------*
010170* 市町村マスタを読み、レセまとめ区分＝１でかつ、本体保険が国保・退職
010180* の時は、フラグYES (金額を助成込みで印字）
010190*（例：横浜市の障害は、本体保険（国保系）のレセプト１枚で請求、助成レセはなし）
010200*---------------------------------------------------------------------------*
010530*
010531     MOVE SPACE TO 助成レセまとめフラグ.
010532*
010533     IF レセ−本体まとめ区分 = 1
010534        MOVE "YES" TO 助成レセまとめフラグ
010535     END-IF.
010536*
010540*================================================================*
010550*================================================================*
010560******************************************************************
010570 END PROGRAM YJK581.
010580******************************************************************
