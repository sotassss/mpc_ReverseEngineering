000010******************************************************************
000020*            IDENTIFICATION      DIVISION                        *
000030******************************************************************
000040 IDENTIFICATION          DIVISION.
000050 PROGRAM-ID.             YJK101.
000060 AUTHOR.                 岡田　憲和
000070*
000080*----------------------------------------------------------------*
000090*      提出FPD作成【ﾃﾞｰﾀ作成】柔ｳｨﾝﾄﾞｳｽﾞ95版
000100* 請求年月Ver.
000110*      MED = YJK100G
000120*----------------------------------------------------------------*
000130 DATE-WRITTEN.           2012-08-17
000140 DATE-COMPILED.          2012-08-17
000150*----------------------------------------------------------------*
000160******************************************************************
000170*            ENVIRONMENT         DIVISION                        *
000180******************************************************************
000190 ENVIRONMENT             DIVISION.
000200 CONFIGURATION           SECTION.
000210 SOURCE-COMPUTER.        FMV-DESKPOWER-TS.
000220 OBJECT-COMPUTER.        FMV-DESKPOWER.
000230 SPECIAL-NAMES.          CONSOLE  IS  CONS
000240                         SYSERR   IS  MSGBOX.
000250 INPUT-OUTPUT            SECTION.
000260 FILE-CONTROL.
000270     SELECT  保険者マスタ    ASSIGN      TO        HOKENSL
000280                             ORGANIZATION             IS  INDEXED
000290                             ACCESS MODE              IS  DYNAMIC
000300                             RECORD KEY               IS  保−保険種別
000310                                                          保−保険者番号
000320                             ALTERNATE RECORD KEY     IS  保−保険種別
000330                                                          保−保険者名称
000340                                                          保−保険者番号
000350                             FILE STATUS              IS  状態キー
000360                             LOCK        MODE         IS  AUTOMATIC.
000370     SELECT  市町村マスタ    ASSIGN      TO        SITYOSNL
000380                             ORGANIZATION             IS  INDEXED
000390                             ACCESS MODE              IS  DYNAMIC
000400                             RECORD KEY               IS  市−公費種別
000410                                                          市−市町村番号
000420                             ALTERNATE RECORD KEY     IS  市−公費種別
000430                                                          市−市町村名称
000440                                                          市−市町村番号
000450                             FILE STATUS              IS  状態キー
000460                             LOCK        MODE         IS  AUTOMATIC.
000470     SELECT  元号マスタ      ASSIGN      TO        GENGOUL
000480                             ORGANIZATION             IS  INDEXED
000490                             ACCESS MODE              IS  DYNAMIC
000500                             RECORD KEY               IS  元−元号区分
000510                             FILE STATUS              IS  状態キー
000520                             LOCK        MODE         IS  AUTOMATIC.
000530     SELECT  名称マスタ      ASSIGN      TO        MEISYOL
000540                             ORGANIZATION             IS  INDEXED
000550                             ACCESS MODE              IS  DYNAMIC
000560                             RECORD KEY               IS  名−区分コード
000570                                                          名−名称コード
000580                             FILE STATUS              IS  状態キー
000590                             LOCK        MODE         IS  AUTOMATIC.
000600     SELECT  施術所情報マスタ ASSIGN      TO        SEJOHOL
000610                             ORGANIZATION             IS  INDEXED
000620                             ACCESS MODE              IS  DYNAMIC
000630                             RECORD KEY               IS 施情−施術所番号
000640                             FILE STATUS              IS  状態キー
000650                             LOCK        MODE         IS  AUTOMATIC.
000660     SELECT  施術記録Ｆ      ASSIGN      TO        SEKIROKL
000670                             ORGANIZATION             IS  INDEXED
000680                             ACCESS MODE              IS  DYNAMIC
000690                             RECORD KEY           IS 施記−施術和暦年月日
000700                                                     施記−患者コード
000710                             ALTERNATE RECORD KEY IS 施記−患者コード
000720                                                     施記−施術和暦年月日
000730                             FILE STATUS              IS  状態キー
000740                             LOCK        MODE         IS  AUTOMATIC.
000750     SELECT  受診者情報Ｆ    ASSIGN      TO        JUSINJL
000760                             ORGANIZATION             IS  INDEXED
000770                             ACCESS MODE              IS  DYNAMIC
000780                             RECORD KEY               IS 受−施術和暦年月
000790                                                          受−患者コード
000800                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000810                                                          受−患者カナ
000820                                                          受−患者コード
000830                             ALTERNATE RECORD KEY     IS  受−患者コード
000840                                                         受−施術和暦年月
000850                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000860                                                          受−保険種別
000870                                                          受−保険者番号
000880                                                          受−患者コード
000890                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000900                                                          受−公費種別
000910                                                     受−費用負担者番号
000920                                                          受−患者コード
000930                             ALTERNATE RECORD KEY     IS 受−施術和暦年月
000940                                                          受−助成種別
000950                                                  受−費用負担者番号助成
000960                                                          受−患者コード
000970                             ALTERNATE RECORD KEY  IS 受−請求和暦年月
000980                                                      受−施術和暦年月
000990                                                      受−患者コード
001000                             FILE STATUS              IS  状態キー
001010                             LOCK        MODE         IS  AUTOMATIC.
001020     SELECT  負傷データＦ    ASSIGN      TO        HUSYOUL
001030                             ORGANIZATION             IS  INDEXED
001040                             ACCESS MODE              IS  DYNAMIC
001050                             RECORD KEY               IS 負−施術和暦年月
001060                                                         負−患者コード
001070                             ALTERNATE RECORD KEY     IS 負−患者コード
001080                                                         負−施術和暦年月
001090                             FILE STATUS              IS  状態キー
001100                             LOCK        MODE         IS  AUTOMATIC.
001120     SELECT  レセプトＦ      ASSIGN      TO        RECEPTL
001121                             ORGANIZATION             IS  INDEXED
001122                             ACCESS MODE              IS  DYNAMIC
001123                             RECORD KEY               IS  レセ−施術和暦年月
001124                                                          レセ−患者コード
001125                                                          レセ−レセ種別
001126                             ALTERNATE RECORD KEY     IS  レセ−患者コード
001127                                                          レセ−施術和暦年月
001128                                                          レセ−レセ種別
001129                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
001130                                                          レセ−施術和暦年月
001131                                                          レセ−患者コード
001132                                                          レセ−レセ種別
001133                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
001134                                                          レセ−レセ種別
001135                                                          レセ−請求保険者番号
001136                                                          レセ−患者コード
001137                                                          レセ−施術和暦年月
001138                             ALTERNATE RECORD KEY     IS  レセ−請求和暦年月
001139                                                          レセ−請求保険者番号
001140                                                          レセ−患者コード
001141                                                          レセ−レセ種別
001142                                                          レセ−施術和暦年月
001143                             FILE STATUS              IS  状態キー
001144                             LOCK        MODE         IS  AUTOMATIC.
001145     SELECT  作業ファイル１  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W1011L.DAT"
001146                             ORGANIZATION             IS  INDEXED
001147                             ACCESS                   IS  DYNAMIC
001148                             RECORD      KEY          IS  作１−並び順
001150                             FILE        STATUS       IS  状態キー
001160                             LOCK        MODE         IS  AUTOMATIC.
001320     SELECT  作業ファイル３  ASSIGN      TO           "C:\MAKISHISYS\YAWOBJ\TEMP\W5803L.DAT"
001321                             ORGANIZATION             IS  INDEXED
001322                             ACCESS                   IS  DYNAMIC
001323                             RECORD      KEY          IS  作３−請求和暦年月
001325                                                          作３−印刷区分
001326                                                          作３−保険区分
001327                                                          作３−保険者番号
001328                                                          作３−本人家族区分
001329                                                          作３−被保険者カナ
001330                                                          作３−患者カナ
001331                                                          作３−当月区分
001332                                                          作３−施術和暦年月
001333                                                          作３−患者コード
001334                                                          作３−親子区分
001335                                                          作３−助成区分
001336                             FILE        STATUS       IS  状態キー
001337                             LOCK        MODE         IS  AUTOMATIC.
001338*
001339******************************************************************
001340*                      DATA DIVISION                             *
001341******************************************************************
001350 DATA                    DIVISION.
001360 FILE                    SECTION.
001370*                           ［ＲＬ＝  ３２０］
001380 FD  保険者マスタ        BLOCK   CONTAINS   1   RECORDS.
001390     COPY HOKENS          OF  XFDLIB  JOINING   保   AS  PREFIX.
001400*                           ［ＲＬ＝  ２５６］
001410 FD  市町村マスタ          BLOCK   CONTAINS   1   RECORDS.
001420     COPY SITYOSN        OF  XFDLIB  JOINING   市   AS  PREFIX.
001430*                           ［ＲＬ＝  １２８］
001440 FD  元号マスタ          BLOCK   CONTAINS   1   RECORDS.
001450     COPY GENGOU          OF  XFDLIB  JOINING   元   AS  PREFIX.
001460*                           ［ＲＬ＝  １２８］
001470 FD  名称マスタ          BLOCK   CONTAINS   1   RECORDS.
001480     COPY MEISYO          OF  XFDLIB  JOINING   名   AS  PREFIX.
001490*
001500 FD  施術所情報マスタ    BLOCK   CONTAINS   1   RECORDS.
001510     COPY SEJOHO         OF  XFDLIB  JOINING   施情   AS  PREFIX.
001520*                           ［ＲＬ＝  ２５６］
001530 FD  施術記録Ｆ          BLOCK   CONTAINS   1   RECORDS.
001540     COPY SEKIROK         OF  XFDLIB  JOINING   施記 AS  PREFIX.
001550*                           ［ＲＬ＝  ３２０］
001560 FD  受診者情報Ｆ        BLOCK   CONTAINS   1   RECORDS.
001570     COPY JUSINJ          OF  XFDLIB  JOINING   受   AS  PREFIX.
001580*                           ［ＲＬ＝  １２８］
001590 FD  負傷データＦ        BLOCK   CONTAINS   1   RECORDS.
001600     COPY HUSYOU          OF  XFDLIB  JOINING   負   AS  PREFIX.
001610*                          ［ＲＬ＝  １５３６］
001611 FD  レセプトＦ          BLOCK   CONTAINS   1   RECORDS.
001612     COPY RECEPT          OF  XFDLIB  JOINING   レセ  AS  PREFIX.
001613*
001620*
001630 FD  作業ファイル１ RECORD  CONTAINS 256 CHARACTERS.
001640 01  作１−レコード.
001650     03  作１−レコードキー.
001660         05  作１−並び順                  PIC X(4).
001670     03  作１−レコードデータ.
001680         05  作１−患者コード.
001690             07 作１−患者番号             PIC X(6).
001700             07 作１−枝番                 PIC X(1).
001710         05  作１−患者氏名                PIC X(20).
001720         05  作１−被保険者氏名            PIC X(20).
001730         05  作１−本人家族区分            PIC X(2).
001740         05  作１−受益者番号              PIC X(7).
001750         05  作１−保険者番号              PIC X(8).
001760         05  作１−部位数                  PIC 9.
001770         05  作１−実日数                  PIC X(2).
001780         05  作１−費用額                  PIC 9(6).
001790         05  作１−負担額                  PIC 9(6).
001800         05  作１−請求額                  PIC 9(6).
001810         05  作１−施術和暦年月.
001820             07  作１−施術年              PIC 9(2).
001830             07  作１−施術月              PIC 9(2).
001840         05  作１−国保種別                PIC X(2).
001850         05  作１−柔整師番号              PIC X(20).
001860         05  作１−代表者名                PIC X(20).
001870         05  作１−患者カナ                PIC X(20).
001880         05  作１−患者生年月日.
001890             07  作１−患者和暦            PIC 9.
001900             07  作１−患者年              PIC 9(2).
001910             07  作１−患者月              PIC 9(2).
001920             07  作１−患者日              PIC 9(2).
001930         05  作１−被保険者カナ            PIC X(20).
001940         05  作１−被保険者生年月日.
001950             07  作１−被保険者和暦        PIC 9.
001960             07  作１−被保険者年          PIC 9(2).
001970             07  作１−被保険者月          PIC 9(2).
001980             07  作１−被保険者日          PIC 9(2).
001990         05  作１−記号                    PIC X(24).
002000         05  作１−番号                    PIC X(30).
002010         05  FILLER                        PIC X(13).
002270* 保険者番号順ファイル
002272 FD  作業ファイル３ RECORD  CONTAINS 192 CHARACTERS.
002273 01  作３−レコード.
002274     03  作３−レコードキー.
002275         05  作３−請求和暦年月.
002276             07  作３−請求和暦            PIC 9.
002277             07  作３−請求年              PIC 9(2).
002278             07  作３−請求月              PIC 9(2).
002279         05  作３−印刷区分                PIC 9(2).
002280         05  作３−保険区分                PIC 9.
002281         05  作３−保険者番号              PIC 9(8).
002282         05  作３−本人家族区分            PIC 9.
002283         05  作３−被保険者カナ            PIC X(50).
002284         05  作３−患者カナ                PIC X(50).
002285         05  作３−当月区分                PIC 9.
002286         05  作３−施術和暦年月.
002287             07  作３−施術和暦            PIC 9.
002288             07  作３−施術年              PIC 9(2).
002289             07  作３−施術月              PIC 9(2).
002290         05  作３−患者コード.
002291             07 作３−患者番号             PIC 9(6).
002292             07 作３−枝番                 PIC X(1).
002293         05  作３−親子区分                PIC 9.
002294         05  作３−助成区分                PIC 9.
002295     03  作３−レコードデータ.
002296         05  FILLER                        PIC X(56).
002297*
002298*----------------------------------------------------------------*
002299******************************************************************
002300*                WORKING-STORAGE SECTION                         *
002301******************************************************************
002310 WORKING-STORAGE         SECTION.
002320 01 キー入力                           PIC X    VALUE SPACE.
002330 01 状態キー                           PIC X(2) VALUE SPACE.
002340 01 終了フラグ                         PIC X(3) VALUE SPACE.
002350 01 終了フラグ２                       PIC X(3) VALUE SPACE.
002360 01 終了フラグ３                       PIC X(3) VALUE SPACE.
002370 01 実行キーＷ                         PIC X(3)  VALUE SPACE.
002380 01 施術記録有Ｗ                       PIC X(3) VALUE SPACE.
002390 01 ファイル名                         PIC N(8) VALUE SPACE.
002400**
002410 01 保険種別ＷＲ                       PIC 9(2) VALUE ZERO.
002420 01 印刷形式ＷＲ                       PIC 9    VALUE ZERO.
002430 01 保険者番号ＷＲ                     PIC X(10) VALUE SPACE.
002440 01 レセプト種類ＷＲ                   PIC X(4) VALUE SPACE.
002450 01 本人家族区分ＷＲ                   PIC 9    VALUE ZERO.
002460 01 請求和暦年月ＷＲ.
002470    03 請求和暦ＷＲ                    PIC 9    VALUE ZERO.
002480    03 請求年ＷＲ                      PIC 9(2) VALUE ZERO.
002490    03 請求月ＷＲ                      PIC 9(2) VALUE ZERO.
002500 01 柔整師番号Ｗ                       PIC X(20) VALUE SPACE.
002510 01 代表者名Ｗ                         PIC X(20) VALUE SPACE.
002520 01 順番ＷＰ                           PIC X(4)  VALUE SPACE.
002530 01 順番Ｗ                             PIC 9(4)  VALUE ZERO.
002540**
002550** 助成レセまとめ用
002560 01 助成レセまとめフラグ               PIC X(3)  VALUE SPACE.
002570*
002580 01 カウンタ                           PIC 9(2)  VALUE ZERO.
002590 01 カウンタ２                         PIC 9(3)  VALUE ZERO.
002600 01 カウンタ３                         PIC 9(2)  VALUE ZERO.
002610 01 全角空白                           PIC X(2)  VALUE X"8140".
002620 01 半角空白                           PIC X(2)  VALUE X"2020".
002630**
002640** エラーメッセージ用
002650 01 エラーメッセージＷ.
002660    03 エラー患者コードＷ              PIC X(7) VALUE SPACE.
002670    03 エラー区切りＷ                  PIC X(1) VALUE SPACE.
002680    03 エラー保険種別Ｗ                PIC X(2) VALUE SPACE.
002690    03 FILLER                          PIC X(10) VALUE SPACE.
002700** 保険者番号右詰め用
002710 01 保険者番号ＷＴ.
002720    03 保険者番号左詰めＷ.
002730      05 保険者番号左詰めＷ１          PIC X OCCURS 8 VALUE SPACE.
002740    03 保険者番号右詰めＷ.
002750      05 保険者番号右詰めＷ１          PIC X OCCURS 8 VALUE ZERO.
002760    03 保険者番号数字Ｗ                PIC 9(8)  VALUE ZERO.
002770    03 保険者番号Ｗ                    PIC X(8)  VALUE SPACE.
002780** 会員番号右詰め用
002790 01 会員番号ＷＴ.
002800    03 会員番号左詰めＷ.
002810      05 会員番号左詰めＷ１            PIC X OCCURS 7 VALUE SPACE.
002820    03 会員番号右詰めＷ.
002830      05 会員番号右詰めＷ１            PIC X OCCURS 7 VALUE ZERO.
002840    03 会員番号数字Ｗ                  PIC 9(7)  VALUE ZERO.
002850    03 会員番号Ｗ                      PIC X(7)  VALUE SPACE.
002860** 西暦日付ワーク用
002870 01 西暦年月Ｗ.
002880    03 西暦年Ｗ                        PIC 9(4) VALUE ZERO.
002890    03 西暦月Ｗ                        PIC 9(2) VALUE ZERO.
002900** 西暦請求年月用
002910 01 西暦請求年月Ｗ.
002920    03 西暦請求年Ｗ                    PIC 9(4) VALUE ZERO.
002930    03 西暦請求月Ｗ                    PIC 9(2) VALUE ZERO.
002940** 西暦施術年月用
002950 01 西暦施術年月Ｗ.
002960    03 西暦施術年Ｗ                    PIC 9(4) VALUE ZERO.
002970    03 西暦施術月Ｗ                    PIC 9(2) VALUE ZERO.
002980** 西暦患者生年月日用
002990 01 西暦患者年月日Ｗ.
003000    03 西暦患者年Ｗ                    PIC 9(4) VALUE ZERO.
003010    03 西暦患者月Ｗ                    PIC 9(2) VALUE ZERO.
003020    03 西暦患者日Ｗ                    PIC 9(2) VALUE ZERO.
003030** 記号左詰め用
003040 01 記号ＷＴ.
003050    03 記号元Ｗ.
003060      05 記号元Ｗ１                    PIC N OCCURS 12 VALUE SPACE.
003070    03 記号左詰めＷ.
003080      05 記号左詰めＷ１                PIC N OCCURS 12 VALUE SPACE.
003090    03 記号Ｗ.
003100      05 記号ＮＷ                      PIC N(12) VALUE SPACE.
003110** 保険者名称用
003120 01 保険者名称Ｗ                       PIC X(40)  VALUE SPACE.
003130*
003140** 番号全角用
003150 01 番号ＷＴ.
003160    03 番号元Ｗ.
003170      05 番号元Ｗ１                    PIC X    OCCURS 20 VALUE SPACE.
003180    03 番号全角Ｗ.
003190      05 番号全角Ｗ１                  PIC X(2) OCCURS 20 VALUE SPACE.
003200    03 番号Ｗ                          PIC X(40) VALUE SPACE.
003210 01 半角文字Ｗ.
003220    03 半角文字１Ｗ                    PIC X(30)  VALUE
003230         "0123456789.+-*/><:;\)('&%$#!".
003240    03 半角文字２Ｗ                    PIC X(60)  VALUE
003250         "QWERTYUIOPASDFGHJKLZXCVBNMqwertyuiopasdfghjklzxcvbnm".
003260    03 半角文字合成Ｗ                  PIC X(90)  VALUE  SPACE.
003270    03 半角文字１Ｗ１ REDEFINES  半角文字合成Ｗ   PIC X OCCURS 90.
003280 01 全角文字Ｗ.
003290    03 全角文字１Ｗ                    PIC X(60)  VALUE
003300         "０１２３４５６７８９．＋−＊／＞＜：；￥）（’＆％＄＃！".
003310    03 全角文字２Ｗ                    PIC X(120)  VALUE
003320         "ＱＷＥＲＴＹＵＩＯＰＡＳＤＦＧＨＪＫＬＺＸＣＶＢＮＭｑｗｅｒｔｙｕｉｏｐａｓｄｆｇｈｊｋｌｚｘｃｖｂｎｍ".
003330    03 全角文字合成Ｗ                  PIC X(180)  VALUE  SPACE.
003340    03 全角文字１Ｗ１ REDEFINES  全角文字合成Ｗ   PIC X(2) OCCURS 90.
003350*
003360 01 複合プログラム名Ｗ                 PIC X(8) VALUE "MOJI2".
003361*
003362*****************************************************************
003370 01 計算機西暦年Ｗ                     PIC 9(2).
003380* 日付ＷＯＲＫ
003390 01 計算機西暦.
003400    03 計算機西暦年                    PIC 9(4).
003410    03 計算機西暦月日                  PIC 9(4).
003420 01 計算機西暦Ｒ REDEFINES 計算機西暦.
003430    03 計算機世紀                      PIC 9(2).
003440    03 計算機日付                      PIC 9(6).
003450    03 計算機日付Ｒ REDEFINES 計算機日付.
003460       05 計算機年月                   PIC 9(4).
003470       05 計算機年月Ｒ REDEFINES 計算機年月.
003480         07 計算機年                   PIC 9(2).
003490         07 計算機月                   PIC 9(2).
003500       05 計算機日                     PIC 9(2).
003510*
003520******************************************************************
003530*                          連結項目                              *
003540******************************************************************
003550*
003560********************
003570* メッセージ表示キー *
003580********************
003590 01 連メ−キー IS EXTERNAL.
003600    03  連メ−メッセージ               PIC N(20).
003610*
003620 01 連メ３−キー IS EXTERNAL.
003630    03  連メ３−メッセージ             PIC N(20).
003640    03  連メ３−メッセージ１           PIC X(20).
003650****************
003660* 画面入力情報 *
003670****************
004090 01 連入−画面情報ＹＪＫ５８０ IS EXTERNAL.
004100    03 連入−印刷形式                  PIC 9.
004110    03 連入−請求和暦年月.
004120       05 連入−請求和暦               PIC 9.
004130       05 連入−請求年月.
004140         07 連入−請求年               PIC 9(2).
004150         07 連入−請求月               PIC 9(2).
004160    03 連入−保険種別                  PIC 9(2).
004170**
004290* 暗号複合用
004291 01 連暗号複合−暗号情報 IS EXTERNAL.
004292    03 連暗号複合−入力情報.
004293       05 連暗号複合−記号               PIC X(24).
004294       05 連暗号複合−番号               PIC X(30).
004295       05 連暗号複合−暗号化項目.
004296          07 連暗号複合−暗号患者番号    PIC X(6).
004297          07 連暗号複合−暗号判定記号    PIC X.
004298          07 連暗号複合−暗号判定番号    PIC X.
004299          07 連暗号複合−暗号記号        PIC X(24).
004300          07 連暗号複合−暗号番号        PIC X(30).
004301    03 連暗号複合−出力情報.
004302       05 連暗号複合−複合した記号       PIC X(24).
004303       05 連暗号複合−複合した番号       PIC X(30).
004304*
004305******************************************************************
004306*                      PROCEDURE  DIVISION                       *
004310******************************************************************
004320 PROCEDURE               DIVISION.
004330************
004340*           *
004350* 初期処理   *
004360*           *
004370************
004381     PERFORM 初期化.
004391     PERFORM 施術所情報取得.
004400************
004410*           *
004420* 主処理     *
004430*           *
004440************
004451     PERFORM 作業ファイル作成.
004460************
004470*           *
004480* 終了処理   *
004490*           *
004500************
004511     PERFORM 終了処理.
004520     MOVE ZERO TO PROGRAM-STATUS.
004530     EXIT PROGRAM.
004540*
004550*<<<<<<<<<<<<<<<<<<<<<<<<< END OF PROGRAM >>>>>>>>>>>>>>>>>>>>>>>>
004560*================================================================*
004570 初期化 SECTION.
004580*
004590     PERFORM ファイルオープン.
004600*
004610*================================================================*
004620 ファイルオープン SECTION.
004630*
004640     OPEN INPUT 保険者マスタ
004650         MOVE NC"保険者" TO ファイル名.
004660         PERFORM オープンチェック.
004670     OPEN INPUT 市町村マスタ
004680         MOVE NC"市町村" TO ファイル名.
004690         PERFORM オープンチェック.
004700     OPEN INPUT 元号マスタ.
004710         MOVE NC"元号マスタ" TO ファイル名.
004720         PERFORM オープンチェック.
004730     OPEN INPUT 名称マスタ.
004740         MOVE NC"名称マスタ" TO ファイル名.
004750         PERFORM オープンチェック.
004760     OPEN INPUT 施術所情報マスタ
004770         MOVE NC"施情" TO ファイル名.
004780         PERFORM オープンチェック.
004800     OPEN INPUT 施術記録Ｆ.
004801         MOVE NC"施術記録Ｆ" TO ファイル名.
004810         PERFORM オープンチェック.
004821     OPEN INPUT 受診者情報Ｆ.
004830         MOVE NC"受診者情報Ｆ" TO ファイル名.
004840         PERFORM オープンチェック.
004850     OPEN INPUT 負傷データＦ.
004860         MOVE NC"負傷データＦ" TO ファイル名.
004870         PERFORM オープンチェック.
004883     OPEN INPUT レセプトＦ.
004884         MOVE NC"レセプトＦ" TO ファイル名.
004885         PERFORM オープンチェック.
004886*================================================================*
004890 オープンチェック SECTION.
004900*
004910     IF 状態キー  NOT =  "00"
004920         DISPLAY ファイル名 NC"Ｆオープンエラー" UPON CONS
004930         DISPLAY NC"状態キー：" 状態キー         UPON CONS
004940         DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"
004950                                                 UPON CONS
004960         ACCEPT  キー入力 FROM CONS
004970         PERFORM ファイル閉鎖
004980         MOVE 99 TO PROGRAM-STATUS
004990         EXIT PROGRAM.
005000*================================================================*
005010 ファイル閉鎖 SECTION.
005020*
005030     CLOSE 元号マスタ 名称マスタ 受診者情報Ｆ 負傷データＦ
005040           施術記録Ｆ 施術所情報マスタ 保険者マスタ 市町村マスタ レセプトＦ.
005050*================================================================*
005060 終了処理 SECTION.
005070*
005080     PERFORM ファイル閉鎖.
005090*================================================================*
005100 エラー表示 SECTION.
005110*
005120     DISPLAY NC"状態キー" 状態キー  UPON CONS.
005130     DISPLAY NC"ファイル書込エラー：" ファイル名   UPON CONS.
005140     DISPLAY NC"システム管理者に連絡してください"  UPON CONS.
005150     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
005160     ACCEPT  キー入力 FROM CONS.
005170     PERFORM ファイル閉鎖.
005180     MOVE 99 TO PROGRAM-STATUS.
005190     EXIT PROGRAM.
005200*================================================================*
005201 エラー表示Ｒ SECTION.
005202*
005203     DISPLAY NC"ファイル読込エラー" ファイル名     UPON CONS.
005204     DISPLAY NC"状態キー" 状態キー                 UPON CONS.
005205     DISPLAY NC"数字１文字入力しＥＮＴＥＲキーを押してください"                                                                    UPON CONS.
005206     ACCEPT  キー入力 FROM CONS.
005207     PERFORM ファイル閉鎖.
005208     MOVE 99 TO PROGRAM-STATUS.
005209     EXIT PROGRAM.
005210*================================================================*
005211*================================================================*
005212 施術所情報取得 SECTION.
005220*
005230     MOVE ZERO  TO 施情−施術所番号.
005240     READ 施術所情報マスタ
005250     INVALID KEY
005260          MOVE  NC"施術所情報マスタに登録後、実行して下さい" TO 連メ−メッセージ
005270          CALL   "MSG001"
005280          CANCEL "MSG001"
005290          PERFORM ファイル閉鎖
005300          MOVE 99 TO PROGRAM-STATUS
005310          EXIT PROGRAM
005320     NOT INVALID KEY
005330          IF  施情−新柔整師番号 = SPACE
005340              MOVE  NC"施術所マスタに新柔整師番号を登録して下さい" TO 連メ−メッセージ
005350              CALL   "MSG001"
005360              CANCEL "MSG001"
005370              PERFORM ファイル閉鎖
005380              MOVE 99 TO PROGRAM-STATUS
005390              EXIT PROGRAM
005400          ELSE
005410              MOVE 施情−新柔整師番号  TO 柔整師番号Ｗ
005420              MOVE 施情−代表者名      TO 代表者名Ｗ
005430          END-IF
005440     END-READ.
005450*
005460*================================================================*
005470 作業ファイル作成 SECTION.
005480*
005491     OPEN OUTPUT 作業ファイル１.
005500         MOVE NC"作１" TO ファイル名.
005510         PERFORM オープンチェック.
005520     OPEN INPUT  作業ファイル３.
005530         MOVE NC"作３" TO ファイル名.
005540         PERFORM オープンチェック.
005553*
005561     PERFORM 作業ファイル１作成１
005570*
005580     CLOSE 作業ファイル３ 作業ファイル１.
005590*
005600*================================================================*
005610 作業ファイル１作成１ SECTION.
005620*
005630*********************
005640*       全出力      *
005650*********************
005660*
005670     MOVE SPACE  TO 終了フラグ３.
005680     MOVE ZERO   TO 順番Ｗ.
005691     PERFORM 作業ファイル３読込.
005700     PERFORM UNTIL  終了フラグ３ = "YES"
005710         PERFORM データチェック
005720** 労災・自賠責・自由は対象外
005730         IF  受−保険種別 = 70 OR 80 OR 90
005740             MOVE SPACE  TO 実行キーＷ
005750         END-IF
005760**
005770         IF  実行キーＷ = "YES"
005780**
005810*↓* 特別処理（助成レセまとめ）
005820             IF 受−助成種別 NOT = ZERO
005830                 PERFORM 助成レセまとめ判定
005840             ELSE
005850                 MOVE SPACE TO 助成レセまとめフラグ
005860             END-IF
005870*↑*
005880*        ********
005890*        * 健保 *
005900*        ********
005910             IF ( 受−保険種別   NOT = ZERO ) AND
005920                ( 受−保険者番号 NOT = SPACE )
005930*            **********************
005940*            * 作業ファイル作成 *
005950*            **********************
005960                 IF ( 受−公費種別       = ZERO  ) AND
005970                    ( 受−費用負担者番号 = SPACE )
005980                     IF 作３−印刷区分  = 1 OR 2 OR 3 OR 4 OR 12 OR 13
005990                         COMPUTE 順番Ｗ = 順番Ｗ + 1
006000                         MOVE SPACE TO 作１−レコード
006010                         INITIALIZE 作１−レコード
006020                         MOVE 受−保険者番号        TO 作１−保険者番号
006030                         MOVE 受−受益者番号助成    TO 作１−受益者番号
006040*↓* 特別処理（助成レセまとめ）
006050                         IF 助成レセまとめフラグ = "YES"
006130                              MOVE レセ−合計           TO 作１−費用額
006131                              MOVE レセ−受給者負担額   TO 作１−負担額
006132                              COMPUTE 作１−請求額 = レセ−合計 - レセ−受給者負担額
006133                          ELSE
006134                              MOVE レセ−合計           TO 作１−費用額
006135                              MOVE レセ−一部負担金     TO 作１−負担額
006136                              MOVE レセ−請求金額       TO 作１−請求額
006137                         END-IF
006140*↑*
006150                         INSPECT 作１−費用額 REPLACING LEADING ZERO BY SPACE
006160                         INSPECT 作１−負担額 REPLACING LEADING ZERO BY SPACE
006170                         INSPECT 作１−請求額 REPLACING LEADING ZERO BY SPACE
006180                         MOVE 01     TO 作１−国保種別
006190                         PERFORM 作１レコードセット
006200                         PERFORM 作１ファイル書込
006210                     END-IF
006220                 END-IF
006230             END-IF
006240*        ********
006250*        * 老人 *
006260*        ********
006270             IF ( 受−公費種別       NOT = ZERO ) AND
006280                ( 受−費用負担者番号 NOT = SPACE )
006290*                **********************
006300*                * 作業ファイル作成 *
006310*                **********************
006320                 IF 作３−印刷区分  = 5
006330                     COMPUTE 順番Ｗ = 順番Ｗ + 1
006340                     MOVE SPACE TO 作１−レコード
006350                     INITIALIZE 作１−レコード
006360                     MOVE 受−費用負担者番号    TO 作１−保険者番号
006370                     MOVE 受−受益者番号老人    TO 作１−受益者番号
006380*↓* 特別処理（助成レセまとめ）
006480                     IF 助成レセまとめフラグ = "YES"
006481                         MOVE レセ−合計            TO 作１−費用額
006482                         MOVE レセ−受給者負担額    TO 作１−負担額
006483                         COMPUTE 作１−請求額 = レセ−合計 - レセ−受給者負担額
006484                     ELSE
006485                         MOVE レセ−合計            TO 作１−費用額
006486                         MOVE レセ−一部負担金      TO 作１−負担額
006487                         MOVE レセ−請求金額        TO 作１−請求額
006488                     END-IF
006489*↑*
006490                     INSPECT 作１−費用額 REPLACING LEADING ZERO BY SPACE
006500                     INSPECT 作１−負担額 REPLACING LEADING ZERO BY SPACE
006510                     INSPECT 作１−請求額 REPLACING LEADING ZERO BY SPACE
006520                     MOVE 06     TO 作１−国保種別
006530                     PERFORM 作１レコードセット
006540                     PERFORM 作１ファイル書込
006550                 END-IF
006560             END-IF
006570*        ********
006580*        * 助成 *
006590*        ********
006600             IF ( 受−助成種別       NOT = ZERO ) AND
006610                ( 受−費用負担者番号助成 NOT = SPACE )
005930*         / 横浜・川崎の小児医療の請求額０円は対象外にする /170411
006880                IF (レセ−助成請求金額 = ZERO) AND
                         (受−助成種別 = "55") AND
                         (受−費用負担者番号助成(3:3) = "144" OR "145")
                          CONTINUE
                      ELSE
006620*                **********************
006630*                * 作業ファイル作成 *
006640*                **********************
006650                   IF 作３−印刷区分  = 6 OR 7 OR 8 OR 9 OR 10 OR 11
006660                      COMPUTE 順番Ｗ = 順番Ｗ + 1
006670                      MOVE SPACE TO 作１−レコード
006680                      INITIALIZE 作１−レコード
006690                      MOVE 受−費用負担者番号助成    TO 作１−保険者番号
006700                      MOVE 受−受益者番号助成        TO 作１−受益者番号
006782                      MOVE レセ−一部負担金          TO 作１−費用額
006783                      MOVE レセ−受給者負担額        TO 作１−負担額
006784                      MOVE レセ−助成請求金額        TO 作１−請求額
006785                      INSPECT 作１−費用額 REPLACING LEADING ZERO BY SPACE
006790                      INSPECT 作１−負担額 REPLACING LEADING ZERO BY SPACE
006800                      INSPECT 作１−請求額 REPLACING LEADING ZERO BY SPACE
006810                      PERFORM 作１レコードセット
006820                      PERFORM 作１ファイル書込
006830                   END-IF
                      END-IF
006840             END-IF
006850         END-IF
006860         PERFORM 作業ファイル３読込
006870     END-PERFORM.
006880*
006890*================================================================*
006900 作業ファイル３読込 SECTION.
006910*
007072     READ 作業ファイル３ NEXT
007073     AT END
007074         MOVE "YES" TO 終了フラグ３
007075     NOT AT END
007076         MOVE 作３−施術和暦    TO 受−施術和暦 レセ−施術和暦  負−施術和暦
007077         MOVE 作３−施術年      TO 受−施術年   レセ−施術年    負−施術年
007078         MOVE 作３−施術月      TO 受−施術月   レセ−施術月    負−施術月
007079         MOVE 作３−患者番号    TO 受−患者番号 レセ−患者番号  負−患者番号
007080         MOVE 作３−枝番        TO 受−枝番     レセ−枝番      負−枝番
007082         READ 受診者情報Ｆ
007083         INVALID KEY
007086              MOVE NC"受診者"   TO ファイル名
007087              PERFORM エラー表示Ｒ
007088         END-READ
007089         IF 作３−親子区分 = 1
007090             MOVE 3             TO レセ−レセ種別
007091         ELSE
007092            IF 受−公費種別 = 5
007093                MOVE 2          TO レセ−レセ種別
007094            ELSE
007095                MOVE 1          TO レセ−レセ種別
007096            END-IF
007097         END-IF
007098         READ レセプトＦ
007099         INVALID KEY
007100              MOVE NC"レセプト"   TO ファイル名
007101              PERFORM エラー表示Ｒ
007102         END-READ
007108         READ 負傷データＦ
007109         INVALID KEY
007111              MOVE NC"レセプト"   TO ファイル名
007112              PERFORM エラー表示Ｒ
007114         END-READ
007115     END-READ.
007116
007117*================================================================*
007118 データチェック SECTION.
007119*
007120     MOVE SPACE  TO 実行キーＷ.
007570* *****************************************************************
007571* * レセプトＦの請求対象区分 = 0 の場合データ作成対象としない *
007572* *****************************************************************
007573     IF ( レセ−請求対象区分 NOT = ZERO ) AND
007574        ( レセ−償還払い区分 NOT = 1 )
007575        IF (レセ−レセ種別 = 3) AND ( レセ−会総括表印刷対象区分 = 1 )
007576            CONTINUE
007577        ELSE
007578           MOVE レセ−施術和暦  TO 受−施術和暦
007579           MOVE レセ−施術年    TO 受−施術年
007580           MOVE レセ−施術月    TO 受−施術月
007581           MOVE レセ−患者番号  TO 受−患者番号
007582           MOVE レセ−枝番      TO 受−枝番
007583           READ 受診者情報Ｆ
007584           NOT INVALID KEY
007585**      健保のみ
007586              IF 受−保険分類 = 1
007587                 MOVE "YES"  TO 実行キーＷ
007588              END-IF
007589           END-READ
007590        END-IF
007591     END-IF.
007593*
007670*================================================================*
007671 レセプトＦ読込 SECTION.
007672*
007673     READ レセプトＦ NEXT
007674     AT END
007675         MOVE "YES" TO 終了フラグ
007676     END-READ.
007677*
007678*================================================================*
007680 受診者情報Ｆ読込 SECTION.
007690*
007700     READ 受診者情報Ｆ NEXT
007710     AT END
007720         MOVE "YES" TO 終了フラグ
007730     END-READ.
007740*
007820*================================================================*
007830 作１レコードセット SECTION.
007840*
007850     MOVE 順番Ｗ             TO 作１−並び順.
007860     INSPECT 作１−並び順 REPLACING LEADING ZERO BY SPACE.
007870     MOVE 受−患者コード     TO 作１−患者コード.
007880     INSPECT 作１−患者番号 REPLACING LEADING ZERO BY SPACE.
007890     MOVE 受−患者氏名       TO 作１−患者氏名.
007900     MOVE 受−被保険者氏名   TO 作１−被保険者氏名.
007910     IF 受−本人家族区分 = 1
007920         MOVE "01"           TO 作１−本人家族区分
007930     ELSE
007940         MOVE "02"           TO 作１−本人家族区分
007950     END-IF.
007970     MOVE 負−部位数         TO 作１−部位数
007971     IF レセ−レセ実日数(1:1) NOT = ZERO
007980         MOVE レセ−レセ実日数   TO 作１−実日数
007990     ELSE
008000         MOVE レセ−レセ実日数(2:1)  TO 作１−実日数
008010     END-IF.
008020     MOVE 受−施術年         TO 作１−施術年.
008030     MOVE 受−施術月         TO 作１−施術月.
008040     MOVE 柔整師番号Ｗ       TO 作１−柔整師番号.
008050     MOVE 代表者名Ｗ         TO 作１−代表者名.
008060     MOVE 受−患者カナ       TO 作１−患者カナ.
008070     MOVE 受−患者生年月日   TO 作１−患者生年月日.
008080     MOVE 受−被保険者カナ   TO 作１−被保険者カナ.
008090     MOVE 受−被保険者生年月日   TO 作１−被保険者生年月日.
008100*
008101*    / 連暗号複合−入力情報セット /
008102     MOVE 受−記号       TO 連暗号複合−記号
008103     MOVE 受−番号       TO 連暗号複合−番号
008104     MOVE 受−暗号化項目 TO 連暗号複合−暗号化項目
008105*
008106     CALL   複合プログラム名Ｗ
008107     CANCEL 複合プログラム名Ｗ
008108*
008113     IF ( 連暗号複合−複合した番号(1:1) = "*"  ) OR
008120        ( 連暗号複合−複合した番号(1:2) = "＊" )
008130         MOVE SPACE                    TO 作１−番号
008140     ELSE
008150         MOVE 連暗号複合−複合した番号 TO 作１−番号
008160     END-IF.
008170*
008180     IF ( 連暗号複合−複合した記号(1:1) = "*"  ) OR
008181        ( 連暗号複合−複合した記号(1:2) = "＊" )
008190         MOVE SPACE                    TO 作１−記号
008200     ELSE
008210         MOVE 連暗号複合−複合した記号 TO 記号Ｗ
008220         MOVE 記号Ｗ                   TO 作１−記号
008230     END-IF.
008242
008243*     IF ( 受−施術和暦年月 >= 42004 ) AND ( 受−保険種別 = 05 )
008250*         MOVE "Ｌ"   TO 作１−国保種別
008260*     END-IF.
008270*     IF ( 受−施術和暦年月 >= 42004 ) AND ( 受−特別区分 = 6 )
008280*         MOVE "Ｋ"   TO 作１−国保種別
008290*     END-IF
008300**/協会健保の保険区分(国保種別)をＭにする/081006
008310*     IF (受−保険種別 = 02) AND (受−保険者番号(5:5) NOT = SPACE)
008320*         MOVE "Ｍ"   TO 作１−国保種別
008330*     END-IF
008340     EVALUATE 受−助成種別
008350     WHEN 51
008360         MOVE 02     TO 作１−国保種別
008370     WHEN 52
008380         MOVE 04     TO 作１−国保種別
008390     WHEN 53
008400         MOVE 05     TO 作１−国保種別
008410     WHEN 54
008420         MOVE 08     TO 作１−国保種別
008430     WHEN 55
008440         MOVE 09     TO 作１−国保種別
008450     WHEN 60
008460         MOVE 10     TO 作１−国保種別
      */東京のその他は子ども(09)/20241111
               IF 受−費用負担者番号助成(1:4) = "8113" OR "8813" OR "8913"
008440             MOVE 09 TO 作１−国保種別
               END-IF
008470     END-EVALUATE.
008480*
008660*================================================================*
008670 作１ファイル書込 SECTION.
008680*
008690     WRITE 作１−レコード
008700     INVALID KEY
008710         MOVE NC"作１"  TO ファイル名
008720         PERFORM エラー表示
008730     END-WRITE.
008740*================================================================*
008750 助成レセまとめ判定 SECTION.
008760*---------------------------------------------------------------------------*
008770* 市町村マスタを読み、レセまとめ区分＝１でかつ、本体保険が国保・退職
008780* の時は、フラグYES (金額を助成込みで印字）
008790*（例：横浜市の障害は、本体保険（国保系）のレセプト１枚で請求、助成レセはなし）
008800*---------------------------------------------------------------------------*
009137*
009138     MOVE SPACE TO 助成レセまとめフラグ.
009139*
009140     IF レセ−本体まとめ区分 = 1
009141        MOVE "YES" TO 助成レセまとめフラグ
009142     END-IF.
009143*
009144*
009145*================================================================*
009150******************************************************************
009160 END PROGRAM YJK101.
009170******************************************************************
